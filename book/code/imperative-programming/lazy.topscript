#use "topfind";;
[%%expect{|
- : unit = ()
- : unit = ()
|}];;
#thread;;
#camlp4o;;
#require "core";;
#require "core.syntax";;
open Core_kernel;;
[@@@part "1"];;
let v = lazy (print_string "performing lazy computation\n"; sqrt 16.);;
[%%expect{|val v : float lazy_t = <lazy>|}];;
Lazy.force v;;
[%%expect{|
- : float = 4.
performing lazy computation
|}];;
Lazy.force v;;
[%%expect{|- : float = 4.|}];;
[@@@part "2"];;
type 'a lazy_state =
  | Delayed of (unit -> 'a)
  | Value of 'a
  | Exn of exn
;;
[%%expect{|type 'a lazy_state = Delayed of (unit -> 'a) | Value of 'a | Exn of exn|}];;
[@@@part "3"];;
let create_lazy f = ref (Delayed f);;
[%%expect{|val create_lazy : (unit -> 'a) -> 'a lazy_state ref = <fun>|}];;
let v = create_lazy
          (fun () -> print_string "performing lazy computation\n"; sqrt 16.);;
[%%expect{|val v : float lazy_state ref = {contents = Delayed <fun>}|}];;
[@@@part "4"];;
let force v =
  match !v with
  | Value x -> x
  | Exn e -> raise e
  | Delayed f ->
    try
      let x = f () in
      v := Value x;
      x
    with exn ->
      v := Exn exn;
      raise exn
;;
[%%expect{|val force : 'a lazy_state ref -> 'a = <fun>|}];;
[@@@part "5"];;
force v;;
[%%expect{|
- : float = 4.
performing lazy computation
|}];;
force v;;
[%%expect{|- : float = 4.|}];;
