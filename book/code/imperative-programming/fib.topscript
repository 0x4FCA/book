#use "topfind";;
[%%expect{|
- : unit = ()
- : unit = ()
|}];;
#thread;;
#camlp4o;;
#require "core";;
#require "core.syntax";;
open Core_kernel;;
#require "mtime.os.top";;
let time f =
  let counter = Mtime.counter () in
  let x = f () in
  let elapsed = Mtime.count counter in
  printf "Time: %F ms\n" (Mtime.to_ms elapsed);
  x 
;;
[%%expect{|val time : (unit -> 'a) -> 'a = <fun>|}];;
let memoize f =
  let table = Hashtbl.Poly.create () in
  (fun x ->
     match Hashtbl.find table x with
     | Some y -> y
     | None ->
       let y = f x in
       Hashtbl.add_exn table ~key:x ~data:y;
       y
  );;
[%%expect{|val memoize : ('a -> 'b) -> 'a -> 'b = <fun>|}];;
[@@@part "1"];;
let rec fib i =
  if i <= 1 then 1 else fib (i - 1) + fib (i - 2);;
[%%expect{|val fib : int -> int = <fun>|}];;
[@@@part "2"];;
time (fun () -> fib 20);;
[%%expect{|
- : int = 10946
Time: 0.251996 ms
|}];;
time (fun () -> fib 40);;
[%%expect{|
- : int = 165580141
Time: 3625.279014 ms
|}];;
[@@@part "3"];;
let fib = memoize fib;;
[%%expect{|val fib : int -> int = <fun>|}];;
time (fun () -> fib 40);;
[%%expect{|
- : int = 165580141
Time: 3492.912275 ms
|}];;
time (fun () -> fib 40);;
[%%expect{|
- : int = 165580141
Time: 0.002622 ms
|}];;
[@@@part "4"];;
let fib_norec fib i =
  if i <= 1 then i
  else fib (i - 1) + fib (i - 2) ;;
[%%expect{|val fib_norec : (int -> int) -> int -> int = <fun>|}];;
[@@@part "5"];;
let rec fib i = fib_norec fib i;;
[%%expect{|val fib : int -> int = <fun>|}];;
fib 20;;
[%%expect{|- : int = 6765|}];;
[@@@part "6"];;
let make_rec f_norec =
  let rec f x = f_norec f x in
  f
;;
[%%expect{|val make_rec : (('a -> 'b) -> 'a -> 'b) -> 'a -> 'b = <fun>|}];;
let fib = make_rec fib_norec;;
[%%expect{|val fib : int -> int = <fun>|}];;
fib 20;;
[%%expect{|- : int = 6765|}];;
[@@@part "7"];;
let memo_rec f_norec x =
  let fref = ref (fun _ -> assert false) in
  let f = memoize (fun x -> f_norec !fref x) in
  fref := f;
  f x
;;
[%%expect{|val memo_rec : (('a -> 'b) -> 'a -> 'b) -> 'a -> 'b = <fun>|}];;
[@@@part "8"];;
let fib = memo_rec fib_norec;;
[%%expect{|val fib : int -> int = <fun>|}];;
time (fun () -> fib 40);;
[%%expect{|
- : int = 102334155
Time: 0.031978 ms
|}];;
[@@@part "9"];;
let fib = memo_rec (fun fib i ->
  if i <= 1 then 1 else fib (i - 1) + fib (i - 2));;
[%%expect{|val fib : int -> int = <fun>|}];;
