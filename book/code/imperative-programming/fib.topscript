#use "topfind";;
#thread;;
#camlp4o;;
#require "core";;
#require "core.syntax";;
open Core_kernel.Std;;

#require "mtime.os.top";;

let time f =
  let counter = Mtime.counter () in
  let x = f () in
  let elapsed = Mtime.count counter in
  printf "Time: %F ms\n" (Mtime.to_ms elapsed);
  x 
;;

let memoize f =
  let table = Hashtbl.Poly.create () in
  (fun x ->
     match Hashtbl.find table x with
     | Some y -> y
     | None ->
       let y = f x in
       Hashtbl.add_exn table ~key:x ~data:y;
       y
  );;

[%%expect{|
- : unit = ()
Findlib has been successfully loaded. Additional directives:
  #require "package";;      to load a package
  #list;;                   to list the available packages
  #camlp4o;;                to load camlp4 (standard syntax)
  #camlp4r;;                to load camlp4 (revised syntax)
  #predicates "p,q,...";;   to set these predicates
  Topfind.reset();;         to force that packages will be reloaded
  #thread;;                 to enable threads

- : unit = ()
|}]
[@@@part "1"];;
let rec fib i =
  if i <= 1 then 1 else fib (i - 1) + fib (i - 2);;


[%%expect]
[@@@part "2"];;
time (fun () -> fib 20);;


time (fun () -> fib 40);;



[%%expect{|
Time: 0.751632 ms
Time: 9872.985882 ms
|}]
[@@@part "3"];;
let fib = memoize fib;;

time (fun () -> fib 40);;


time (fun () -> fib 40);;



[%%expect{|
Time: 10693.530227 ms
Time: 0.007144 ms
|}]
[@@@part "4"];;
let fib_norec fib i =
  if i <= 1 then i
  else fib (i - 1) + fib (i - 2) ;;


[%%expect]
[@@@part "5"];;
let rec fib i = fib_norec fib i;;

fib 20;;


[%%expect]
[@@@part "6"];;
let make_rec f_norec =
  let rec f x = f_norec f x in
  f
;;

let fib = make_rec fib_norec;;

fib 20;;


[%%expect]
[@@@part "7"];;
let memo_rec f_norec x =
  let fref = ref (fun _ -> assert false) in
  let f = memoize (fun x -> f_norec !fref x) in
  fref := f;
  f x
;;


[%%expect{|
|}]
[@@@part "8"];;
let fib = memo_rec fib_norec;;

time (fun () -> fib 40);;



[%%expect{|
Time: 0.091368 ms
|}]
[@@@part "9"];;
let fib = memo_rec (fun fib i ->
  if i <= 1 then 1 else fib (i - 1) + fib (i - 2));;

[%%expect{|
|}]