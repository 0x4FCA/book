#use "topfind";;
[%%expect{|
- : unit = ()
- : unit = ()
|}];;
#thread;;
#camlp4o;;
#require "core";;
#require "core.syntax";;
open Core_kernel.Std;;
type shape = < area : float > ;;
[%%expect{|
type shape = < area : float >
|}];;
type square = < area : float; width : int > ;;
[%%expect{|
type square = < area : float; width : int >
|}];;
let square w = object
  method area = Float.of_int (w * w)
  method width = w
end ;;
[%%expect{|
val square : int -> < area : Core_kernel.Std.Float.t; width : int > = <fun>
|}];;
type circle = < area : float; radius : int > ;;
[%%expect{|
type circle = < area : float; radius : int >
|}];;
let circle r = object
  method area = 3.14 *. (Float.of_int r) ** 2.0
  method radius = r
end ;;
[%%expect{|
val circle : int -> < area : float; radius : int > = <fun>
|}];;
[@@@part "1"];;
let remove_large l =
  List.filter ~f:(fun s -> s#area <= 100.) l ;;
[%%expect{|
val remove_large :
  (< area : float; .. > as 'a) Core_kernel.Std.List.t ->
  'a Core_kernel.Std.List.t = <fun>
|}];;
[@@@part "2"];;
let squares : < area : float; width : int > list = 
  [square 5; square 15; square 10] ;;
[%%expect{|
val squares : < area : float; width : int > list = [<obj>; <obj>; <obj>]
|}];;
remove_large squares ;;
[%%expect{|
- : < area : float; width : int > Core_kernel.Std.List.t = [<obj>; <obj>]
|}];;
[@@@part "3"];;
let remove_large (l: < area : float > list) = 
  List.filter ~f:(fun s -> s#area <= 100.) l ;;
[%%expect{|
val remove_large :
  < area : float > list -> < area : float > Core_kernel.Std.List.t = <fun>
|}];;
remove_large (squares :> < area : float > list ) ;;
[%%expect{|
- : < area : float > Core_kernel.Std.List.t = [<obj>; <obj>]
|}];;
[@@@part "4"];;
let hlist: < area: float; ..> list = [square 10; circle 30] ;;
[%%expect{|
Characters 49-58:
Error: This expression has type < area : float; radius : int >
       but an expression was expected of type
         < area : Core_kernel.Std.Float.t; width : int >
       The second object type has no method radius
|}];;
[@@@part "5"];;
let shape_ref: < area: float; ..> ref = ref (square 40) ;;
[%%expect{|
val shape_ref : < area : float; width : int > Core_kernel.Std.ref =
  {contents = <obj>}
|}];;
shape_ref := circle 20 ;;
[%%expect{|
Characters 14-23:
Error: This expression has type < area : float; radius : int >
       but an expression was expected of type < area : float; width : int >
       The second object type has no method radius
|}];;
[@@@part "6"];;
let hlist: shape list = [(square 10 :> shape); (circle 30 :> shape)] ;;
[%%expect{|
val hlist : shape list = [<obj>; <obj>]
|}];;
let shape_ref: shape ref = ref (square 40 :> shape) ;;
[%%expect{|
val shape_ref : shape Core_kernel.Std.ref = {contents = <obj>}
|}];;
shape_ref := (circle 20 :> shape) ;;
[%%expect{|
- : unit = ()
|}];;
