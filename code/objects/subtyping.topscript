let stack init = object
  val mutable v = init

  method pop =
    match v with
      hd :: tl -> 
        v <- tl;
        Some hd
    | [] -> None

  method push hd = 
    v <- hd :: v
end;;

#part 1
type shape = < area : float >;;

type square = < area : float; width : float >;;

let square w = object
  method area = w *. w
  method width = w
end;;

type circle = < area : float; radius : float >;;

let circle r = object
  method area = 3.14 *. r ** 2.0
  method radius = r
end;;

#part 2
let shape w : shape = square w;;
let shape w : shape = (square w :> shape);;

#part 3
let coin = object
  method shape = circle 0.5
  method color = "silver"
end;;
let map = object
  method shape = square 1.0
end;;

#part 4
type item = < shape : shape >;;
let items = [ (coin :> item) ; (map :> item) ];;

#part 5
type num = [ `Int of int | `Float of float ];;
type const = [ num | `String of string ];;
let n : num = `Int 3;;
let c : const = (n :> const);;

#part 6
let squares: square list = [ square 1.0; square 2.0 ];;
let shapes: shape list = (squares :> shape list);;

#part 7
let square_array: square array = [| square 1.0; square 2.0 |];;
let shape_array: shape array = (square_array :> shape array);;

#part 8
let shape_to_string: shape -> string = 
    fun s -> sprintf "Shape(%F)" s#area;;
let square_to_string: square -> string = 
    (shape_to_string :> square -> string);;

#part 9
module Either = struct
  type ('a, 'b) t = 
      Left of 'a
    | Right of 'b
end;;
let sq : (square, circle) Either.t = Either.Left (square 4.0);;
let sh : (shape, shape) Either.t = (sq :> (shape, shape) Either.t);;

#part 10
module Either : sig 
  type ('a, 'b) t
  val left: 'a -> ('a, 'b) t
  val right: 'b -> ('a, 'b) t
end = struct
  type ('a, 'b) t = 
    | Left of 'a
    | Right of 'b
  let left x = Left x
  let right x = Right x
end;;
let sq : (square, circle) Either.t = Either.left (square 4.0);;
let sh : (shape, shape) Either.t = (sq :> (shape, shape) Either.t);;

#part 11
module Either : sig 
  type (+'a, +'b) t
  val left: 'a -> ('a, 'b) t
  val right: 'b -> ('a, 'b) t
end = struct
  type ('a, 'b) t = 
    | Left of 'a
    | Right of 'b
  let left x = Left x
  let right x = Right x
end;;
let sq : (square, circle) Either.t = Either.left (square 4.0);;
let sh : (shape, shape) Either.t = (sq :> (shape, shape) Either.t);;

#part 12
type 'a stack = < pop: 'a option; push: 'a -> unit >;;
let square_stack: square stack = stack [square 3.0; square 1.0];;
let circle_stack: circle stack = stack [circle 2.0; circle 4.0];;

#part 13
let total_area (shape_stacks: shape stack list) =
  let stack_area acc st = 
    let rec loop acc =
      match st#pop with
      | Some s -> loop (acc +. s#area)
      | None -> acc
    in
      loop acc
  in
    List.fold ~init:0.0 ~f:stack_area shape_stacks;;

#part 14
total_area [(square_stack :> shape stack); (circle_stack :> shape stack)];;

#part 15
type 'a readonly_stack = < pop : 'a option >;;
let total_area (shape_stacks: shape readonly_stack list) =
  let stack_area acc st = 
    let rec loop acc =
      match st#pop with
      | Some s -> loop (acc +. s#area)
      | None -> acc
    in
      loop acc
  in
    List.fold ~init:0.0 ~f:stack_area shape_stacks;;
total_area [(square_stack :> shape readonly_stack); (circle_stack :> shape readonly_stack)];;
