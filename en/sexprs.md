# Data Serialization with S-Expressions

We've already shown you how to parse third-party data formats into
OCaml in earlier chapters.  Sometimes though, you just want to quickly
convert an OCaml type to and from a human-readable and editable form
in your own code, and not worry about interoperability.  Core's
solution to this problem is to use s-expressions.

S-expressions are nested parenthetical expressions whose atomic values
are strings.  They were first popularized by the Lisp programming
language in the 1960s, and have remained one of the simplest and most
effective ways to encode structured data.  There's a full definition
of them available
[online](http://people.csail.mit.edu/rivest/Sexp.txt).

An example s-expression might look like this:

```scheme
(this (is an) (s expression))
```

The OCaml type of an s-expression is quite simple.  Here's what the
type definition for an s-expression would look like.

```ocaml
# module Our_sexp = struct
    type t = Atom of string | List of t list
  end;;
```

An s-expression can be thought of as a tree where each node contains a
list of its children, and where the leaves of the tree are strings.
Here's how we can use this type to represent the our example
s-expression.

```ocaml
# let sexp =
    let a x = Our_sexp.Atom x and l x = Our_sexp.List x in
    l [a "this";l [a "is"; a "an"]; l [a "s"; a "expression"]];;
val sexp : Our_sexp.t =
  Our_sexp.List
   [Our_sexp.Atom "this";
    Our_sexp.List [Our_sexp.Atom "is"; Our_sexp.Atom "an"];
    Our_sexp.List [Our_sexp.Atom "s"; Our_sexp.Atom "expression"]]
```

Core provides good support for `s-expressions` in its `Sexp` module,
including functions for converting s-expressions to and from strings.
If we do the same example above with Core's s-expression type, we'll
see that the output in the top-level is easier to read.

```ocaml
# let sexp =
    let a x = Sexp.Atom x and l x = Sexp.List x in
    l [a "this";l [a "is"; a "an"]; l [a "s"; a "expression"]];;
val sexp : Sexp.t = (this (is an) (s expression))
```

This prints out nicely because Core registers a pretty printer with
the toplevel.  This pretty-printer uses Core's functions for
converting s-expressions to and from strings.

```ocaml
# Sexp.of_string ("(1 2 (3 4))");;
- : Sexp.t = (1 2 (3 4))
# Sexp.to_string (Sexp.List [Sexp.Atom "1"; Sexp.Atom "2"]);;
- : string = "(1 2)"
```

In addition to providing the `Sexp` module, most of the base types in
Core support conversion to and from s-expressions.  For example, we
can write:

```ocaml
# Int.sexp_of_t 3;;
- : Sexp.t = 3
# List.sexp_of_t;;
- : ('a -> Sexp.t) -> 'a List.t -> Sexp.t = <fun>
# List.sexp_of_t Int.sexp_of_t [1;2;3];;
- : Sexp.t = (1 2 3)
```

Notice that `List.sexp_of_t` is polymorphic, and takes as its first
argument another conversion function to handle the elements of the
list to be converted.  Core uses this scheme more generally for
defining sexp-converters for polymorphic types.

But what if you want a function to convert some brand new type to an
s-expression?  You can of course write it yourself manually:

```ocaml
# type t = { foo: int; bar: float };;
type t = { foo : int; bar : float; }
# let sexp_of_t t =
    let a x = Sexp.Atom x and l x = Sexp.List x in
    l [ l [a "foo"; Int.sexp_of_t t.foo  ];
        l [a "bar"; Float.sexp_of_t t.bar]; ]
  ;;
val sexp_of_t : t -> Sexp.t = <fun>
# sexp_of_t { foo = 3; bar = -5.5 };;
- : Sexp.t = ((foo 3) (bar -5.5))
```

This is somewhat tiresome to write, and it gets more so when you
consider the parser, _i.e._, `t_of_sexp`, which is considerably more
complex.  Writing this kind of parsing and printing code by hand is
mechanical and error prone, not to mention a drag.

Given how mechanical the code is, you could imagine writing a program
that inspected the type definition and autogenerated the conversion
code for you.  As it turns out, we can do just that using `sexplib`.
The `sexplib` package, which is included with Core, provides both a
library for manipulating s-expressions and a _syntax extension_ for
generating such conversion functions.  With that syntax extension
enabled, any type that has `with sexp` as an annotation will trigger
the generation of the functions we want for free.

```ocaml
# type t = { foo: int; bar: float } with sexp;;
type t = { foo : int; bar : float; }
val t_of_sexp : Sexp.t -> t = <fun>
val sexp_of_t : t -> Sexp.t = <fun>
# t_of_sexp (Sexp.of_string "((bar 35) (foo 3))");;
- : t = {foo = 3; bar = 35.}
```

The `sexplib` syntax extension sees the `with sexp` annotation
replaces it with the definition of the conversions functions we see
above.

The syntax extension can be used outside of type declarations as
well.  As discussed in [xref](#error-handling), `with sexp` can be
attached to the declaration of an exception, which will improve the
ability of Core to generate a useful string representation of an
exception.

```ocaml
# exception Bad_message of string list;;
exception Bad_message of string list
# Exn.to_string (Bad_message ["1";"2";"3"]) |> print_endline;;
- : string = "Bad_message(_)"
# exception Good_message of string list with sexp;;
exception Good_message of string list
# Exn.to_string (Good_message ["1";"2";"3"]) |> print_endline;;
(//toplevel//.Good_message (1 2 3))
```

Unsurprisingly, the string `//toplevel//` only gets printed out as
part of the module path within a toplevel like `utop`.

You don't always have to declare a named type to create an
s-expression converter.  The following syntax lets you create one
inline, as part of a larger expression.

```ocaml
# let l = [(1,"one"); (2,"two")];;
val l : (int * string) list = [(1, "one"); (2, "two")]
# List.iter l ~f:(fun x ->
    <:sexp_of<int * string>> x
    |> Sexp.to_string |> print_endline);;
(1 one)
(2 two)
- : unit = ()
```

The declaration `<:sexp_of<int * string>>` simply gets expended to the
sexp-converter for the type `int * string`.  This is useful whever you
need a sexp-converter for an anonymous type.

The syntax extensions bundled with Core almost all have the same basic
structure: they autogenerate code based on type definitions,
implementing functionality that you could in theory have implemented
by hand, but with far less programmer effort.

<note> <title>Syntax extensions, `camlp4` and `type_conv`</title>

OCaml doesn't directly support generating code from type definitions.
Instead, it supplies a powerful syntax extension mechanism known as
`camlp4`, which lets you extend the grammar of the language.  In the
case of `sexplib`, `camlp4` is used to create s-expression conversion
functions.  `camlp4` is well integrated into the OCaml toolchain, and
can be activated within the toplevel and also included in compilation
using the `-pp` compiler flag.

`sexplib` is part of a family of syntax extensions, including
`comparelib`, described in [xref](#maps-and-hashtables), and
`fieldslib`, described in [xref](#records), that generate code based
on type declarations, and are all based on a common library called
`type_conv`.  This library provides a common language for annotating
types (_e.g._, using the `with` notation) and utilities for working
with type definitions.  If you want to build your own type-driven
syntax extension, you should consider basing it on `type_conv`.

</note>

## The Sexp format

The textual representation of s-expressions is pretty
straightforward. An s-expression is written down as a nested
parenthetical expression, with whitespace-separated strings as the
atoms.  Quotes are used for atoms that contain parentheses or spaces
themselves; backslash is the escape character; and semicolons are used
to introduce single-line comments.  Thus, the following file,
`example.scm`:

```scheme
;; example.scm

((foo 3.3) ;; This is a comment
 (bar "this is () an \" atom"))
```

can be loaded using sexplib.  As you can see, the commented data is
not part of the resulting s-expression.

```ocaml
# Sexp.load_sexp "example.scm";;
- : Core.Std.Sexp.t = ((foo 3.3) (bar "this is () an \" atom"))
```

All in, the s-expression format actually supports three comment
syntaxes:

- `;`, which comments out everything to the end of a line
- `#|` and `|#`, which are delimiters for commenting out a block
- `#;`, which comments out the first complete s-expression that follows.

The following example shows all of these in action.

```scheme
;; comment_heavy_example.scm
((this is included)
 ; (this is commented out
 (this stays)
 #; (all of this is commented
     out (even though it crosses lines.))
  (and #| block delimiters #| which can be nested #|
     will comment out
    an arbitrary multi-line block))) |#
   now we're done
   ))
```

Again, loading the file as an s-expression drops the comments.

```ocaml
# Sexp.load_sexp "comment_heavy_example.scm";;
- : Core.Std.Sexp.t = ((this is included) (this stays) (and now we're done))
```

Note that the comments were dropped from the file upon reading.  This
is expected, since there's no place in the `Sexp.t` type to store
comments.

If we introduce an error into our s-expression, by, say, deleting the
open-paren in front of `bar`, we'll get a parse error:

```ocaml
# Exn.handle_uncaught ~exit:false (fun () ->
    ignore (Sexp.load_sexp "example.scm"));;
  Uncaught exception:

  (Sexplib.Sexp.Parse_error
   ((location parse) (err_msg "unexpected character: ')'") (text_line 4)
    (text_char 29) (global_offset 94) (buf_pos 94)))
```

In the above, we use `Exn.handle_uncaught` to make sure that the
exception gets printed out in full detail.  You should generally wrap
every Core program in this handler to get good error messages for any
unexpected exceptions.

## Sexp converters

The most important functionality provided by Sexplib is the
auto-generation of converters for new types.  We've seen a bit of how
this works already, but let's walk through a complete example.  Here's
the source for the beginning of a library for representing integer
intervals.

```ocaml
(* file: int_interval.ml *)
(* Module for representing closed integer intervals *)

open Core.Std

(* Invariant: For any Range (x,y), y >= x *)
type t = | Range of int * int
         | Empty
with sexp

let is_empty = function Empty -> true | Range _ -> false
let create x y = if x > y then Empty else Range (x,y)
let contains i x = match i with
   | Empty -> false
   | Range (low,high) -> x >= low && x <= high
```

We can now use this module as follows:

```ocaml
(* file: test_interval.ml *)

open Core.Std

let intervals =
  let module I = Int_interval in
  [ I.create 3 4;
    I.create 5 4; (* should be empty *)
    I.create 2 3;
    I.create 1 6;
  ]

let () =
  intervals
  |> List.sexp_of_t Int_interval.sexp_of_t
  |> Sexp.to_string_hum
  |> print_endline
```

But we're still missing something: we haven't created an `mli`
signature for `Int_interval` yet.  Note that we need to explicitly
export the s-expression converters that were created within the ml.
If we don't:

```ocaml
(* file: int_interval.mli *)
(* Module for representing closed integer intervals *)

type t

val is_empty : t -> bool
val create : int -> int -> t
val contains : t -> int -> bool
```

then we'll get the following error:

```
File "test_interval.ml", line 15, characters 20-42:
Error: Unbound value Int_interval.sexp_of_t
Command exited with code 2.
```

We could export the types by hand in the signature:

```ocaml
type t
val sexp_of_t : Sexp.t -> t
val t_of_sexp : t -> Sexp.t
```

But Sexplib has a shorthand for this as well, so that we can just use
the same `with` shorthand in the `mli`.

```ocaml
type t with sexp
```

at which point `test_interval.ml` will compile again, and if we run
it, we'll get the following output:

```
$ ./test_interval.native
((Range 3 4) Empty (Range 2 3) (Range 1 6))
```

### Preserving invariants

One easy mistake to make when dealing with sexp converters is to
ignore the fact that those converters can violate the invariants of
your code.  For example, the `Int_interval` module depends for the
correctness of the `is_empty` check on the fact that for any value
`Range (x,y)`, `y` is greater than or equal to `x`.  The `create`
function preserves this invariant, but the `t_of_sexp` function does
not.

We can fix this problem by overriding the autogenerated function and
writing a custom sexp-converter that is based on the autogenerated
converter.

```ocaml
type t = | Range of int * int
         | Empty
with sexp

let create x y = if x > y then Empty else Range (x,y)

let t_of_sexp sexp =
  let t = t_of_sexp sexp in
  begin match t with
  | Empty -> ()
  | Range (x,y) ->
    if y < x then of_sexp_error "Upper and lower bound of Range swapped" sexp
  end;
  t
```

This trick of overriding an existing function definition with a new
one is perfectly acceptable in OCaml.  Function definitions are only
recursive if the `rec` keyword is specified, and so in this case the
inner `t_of_sexp` call will go to the earlier autogenerated definition
that resulted from the `type t with sexp` definition.

We call the function `of_sexp_error` to raise an exception because
that improves the error reporting that Sexplib can provide when a
conversion fails.

### Getting good error messages

There are two steps to deserializing a type from an s-expression:
first, converting the bytes in a file to an s-expression, and the
second, converting that s-expression into the type in question.  One
problem with this is that it can be hard to localize errors to the
right place using this scheme.  Consider the following example:

```ocaml
(* file: read_foo.ml *)

open Core.Std

type t = { a: string; b: int; c: float option } with sexp

let run () =
  let t =
    Sexp.load_sexp "example.scm"
    |> t_of_sexp
  in
  printf "b is: %d\n%!" t.b

let () =
  Exn.handle_uncaught ~exit:true run
```

If you were to run this on a malformatted file, say, this one:

```
;; example.scm
((a not-an-integer)
 (b not-an-integer)
 (c ()))
```

you'll get the following error:

```
read_foo $ ./read_foo.native
Uncaught exception:

  (Sexplib.Conv.Of_sexp_error
   (Failure "int_of_sexp: (Failure int_of_string)") not-an-integer)
```

If all you have is the error message and the string, it's not terribly
informative.  In particular, you know that the parsing error-ed out on
the atom "not-an-integer", but you don't know which one!  In a large
file, this kind of bad error message can be pure misery.

But there's hope!  If we make small change to the `run` function as
follows:

```ocaml
let run () =
  let t = Sexp.load_sexp_conv_exn "example.scm" t_of_sexp in
  printf "b is: %d\n%!" t.b
```

and run it again, we'll get the following much more helpful error
message:

```ocaml
read_foo $ ./read_foo.native
Uncaught exception:

  (Sexplib.Conv.Of_sexp_error
   (Sexplib.Sexp.Annotated.Conv_exn example.scm:3:4
    (Failure "int_of_sexp: (Failure int_of_string)"))
   not-an-integer)
```

In the above error, "example.scm:3:4" tells us that the error occurred on
"example.scm", line 3, character 4, which is a much better start for
figuring out what has gone wrong.

## Sexp-conversion directives

Sexplib supports a collection of directives for modifying the default
behavior of the autogenerated sexp-converters.  These directives allow
you to customize the way in which types are represented as
s-expressions without having to write a custom parser.

### `sexp_opaque`

The most commonly used directive is `sexp_opaque`, whose purpose is to
mark a given component of a type as being unconvertible.  Anything
marked with `sexp_opaque` will be presented as the atom `<opaque>` by
the to-sexp converter, and will trigger an exception from the
from-sexp converter.

Note that the type of a component marked as opaque doesn't need to have a
sexp-converter defined.  Here, if we define a type without a sexp-converter,
and then try to use another type with a sexp-converter, we'll error out:

```ocaml
# type no_converter = int * int;;
type no_converter = int * int
# type t = { a: no_converter; b: string } with sexp;;
Characters 14-26:
  type t = { a: no_converter; b: string } with sexp;;
                ^^^^^^^^^^^^
Error: Unbound value no_converter_of_sexp
```

But with `sexp_opaque`, we won't:

```ocaml
# type t = { a: no_converter sexp_opaque; b: string } with sexp;;
type t = { a : no_converter; b : string; }
val t_of_sexp : Sexp.t -> t = <fun>
val sexp_of_t : t -> Sexp.t = <fun>
```

And if we now convert a value of this type to an s-expression, we'll
see the contents of field `a` marked as opaque:

```ocaml
# sexp_of_t { a = (3,4); b = "foo" };;
- : Sexp.t = ((a <opaque>) (b foo))
```

Note that the `t_of_sexp` function for an opaque type is generated,
but will fail at runtime if it is used.

```ocaml
# t_of_sexp (Sexp.of_string "((a whatever) (b foo))");;
Exception:
(Sexplib.Conv.Of_sexp_error
 (Failure "opaque_of_sexp: cannot convert opaque values") whatever).
```

This is there to allow for s-expression converters to be created for
types containing `sexp_opaque` values, and the resulting converters
won't necessarily fail.  For example, if we made the field containing
a `no_converter` a list, the `t_of_sexp` function could still succeed
when that list was empty, as shown below.

```ocaml
# type t = { a: no_converter sexp_opaque list; b: string } with sexp;;
type t = { a : no_converter list; b : string; }
val t_of_sexp : Sexp.t -> t = <fun>
val sexp_of_t : t -> Sexp.t = <fun>
# t_of_sexp (Sexp.of_string "((a ()) (b foo))");;
- : t = {a = []; b = "foo"}
```

If you really only want to generate one direction of converter, one
can do this by annotating the type with `with sexp_of` or `with
of_sexp`, as shown below.

```ocaml
# type t = { a: no_converter sexp_opaque; b: string } with sexp_of;;
type t = { a : no_converter; b : string; }
val sexp_of_t : t -> Sexp.t = <fun>
# type t = { a: no_converter sexp_opaque; b: string } with of_sexp;;
type t = { a : no_converter; b : string; }
val t_of_sexp : Sexp.t -> t = <fun>
``

### `sexp_list`

Sometimes, sexp-converters have more parentheses than one would
ideally like.  Consider, for example, the following variant type:

```ocaml
# type compatible_versions = | Specific of string list
                             | All
  with sexp;;
# sexp_of_compatible_versions (Specific ["3.12.0"; "3.12.1"; "3.13.0"]);;
- : Sexp.t = (Specific (3.12.0 3.12.1 3.13.0))
```

You might prefer to make the syntax a bit less parenthesis-laden by
dropping the parentheses around the list.  `sexp_list` gives us this
alternate syntax:

```ocaml
# type compatible_versions = | Specific of string sexp_list
                             | All
  with sexp;;
# sexp_of_compatible_versions (Specific ["3.12.0"; "3.12.1"; "3.13.0"]);;
- : Sexp.t = (Specific 3.12.0 3.12.1 3.13.0)
```

### `sexp_option`

Another common directive is `sexp_option`, which is used to to make a
record field optional in the s-expression.  Normally, optional values
are represented either as `()` for `None`, or as `(x)` for `Some x`,
and a record field containing an option would be rendered accordingly.
For example:

```ocaml
# type t = { a: int option; b: string } with sexp;;
# sexp_of_t { a = None; b = "hello" };;
- : Sexp.t = ((a ()) (b hello))
# sexp_of_t { a = Some 3; b = "hello" };;
- : Sexp.t = ((a (3)) (b hello))
```

But what if we want a field to be optional, _i.e._, we want to allow
it to be omitted from the record entirely?  In that case, we can mark
it with `sexp_option`:

```ocaml
# type t = { a: int sexp_option; b: string } with sexp;;
# sexp_of_t { a = Some 3; b = "hello" };;
- : Sexp.t = ((a 3) (b hello))
# sexp_of_t { a = None; b = "hello" };;
- : Sexp.t = ((b hello))
```

### Specifying defaults

The `sexp_option` declaration is really just an example of how one
might want to deal with default values.  With `sexp_option`, your type
on the OCaml side is an option, with `None` representing the case
where no value is provided.  But you might want to allow other ways of
filling in default values.

Consider the following type which represents the configuration of a
very simple web-server.

```ocaml
# type http_server_config = {
     web_root: string;
     port: int;
     addr: string;
  } with sexp;;
```

One could imagine making some of these parameters optional; in particular, by
default, we might want the web server to bind to port 80, and to listen as
localhost.  The sexp-syntax allows this as follows.

```ocaml
# type http_server_config = {
     web_root: string;
     port: int with default(80);
     addr: string with default("localhost");
  } with sexp;;
```

The top-level will echo back the type you just defined as usual, but also
generate some additional conversion functions.

```ocaml
type http_server_config = { web_root : string; port : int; addr : string; }
val http_server_config_of_sexp__ : Sexplib.Sexp.t -> http_server_config =
  <fun>
val http_server_config_of_sexp : Sexplib.Sexp.t -> http_server_config = <fun>
val sexp_of_http_server_config : http_server_config -> Sexplib.Sexp.t = <fun>
```

These new functions let you convert to and from s-expressions.

```ocaml
# http_server_config_of_sexp (Sexp.of_string "((web_root /var/www/html))";;
# let cfg =
  http_server_config_of_sexp (Sexp.of_string "((web_root /var/www/html))");;
val cfg : http_server_config =
  {web_root = "/var/www/html"; port = 80; addr = "localhost"}
```

When we convert the configuration back out to an s-expression, you'll notice
that no data is dropped.

```ocaml
# sexp_of_http_server_config cfg;;
- : Sexplib.Sexp.t = ((web_root /var/www/html) (port 80) (addr localhost))
```

We could make the generated s-expression also drop exported values, by
using the `sexp_drop_default` directive.

```ocaml
# type http_server_config = {
     web_root: string;
     port: int with default(80), sexp_drop_default;
     addr: string with default("localhost"), sexp_drop_default;
  } with sexp;;
type http_server_config = { web_root : string; port : int; addr : string; }
val http_server_config_of_sexp__ : Sexplib.Sexp.t -> http_server_config =
  <fun>
val http_server_config_of_sexp : Sexplib.Sexp.t -> http_server_config = <fun>
val sexp_of_http_server_config : http_server_config -> Sexplib.Sexp.t = <fun>
# let cfg = http_server_config_of_sexp (Sexp.of_string "((web_root /var/www/html))");;
val cfg : http_server_config =
  {web_root = "/var/www/html"; port = 80; addr = "localhost"}
# sexp_of_http_server_config cfg;;
- : Sexplib.Sexp.t = ((web_root /var/www/html))
```

As you can see, the fields that are at their default values are simply
omitted from the s-expression.  On the other hand, if we convert a
config with other values, then those values will be included in the
s-expression.

```ocaml
# sexp_of_http_server_config { cfg with port = 8080 };;
- : Sexplib.Sexp.t = ((web_root /var/www/html) (port 8080))
# sexp_of_http_server_config { cfg with port = 8080; addr = "192.168.0.1" };;
- : Sexplib.Sexp.t =
((web_root /var/www/html) (port 8080) (addr 192.168.0.1))
```

This can be very useful in designing config file formats that are both
reasonably terse and easy to generate and maintain.  It can also be
useful for backwards compatibility: if you add a new field to your
config record, but you make that field optional, then you should still
be able to parse older version of your config.
