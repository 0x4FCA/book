<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE book PUBLIC '-//OASIS//DTD DocBook XML V4.5//EN' 'http://www.oasis-open.org/docbook/xml/4.5/docbookx.dtd'>
<book>
  
  
  
  
  

<title>Real World OCaml</title>
<xi:include href="bookinfo.xml" xmlns:xi="http://www.w3.org/2001/XInclude"/>
<xi:include href="ch00.xml" xmlns:xi="http://www.w3.org/2001/XInclude"/>



























<preface id="prologue">
  <title>Prologue</title>
  <sect1 id="why-ocaml">
    <title>Why OCaml?</title>
    <para>
      The programming languages that you use affect the software you
      create. They influence your software's reliability, security and
      efficiency, and how easy it is to read, refactor, and extend. And
      the languages you know can also deeply affect how you think about
      programming and software design.
    </para>
    <para>
      But not all ideas about how to design a programming language are
      created equal. Over the last 40 years, a few key language features
      have emerged that together form a kind of sweet-spot in language
      design. These features include:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          <emphasis>Garbage collection</emphasis> for automatic memory
          management, now a feature of almost every modern high-level
          language.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis>Higher-order functions</emphasis> that can be passed
          around as first-class values, as seen in Javascript or Scala.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis>Static type-checking</emphasis> to reduce run-time
          errors, such as Java class interfaces or Objective-C methods.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis>Generics</emphasis> to enable abstractions to be
          constructed across different datatypes, available in Java and
          .NET.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis>Immutable data structures</emphasis> that cannot be
          destructively updated, famously enforced in Haskell but also a
          common feature of many distributed big data frameworks.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis>Algebraic datatypes</emphasis> and <emphasis>pattern
          matching</emphasis> to define and manipulate complex data
          structures, available in Miranda, F# and Standard ML.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis>Automatic type inference</emphasis> to avoid having
          to laboriously define the type of every single variable in a
          program, and instead have them inferred based on how a value
          is used. Available in Standard ML, F# and even modern C++11
          via its <literal>auto</literal> keyword.
        </para>
      </listitem>
    </itemizedlist>
    <para>
      Some of you will know and love these features, and others will be
      completely new to them. Most of you will have seen
      <emphasis>some</emphasis> of them in other languages that you've
      used. As we'll demonstrate over the course of this book, it turns
      out that there is something transformative about having them all
      together and able to interact in a single language. Despite their
      importance, these ideas have made only limited inroads into
      mainstream languages. And when they do arrive there, like
      higher-order functions in C# or parametric polymorphism in Java,
      it's typically in a limited and awkward form. The only languages
      that support these ideas well are statically-typed functional
      programming languages like OCaml, F#, Haskell, Scala and Standard
      ML.
    </para>
    <para>
      Among this worthy set of languages, OCaml stands apart because it
      manages to provide a great deal of power while remaining highly
      pragmatic, highly performant, and comparatively simple to use and
      understand. It is this that makes OCaml a great choice for
      programmers who want to step up to a better programming language,
      and at the same time want to get practical work done.
    </para>
    <sect2 id="a-brief-history-from-the-1960s">
      <title>A brief history from the 1960s</title>
      <para>
        OCaml was written in 1996 by Xavier Leroy, Jérôme Vouillon,
        Damien Doligez and Didier Rémy at INRIA in France. It was
        inspired by a long line of research into ML starting in the
        1960s, and continues to have deep links to the academic
        community.
      </para>
      <para>
        ML was originally the <emphasis>meta language</emphasis> of the
        LCF proof assistant released by Robin Milner in 1972 (at
        Stanford, and later at Cambridge). ML was turned into a compiler
        in order to make it easier to use LCF on different machines, and
        gradually turned into a fully fledged system of its own by the
        1980s.
      </para>
      <para>
        In 1990, Xavier Leroy and Damien Doligez built a new
        implementation called Caml Light that was based on a bytecode
        interpreter with a fast sequential garbage collector. Over the
        next few years useful libraries appeared, such as Michel Mauny's
        parsing system, and performance further improved with a fast
        native code compiler that made OCaml's performance competitive
        with mainstream languages such as C++. A module system inspired
        by Standard ML also provided powerful facilities for abstraction
        and larger scale programs.
      </para>
      <para>
        The modern OCaml emerged in 1996, when a powerful and elegant
        object system was implemented by Didier Rémy and Jérôme
        Vouillon. This object system was notable for supporting many
        common OO idioms in a statically type-safe way, whereas the same
        idioms required runtime checks in languages such as C++ or Java.
        In 2000, Jacques Garrique extended OCaml with several new
        features such as polymorphic methods and variants and labelled
        and optional arguments.
      </para>
      <para>
        The last decade has seen OCaml attract a significant user base,
        and language improvements have been steadily added to support
        the growing codebases that use the language both commercially
        and for academic use. First-class modules, GADTs and dynamic
        linking have improved the flexibility of the language, and there
        is fast native code support for x86_64, ARM, PowerPC and
        Sparc64, making OCaml a good choice for systems where resource
        usage, predictability and performance matters.
      </para>
    </sect2>
    <sect2 id="the-core-standard-library">
      <title>The Core Standard Library</title>
      <para>
        A language on its own isn't enough. You also need a rich set of
        libraries to base your applications on. A common source of
        frustration for those learning OCaml is that the standard
        library that ships with the OCaml compiler is not ideal. While
        it's well implemented, it is really intended for use within the
        compiler itself, and covers only a small subset of the
        functionality you expect for more general-purpose use.
      </para>
      <para>
        But all is not lost! There is an effective alternative to the
        OCaml standard library called Core. Jane Street, a company that
        has been using OCaml for more than a decade, developed Core for
        its own internal use, but it was designed from the start with an
        eye towards being a general-purpose standard library, and has
        very broad applicability. Core is also engineered with
        correctness, reliability and performance very much in mind.
      </para>
      <para>
        Core is also distributed with syntax extensions which provide
        useful new functionality to OCaml, and there are additional
        libraries such as the Async network communications library that
        extend the reach of Core into building complex distributed
        systems. All of these libraries are distributed under a liberal
        Apache-style license.
      </para>
      <para>
        If you've learnt some OCaml before, this book may surprise you
        with some differences from your past experience. The Core
        standard library redefines most of the standard modules to be
        much more consistent, and so you'll need to adapt older code. We
        believe the Core model is worth learning; it's been successfully
        used on large, million-line codebases, and removes a big barrier
        to more widespread OCaml adoption. There will always exist code
        that uses only the compiler standard library of course, but
        there are other online resources available to learn that. Real
        World OCaml focuses on the techniques the authors have used in
        their personal experience to construct scalable, robust computer
        systems.
      </para>
    </sect2>
    <sect2 id="the-ocaml-platform">
      <title>The OCaml Platform</title>
      <para>
        Core is comprehensive and effective standard library, but
        there's a lot more out there than Core. A large community of
        programmers have been using OCaml since its first release in
        1996 and have generated a lot of useful libraries and tools. In
        Real World OCaml, we'll introduce some of these libraries for
        you to experiment with realistic examples. The installation and
        management of these third-party libraries is made much easier
        via a package management tool known as OPAM. We'll explain more
        about OPAM as the book unfolds, but it forms the basis of the
        Platform, which is a set of tools and libraries that, along with
        the OCaml compiler, let you build realistic applications quickly
        and effectively.
      </para>
      <para>
        Another big improvement over the standard library is the
        <literal>utop</literal> interactive top level. This is a modern
        interactive tool that supports command history, macro expansion,
        module completion, and other niceties that make it much more
        pleasant to work with the language. We'll be using
        <literal>utop</literal> throughout the book instead of the
        normal OCaml toplevel. It can, of course, be installed using
        OPAM, and <xref linkend="installation"/> guides you
        through that process.
      </para>
    </sect2>
  </sect1>
  <sect1 id="about-this-book">
    <title>About this book</title>
    <para>
      Real World OCaml is aimed at programmers who have some experience
      with conventional programming languages, but not specifically with
      <emphasis>statically-typed functional programming</emphasis>. The
      world of dynamic scripting languages such as Javascript, Ruby and
      Python have all adopted healthy elements of functional
      programming, but not all of it. Real World OCaml takes you through
      the full lifecycle of how to construct software with static
      typing, including the powerful module system that makes code
      re-use so much more robust.
    </para>
    <para>
      At the same time, OCaml is not Haskell. It takes a much more
      pragmatic approach by being strictly evaluated by default and
      permitting arbitrary side-effects. In fact, you can write OCaml
      code that looks very similar to imperative C but remains
      completely type-safe. One of the major strengths of OCaml for
      systems programming is that, with some experience, you can predict
      the runtime behaviour of a block of code very easily, with very
      little compiler magic involved. We'll explain some of these tricks
      to you as we go through the book and gradually introduce more
      complex concepts.
    </para>
    <sect2 id="what-to-expect">
      <title>What to expect</title>
      <para>
        Real World OCaml is split into three parts and appendices:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            Part I covers the basic concepts you'll need to know when
            building OCaml programs. You won't need to memorise all of
            this (objects, for example, are used rarely in practice),
            but understanding the concepts and examples is important.
            This part opens up with a guided tour to give you a quick
            overview of the language. It then moves onto modules,
            functors and objects, which may take some time to digest.
            Persevere though; even though these concepts may be
            difficult at first, they will put you in good stead even
            when switching to other languages, many of which have drawn
            inspiration from ML.
          </para>
        </listitem>
        <listitem>
          <para>
            Part II builds on the basics by working through complete
            examples. Here you'll pick up useful techniques for building
            networked systems, as well as functional design patterns
            that help combine different features of the language to good
            effect. The focus throughout this section is on networked
            systems, and among other examples we'll build a running
            example that will perform Internet queries using the
            DuckDuckGo search engine.
          </para>
        </listitem>
        <listitem>
          <para>
            Part III is all about understanding the runtime system in
            OCaml. It's a remarkably simple system in comparison to
            other language runtimes (such as Java or the .NET CLR), and
            you'll need to read this to build very high performance
            systems that have to minimise resource usage or interface to
            C libraries. This is also where we talk about profiling and
            debugging techniques using tools such as GNU
            <literal>gdb</literal> and <literal>gprof</literal>.
            Contributing your code back to the community is also
            important (if only to get bug fixes from other people!), and
            this part also explains how to do this via OPAM and Github.
          </para>
        </listitem>
      </itemizedlist>
      <note>
      <title>
      Note to reviewers
      </title>
      <para>
        Real World OCaml uses some tools that we've developed while
        writing this book. Some of these resulted in improvements to the
        OCaml compiler, which means that you will need to ensure that
        you have an up-to-date development environment (using the 4.01.0
        compiler). We've automated everything you need via the OPAM
        package manager, so please do follow the installation
        instructions in <xref linkend="installation"/>
        carefully.
      </para>
      <para>
        At this stage, the Windows operating system is also unsupported,
        and only Mac OS X, Linux, FreeBSD and OpenBSD can be expected to
        work reliably. We realize this is a concern; there are no
        fundamental barriers to Windows support, but we're focussed on
        getting the main content finished before getting stuck into the
        porting effort.
      </para>
      </note>
    </sect2>
  </sect1>
  <sect1 id="about-the-authors">
    <title>About the Authors</title>
    <sect2 id="jason-hickey">
      <title>Jason Hickey</title>
      <para>
        Jason Hickey is a Software Engineer at Google Inc. in Mountain
        View, California. He is part of the team that designs and
        develops the global computing infrastructure used to support
        Google services, including the software systems for managing and
        scheduling massively distributed computing resources.
      </para>
      <para>
        Prior to joining Google, Jason was an Assistant Professor of
        Computer Science at Caltech, where his research was in reliable
        and fault-tolerant computing systems, including programming
        language design, formal methods, compilers, and new models of
        distributed computation. He obtained his PhD in Computer Science
        from Cornell University, where he studied programming languages.
        He is the author of the MetaPRL system, a logical framework for
        design and analysis of large software systems; and OMake, an
        advanced build system for large software projects. He is the
        author of the textbook, <emphasis>An Introduction to Objective
        Caml</emphasis> (unpublished).
      </para>
    </sect2>
    <sect2 id="anil-madhavapeddy">
      <title>Anil Madhavapeddy</title>
      <para>
        Anil Madhavapeddy is a Senior Research Fellow at the University
        of Cambridge, based in the Systems Research Group. He was on the
        original team that developed the Xen hypervisor, and helped
        develop an industry-leading cloud management toolstack written
        entirely in OCaml. This XenServer product has been deployed on
        millions of physical hosts, and drives critical infrastructure
        for many Fortune 500 companies.
      </para>
      <para>
        Prior to obtaining his PhD in 2006 from the University of
        Cambridge, Anil had a diverse background in industry at NetApp,
        NASA and Internet Vision. He is an active member of the
        open-source development community with the OpenBSD operating
        system, is on the steering committee of the Commercial Uses of
        Functional Programming ACM workshop, and serves on the boards of
        startup companies where OCaml is extensively used. He has also
        developed the Mirage unikernel system that is written entirely
        in OCaml from the device drivers up.
      </para>
    </sect2>
    <sect2 id="yaron-minsky">
      <title>Yaron Minsky</title>
      <para>
        Yaron Minsky heads the Technology group at Jane Street, a
        proprietary trading firm that is the largest industrial user of
        OCaml. He was responsible for introducing OCaml to the company
        and for managing the company's transition to using OCaml for all
        of its core infrastructure. Today, billions of dollars worth of
        securities transactions flow each day through those systems.
      </para>
      <para>
        Yaron obtained his PhD in Computer Science from Cornell
        University, where he studied distributed systems. Yaron has
        lectured, blogged and written about OCaml for years, with
        articles published in Communications of the ACM and the Journal
        of Functional Programming. He chairs the steering committee of
        the Commercial Users of Functional Programming, and is a member
        of the steering committee for the International Conference on
        Functional Programming.
      </para>
    </sect2>
  </sect1>
</preface><part label="I"><title>Basic Concepts</title><chapter id="a-guided-tour">
  <title>A Guided Tour</title>
  <para>
    This chapter gives an overview of OCaml by walking through a series
    of small examples that cover most of the major features of the
    language. This should give a sense of what OCaml can do, without
    getting too deep in any one topic.
  </para>
  <para>
    We'll present this guided tour using the Core standard library and
    the <literal>utop</literal> OCaml toplevel, a shell that lets you
    type in expressions and evaluate them interactively.
    <literal>utop</literal> is an easier-to-use version of the standard
    toplevel (which you can start by typing <literal>ocaml</literal> at
    the command line). These instructions will assume you're using
    <literal>utop</literal> specifically.
  </para>
  <para>
    Before getting started, do make sure you have a working OCaml
    installation and toplevel as you read through this chapter so you
    can try out the examples.
  </para>
  <note>
  <title>
  Installing <literal>utop</literal>
  </title>
  <para>
    The easiest way to get the examples running is to set up the OPAM
    package manager, which is explained in
    <xref linkend="installation"/>. In a nutshell, you need to
    have a working C compilation environment and the PCRE library
    installed, and then:
  </para>
  <programlisting>
$ opam init
$ opam switch 4.01.0dev+trunk
$ opam install utop core_extended
$ eval `opam config -env`
</programlisting>
  <para>
    Note that the above commands will take some time to run. When
    they're done, create a file called <literal>~/.ocamlinit</literal>
    in your home directory:
  </para>
  <programlisting language="ocaml">
#use &quot;topfind&quot;
#camlp4o
#thread
#require &quot;core.top&quot;
</programlisting>
  <para>
    Then type in <literal>utop</literal>, and you'll be in an
    interactive toplevel environment. OCaml phrases are only evaluated
    when you enter a double semicolon (<literal>;;</literal>), so you
    can split your typing over multiple lines. You can exit
    <literal>utop</literal> by pressing <literal>control-D</literal> and
    return. For complete instructions, please refer to
    <xref linkend="installation"/>.
  </para>
  </note>
  <sect1 id="ocaml-as-a-calculator">
    <title>OCaml as a calculator</title>
    <para>
      Let's spin up <literal>utop</literal>. Throughout the book we're
      going to use Core, a more full-featured and capable replacement
      for OCaml's standard library. Accordingly, we'll start by opening
      the <literal>Core.Std</literal> module to get access to Core's
      libraries. If you don't open <literal>Core.Std</literal> many of
      the examples below will fail.
    </para>
    <programlisting language="ocaml">
$ utop
# open Core.Std;;
</programlisting>
    <para>
      Now that we have Core open, let's try a few simple numerical
      calculations.
    </para>
    <programlisting language="ocaml">
# 3 + 4;;
- : int = 7
# 8 / 3;;
- : int = 2
# 3.5 +. 6.;;
- : float = 9.5
# sqrt 9.;;
- : float = 3.
</programlisting>
    <para>
      By and large, this is pretty similar to what you'd find in any
      programming language, but there are a few things that jump right
      out at you.
    </para>
    <itemizedlist>
      <listitem>
        <para>
          We needed to type <literal>;;</literal> in order to tell the
          toplevel that it should evaluate an expression. This is a
          peculiarity of the toplevel that is not required in
          stand-alone programs (though it is sometimes helpful to
          include <literal>;;</literal> to improve OCaml's error
          reporting.)
        </para>
      </listitem>
      <listitem>
        <para>
          After evaluating an expression, the toplevel spits out both
          the type of the result and the result itself.
        </para>
      </listitem>
      <listitem>
        <para>
          Function arguments are separated by spaces, instead of by
          parenthesis and commas, which is more like the UNIX shell than
          C or Java.
        </para>
      </listitem>
      <listitem>
        <para>
          OCaml carefully distinguishes between
          <literal>float</literal>, the type for floating point numbers
          and <literal>int</literal> the type for integers. The types
          have different literals (<literal>6.</literal> instead of
          <literal>6</literal>) and different infix operators
          (<literal>+.</literal> instead of <literal>+</literal>), and
          OCaml doesn't automatically cast between types. This can be a
          bit of a nuisance, but it has its benefits, since it prevents
          some kinds of bugs that arise in other languages due to
          unexpected differences between the behavior of
          <literal>int</literal> and <literal>float</literal>.
        </para>
      </listitem>
    </itemizedlist>
    <para>
      We can also create a variable to name the value of a given
      expression, using the <literal>let</literal> keyword (also known
      as a <emphasis>let binding</emphasis>).
    </para>
    <programlisting language="ocaml">
# let x = 3 + 4;;
val x : int = 7
# let y = x + x;;
val y : int = 14
</programlisting>
    <para>
      After a new variable is created, the toplevel tells us the name of
      the variable (<literal>x</literal> or <literal>y</literal>), in
      addition to its type (<literal>int</literal>) and value
      (<literal>7</literal> or <literal>14</literal>).
    </para>
  </sect1>
  <sect1 id="functions-and-type-inference">
    <title>Functions and Type Inference</title>
    <para>
      The <literal>let</literal> syntax can also be used for creating
      functions.
    </para>
    <programlisting language="ocaml">
# let square x = x * x ;;
val square : int -&gt; int = &lt;fun&gt;
# square 2;;
- : int = 4
# square (square 2);;
- : int = 16
</programlisting>
    <para>
      Functions in OCaml are values like any other, which is why we bind
      one to a variable using the same <literal>let</literal> keyword
      used for binding a variable to a simple value such as an integer.
    </para>
    <para>
      When using <literal>let</literal> to define a function, the first
      identifier after the <literal>let</literal> is the function name,
      and each subsequent identifier is a different argument to the
      function. Thus, <literal>square</literal> is a function with a
      single argument. If no arguments are given, then we just have the
      ordinary definition of a variable that we saw earlier.
    </para>
    <para>
      Now that we're creating more interesting values like functions,
      the types have gotten more interesting too.
      <literal>int -&gt; int</literal> is a function type, in this case
      indicating a function that takes an <literal>int</literal> and
      returns an <literal>int</literal>. We can also write functions
      that take multiple arguments.
    </para>
    <programlisting language="ocaml">
# let ratio x y =
     Float.of_int x /. Float.of_int y
  ;;
val ratio : int -&gt; int -&gt; float = &lt;fun&gt;
# ratio 4 7;;
- : float = 0.571428571428571397
</programlisting>
    <para>
      As a side note, the above is our first use of OCaml modules. Here,
      <literal>FLoat.of_int</literal> refers to the
      <literal>of_int</literal> function contained in the
      <literal>FLoat</literal> module, and not, as you might expect from
      an object-oriented language, accessing a method of an object. The
      <literal>Float</literal> module in particular contains
      <literal>of_int</literal> as well as many other useful functions
      for dealing with floats.
    </para>
    <para>
      The notation for the type-signature of a multi-argument functions
      may be a little surprising at first, but we'll explain where it
      comes from when we get to function currying in
      <xref linkend="multi-argument-functions"/>. For the
      moment, think of the arrows as separating different arguments of
      the function, with the type after the final arrow being the return
      value of the function. Thus,
      <literal>int -&gt; int -&gt; float</literal> describes a function
      that takes two <literal>int</literal> arguments and returns a
      <literal>float</literal>.
    </para>
    <para>
      We can even write functions that take other functions as
      arguments. Here's an example of a function that takes three
      arguments: a test function and two integer arguments. The function
      returns the sum of the integers that pass the test.
    </para>
    <programlisting language="ocaml">
# let sum_if_true test first second =
    (if test first then first else 0)
    + (if test second then second else 0)
  ;;
val sum_if_true : (int -&gt; bool) -&gt; int -&gt; int -&gt; int = &lt;fun&gt;
</programlisting>
    <para>
      If we look at the inferred type signature in detail, we see that
      the first argument is a function that takes an integer and returns
      a boolean, and that the remaining two arguments are integers.
      Here's an example of this function in action.
    </para>
    <programlisting language="ocaml">
# let even x =
    x mod 2 = 0 ;;
val even : int -&gt; bool = &lt;fun&gt;
# sum_if_true even 3 4;;
- : int = 4
# sum_if_true even 2 4;;
- : int = 6
</programlisting>
    <para>
      Note that in the definition of <literal>even</literal> we used
      <literal>=</literal> in two different ways: once as the part of
      the let binding that separates the thing being defined from its
      definition; and once as an equality test, when comparing
      <literal>x mod 2</literal> to <literal>0</literal>. These two uses
      of <literal>=</literal> are basically unrelated.
    </para>
    <sect2 id="type-inference">
      <title>Type inference</title>
      <para>
        As the types we encounter get more complicated, you might ask
        yourself how OCaml is able to figure them out, given that we
        didn't write down any explicit type information.
      </para>
      <para>
        OCaml determines the type of an expression using a technique
        called <emphasis>type inference</emphasis>, by which it infers
        the type of a given expression based on what it already knows
        about the types of other related variables, and on constraints
        on the types that arise from the structure of the code.
      </para>
      <para>
        As an example, let's walk through the process of inferring the
        type of <literal>sum_if_true</literal>.
      </para>
      <itemizedlist>
        <listitem>
          <para>
            OCaml requires that both arms of an <literal>if</literal>
            statement return the same type, so the expression
            <literal>if test x then x else 0</literal> requires that
            <literal>x</literal> must be the same type as
            <literal>0</literal>, which is <literal>int</literal>. By
            the same logic we can conclude that <literal>y</literal> has
            type <literal>int</literal>.
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>test</literal> is passed <literal>x</literal> as an
            argument. Since <literal>x</literal> has type
            <literal>int</literal>, the input type of
            <literal>test</literal> must be <literal>int</literal>.
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>test x</literal> is used as the condition in an
            <literal>if</literal> statement, so the return type of
            <literal>test</literal> must be <literal>bool</literal>.
          </para>
        </listitem>
        <listitem>
          <para>
            The fact that <literal>+</literal> returns an int implies
            that the return value of <literal>sum_if_true</literal> must
            be int.
          </para>
        </listitem>
      </itemizedlist>
      <para>
        Together, that nails down the types of all the variables, which
        determines the overall type of <literal>sum_if_true</literal>.
      </para>
      <para>
        Over time, you'll build a rough intuition for how the OCaml
        inference engine works, which makes it easier to reason through
        your programs. One way of making it easier to understand the
        types is to add explicit type annotations. These annotations
        never change the behavior of an OCaml program, but they can
        serve as useful documentation, as well as catch unintended type
        changes. Here's an annotated version of
        <literal>sum_if_true</literal>:
      </para>
      <programlisting language="ocaml">
# let sum_if_true (test : int -&gt; bool) (x : int) (y : int) : int =
     (if test x then x else 0)
     + (if test y then y else 0)
  ;;
val sum_if_true : (int -&gt; bool) -&gt; int -&gt; int -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        In the above, we've marked every argument to the function with
        its type, with the final annotation indicating the type of the
        return value. Such type annotations can actually go around any
        value in an OCaml program, and can be useful for figuring out
        why a given program is failing to compile.
      </para>
    </sect2>
    <sect2 id="inferring-generic-types">
      <title>Inferring generic types</title>
      <para>
        Sometimes, there isn't enough information to fully determine the
        concrete type of a given value. Consider this function:
      </para>
      <programlisting language="ocaml">
# let first_if_true test x y =
    if test x then x else y
  ;;
</programlisting>
      <para>
        <literal>first_if_true</literal> takes as its arguments a
        function <literal>test</literal>, and two values,
        <literal>x</literal> and <literal>y</literal>, where
        <literal>x</literal> is to be returned if
        <literal>test x</literal> evaluates to <literal>true</literal>,
        and <literal>y</literal> otherwise. So what's the type of
        <literal>first_if_true</literal>? There are no obvious clues
        such as arithmetic operators or literals to tell you what the
        type of <literal>x</literal> and <literal>y</literal> are. That
        makes it seem like one could use this
        <literal>first_if_true</literal> on values of any type. Indeed,
        if we look at the type returned by the toplevel:
      </para>
      <programlisting language="ocaml">
val first_if_true : ('a -&gt; bool) -&gt; 'a -&gt; 'a -&gt; 'a = &lt;fun&gt;
</programlisting>
      <para>
        we see that rather than choose a single concrete type, OCaml has
        introduced a <emphasis>type variable</emphasis>
        <literal>'a</literal> to express that the type is generic. In
        particular, the type of the <literal>test</literal> argument is
        <literal>('a -&gt; bool)</literal>, which means that test is a
        one-argument function whose return value is
        <literal>bool</literal>, and whose argument could be of any type
        <literal>'a</literal>. But, whatever type <literal>'a</literal>
        is, it has to be the same as the type of the other two
        arguments, <literal>x</literal> and <literal>y</literal>, and of
        the return value of <literal>first_if_true</literal>. This kind
        of genericity is called <emphasis>parametric
        polymorphism</emphasis>, and is very similar to generics in C#
        and Java.
      </para>
      <para>
        The generic type of <literal>first_if_true</literal> allows us
        to write:
      </para>
      <programlisting language="ocaml">
# let long_string s = String.length s &gt; 6;;
val long_string : string -&gt; bool = &lt;fun&gt;
# first_if_true long_string &quot;short&quot; &quot;loooooong&quot;;;
- : string = &quot;loooooong&quot;
</programlisting>
      <para>
        as well as:
      </para>
      <programlisting language="ocaml">
# let big_number x = x &gt; 3;;
val big_number : int -&gt; bool = &lt;fun&gt;
# first_if_true big_number 4 3;;
- : int = 4
</programlisting>
      <para>
        Both <literal>long_string</literal> and
        <literal>big_number</literal> are functions, and each is passed
        to <literal>first_if_true</literal> with two other arguments of
        the appropriate type (strings in the first example, and integers
        in the second). But we can't mix and match two different
        concrete types for <literal>'a</literal> in the same use of
        <literal>first_if_true</literal>.
      </para>
      <programlisting language="ocaml">
# first_if_true big_number &quot;short&quot; &quot;loooooong&quot;;;
Characters 25-30:
  first_if_true big_number &quot;short&quot; &quot;loooooong&quot;;;
                           ^^^^^^^
Error: This expression has type string but
    an expression was expected of type int
</programlisting>
      <para>
        In this example, <literal>big_number</literal> requires that
        <literal>'a</literal> be instantiated as <literal>int</literal>,
        whereas <literal>&quot;short&quot;</literal> and
        <literal>&quot;loooooong&quot;</literal> require that
        <literal>'a</literal> be instantiated as
        <literal>string</literal>, and they can't both be right at the
        same time.
      </para>
      <note><title>
      Type errors vs exceptions
      </title>
      <para>
        There's a big difference in OCaml (and really in any compiled
        language) between errors that are caught at compile time and
        those that are caught at run-time. It's better to catch errors
        as early as possible in the development process, and compilation
        time is best of all.
      </para>
      <para>
        Working in the toplevel somewhat obscures the difference between
        run-time and compile time errors, but that difference is still
        there. Generally, type errors, like this one:
      </para>
      <programlisting language="ocaml">
# let add_potato x =
     x + &quot;potato&quot;;;
  Characters 28-36:
       x + &quot;potato&quot;;;
           ^^^^^^^^
Error: This expression has type string but an expression was expected of type
         int
</programlisting>
      <para>
        are compile-time errors, whereas an error that can't be caught
        by the type system, like division by zero, leads to a runtime
        exception.
      </para>
      <programlisting language="ocaml">
# let is_a_multiple x y =
     x mod y = 0 ;;
  val is_a_multiple : int -&gt; int -&gt; bool = &lt;fun&gt;
# is_a_multiple 8 2;;
- : bool = true
# is_a_multiple 8 0;;
Exception: Division_by_zero.
</programlisting>
      <para>
        The distinction here is that type errors will stop you whether
        or not the offending code is ever actually executed. Merely
        defining <literal>add_potato</literal> is an error, whereas
        <literal>is_a_multiple</literal> only fails when it's called,
        and then, only when it's called with an input that triggers the
        exception.
      </para>
      </note>
    </sect2>
  </sect1>
  <sect1 id="tuples-lists-options-and-pattern-matching">
    <title>Tuples, Lists, Options and Pattern-matching</title>
    <sect2 id="tuples">
      <title>Tuples</title>
      <para>
        So far we've encountered a handful of basic types like
        <literal>int</literal>, <literal>float</literal> and
        <literal>string</literal> as well as function types like
        <literal>string -&gt; int</literal>. But we haven't yet talked
        about any data structures. We'll start by looking at a
        particularly simple data structure, the tuple. A tuple is an
        ordered collection of values that can each be of different type.
        You can create a tuple by joining values together with a comma:
      </para>
      <programlisting language="ocaml">
# let a_tuple = (3,&quot;three&quot;);;
val a_tuple : int * string = (3, &quot;three&quot;)
</programlisting>
      <para>
        For the mathematically inclined, the <literal>*</literal>
        character is used because the set of all pairs of type
        <literal>t * s</literal> corresponds to the Cartesian product of
        the set of elements of type <literal>t</literal> and the set of
        elements of type <literal>s</literal>.
      </para>
      <para>
        You can extract the components of a tuple using OCaml's
        pattern-matching syntax. For example:
      </para>
      <programlisting language="ocaml">
# let (x,y) = a_tuple;;
val x : int = 3
val y : string = &quot;three&quot;
</programlisting>
      <para>
        Here, the <literal>(x,y)</literal> on the left-hand side of the
        <literal>let</literal> binding is the pattern. This pattern lets
        us mint the new variables <literal>x</literal> and
        <literal>y</literal>, each bound to different components of the
        value being matched, which can now be used in subsequent
        expressions.
      </para>
      <programlisting language="ocaml">
# x + String.length y;;
- : int = 8
</programlisting>
      <para>
        Note that the same syntax is used both for constructing and for
        pattern-matching on tuples.
      </para>
      <para>
        Pattern matching can also show up in function arguments. Here's
        a function for computing the distance between two points on the
        plane, where each point is represented as a pair of
        <literal>float</literal>s. The pattern matching syntax lets us
        get at the values we need with a minimum of fuss.
      </para>
      <programlisting language="ocaml">
# let distance (x1,y1) (x2,y2) =
    sqrt ((x1 -. x2) ** 2. +. (y1 -. y2) ** 2.)
  ;;
val distance : float * float -&gt; float * float -&gt; float = &lt;fun&gt;
</programlisting>
      <para>
        The <literal>**</literal> operator used above is for raising a
        floating-point number to a power.
      </para>
      <para>
        This is just a first taste of pattern matching. Pattern matching
        is a pervasive tool in OCaml, and as you'll see, it has
        surprising power.
      </para>
    </sect2>
    <sect2 id="lists">
      <title>Lists</title>
      <para>
        Where tuples let you combine a fixed number of items,
        potentially of different types, lists let you hold any number of
        items of the same type. For example:
      </para>
      <programlisting language="ocaml">
# let languages = [&quot;OCaml&quot;;&quot;Perl&quot;;&quot;C&quot;];;
val languages : string list = [&quot;OCaml&quot;; &quot;Perl&quot;; &quot;C&quot;]
</programlisting>
      <para>
        Note that you can't mix elements of different types on the same
        list, as we did with tuples.
      </para>
      <programlisting language="ocaml">
# let numbers = [3;&quot;four&quot;;5];;
Characters 17-23:
  let numbers = [3;&quot;four&quot;;5];;
                   ^^^^^^
Error: This expression has type string but an expression was expected of type
         int
</programlisting>
      <sect3 id="the-list-module">
        <title>The <literal>List</literal> module</title>
        <para>
          Core comes with a <literal>List</literal> module that has a
          rich collection of functions for working with lists. We can
          access values from within a module by using dot-notation.
          Here, for example, is how we compute the length of a list.
        </para>
        <programlisting language="ocaml">
# List.length languages;;
- : int = 3
</programlisting>
        <para>
          Here's something a little more complicated. We can compute the
          list of the lengths of each language as follows.
        </para>
        <programlisting language="ocaml">
# List.map languages ~f:String.length;;
- : int list = [5; 4; 1]
</programlisting>
        <para>
          <literal>List.map</literal> takes two arguments: a list and a
          function for transforming the elements of that list. Note that
          <literal>List.map</literal> creates a new list and does not
          modify the original.
        </para>
        <para>
          In this example, the function <literal>String.length</literal>
          is passed under the <emphasis>labeled argument</emphasis>
          <literal>~f</literal>. Labels allow you to specify function
          arguments by name rather than by position. As you can see
          below, we can change the order of labeled arguments without
          changing the function's behavior.
        </para>
        <programlisting language="ocaml">
# List.map ~f:String.length languages;;
- : int list = [5; 4; 1]
</programlisting>
        <para>
          We'll learn more about labeled arguments and why they're
          important in
          <xref linkend="variables-and-functions"/>.
        </para>
      </sect3>
      <sect3 id="constructing-lists-with">
        <title>Constructing lists with <literal>::</literal></title>
        <para>
          In addition to constructing lists using brackets, we can use
          the operator <literal>::</literal> for adding elements to the
          front of a list.
        </para>
        <programlisting language="ocaml">
# &quot;French&quot; :: &quot;Spanish&quot; :: languages;;
- : string list = [&quot;French&quot;; &quot;Spanish&quot;; &quot;OCaml&quot;; &quot;Perl&quot;; &quot;C&quot;]
</programlisting>
        <para>
          Here, we're creating a new and extended list, not changing the
          list we started with, as you can see below.
        </para>
        <programlisting language="ocaml">
# languages;;
- : string list = [&quot;OCaml&quot;; &quot;Perl&quot;; &quot;C&quot;]
</programlisting>
        <para>
          The bracket notation for lists is really just syntactic sugar
          for <literal>::</literal>. Thus, the following declarations
          are all equivalent. Note that <literal>[]</literal> is used to
          represent the empty list.
        </para>
        <programlisting language="ocaml">
# [1; 2; 3];;
- : int list = [1; 2; 3]
# 1 :: (2 :: (3 :: []));;
- : int list = [1; 2; 3]
# 1 :: 2 :: 3 :: [];;
- : int list = [1; 2; 3]
</programlisting>
        <para>
          The <literal>::</literal> operator can only be used for adding
          one element to the front of the list, with the list
          terminating at <literal>[]</literal>, the empty list. There's
          also a list concatenation operator, <literal>@</literal>,
          which can concatenate two lists.
        </para>
        <programlisting language="ocaml">
# [1;2;3] @ [4;5;6];;
- : int list = [1; 2; 3; 4; 5; 6]
</programlisting>
        <para>
          It's important to remember that this is not a constant-time
          operation. Concatenating two lists takes time proportional to
          the length of the first list.
        </para>
      </sect3>
      <sect3 id="list-patterns-using-match">
        <title>List patterns using <literal>match</literal></title>
        <para>
          The elements of a list can be accessed through
          pattern-matching. List patterns are based on the two list
          constructors, <literal>[]</literal> and <literal>::</literal>.
          Here's a simple example.
        </para>
        <programlisting language="ocaml">
# let my_favorite_language (my_favorite :: the_rest) =
     my_favorite
  ;;
</programlisting>
        <para>
          By pattern matching using <literal>::</literal>, we've
          isolated and named the first element of the list
          (<literal>my_favorite</literal>) and the remainder of the list
          (<literal>the_rest</literal>). If you know Lisp or Scheme,
          what we've done is the equivalent of using the functions
          <literal>car</literal> and <literal>cdr</literal> to isolate
          the first element of a list and the remainder of that list.
        </para>
        <para>
          If you try the above example in the toplevel, however, you'll
          see that it spits out a warning:
        </para>
        <programlisting language="ocaml">
    Characters 25-69:
  .........................(my_favorite :: the_rest) =
       my_favorite
Warning 8: this pattern-matching is not exhaustive.
Here is an example of a value that is not matched:
[]
val my_favorite_language : 'a list -&gt; 'a = &lt;fun&gt;
</programlisting>
        <para>
          The warning comes because the compiler can't be certain that
          the pattern match won't lead to a runtime error. Indeed, the
          warning gives an example of a list, (<literal>[]</literal>,
          the empty list) that doesn't match the provided pattern.
          Indeed, if we try to run
          <literal>my_favorite_language</literal>, we'll see that it
          works on non-empty list, and fails on empty ones.
        </para>
        <programlisting language="ocaml">
# my_favorite_language [&quot;English&quot;;&quot;Spanish&quot;;&quot;French&quot;];;
- : string = &quot;English&quot;
# my_favorite_language [];;
Exception: Match_failure (&quot;//toplevel//&quot;, 11, 10).
</programlisting>
        <para>
          You can avoid these warnings, and more importantly make sure
          that your code actually handles all of the possible cases, by
          using a <literal>match</literal> statement instead.
        </para>
        <para>
          A <literal>match</literal> statement is a kind of juiced-up
          version of the switch statement found in <literal>C</literal>
          and <literal>Java</literal>. It essentially lets you list a
          sequence of patterns (separated by <literal>|</literal>
          characters --- the one before the first case is optional), and
          the compiler then dispatches to the code following the first
          matched pattern. And, as we've already seen, we can name new
          variables in our patterns that correspond to sub-structures of
          the value being matched.
        </para>
        <para>
          Here's a new version of
          <literal>my_favorite_language</literal> that uses
          <literal>match</literal> and doesn't trigger a compiler
          warning.
        </para>
        <programlisting language="ocaml">
# let my_favorite_language languages =
    match languages with
    | first :: the_rest -&gt; first
    | [] -&gt; &quot;OCaml&quot; (* A good default! *)
 ;;
val my_favorite_language : string list -&gt; string = &lt;fun&gt;
# my_favorite_language [&quot;English&quot;;&quot;Spanish&quot;;&quot;French&quot;];;
- : string = &quot;English&quot;
# my_favorite_language [];;
- : string = &quot;OCaml&quot;
</programlisting>
        <para>
          Note that we included a comment in the above code. OCaml
          comments are bounded by <literal>(*</literal> and
          <literal>*)</literal>, and can be nested arbitrarily and cover
          multiple lines. There's no equivalent of C-style single line
          comments that are prefixed by <literal>//</literal>.
        </para>
        <para>
          The first pattern, <literal>first :: the_rest</literal>,
          covers the case where <literal>languages</literal> has at
          least one element, since every list except for the empty list
          can be written down with one or more <literal>::</literal>'s.
          The second pattern, <literal>[]</literal>, matches only the
          empty list. These cases are exhaustive (every list is either
          empty, or has at least one element), and the compiler can
          detect that exhaustiveness, which is why it doesn't spit out a
          warning.
        </para>
      </sect3>
      <sect3 id="recursive-list-functions">
        <title>Recursive list functions</title>
        <para>
          Recursive functions, or, functions that call themselves, are
          an important technique in OCaml and in any functional
          language. The typical approach to designing a recursive
          function is to separate the logic into a set of <emphasis>base
          cases</emphasis>, that can be solved directly, and a set of
          <emphasis>inductive cases</emphasis>, where the function
          breaks the problem down into smaller pieces and then calls
          itself to solve those smaller problems.
        </para>
        <para>
          When writing recursive list functions, this separation between
          the base cases and the inductive cases is often done using
          pattern matching. Here's a simple example of a function that
          sums the elements of a list.
        </para>
        <programlisting language="ocaml">
# let rec sum l =
    match l with
    | [] -&gt; 0                   (* base case *)
    | hd :: tl -&gt; hd + sum tl   (* inductive case *)
  ;;
val sum : int list -&gt; int
# sum [1;2;3];;
- : int = 6
</programlisting>
        <para>
          Following the common OCaml idiom, we use <literal>hd</literal>
          to refer to the head of the list and <literal>tl</literal> to
          refer to the tail. Note that we had to use the
          <literal>rec</literal> keyword to allow <literal>sum</literal>
          to refer to itself. As you might imagine, the base case and
          inductive case are different arms of the match.
        </para>
        <para>
          Logically, you can think of the evaluation of a simple
          recursive function like <literal>sum</literal> almost as if it
          were a mathematical equation whose meaning you were unfolding
          step by step.
        </para>
        <programlisting language="ocaml">
sum [1;2;3]
1 + sum [2;3]
1 + (2 + sum [3])
1 + (2 + (3 + sum []))
1 + (2 + (3 + 0))
1 + (2 + 3)
1 + 5
6
</programlisting>
        <para>
          This suggests a reasonable mental model for what OCaml is
          actually doing to evaluate a recursive function.
        </para>
        <para>
          We can introduce more complicated list patterns as well.
          Here's a function for destuttering a list,
          <emphasis>i.e.</emphasis>, for removing sequential duplicates.
        </para>
        <programlisting language="ocaml">
# let rec destutter list =
    match list with
    | [] -&gt; []
    | hd1 :: hd2 :: tl -&gt;
      if hd1 = hd2 then destutter (hd2 :: tl)
      else hd1 :: destutter (hd2 :: tl)
  ;;
</programlisting>
        <para>
          Again, the first arm of the match is the base case, and the
          second is the inductive. Unfortunately, this code has a
          problem. If you type it into the toplevel, you'll see this
          error:
        </para>
        <programlisting>
Warning 8: this pattern-matching is not exhaustive.
Here is an example of a value that is not matched:
_::[]
</programlisting>
        <para>
          This indicates that we're missing a case, in particular we
          don't handle one-element lists. That's easy enough to fix by
          adding another case to the match:
        </para>
        <programlisting language="ocaml">
# let rec destutter list =
    match list with
    | [] -&gt; []
    | hd :: [] -&gt; hd :: []
    | hd1 :: hd2 :: tl -&gt;
      if hd1 = hd2 then destutter (hd2 :: tl)
      else hd1 :: destutter (hd2 :: tl)
  ;;
val destutter : 'a list -&gt; 'a list = &lt;fun&gt;
# destutter [&quot;hey&quot;;&quot;hey&quot;;&quot;hey&quot;;&quot;man!&quot;];;
- : string list = [&quot;hey&quot;; &quot;man!&quot;]
</programlisting>
        <para>
          Note that this code used another variant of the list pattern,
          <literal>[hd]</literal>, to match a list with a single
          element. We can do this to match a list with any fixed number
          of elements, <emphasis>e.g.</emphasis>,
          <literal>[x;y;z]</literal> will match any list with exactly
          three elements, and will bind those elements to the variables
          <literal>x</literal>, <literal>y</literal> and
          <literal>z</literal>.
        </para>
        <para>
          In the last few examples, our list processing code involved a
          lot of recursive functions. In practice, this isn't usually
          necessary. Most of the time, you'll find yourself happy to use
          the iteration functions found in the <literal>List</literal>
          module. But it's good to know how to use recursion when you
          need to do something new that's not already supported.
        </para>
      </sect3>
    </sect2>
    <sect2 id="options">
      <title>Options</title>
      <para>
        Another common data structure in OCaml is the option. An option
        is used to express that a value might or might not be present.
        For example,
      </para>
      <programlisting language="ocaml">
# let divide x y =
    if y = 0 then None else Some (x/y) ;;
val divide : int -&gt; int -&gt; int option = &lt;fun&gt;
</programlisting>
      <para>
        The function <literal>divide</literal> either returns
        <literal>None</literal>, if the divisor is zero, or
        <literal>Some</literal> of the result of the division,
        otherwise. <literal>Some</literal> and <literal>None</literal>
        are constructors, like <literal>::</literal> and
        <literal>[]</literal> for lists, which let you build optional
        values. You can think of an option as a specialized list that
        can only have zero or one element.
      </para>
      <para>
        To examine the contents of an option, we use pattern matching,
        as we did with tuples and lists. Consider the following simple
        function for printing a log entry given an optional time and a
        message. If no time is provided (<emphasis>i.e.</emphasis>, if
        the time is <literal>None</literal>), the current time is
        computed and used in its place.
      </para>
      <programlisting language="ocaml">
# let print_log_entry maybe_time message =
    let time =
      match maybe_time with
      | Some x -&gt; x
      | None -&gt; Time.now ()
    in
    printf &quot;%s: %s\n&quot; (Time.to_sec_string time) message ;;
val print_log_entry : Time.t option -&gt; string -&gt; unit
# print_log_entry (Some Time.epoch) &quot;A long long time ago&quot;;;
1969-12-31 19:00:00: A long long time ago
- : unit = ()
# print_log_entry None &quot;Up to the minute&quot;;;
2013-02-23 16:49:25: Up to the minute
- : unit = ()
</programlisting>
      <para>
        We use a <literal>match</literal> statement for handling the two
        possible states of an option.
      </para>
      <note> <title> 
      Nesting <literal>let</literal>s with <literal>let</literal> and
      <literal>in</literal>
      </title>
      <para>
        As a side note, this is our first use of <literal>let</literal>
        to define a new variable within the body of a function. A
        <literal>let</literal> bounded with an <literal>in</literal> can
        be used to introduce a new binding within any local scope,
        including a function body. The <literal>in</literal> marks the
        beginning of the scope within which the new variable can be
        used. Thus, we could write:
      </para>
      <programlisting language="ocaml">
# let x = 7 in
  x + x
  ;;
- : int = 14
</programlisting>
      <para>
        Note that the scope of the let binding is terminated by the
        double-semicolon.
      </para>
      <para>
        We can also have multiple let statements in a row, each one
        adding a new variable binding to what came before.
      </para>
      <programlisting language="ocaml">
# let x = 7 in
  let y = x * x in
  x + y
  ;;
- : int = 56
</programlisting>
      <para>
        This kind of nested let binding is a common way of building up a
        complex expression, with each <literal>let</literal> breaking
        off and naming an individual component, and then combining them
        in one final expression.
      </para>
      </note>
      <para>
        Options are important because they are the standard way in OCaml
        to encode a value that might not be there --- there's no such
        thing as a <literal>NullPointerException</literal> in OCaml.
        This is different from most other languages, including Java and
        C#, where most if not all datatypes are
        <emphasis>nullable</emphasis>, meaning that, whatever their type
        is, any given value also contains the possibility of being a
        null value. In such languages, null is lurking everywhere.
      </para>
      <para>
        In OCaml, however, nulls are explicit. A value of type
        <literal>string * string</literal> always actually contains two
        well-defined values of type <literal>string</literal>. If you
        want to allow, say, the first of those to be absent, then you
        need to change the type to
        <literal>string option * string</literal>. As we'll see, this
        explicitness allows the compiler to provide a great deal of help
        in making sure you're correctly handing the possibility of
        missing data.
      </para>
    </sect2>
  </sect1>
  <sect1 id="records-and-variants">
    <title>Records and Variants</title>
    <para>
      So far, we've looked only at data structures that were predefined
      in the language, like lists and tuples. But OCaml also allows us
      to define new datatypes. Here's a toy example of a datatype
      representing a point in 2-dimensional space:
    </para>
    <programlisting language="ocaml">
# type point2d = { x : float; y : float };;
type point2d = { x : float; y : float; }
</programlisting>
    <para>
      <literal>point2d</literal> is a <emphasis>record</emphasis> type,
      which you can think of as a tuple where the individual fields are
      named, rather than being defined positionally. Record types are
      easy enough to construct:
    </para>
    <programlisting language="ocaml">
# let p = { x = 3.; y = -4. };;
val p : point2d = {x = 3.; y = -4.}
</programlisting>
    <para>
      And we can get access to the contents of these types using pattern
      matching:
    </para>
    <programlisting language="ocaml">
# let magnitude { x = x_pos; y = y_pos } =
    sqrt (x_pos ** 2. +. y_pos ** 2.);;
val magnitude : point2d -&gt; float = &lt;fun&gt;
</programlisting>
    <para>
      We can write the pattern match even more tersely, using what's
      called <emphasis>field punning</emphasis>. In particular, when the
      name of the field and the name of the variable coincide, we don't
      have to write them both down. Thus, the magnitude function can be
      rewritten as follows.
    </para>
    <programlisting language="ocaml">
# let magnitude { x; y } = sqrt (x ** 2. +. y ** 2.);;
</programlisting>
    <para>
      We can also use dot-notation for accessing record fields:
    </para>
    <programlisting language="ocaml">
# let distance v1 v2 =
     magnitude { x = v1.x -. v2.x; y = v1.y -. v2.y };;
val distance : point2d -&gt; point2d -&gt; float = &lt;fun&gt;
</programlisting>
    <para>
      And we can of course include our newly defined types as components
      in larger types, as in the following types, each of which is a
      description of a different geometric object.
    </para>
    <programlisting language="ocaml">
# type circle_desc  = { center: point2d; radius: float }
  type rect_desc    = { lower_left: point2d; width: float; height: float }
  type segment_desc = { endpoint1: point2d; endpoint2: point2d } ;;
</programlisting>
    <para>
      Now, imagine that you want to combine multiple objects of these
      types together as a description of a multi-object scene. You need
      some unified way of representing these objects together in a
      single type. One way of doing this is using a
      <emphasis>variant</emphasis> type:
    </para>
    <programlisting language="ocaml">
# type scene_element =
    | Circle  of circle_desc
    | Rect    of rect_desc
    | Segment of segment_desc
  ;;
</programlisting>
    <para>
      The <literal>|</literal> character separates the different cases
      of the variant (the first <literal>|</literal> is optional), and
      each case has a tag, like <literal>Circle</literal>,
      <literal>Rect</literal> and <literal>Segment</literal>, to
      distinguish that case from the others. Here's how we might write a
      function for testing whether a point is in the interior of some
      element of a list of <literal>scene_element</literal>s.
    </para>
    <programlisting language="ocaml">
# let is_inside_scene_element point scene_element =
     match scene_element with
     | Circle { center; radius } -&gt;
       distance center point &lt; radius
     | Rect { lower_left; width; height } -&gt;
       point.x &gt; lower_left.x &amp;&amp; point.x &lt; lower_left.x +. width
       &amp;&amp; point.y &gt; lower_left.y &amp;&amp; point.y &lt; lower_left.y +. height
     | Segment { endpoint1; endpoint2 } -&gt; false
  ;;
val is_inside_scene_element : point2d -&gt; scene_element -&gt; bool = &lt;fun&gt;
# let is_inside_scene point scene =
     List.exists scene
       ~f:(fun el -&gt; is_inside_scene_element point el)
   ;;
val is_inside_scene : point2d -&gt; scene_element list -&gt; bool = &lt;fun&gt;
# is_inside_scene {x=3.;y=7.}
    [ Circle {center = {x=4.;y= 4.}; radius = 0.5 } ];;
- : bool = false
# is_inside_scene {x=3.;y=7.}
    [ Circle {center = {x=4.;y= 4.}; radius = 5.0 } ];;
- : bool = true
</programlisting>
    <para>
      You might at this point notice that the use of
      <literal>match</literal> here is reminiscent of how we used
      <literal>match</literal> with <literal>option</literal> and
      <literal>list</literal>. This is no accident:
      <literal>option</literal> and <literal>list</literal> are really
      just examples of variant types that happen to be important enough
      to be defined in the standard library (and in the case of lists,
      to have some special syntax).
    </para>
    <para>
      We also made our first use of an <emphasis>anonymous
      function</emphasis> in the call to <literal>List.exists</literal>.
      An anonymous function is a function that is defined but not named,
      in this case, using the <literal>fun</literal> keyword. Anonymous
      functions are common in OCaml, particularly when using iteration
      functions like <literal>List.exists</literal>.
    </para>
    <para>
      The purpose of <literal>List.exists</literal> is to check if there
      are any elements of the given list in question on which the
      provided function evaluates to <literal>true</literal>. In this
      case, we're using <literal>List.exists</literal> to check if there
      is a scene element within which our point resides.
    </para>
  </sect1>
  <sect1 id="imperative-programming">
    <title>Imperative programming</title>
    <para>
      So far, we've only written so-called <emphasis>pure</emphasis> or
      <emphasis>functional</emphasis> code, meaning that we didn't write
      any code that modified a variable or value after its creation.
      Indeed, almost all of the data structures we've encountered so far
      are <emphasis>immutable</emphasis>, meaning there's no way in the
      language to modify them at all. This is a quite different style
      from <emphasis>imperative</emphasis> programming, where
      computations are structured as sequences of instructions that
      operate by modifying state as they go.
    </para>
    <para>
      Functional code is the default in OCaml, with variable bindings
      and most data structures being immutable. But OCaml also has
      excellent support for imperative programming, including mutable
      data structures like arrays and hashtables and control-flow
      constructs like for and while loops.
    </para>
    <sect2 id="arrays">
      <title>Arrays</title>
      <para>
        Perhaps the simplest mutable data structure in OCaml is the
        array. Arrays in OCaml are very similar to arrays in other
        languages like C: indexing starts at 0, and accessing or
        modifying an array element is a constant-time operation. Arrays
        are more compact in terms of memory utilization than most other
        data structures in OCaml, including lists. Here's an example:
      </para>
      <programlisting language="ocaml">
# let numbers = [| 1;2;3;4 |];;
val numbers : int array = [|1; 2; 3; 4|]
# numbers.(2) &lt;- 4;;
- : unit = ()
# numbers;;
- : int array = [|1; 2; 4; 4|]
</programlisting>
      <para>
        the <literal>.(i)</literal> syntax is used to refer to an
        element of an array, and the <literal>&lt;-</literal> syntax is
        for modification. Because the elements of the array are counted
        starting at zero, element <literal>.(2)</literal> is the third
        element.
      </para>
    </sect2>
    <sect2 id="mutable-record-fields">
      <title>Mutable record fields</title>
      <para>
        The array is an important mutable data structure, but it's not
        the only one. Records, which are immutable by default, can be
        declared with specific fields as being mutable. Here's a small
        example of a data structure for storing a running statistical
        summary of a collection of numbers. Here's the basic data
        structure:
      </para>
      <programlisting language="ocaml">
# type running_sum =
   { mutable sum: float;
     mutable sum_sq: float; (* sum of squares *)
     mutable samples: int;
   }
  ;;
</programlisting>
      <para>
        The fields in <literal>running_sum</literal> are designed to be
        easy to extend incrementally, and sufficient to compute means
        and standard deviations, as shown below.
      </para>
      <programlisting language="ocaml">
# let mean rsum = rsum.sum /. float rsum.samples
  let stdev rsum =
     sqrt (rsum.sum_sq /. float rsum.samples
           -. (rsum.sum /. float rsum.samples) ** 2.) ;;
val mean : running_sum -&gt; float = &lt;fun&gt;
val stdev : running_sum -&gt; float = &lt;fun&gt;
</programlisting>
      <para>
        We also need functions to create and update
        <literal>running_sum</literal>s:
      </para>
      <programlisting language="ocaml">
# let create () = { sum = 0.; sum_sq = 0.; samples = 0 }
  let update rsum x =
     rsum.samples &lt;- rsum.samples + 1;
     rsum.sum     &lt;- rsum.sum     +. x;
     rsum.sum_sq  &lt;- rsum.sum_sq  +. x *. x
  ;;
val create : unit -&gt; running_sum = &lt;fun&gt;
val update : running_sum -&gt; float -&gt; unit = &lt;fun&gt;
</programlisting>
      <para>
        <literal>create</literal> returns a
        <literal>running_sum</literal> corresponding to the empty set,
        and <literal>update rsum x</literal> changes
        <literal>rsum</literal> to reflect the addition of
        <literal>x</literal> to its set of samples, by updating the
        number of samples, the sum, and the sum of squares.
      </para>
      <para>
        Note the use in the above code of single semi-colons to sequence
        operations. When we were working purely functionally, this
        wasn't necessary, but you start needing it when your code is
        acting by side-effect.
      </para>
      <para>
        A new and somewhat odd type has cropped up in this example:
        <literal>unit</literal>. What makes <literal>unit</literal>
        different is that there is only one value of type
        <literal>unit</literal>, which is written <literal>()</literal>.
        Because there is only one value of type <literal>unit</literal>
        that value doesn't really convey any information.
      </para>
      <para>
        If it doesn't convey any information, then what is
        <literal>unit</literal> good for? Most of the time,
        <literal>unit</literal> acts as a placeholder. Thus, we use
        <literal>unit</literal> for the return value of a function like
        <literal>update</literal> that operates by side effect rather
        than by returning a value, and for the argument to a function
        like <literal>create</literal> that doesn't require any
        information to be passed into it in order to run. This is
        similar to the role that <literal>void</literal> plays in
        languages like C and Java.
      </para>
      <para>
        Here's an example of <literal>create</literal> and
        <literal>update</literal> in action.
      </para>
      <programlisting language="ocaml">
# let rsum = create ();;
val rsum : running_sum = {sum = 0.; sum_sq = 0.; samples = 0}
# List.iter [1.;3.;2.;-7.;4.;5.] ~f:(fun x -&gt; update rsum x);;
- : unit = ()
# mean rsum;;
- : float = 1.33333333333333326
# stdev rsum;;
- : float = 3.94405318873307698
</programlisting>
    </sect2>
    <sect2 id="refs">
      <title>Refs</title>
      <para>
        We can declare a single mutable value by using a
        <literal>ref</literal>, which is a record type with a single
        mutable field that is defined in the standard library.
      </para>
      <programlisting language="ocaml">
# let x = { contents = 0 };;
val x : int ref = {contents = 0}
# x.contents &lt;- x.contents + 1;;
- : unit = ()
# x;;
- : int ref = {contents = 1}
</programlisting>
      <para>
        There are a handful of useful functions and operators defined
        for refs to make them more convenient to work with.
      </para>
      <programlisting language="ocaml">
# let x = ref 0 ;; (* create a ref, i.e., { contents = 0 } *)
val x : int ref = {contents = 0}
# !x ;;            (* get the contents of a ref, i.e., x.contents *)
- : int = 0
# x := !x + 1 ;;   (* assignment, i.e., x.contents &lt;- ... *)
- : unit = ()
# !x ;;
- : int = 1
</programlisting>
      <para>
        The definition of all this is quite straightforward. Here is the
        complete implementation of the <literal>ref</literal> type. The
        <literal>'a</literal> before the ref indicates that the
        <literal>ref</literal> type is polymorphic, in the same way that
        lists are polymorphic, meaning it can contain values of any
        type.
      </para>
      <programlisting language="ocaml">
type 'a ref = { mutable contents : 'a }

let ref x = { contents = x }
let (!) r = r.contents
let (:=) r x = r.contents &lt;- x
</programlisting>
      <para>
        Here, <literal>!</literal> and <literal>:=</literal> are infix
        operators that we're defining, where the parenthetical syntax
        marks them as such.
      </para>
      <para>
        Even though a <literal>ref</literal> is just another record
        type, it's notable because it is the standard way of simulating
        the traditional mutable variable you'll find in most imperative
        languages. For example, we can sum over the elements of a list
        imperatively by calling <literal>List.iter</literal> to call a
        simple function on every element of a list, using a ref to
        accumulate the results.
      </para>
      <programlisting language="ocaml">
# let sum list =
    let sum = ref 0 in
    List.iter list ~f:(fun x -&gt; sum := !sum + x);
    !sum
</programlisting>
      <para>
        This isn't the most idiomatic (or the fastest) way to sum up a
        list, but it shows how you can use a ref in place of a mutable
        variable.
      </para>
    </sect2>
    <sect2 id="for-and-while-loops">
      <title>For and while loops</title>
      <para>
        Along with mutable data structures, OCaml gives you constructs
        like while and for loops for interacting with them. Here, for
        example, is some code that uses a for loop for permuting an
        array. We use the <literal>Random</literal> module as our source
        of randomness. <literal>Random</literal> starts out with a
        deterministic seed, but you can call
        <literal>Random.self_init</literal> to choose a new seed at
        random.
      </para>
      <programlisting language="ocaml">
# let permute ar =
    for i = 0 to Array.length ar - 2 do
       (* pick a j that is after i and before the end of the list *)
       let j = i + 1 + Random.int (Array.length ar - i - 1) in
       (* Swap i and j *)
       let tmp = ar.(i) in
       ar.(i) &lt;- ar.(j);
       ar.(j) &lt;- tmp
    done
  ;;
val permute : 'a array -&gt; unit = &lt;fun&gt;
</programlisting>
      <para>
        From a syntactic perspective, you should note the keywords that
        distinguish a for loop: <literal>for</literal>,
        <literal>to</literal>, <literal>do</literal> and
        <literal>done</literal>.
      </para>
      <para>
        Here's an example run of this code.
      </para>
      <programlisting language="ocaml">
# let ar = Array.init 20 ~f:(fun i -&gt; i);;
val ar : int array =
  [|0; 1; 2; 3; 4; 5; 6; 7; 8; 9; 10; 11; 12; 13; 14; 15; 16; 17; 18; 19|]
# permute ar;;
- : unit = ()
# ar;;
- : int array =
[|14; 13; 1; 3; 2; 19; 17; 18; 9; 16; 15; 7; 12; 11; 4; 10; 0; 5; 6; 8|]
</programlisting>
      <para>
        OCaml also supports while loops, as shown in the following
        function for finding the first non-negative position in an
        array. Note that <literal>while</literal> (like
        <literal>for</literal>) is also a keyword.
      </para>
      <programlisting language="ocaml">
# let find_first_negative_entry ar =
     let pos = ref 0 in
     while !pos &lt; Array.length ar &amp;&amp; ar.(!pos) &gt;= 0 do
       pos := !pos + 1
     done;
     if !pos = Array.length ar then None else Some !pos
  ;;
            val find_first_negative_entry : int Core.Std.Array.t -&gt; int option = &lt;fun&gt;
# find_first_negative_entry [|1;2;0;3|];;
- : int option = None
# find_first_negative_entry [|1;-2;0;3|];;
- : int option = Some 1
</programlisting>
    </sect2>
  </sect1>
  <sect1 id="a-complete-program">
    <title>A complete program</title>
    <para>
      So far, we've played with the basic features of the language using
      the toplevel. Now we'll create a simple, complete stand-along
      program that does something useful: sum up a list of numbers read
      in from the standard input.
    </para>
    <para>
      Here's the code, which you can save in a file called
      <literal>sum.ml</literal>.
    </para>
    <programlisting language="ocaml">
(* file: sum.ml *)

open Core.Std

let rec read_and_accumulate accum =
  let line = In_channel.input_line In_channel.stdin in
  match line with
  | None -&gt; accum
  | Some x -&gt; read_and_accumulate (accum +. Float.of_string x)

let () =
  printf &quot;Total: %F\n&quot; (read_and_accumulate 0.)
</programlisting>
    <para>
      This is our first use of OCaml's input and output routines. The
      function <literal>read_and_accumulate</literal> is a recursive
      function that uses <literal>In_channel.input_line</literal> to
      read in lines one by one from the standard input, invoking itself
      at each iteration with its updated accumulated sum. Note that
      <literal>input_line</literal> returns an optional value, with
      <literal>None</literal> indicating the end of the input.
    </para>
    <para>
      After <literal>read_and_accumulate</literal> returns, the total
      needs to be printed. This is done using the
      <literal>printf</literal> command, which provides support for
      type-safe format strings, similar to what you'll find in a variety
      of languages. The format string is parsed by the compiler and used
      to determine the number and type of the remaining arguments that
      are required. In this case, there is a single formatting
      directive, <literal>%F</literal>, so <literal>printf</literal>
      expects one additional argument of type <literal>float</literal>.
    </para>
    <sect2 id="compiling-and-running">
      <title>Compiling and running</title>
      <para>
        We can use <literal>ocamlbuild</literal> to compile the program.
        We'll need to create a file, in the same directory as
        <literal>sum.ml</literal>, called <literal>_tags</literal>. We
        can put the following in <literal>_tags</literal> to indicate
        that we're building against Core, and that threads should be
        enabled, which is required by Core.
      </para>
      <programlisting>
true:package(core),thread
</programlisting>
      <para>
        With our <literal>_tags</literal> file in place, we can build
        our executable by issuing this command.
      </para>
      <programlisting>
ocamlbuild -use-ocamlfind sum.native
</programlisting>
      <para>
        The <literal>.native</literal> suffix indicates that we're
        building a native-code executable, which we'll discuss more in
        <xref linkend="files-modules-and-programs"/>. Once the
        build completes, we can use the resulting program like any
        command-line utility. In this example, we can just type in a
        sequence of numbers, one per line, hitting control-d to exit
        when the input is complete.
      </para>
      <programlisting>
max $ ./sum.native
1
2
3
94.5
Total: 100.5
</programlisting>
      <para>
        More work is needed to make a really usable command-line
        program, including a proper command-line parsing interface and
        better error handling, all of which is covered in
        <xref linkend="command-line-parsing"/>.
      </para>
    </sect2>
  </sect1>
  <sect1 id="where-to-go-from-here">
    <title>Where to go from here</title>
    <para>
      That's it for our guided tour! There are plenty of features left
      to touch upon and lots of details to explain, but the hope is that
      this has given you enough of a feel for the language that you have
      a sense as to what to expect, and will be comfortable reading
      examples in the rest of the book.
    </para>
  </sect1>
</chapter><chapter id="variables-and-functions">
  <title>Variables and Functions</title>
  <para>
    Variables and functions are fundamental ideas that show up in
    virtually all programming languages. But OCaml has a different take
    on these basic concepts, and so we'll spend some time digging into
    the details of OCaml's variables and functions differ from what you
    may have seen elsewhere.
  </para>
  <sect1 id="variables">
    <title>Variables</title>
    <para>
      At its simplest, a variable is an identifier whose meaning is
      bound to a particular value. In OCaml these bindings are often
      introduced using the <literal>let</literal> keyword. When typed in
      at the prompt of the interpreter, a let binding has the following
      syntax.
    </para>
    <programlisting language="ocaml">
let &lt;identifier&gt; = &lt;expr&gt;
</programlisting>
    <para>
      As we'll see when we get to the module system in
      <xref linkend="files-modules-and-programs"/>, this same
      syntax is used for toplevel definitions in a module.
    </para>
    <para>
      Every variable binding has a <emphasis>scope</emphasis>, which is
      the portion of the code that can refer to that binding. The scope
      of a toplevel let binding is everything that follows it in the
      toplevel session (or in the remainder of the module).
    </para>
    <para>
      Here's a simple example.
    </para>
    <programlisting language="ocaml">
# let x = 3;;
val x : int = 3
# let y = 4;;
val y : int = 4
# let z = x + y;;
val z : int = 7
</programlisting>
    <para>
      <literal>let</literal> can also be used to create a variable
      binding whose scope is limited to a particular expression, using
      the following syntax.
    </para>
    <programlisting language="ocaml">
let &lt;identifier&gt; = &lt;expr1&gt; in &lt;expr2&gt;
</programlisting>
    <para>
      This first evaluates <emphasis><literal>expr1</literal></emphasis>
      and then evaluates <emphasis><literal>expr2</literal></emphasis>
      with <emphasis><literal>identifier</literal></emphasis> bound to
      whatever value was produced by the evaluation of
      <emphasis><literal>expr1</literal></emphasis>. Here's how it looks
      in practice.
    </para>
    <programlisting language="ocaml">
# let languages = &quot;OCaml,Perl,C++,C&quot;;;
val languages : string = &quot;OCaml,Perl,C++,C&quot;
# let dashed_languages =
    let language_list = String.split languages ~on:',' in
    String.concat ~sep:&quot;-&quot; language_list
  ;;
val dashed_languages : string = &quot;OCaml-Perl-C++-C&quot;
</programlisting>
    <para>
      Note that the scope of <literal>language_list</literal> is just
      the expression
      <literal>String.concat ~sep:&quot;-&quot; language_list</literal>,
      and is not available at the toplevel, as we can see if we try to
      access it now.
    </para>
    <programlisting language="ocaml">
# language_list;;
Characters 0-13:
  language_list;;
  ^^^^^^^^^^^^^
Error: Unbound value language_list
</programlisting>
    <para>
      A let binding in an inner scope can <emphasis>shadow</emphasis>,
      or hide, the definition from an outer scope. So, for example, we
      could have written the <literal>dashed_languages</literal> example
      as follows:
    </para>
    <programlisting language="ocaml">
# let languages = &quot;OCaml,Perl,C++,C&quot;;;
val languages : string = &quot;OCaml,Perl,C++,C&quot;
# let dashed_languages =
     let languages = String.split languages ~on:',' in
     String.concat ~sep:&quot;-&quot; languages
  ;;
val dashed_languages : Core.Std.String.t = &quot;OCaml-Perl-C++-C&quot;
</programlisting>
    <para>
      This time, in the inner scope we called the list of strings
      <literal>languages</literal> instead of
      <literal>language_list</literal>, thus hiding the original
      definition of <literal>languages</literal>. But once the
      definition of <literal>dashed_languages</literal> is complete, the
      inner scope has closed and the original definition of languages
      reappears.
    </para>
    <programlisting language="ocaml">
# languages;;
- : string = &quot;OCaml,Perl,C++,C&quot;
</programlisting>
    <para>
      One common idiom is to use a series of nested
      <literal>let</literal>/<literal>in</literal> expressions to build
      up the components of a larger computation. Thus, we might write:
    </para>
    <programlisting language="ocaml">
# let area_of_ring inner_radius outer_radius =
     let pi = acos (-1.) in
     let area_of_circle r = pi *. r *. r in
     area_of_circle outer_radius -. area_of_circle inner_radius
  ;;
# area_of_ring 1. 3.;;
- : float = 25.1327412287183449
</programlisting>
    <para>
      It's important not to confuse this sequence of let bindings with
      the modification of a mutable variable. How would
      <literal>area_of_ring</literal> be different, for example, if we
      had instead written this purposefully confusing bit of code:
    </para>
    <programlisting language="ocaml">
# let area_of_ring inner_radius outer_radius =
     let pi = acos (-1.) in
     let area_of_circle r = pi *. r *. r in
     let pi = 0. in
     area_of_circle outer_radius -. area_of_circle inner_radius
  ;;
</programlisting>
    <para>
      Here, we redefined <literal>pi</literal> to be zero after the
      definition of <literal>area_of_circle</literal>. You might think
      that this would mean that the result of the computation would now
      be zero, but you'd be wrong. In fact, the behavior of the function
      is unchanged. That's because the original definition of
      <literal>pi</literal> wasn't changed, it was just shadowed, so
      that any subsequent reference to <literal>pi</literal> would see
      the new definition of <literal>pi</literal> as zero. But there is
      no later use of <literal>pi</literal>, so the binding doesn't make
      a difference. Indeed, if you type the example above into the
      toplevel, OCaml will warn you that the definition is unused.
    </para>
    <programlisting language="ocaml">
Characters 126-128:
    let pi = 0. in
        ^^
Warning 26: unused variable pi.
</programlisting>
    <para>
      In OCaml, let bindings are immutable. As we'll see in
      <xref linkend="imperative-programming"/>, there are
      mutable values in OCaml, but no mutable variables.
    </para>
    <note> <title> 
    Why don't variables vary?
    </title>
    <para>
      One source confusion for people new to functional langauges is the
      fact that variables are typically immutable. This seems pretty
      surprising even on linguistic terms. Isn't the whole point of a
      variable that it can vary?
    </para>
    <para>
      The answer to this is that variables in a functional language are
      really more like variables in an equation. If you think about the
      mathematical equation <literal>x (y + z) = x y + x z</literal>,
      there's no notion of mutating the variables <literal>x</literal>,
      <literal>y</literal> and <literal>z</literal>. They vary in the
      sense that you can instantiate this equation with different
      numbers for those variables, and it still holds.
    </para>
    <para>
      The same is true in a functional language. A function can be
      applied to different inputs, and thus its variables will take on
      different values, even though there's absolutely no mutation.
    </para>
    </note>

    <sect2 id="pattern-matching-and-let">
      <title>Pattern matching and <literal>let</literal></title>
      <para>
        Another useful feature of let bindings is that they support the
        use of patterns on the left-hand side of the bind. Consider the
        following code, which uses <literal>List.unzip</literal>, a
        function for converting a list of pairs into a pair of lists.
      </para>
      <programlisting language="ocaml">
# let (ints,strings) = List.unzip [(1,&quot;one&quot;); (2,&quot;two&quot;); (3,&quot;three&quot;)]
val ints : int list = [1; 2; 3]
val strings : string list = [&quot;one&quot;; &quot;two&quot;; &quot;three&quot;]
</programlisting>
      <para>
        This actually binds two variables, one for each element of the
        pair. Using a pattern in a let-binding makes the most sense for
        a pattern that is <emphasis>irrefutable</emphasis>,
        <emphasis>i.e.</emphasis>, where any value of the type in
        question is guaranteed to match the pattern. Tuple and record
        patterns are irrefutable, but list patterns are not. Consider
        the following code that implements a function for up-casing the
        first element of a comma-separated list.
      </para>
      <programlisting language="ocaml">
# let upcase_first_entry line =
     let (key :: values) = String.split ~on:',' line in
     String.concat ~sep:&quot;,&quot; (String.uppercase key :: values)
  ;;
      Characters 40-53:
       let (key :: values) = String.split ~on:',' line in
            ^^^^^^^^^^^^^
Warning 8: this pattern-matching is not exhaustive.
Here is an example of a value that is not matched:
[]
val upcase_first_entry : string -&gt; string = &lt;fun&gt;
</programlisting>
      <para>
        This case can't really come up in practice, because
        <literal>String.split</literal> always returns a list with at
        least one element. But the compiler doesn't know this, and so it
        emits the warning. It's generally better to use a match
        statement to handle such cases explicitly:
      </para>
      <programlisting language="ocaml">
# let upcase_first_entry line =
     match String.split ~on:',' line with
     | [] -&gt; assert false (* String.split returns at least one element *)
     | first :: rest -&gt; String.concat ~sep:&quot;,&quot; (String.uppercase first :: rest)
  ;;
val upcase_first_entry : string -&gt; string = &lt;fun&gt;
</programlisting>
    </sect2>
    <sect2 id="letand-bindings">
      <title><literal>let</literal>/<literal>and</literal>
      bindings</title>
      <para>
        Another variant on the let binding is the use of
        <literal>and</literal> to join multiple variable definitions
        into a single declaration. For example, we can write:
      </para>
      <programlisting language="ocaml">
# let x = 100 and y = 3.5;;
val x : int = 100
val y : float = 3.5
</programlisting>
      <para>
        This can be useful when you want to create a number of new let
        bindings at once, without having each definition affect the
        next. So, if we wanted to create new bindings that swapped the
        values of <literal>x</literal> and <literal>y</literal>, we
        could write:
      </para>
      <programlisting language="ocaml">
# let x = y and y = x ;;
val x : float = 3.5
val y : int = 100
</programlisting>
      <para>
        Note that this is just shadowing the definitions of
        <literal>x</literal> and <literal>y</literal>, not mutating
        anything.
      </para>
      <para>
        Without this trick, we would need to do something like the
        following:
      </para>
      <programlisting language="ocaml">
# let tmp = x
  let x = y
  let y = tmp;;
val tmp : int = 100
val x : float = 3.5
val y : int = 100
</programlisting>
      <para>
        This use-case doesn't come up that often, however. Most of the
        time that <literal>and</literal> comes into play, it's used to
        define multiple mutually recursive values, which we'll learn
        about later in the chapter.
      </para>
      <para>
        Note that when doing a
        <literal>let</literal>/<literal>and</literal> style declaration,
        the order of execution of the right-hand side of the binds is
        undefined by the language definition, so one should not write
        code that relies on it. If you want to make sure about the order
        of evaluation, you should use a sequence of
        <literal>let</literal>/<literal>in</literal> bindings.
      </para>
    </sect2>
  </sect1>
  <sect1 id="functions">
    <title>Functions</title>
    <para>
      OCaml being a functional language, it's no surprise that functions
      are an important and pervasive element of programming in OCaml.
      Indeed, we've seen functions pop up already in many of the
      examples we've looked at thus far. But while we've introduced the
      basics of functions, we're now going to cover them in more depth,
      starting from the foundations.
    </para>
    <sect2 id="anonymous-functions">
      <title>Anonymous Functions</title>
      <para>
        We'll start by looking at the most basic style of function
        declaration in OCaml: the <emphasis>anonymous</emphasis>
        function. An anonymous function is a function value that is
        declared without being named. They can be declared using the
        <literal>fun</literal> keyword, as shown here.
      </para>
      <programlisting language="ocaml">
# (fun x -&gt; x + 1);;
- : int -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        Anonymous functions aren't named, but they can be used for many
        different purposes nonetheless. You can, for example, apply an
        anonymous function to an argument.
      </para>
      <programlisting language="ocaml">
# (fun x -&gt; x + 1) 7;;
- : int = 8
</programlisting>
      <para>
        Or pass it to another function. Passing functions to iteration
        functions like <literal>List.map</literal> is probably the most
        common use-case for anonymous functions.
      </para>
      <programlisting language="ocaml">
# List.map ~f:(fun x -&gt; x + 1) [1;2;3];;
- : int list = [2; 3; 4]
</programlisting>
      <para>
        Or even stuff them into a data structure.
      </para>
      <programlisting language="ocaml">
# let increments = [ (fun x -&gt; x + 1); (fun x -&gt; x + 2) ] ;;
val increments : (int -&gt; int) list = [&lt;fun&gt;; &lt;fun&gt;]
# List.map ~f:(fun f -&gt; f 5) increments;;
- : int list = [6; 7]
</programlisting>
      <para>
        It's worth stopping for a moment to puzzle this example out,
        since this kind of higher-order use of functions can be a bit
        obscure at first. The first thing to understand is the function
        <literal>(fun f -&gt; f 5)</literal>, which takes a function as
        its argument and applies that function to the number
        <literal>5</literal>. The invocation of
        <literal>List.map</literal> applies
        <literal>(fun f -&gt; f 5)</literal> to the elements of the
        <literal>increments</literal> list (which are themselves
        functions) and returns the list containing the results of these
        function applications.
      </para>
      <para>
        The key thing to understand is that functions are ordinary
        values in OCaml, and you can do everything with them that you'd
        do with an ordinary value, including passing them to and
        returning them from other functions and storing them in data
        structures. We even name functions in the same way that we name
        other values, by using a let binding.
      </para>
      <programlisting language="ocaml">
# let plusone = (fun x -&gt; x + 1);;
val plusone : int -&gt; int = &lt;fun&gt;
# plusone 3;;
- : int = 4
</programlisting>
      <para>
        Defining named functions is so common that there is a built in
        syntax for it. Thus, the following definition of
        <literal>plusone</literal> is equivalent to the definition
        above.
      </para>
      <programlisting language="ocaml">
# let plusone x = x + 1;;
val plusone : int -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        This is the most common and convenient way to declare a
        function, but syntactic niceties aside, the two styles of
        function definition are entirely equivalent.
      </para>
      <note>
      <title>
      <literal>let</literal> and <literal>fun</literal>
      </title>
      <para>
        Functions and let bindings have a lot to do with each other. In
        some sense, you can think of the argument of a function as a
        variable being bound to the value passed by the caller. Indeed,
        the following two expressions are nearly equivalent:
      </para>
      <programlisting language="ocaml">
# (fun x -&gt; x + 1) 7;;
- : int = 8
# let x = 7 in x + 1;;
- : int = 8
</programlisting>
      <para>
        This connection is important, and will come up more when
        programming in a monadic style, as we'll see in
        <xref linkend="concurrent-programming-with-async"/>.
      </para>
      </note>
    </sect2>
    <sect2 id="multi-argument-functions">
      <title>Multi-argument functions</title>
      <para>
        OCaml of course also supports multi-argument functions, for
        example:
      </para>
      <programlisting language="ocaml">
# let abs_diff x y = abs (x - y);;
val abs_diff : int -&gt; int -&gt; int = &lt;fun&gt;
# abs_diff 3 4;;
- : int = 1
</programlisting>
      <para>
        You may find the type signature of <literal>abs_diff</literal>
        with all of its arrows a little hard to parse. To understand
        what's going on, let's rewrite <literal>abs_diff</literal> in an
        equivalent form, using the <literal>fun</literal> keyword:
      </para>
      <programlisting language="ocaml">
# let abs_diff =
    (fun x -&gt; (fun y -&gt; abs (x - y)));;
val abs_diff : int -&gt; int -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        This rewrite makes it explicit that <literal>abs_diff</literal>
        is actually a function of one argument that returns another
        function of one argument, which itself returns the final
        computation. Because the functions are nested, the inner
        expression <literal>abs (x - y)</literal> has access to both
        <literal>x</literal>, which was captured by the first function
        application, and <literal>y</literal>, which was captured by the
        second one.
      </para>
      <para>
        This style of function is called a <emphasis>curried</emphasis>
        function. (Currying is named after Haskell Curry, a famous
        logician who had a significant impact on the design and theory
        of programming languages.) The key to interpreting the type
        signature of a curried function is the observation that
        <literal>-&gt;</literal> is right-associative. The type
        signature of <literal>abs_diff</literal> can therefore be
        parenthesized as follows. This doesn't change the meaning of the
        signature, but it makes it easier to see how the currying fits
        in.
      </para>
      <programlisting language="ocaml">
val abs_diff : int -&gt; (int -&gt; int)
</programlisting>
      <para>
        Currying is more than just a theoretical curiosity. You can make
        use of currying to specialize a function by feeding in some of
        the arguments. Here's an example where we create a specialized
        version of <literal>abs_diff</literal> that measures the
        distance of a given number from <literal>3</literal>.
      </para>
      <programlisting language="ocaml">
# let dist_from_3 = abs_diff 3;;
val dist_from_3 : int -&gt; int = &lt;fun&gt;
# dist_from_3 8;;
- : int = 5
# dist_from_3 (-1);;
- : int = 4
</programlisting>
      <para>
        The practice of applying some of the arguments of a curried
        function to get a new function is called <emphasis>partial
        application</emphasis>.
      </para>
      <para>
        Note that the <literal>fun</literal> keyword supports its own
        syntax for currying, so we the following definition of
        <literal>abs_diff</literal> is equivalent to the definition
        above.
      </para>
      <programlisting language="ocaml">
# let abs_diff = (fun x y -&gt; abs (x - y));;
</programlisting>
      <para>
        You might worry that curried functions are terribly expensive,
        but this is not the case. In OCaml, there is no penalty for
        calling a curried function with all of its arguments. (Partial
        application, unsurprisingly, does have a small extra cost.)
      </para>
      <para>
        Currying is not the only way of writing a multi-argument
        function in OCaml. It's also possible to use the different arms
        of a tuple as different arguments. So, we could write:
      </para>
      <programlisting language="ocaml">
# let abs_diff (x,y) = abs (x - y)
val abs_diff : int * int -&gt; int = &lt;fun&gt;
# abs_diff (3,4);;
- : int = 1
</programlisting>
      <para>
        OCaml handles this calling convention efficiently as well. In
        particular it does not generally have to allocate a tuple just
        for the purpose of sending arguments to a tuple-style function.
        (You can't, however, use partial application for this style of
        function.)
      </para>
      <para>
        There are small tradeoffs between these two approaches, but most
        of the time, one should stick to currying, since it's the
        default style in the OCaml world.
      </para>
    </sect2>
    <sect2 id="recursive-functions">
      <title>Recursive functions</title>
      <para>
        A function is <emphasis>recursive</emphasis> if it refers to
        itself in its definition. Recursion is important in any
        programming language, but is particularly important in
        functional languages, because it is the fundamental building
        block that is used for building looping constructs. (As we'll
        see in <xref linkend="imperative-programming"/>, OCaml
        also supports imperative looping constructs like
        <literal>for</literal> and <literal>while</literal>, but these
        are only useful when using OCaml's imperative features.)
      </para>
      <para>
        In order to define a recursive function, you need to mark the
        let binding as recursive with the <literal>rec</literal>
        keyword, as shown in this example:
      </para>
      <programlisting language="ocaml">
# let rec find_first_stutter list =
    match list with
    | [] | [_] -&gt;
      (* only zero or one elements, so no repeats *)
      None
    | x :: y :: tl -&gt;
      if x = y then Some x else find_first_stutter (y::tl)
   ;;
val find_first_stutter : 'a list -&gt; 'a option = &lt;fun&gt;
</programlisting>
      <para>
        Note that in the above, the pattern
        <literal>| [] | [_]</literal> is actually the combination of two
        patterns; <literal>[]</literal>, matching the empty list, and
        <literal>[_]</literal>, matching any single element list. The
        <literal>_</literal> is there so we don't have to put an
        explicit name on that single element.
      </para>
      <para>
        We can also define multiple mutually recursive values by using
        <literal>let rec</literal> and <literal>and</literal> together,
        as in this (gratuitously inefficient) example.
      </para>
      <programlisting language="ocaml">
# let rec is_even x =
    if x = 0 then true else is_odd (x - 1)
  and is_odd x =
    if x = 0 then false else is_even (x - 1)
 ;;
val is_even : int -&gt; bool = &lt;fun&gt;
val is_odd : int -&gt; bool = &lt;fun&gt;
# List.map ~f:is_even [0;1;2;3;4;5];;
- : bool Core.Std.List.t = [true; false; true; false; true; false]
# List.map ~f:is_odd [0;1;2;3;4;5];;
- : bool Core.Std.List.t = [false; true; false; true; false; true]
</programlisting>
      <para>
        OCaml distinguishes between non-recursive definitions (using
        <literal>let</literal>) and recursive definitions (using
        <literal>let rec</literal>) largely for technical reasons: the
        type-inference algorithm needs to know when a set of function
        definitions are mutually recursive, and for some technical
        reasons that don't apply to a pure language like Haskell, these
        have to be marked explicitly by the programmer.
      </para>
      <para>
        But this decision has some good effects. For one thing,
        recursive (and especially mutually recursive) definitions are
        harder to reason about than non-recursive definitions that
        proceed in order, each building on top of what has already been
        defined. It's therefore useful that, in the absence of an
        explicit marker, new definitions can only build upon ones that
        were previously defined.
      </para>
      <para>
        In addition, having a non-recursive form makes it easier to
        create a new definition that extends and supersedes an existing
        one by shadowing it.
      </para>
    </sect2>
    <sect2 id="prefix-and-infix-operators">
      <title>Prefix and Infix operators</title>
      <para>
        So far, we've seen examples of functions used in both prefix and
        infix style:
      </para>
      <programlisting language="ocaml">
# Int.max 3 4;;  (* prefix *)
- : int = 4
# 3 + 4;;        (* infix  *)
- : int = 7
</programlisting>
      <para>
        You might not have thought of the second example as an ordinary
        function, but it very much is. Infix operators like
        <literal>+</literal> really only differ syntactically from other
        functions. In fact, if we put parenthesis around an infix
        operator, you can use it as an ordinary prefix function.
      </para>
      <programlisting language="ocaml">
# (+) 3 4;;
- : int = 7
# List.map ~f:((+) 3) [4;5;6];;
- : int list = [7; 8; 9]
</programlisting>
      <para>
        In the second expression above, we've partially applied
        <literal>(+)</literal> to gain a function that increments its
        single argument by <literal>3</literal>, and then applied that
        to all the elements of a list.
      </para>
      <para>
        A function is treated syntactically as an operator if the name
        of that function is chosen from one of a specialized set of
        identifiers. This set includes any identifier that is a sequence
        of characters from the following set
      </para>
      <programlisting>
! $ % &amp; * + - . / : &lt; = &gt; ? @ ^ | ~
</programlisting>
      <para>
        or is one of a handful of pre-determined strings, including
        <literal>mod</literal>, the modulus operator, and
        <literal>lsl</literal>, for &quot;logical shift left&quot;, a
        bit-shifting operation.
      </para>
      <para>
        We can define (or redefine) the meaning of an operator as
        follows. Here's an example of a simple vector-addition operator
        on int pairs.
      </para>
      <programlisting language="ocaml">
# let (+!) (x1,y1) (x2,y2) = (x1 + x2, y1 + y2)
val ( +! ) : int * int -&gt; int * int -&gt; int *int = &lt;fun&gt;
# (3,2) +! (-2,4);;
- : int * int = (1,6)
</programlisting>
      <para>
        The syntactic role of an operator is typically determined by
        first character or two, though there are a few exceptions. This
        table breaks the different operators and other syntactic forms
        into groups from highest to lowest precedence, explaining how
        each behaves syntactically. We write <literal>!</literal>... to
        indicate the class of operators beginning with
        <literal>!</literal>.
      </para>
      <informaltable>
        <tgroup cols="2">
          <colspec colwidth="37*" align="left"/>
          <colspec colwidth="23*" align="left"/>
          <thead>
            <row>
              <entry>
                Prefix
              </entry>
              <entry>
                Usage
              </entry>
            </row>
          </thead>
          <tbody>
            <row>
              <entry>
                <literal>!</literal>..., <literal>?</literal>...,
                <literal>~</literal>...
              </entry>
              <entry>
                Unary prefix
              </entry>
            </row>
            <row>
              <entry>
                <literal>.</literal>, <literal>.(</literal>,
                <literal>.[</literal>
              </entry>
              <entry>
              </entry>
            </row>
            <row>
              <entry>
                function application, constructor,
                <literal>assert</literal>, <literal>lazy</literal>
              </entry>
              <entry>
                Left associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>-</literal>, <literal>-.</literal>
              </entry>
              <entry>
                Unary prefix
              </entry>
            </row>
            <row>
              <entry>
                <literal>**</literal>..., <literal>lsl</literal>,
                <literal>lsr</literal>, <literal>asr</literal>
              </entry>
              <entry>
                Right associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>*</literal>..., <literal>/</literal>...,
                <literal>%</literal>..., <literal>mod</literal>,
                <literal>land</literal>, <literal>lor</literal>,
                <literal>lxor</literal>
              </entry>
              <entry>
                Left associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>+</literal>, <literal>-</literal>
              </entry>
              <entry>
                Left associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>::</literal>
              </entry>
              <entry>
                Right associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>@</literal>..., <literal>^</literal>...
              </entry>
              <entry>
                Right associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>=</literal>..., <literal>&lt;</literal>...,
                <literal>&gt;</literal>..., <literal>|</literal>...,
                <literal>&amp;</literal>..., <literal>$</literal>...
              </entry>
              <entry>
                Left associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>&amp;</literal>, <literal>&amp;&amp;</literal>
              </entry>
              <entry>
                Right associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>or</literal>, <literal>||</literal>
              </entry>
              <entry>
                Right associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>,</literal>
              </entry>
              <entry>
              </entry>
            </row>
            <row>
              <entry>
                <literal>&lt;-</literal>, <literal>:=</literal>
              </entry>
              <entry>
                Right associative
              </entry>
            </row>
            <row>
              <entry>
                <literal>if</literal>
              </entry>
              <entry>
              </entry>
            </row>
            <row>
              <entry>
                <literal>;</literal>
              </entry>
              <entry>
                Right associative
              </entry>
            </row>
          </tbody>
        </tgroup>
      </informaltable>
      <para>
        There's one important special case: <literal>-</literal> and
        <literal>-.</literal>, which are the integer and floating point
        subtraction operators, can act as both prefix operators (for
        negation) and infix operators (for subtraction), So, both
        <literal>-x</literal> and <literal>x - y</literal> are
        meaningful expressions.
      </para>
      <para>
        Here's an example of a very useful operator that's defined in
        Core, following these rules. Here's the definition:
      </para>
      <programlisting language="ocaml">
# let (|&gt;) x f = f x ;;
val ( |&gt; ) : 'a -&gt; ('a -&gt; 'b) -&gt; 'b = &lt;fun&gt;
</programlisting>
      <para>
        It's not quite obvious at first what the purpose of this
        operator is: it just takes some value and a function, and
        applies the function to the value. But its utility is clearer
        when you see it in action. It works as a kind of sequencing
        operator, similar in spirit to using pipe in the UNIX shell.
        Consider, for example, the following code for printing out the
        unique elements of your <literal>PATH</literal>. Note that
        <literal>List.dedup</literal> below removes duplicates from a
        list by sorting the list using the provided comparison function.
      </para>
      <programlisting language="ocaml">
# Sys.getenv_exn &quot;PATH&quot;
  |&gt; String.split ~on:':'
  |&gt; List.dedup ~compare:String.compare
  |&gt; List.iter ~f:print_endline
  ;;
/bin
/opt/local/bin
/usr/bin
/usr/local/bin
- : unit = ()
</programlisting>
      <para>
        Note that we can do this without <literal>|&gt;</literal>, but
        the result is a bit more verbose.
      </para>
      <programlisting language="ocaml">
# let path = Sys.getenv_exn &quot;PATH&quot; in
  let split_path = String.split ~on:':' path in
  let deduped_path = List.dedup ~compare:String.compare split_path in
  List.iter ~f:print_endline deduped_path
  ;;
/bin
/opt/local/bin
/usr/bin
/usr/local/bin
- : unit = ()
</programlisting>
      <para>
        An important part of what's happening here is partial
        application. Normally, <literal>List.iter</literal> takes two
        arguments: a function to be called on each element of the list,
        and the list to iterate over. We can call
        <literal>List.iter</literal> with all it's arguments:
      </para>
      <programlisting language="ocaml">
# List.iter ~f:print_endline [&quot;Two&quot;; &quot;lines&quot;];;
Two
lines
- : unit = ()
</programlisting>
      <para>
        Or, we can pass it just the function argument, leaving us with a
        function for printing out a list of strings.
      </para>
      <programlisting language="ocaml">
# List.iter ~f:print_endline;;
- : string list -&gt; unit = &lt;fun&gt;
</programlisting>
      <para>
        It is this later form that we're using in the
        <literal>|&gt;</literal> pipeline above.
      </para>
      <para>
        Note that <literal>|&gt;</literal> only works in the intended
        way because it is left-associative. Indeed, let's see what
        happens if we try using a right associative operator, like (^!).
      </para>
      <programlisting language="ocaml">
# let (^!) = (|&gt;);;
val ( ^! ) : 'a -&gt; ('a -&gt; 'b) -&gt; 'b = &lt;fun&gt;
# Sys.getenv_exn &quot;PATH&quot;
  ^! String.split ~on:':'
  ^! List.dedup ~compare:String.compare
  ^! List.iter ~f:print_endline
  ;;
        Characters 93-119:
    ^! List.iter ~f:print_endline
       ^^^^^^^^^^^^^^^^^^^^^^^^^^
Error: This expression has type string list -&gt; unit
       but an expression was expected of type
         (string list -&gt; string list) -&gt; 'a
</programlisting>
      <para>
        The above type error is a little bewildering at first glance.
        What's going on is that, because <literal>^!</literal> is right
        associative, the operator is trying to feed the value
        <literal>List.dedup ~compare:String.compare</literal> to the
        function <literal>List.iter ~f:print_endline</literal>. But
        <literal>List.iter ~f:print_endline</literal> expects a list of
        strings as its input, not a function.
      </para>
      <para>
        The type error aside, this example highlights the importance of
        choosing the operator you use with care, particularly with
        respect to associativity.
      </para>
    </sect2>
    <sect2 id="declaring-functions-with-function">
      <title>Declaring functions with
      <literal>function</literal></title>
      <para>
        Another way to define a function is using the
        <literal>function</literal> keyword. Instead of having syntactic
        support for declaring multi-argument (curried) functions,
        <literal>function</literal> has built-in pattern matching.
        Here's an example:
      </para>
      <programlisting language="ocaml">
# let some_or_zero = function
     | Some x -&gt; x
     | None -&gt; 0
  ;;
val some_or_zero : int option -&gt; int = &lt;fun&gt;
# List.map ~f:some_or_zero [Some 3; None; Some 4];;
- : int list = [3; 0; 4]
</programlisting>
      <para>
        This is equivalent to combining a <literal>fun</literal> with
        <literal>match</literal>, as follows:
      </para>
      <programlisting language="ocaml">
# let some_or_zero num_opt =
    match num_opt with
    | Some x -&gt; x
    | None -&gt; 0
  ;;
val some_or_zero : int option -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        We can also combine the different styles of function declaration
        together, as in the following example where we declare a two
        argument (curried) function with a pattern-match on the second
        argument.
      </para>
      <programlisting language="ocaml">
# let some_or_default default = function
     | Some x -&gt; x
     | None -&gt; default
  ;;
# some_or_default 3 (Some 5);;
- : int = 5
# List.map ~f:(some_or_default 100) [Some 3; None; Some 4];;
- : int list = [3; 100; 4]
</programlisting>
      <para>
        Also, note the use of partial application to generate the
        function passed to <literal>List.map</literal>. In other words,
        <literal>some_or_default 100</literal> is a function that was
        created by feeding just the first argument to
        <literal>some_or_default</literal>.
      </para>
    </sect2>
    <sect2 id="labeled-arguments">
      <title>Labeled Arguments</title>
      <para>
        Up until now, we've written functions where the arguments are
        specified positionally, <emphasis>i.e.</emphasis>, by the order
        in which the arguments are passed to the function. OCaml also
        supports labeled arguments, which let you identify a function
        argument by name. Labels are marked by a leading tilde, and a
        label (followed by a colon) are put in front of the variable to
        be labeled.
      </para>
      <programlisting language="ocaml">
# let ratio ~num ~denom = float num /. float denom;;
val ratio : num:int -&gt; denom:int -&gt; float = &lt;fun&gt;
</programlisting>
      <para>
        We can then provide a labeled argument using a similar
        convention. As you can see, the arguments can be provided in any
        order.
      </para>
      <programlisting language="ocaml">
# ratio ~num:3 ~denom:10;;
- : float = 0.3
# ratio ~denom:10 ~num:3;;
- : float = 0.3
</programlisting>
      <para>
        OCaml also supports <emphasis>label punning</emphasis>, meaning
        that you get to drop the text after the <literal>:</literal> if
        the name of the label and the name of the variable being used
        are the same. We've seen above how label punning works when
        defining a function. The following shows how it can be used when
        invoking a function.
      </para>
      <programlisting language="ocaml">
# let num = 3;;
# let denom = 4;;
# ratio ~num ~denom;;
- : float = 0.75
</programlisting>
      <para>
        Labeled arguments are useful in a few different cases:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            When defining a function with lots of arguments. Beyond a
            certain number, arguments are easier to remember by name
            than by position.
          </para>
        </listitem>
        <listitem>
          <para>
            When defining functions that have multiple arguments that
            might get confused with each other. This is most at issue
            when the arguments are of the same type. For example,
            consider this signature for a function for extracting a
            substring of another string.
          </para>
          <programlisting language="ocaml">
val substring: string -&gt; int -&gt; int -&gt; string
</programlisting>
          <para>
            where the two ints are the starting position and length of
            the substring to extract. Labeled arguments can make this
            signature clearer:
          </para>
          <programlisting language="ocaml">
val substring: string -&gt; pos:int -&gt; len:int -&gt; string
</programlisting>
          <para>
            This improves the readability of both the signature and of
            client code that makes use of <literal>substring</literal>,
            and makes it harder to accidentally swap the position and
            the length.
          </para>
        </listitem>
        <listitem>
          <para>
            When the meaning of a particular argument is unclear from
            the type alone. For example, consider a function for
            creating a hashtable where the first argument is the initial
            size of the table, and the second argument is a flag which,
            when true, indicates that the hashtable will reduce its size
            when the hashtable contains few elements. The following
            signature doesn't give you much of a hint as to the meaning
            of the arguments.
          </para>
          <programlisting language="ocaml">
val create_hashtable : int -&gt; bool -&gt; ('a,'b) Hashtable.t
</programlisting>
          <para>
            but with labeled arguments, we can make the intent much
            clearer.
          </para>
          <programlisting language="ocaml">
val create_hashtable : init_size:int -&gt; allow_shrinking:bool -&gt; ('a,'b) Hashtable.t
</programlisting>
        </listitem>
        <listitem>
          <para>
            When you want flexibility on the order in which arguments
            are passed. Consider a function like
            <literal>List.iter</literal>, that takes two arguments: a
            function, and a list of elements to call that function on. A
            common pattern is to partially apply
            <literal>List.iter</literal> by giving it just the function,
            as in the following example from earlier in the chapter.
            This requires putting the function argument first.
          </para>
          <programlisting language="ocaml">
# Sys.getenv_exn &quot;PATH&quot;
  |&gt; String.split ~on:':'
  |&gt; List.dedup ~compare:String.compare
  |&gt; List.iter ~f:print_endline
  ;;
</programlisting>
          <para>
            In other cases, you want to put the function argument
            second. One common reason is readability. In particular, a
            function that spans multiple lines is easiest to read when
            it's the last argument provided.
          </para>
        </listitem>
      </itemizedlist>
      <sect3 id="higher-order-functions-and-labels">
        <title>Higher-order functions and labels</title>
        <para>
          One surprising gotcha with labeled arguments is that while
          order doesn't matter when calling a function with labeled
          arguments, it does matter in a higher-order context,
          <emphasis>e.g.</emphasis>, when passing a function with
          labeled arguments to another function. Here's an example.
        </para>
        <programlisting language="ocaml">
# let apply_to_tuple f (first,second) = f ~first ~second;;
val apply_to_tuple : (first:'a -&gt; second:'b -&gt; 'c) -&gt; 'a * 'b -&gt; 'c = &lt;fun&gt;
</programlisting>
        <para>
          Here, the definition of <literal>apply_to_tuple</literal> sets
          up the expectation that its first argument is a function with
          two labeled arguments, <literal>first</literal> and
          <literal>second</literal>, listed in that order. We could have
          defined <literal>apply_to_tuple</literal> differently to
          change the order in which the labeled arguments were listed.
        </para>
        <programlisting language="ocaml">
# let apply_to_tuple_2 f (first,second) = f ~second ~first;;
val apply_to_tuple_2 : (second:'a -&gt; first:'b -&gt; 'c) -&gt; 'b * 'a -&gt; 'c = &lt;fun&gt;
</programlisting>
        <para>
          It turns out this order of listing matters. In particular, if
          we define a function that has a different order
        </para>
        <programlisting language="ocaml">
# let divide ~first ~second = first / second;;
val divide : first:int -&gt; second:int -&gt; int = &lt;fun&gt;
</programlisting>
        <para>
          we'll find that it can't be passed in to
          <literal>apply_to_tuple_2</literal>.
        </para>
        <programlisting language="ocaml">
# apply_to_tuple_2 divide (3,4);;
Characters 15-21:
  apply_to_tuple_2 divide (3,4);;
                   ^^^^^^
Error: This expression has type first:int -&gt; second:int -&gt; int
       but an expression was expected of type second:'a -&gt; first:'b -&gt; 'c
</programlisting>
        <para>
          But, it works smoothly with the original
          <literal>apply_to_tuple</literal>.
        </para>
        <programlisting language="ocaml">
# let apply_to_tuple f (first,second) = f ~first ~second;;
val apply_to_tuple : (first:'a -&gt; second:'b -&gt; 'c) -&gt; 'a * 'b -&gt; 'c = &lt;fun&gt;
# apply_to_tuple divide (3,4);;
- : int = 0
</programlisting>
        <para>
          So, even though the order of labeled arguments usually doesn't
          matter, it will sometimes bite you in higher-ordered contexts,
          where you're passing functions as arguments to other functions
          as we were in the above examples.
        </para>
      </sect3>
    </sect2>
    <sect2 id="optional-arguments">
      <title>Optional arguments</title>
      <para>
        An optional argument is like a labeled argument that the caller
        can choose whether or not to provide. Optional arguments are
        passed in using the same syntax as labeled arguments, and,
        similarly to labeled arguments, optional arguments can be
        provided in any order.
      </para>
      <para>
        Here's an example of a string concatenation function with an
        optional separator. This function uses the <literal>^</literal>
        operator for simple pairwise string concatenation.
      </para>
      <programlisting language="ocaml">
# let concat ?sep x y =
     let sep = match sep with None -&gt; &quot;&quot; | Some x -&gt; x in
     x ^ sep ^ y
  ;;
val concat : ?sep:string -&gt; string -&gt; string -&gt; string = &lt;fun&gt;
# concat &quot;foo&quot; &quot;bar&quot;;;             (* without the optional argument *)
- : string = &quot;foobar&quot;
# concat ~sep:&quot;:&quot; &quot;foo&quot; &quot;bar&quot;;;    (* with the optional argument    *)
- : string = &quot;foo:bar&quot;
</programlisting>
      <para>
        Here, <literal>?</literal> is used in the definition of the
        function to mark <literal>sep</literal> as optional. And while
        the caller can pass a value of type <literal>string</literal>
        for <literal>sep</literal>, internally to the function,
        <literal>sep</literal> is seen as a
        <literal>string option</literal>, with <literal>None</literal>
        appearing when <literal>sep</literal> is not provided by the
        caller.
      </para>
      <para>
        In the above example, we had a bit of code to substitute in the
        empty string when no argument was provided. This is a common
        enough pattern that there's an explicit syntax for providing a
        default value, which allows us to write
        <literal>concat</literal> even more concisely.
      </para>
      <programlisting language="ocaml">
# let concat ?(sep=&quot;&quot;) x y = x ^ sep ^ y ;;
val concat : ?sep:string -&gt; string -&gt; string -&gt; string = &lt;fun&gt;
</programlisting>
      <para>
        Optional arguments are very useful, but they're also easy to
        abuse. The key advantage of optional arguments is that they let
        you write functions with multiple arguments that users can
        ignore most of the time, only worrying about them when they
        specifically want to invoke those options.
      </para>
      <para>
        The downside is that the caller may be unaware that there is a
        choice to be made, and so may unknowingly (and wrongly) pick
        that default behavior. Optional arguments really only make sense
        when the extra concision of omitting the argument overwhelms the
        corresponding loss of explicitness.
      </para>
      <para>
        This means that rarely used functions should not have optional
        arguments. A good rule of thumb is not to use optional arguments
        for functions internal to a module, <emphasis>i.e.</emphasis>,
        functions that are not included in the module's interface, or
        <literal>mli</literal> file. We'll learn more about
        <literal>mli</literal>s in
        <xref linkend="files-modules-and-programs"/>.
      </para>
      <sect3 id="explicit-passing-of-an-optional-argument">
        <title>Explicit passing of an optional argument</title>
        <para>
          Under the covers, a function with an optional argument
          receives <literal>None</literal> when the caller doesn't
          provide the argument, and <literal>Some</literal> when it
          does. But the <literal>Some</literal> and
          <literal>None</literal> are normally not explicitly passed in
          by the caller.
        </para>
        <para>
          But sometimes, passing in <literal>Some</literal> or
          <literal>None</literal> explicitly is exactly what you want.
          OCaml lets you do this by using <literal>?</literal> instead
          of <literal>~</literal> to mark the argument. Thus, the
          following two lines are equivalent ways of specifying the
          <literal>sep</literal> argument to concat.
        </para>
        <programlisting language="ocaml">
# concat ~sep:&quot;:&quot; &quot;foo&quot; &quot;bar&quot;;; (* provide the optional argument *)
- : string = &quot;foo:bar&quot;
# concat ?sep:(Some &quot;:&quot;) &quot;foo&quot; &quot;bar&quot;;; (* pass an explicit [Some] *)
- : string = &quot;foo:bar&quot;
</programlisting>
        <para>
          And the following two lines are equivalent ways of calling
          <literal>concat</literal> without specifying
          <literal>sep</literal>.
        </para>
        <programlisting language="ocaml">
# concat &quot;foo&quot; &quot;bar&quot;;; (* don't provide the optional argument *)
- : string = &quot;foobar&quot;
# concat ?sep:None &quot;foo&quot; &quot;bar&quot;;; (* explicitly pass `None` *)
- : string = &quot;foobar&quot;
</programlisting>
        <para>
          One use-case for this is when you want to define a wrapper
          function that mimics the optional arguments of the function
          it's wrapping. For example, imagine we wanted to create a
          function called <literal>uppercase_concat</literal>, which is
          the same as <literal>concat</literal> except that it converts
          the first string that it's passed to uppercase. We could write
          the function as follows.
        </para>
        <programlisting language="ocaml">
# let uppercase_concat ?(sep=&quot;&quot;) a b = concat ~sep (String.uppercase a) b ;;
val uppercase_concat : ?sep:string -&gt; string -&gt; string -&gt; string = &lt;fun&gt;
# uppercase_concat &quot;foo&quot; &quot;bar&quot;;;
- : string = &quot;FOObar&quot;
# uppercase_concat &quot;foo&quot; &quot;bar&quot; ~sep:&quot;:&quot;;;
- : string = &quot;FOO:bar&quot;
</programlisting>
        <para>
          In the way we've written it, we've been forced to separately
          make the decision as to what the default separator is. Thus,
          if we later change <literal>concat</literal>'s default
          behavior, we'll need to remember to change
          <literal>uppercase_concat</literal> to match it.
        </para>
        <para>
          Instead, we can have <literal>uppercase_concat</literal>
          simply pass through the optional argument to
          <literal>concat</literal> using the <literal>?</literal>
          syntax.
        </para>
        <programlisting language="ocaml">
# let uppercase_concat ?sep a b = concat ?sep (String.uppercase a) b ;;
val uppercase_concat : ?sep:string -&gt; string -&gt; string -&gt; string = &lt;fun&gt;
</programlisting>
        <para>
          Now, if someone calls <literal>uppercase_concat</literal>
          without an argument, an explicit <literal>None</literal> will
          be passed to <literal>concat</literal>, leaving
          <literal>concat</literal> to decide what the default behavior
          should be.
        </para>
      </sect3>
      <sect3 id="inference-of-labeled-and-optional-arguments">
        <title>Inference of labeled and optional arguments</title>
        <para>
          One subtle aspect of labeled and optional arguments is how
          they are inferred by the type system. Consider the following
          example for computing numerical derivatives of a function of
          two dimensions. The function takes an argument
          <literal>delta</literal> which determines the scale at which
          to compute the derivative, values <literal>x</literal> and
          <literal>y</literal> which determine which point to compute
          the derivative at, and the function <literal>f</literal> whose
          derivative is being computed. The function
          <literal>f</literal> itself takes two labeled arguments
          <literal>x</literal> and <literal>y</literal>. Note that you
          can use an apostrophe as part of a variable name, so
          <literal>x'</literal> and <literal>y'</literal> are just
          ordinary variables.
        </para>
        <programlisting language="ocaml">
# let numeric_deriv ~delta ~x ~y ~f =
    let x' = x +. delta in
    let y' = y +. delta in
    let base = f ~x ~y in
    let dx = (f ~x:x' ~y -. base) /. delta in
    let dy = (f ~x ~y:y' -. base) /. delta in
    (dx,dy)
  ;;
val numeric_deriv :
  delta:float -&gt;
  x:float -&gt; y:float -&gt; f:(x:float -&gt; y:float -&gt; float) -&gt; float * float =
  &lt;fun&gt;
</programlisting>
        <para>
          In principle, it's not obvious how the order of the arguments
          to <literal>f</literal> should be chosen. Since labeled
          arguments can be passed in arbitrary order, it seems like it
          could as well be
          <literal>y:float -&gt; x:float -&gt; float</literal> as it is
          <literal>x:float -&gt; y:float -&gt; float</literal>.
        </para>
        <para>
          Even worse, it would be perfectly consistent for
          <literal>f</literal> to take an optional argument instead of a
          labeled one, which could lead to this type signature for
          <literal>numeric_deriv</literal>:
        </para>
        <programlisting language="ocaml">
val numeric_deriv :
  delta:float -&gt;
  x:float -&gt; y:float -&gt; f:(?x:float -&gt; y:float -&gt; float) -&gt; float * float =
  &lt;fun&gt;
</programlisting>
        <para>
          Since there are multiple plausible types to choose from, OCaml
          needs some heuristic for choosing between them. The heuristic
          the compiler uses is to prefer labels to options, and to
          choose the order of arguments that shows up in the source
          code.
        </para>
        <para>
          Note that these heuristics might at different points in the
          source suggest different types. Here's a version of
          <literal>numeric_deriv</literal> where different invocations
          of <literal>f</literal> list the arguments in different
          orders.
        </para>
        <programlisting language="ocaml">
# let numeric_deriv ~delta ~x ~y ~f =
    let x' = x +. delta in
    let y' = y +. delta in
    let base = f ~x ~y in
    let dx = (f ~y ~x:x' -. base) /. delta in
    let dy = (f ~x ~y:y' -. base) /. delta in
    (dx,dy)
  ;;
Characters 131-132:
      let dx = (f ~y ~x:x' -. base) /. delta in
                ^
Error: This function is applied to arguments
in an order different from other calls.
This is only allowed when the real type is known.
</programlisting>
        <para>
          As suggested by the error message, we can get OCaml to accept
          the fact that <literal>f</literal> is used with different
          argument orders if we provide explicit type information. Thus,
          the following code compiles without error, due to the type
          annotation on <literal>f</literal>.
        </para>
        <programlisting language="ocaml">
# let numeric_deriv ~delta ~x ~y ~(f: x:float -&gt; y:float -&gt; float) =
    let x' = x +. delta in
    let y' = y +. delta in
    let base = f ~x ~y in
    let dx = (f ~y ~x:x' -. base) /. delta in
    let dy = (f ~x ~y:y' -. base) /. delta in
    (dx,dy)
  ;;
val numeric_deriv :
  delta:float -&gt;
  x:float -&gt; y:float -&gt; f:(x:float -&gt; y:float -&gt; float) -&gt; float * float =
  &lt;fun&gt;
</programlisting>
      </sect3>
      <sect3 id="optional-arguments-and-partial-application">
        <title>Optional arguments and partial application</title>
        <para>
          Optional arguments can be tricky to think about in the
          presence of partial application. We can of course partially
          apply the optional argument itself:
        </para>
        <programlisting language="ocaml">
# let colon_concat = concat ~sep:&quot;:&quot;;;
val colon_concat : string -&gt; string -&gt; string = &lt;fun&gt;
# colon_concat &quot;a&quot; &quot;b&quot;;;
- : string = &quot;a:b&quot;
</programlisting>
        <para>
          But what happens if we partially apply just the first
          argument?
        </para>
        <programlisting language="ocaml">
# let prepend_pound = concat &quot;# &quot;;;
val prepend_pound : string -&gt; string = &lt;fun&gt;
# prepend_pound &quot;a BASH comment&quot;;;
- : string = &quot;# a BASH comment&quot;
</programlisting>
        <para>
          The optional argument <literal>?sep</literal> has now
          disappeared, or been <emphasis>erased</emphasis>. Indeed, if
          we try to pass in that optional argument now, it will be
          rejected.
        </para>
        <programlisting language="ocaml">
# prepend_pound &quot;a BASH comment&quot; ~sep:&quot;:&quot;;;
Characters 0-13:
  prepend_pound &quot;a BASH comment&quot; ~sep:&quot;:&quot;;;
  ^^^^^^^^^^^^^
Error: This function has type string -&gt; string
       It is applied to too many arguments; maybe you forgot a `;'.
</programlisting>
        <para>
          So when does OCaml decide to erase an optional argument?
        </para>
        <para>
          The rule is: an optional argument is erased as soon as the
          first positional (<emphasis>i.e.</emphasis>, neither labeled
          nor optional) argument defined <emphasis>after</emphasis> the
          optional argument is passed in. That explains the behavior of
          <literal>prepend_pound</literal> above. But if we had instead
          defined <literal>concat</literal> with the optional argument
          in the second position:
        </para>
        <programlisting language="ocaml">
# let concat x ?(sep=&quot;&quot;) y = x ^ sep ^ y ;;
val concat : string -&gt; ?sep:string -&gt; string -&gt; string = &lt;fun&gt;
</programlisting>
        <para>
          then application of the first argument would not cause the
          optional argument to be erased.
        </para>
        <programlisting language="ocaml">
# let prepend_pound = concat &quot;# &quot;;;
val prepend_pound : ?sep:string -&gt; string -&gt; string = &lt;fun&gt;
# prepend_pound &quot;a BASH comment&quot;;;
- : string = &quot;# a BASH comment&quot;
# prepend_pound &quot;a BASH comment&quot; ~sep:&quot;--- &quot;;;
- : string = &quot;# --- a BASH comment&quot;
</programlisting>
        <para>
          However, if all arguments to a function are presented at once,
          then erasure of optional arguments isn't applied until all of
          the arguments are passed in. This preserves our ability to
          pass in optional arguments anywhere on the argument list.
          Thus, we can write:
        </para>
        <programlisting language="ocaml">
# concat &quot;a&quot; &quot;b&quot; ~sep:&quot;=&quot;;;
- : string = &quot;a=b&quot;
</programlisting>
        <para>
          An optional argument that doesn't have any following
          positional arguments can't be erased at all, which leads to a
          compiler warning.
        </para>
        <programlisting language="ocaml">
# let concat x y ?(sep=&quot;&quot;) = x ^ sep ^ y ;;
Characters 15-38:
  let concat x y ?(sep=&quot;&quot;) = x ^ sep ^ y ;;
                 ^^^^^^^^^^^^^^^^^^^^^^^
Warning 16: this optional argument cannot be erased.
val concat : string -&gt; string -&gt; ?sep:string -&gt; string = &lt;fun&gt;
</programlisting>
        <para>
          And indeed, when we provide the two positional arguments, the
          <literal>sep</literal> argument is not erased, instead
          returning a function that expects the <literal>sep</literal>
          argument to be provided.
        </para>
        <programlisting language="ocaml">
# concat &quot;a&quot; &quot;b&quot;;;
- : ?sep:string -&gt; string = &lt;fun&gt;
</programlisting>
      </sect3>
    </sect2>
  </sect1>
</chapter><chapter id="lists-options-and-patterns">
  <title>Lists, options and patterns</title>
  <para>
    <emphasis>(This chapter yet to be completed)</emphasis>
  </para>
</chapter><chapter id="files-modules-and-programs">
  <title>Files, Modules and Programs</title>
  <para>
    We've so far experienced OCaml largely through the toplevel. As you
    move from exercises to real-world programs, you'll need to leave the
    toplevel behind and start building programs from files. Files are
    more than just a convenient way to store and manage your code; in
    OCaml, they also act as boundaries that divide your program into
    conceptual units.
  </para>
  <para>
    In this chapter, we'll show you how to build an OCaml program from a
    collection of files, as well as the basics of working with modules
    and module signatures.
  </para>
  <sect1 id="single-file-programs">
    <title>Single File Programs</title>
    <para>
      We'll start with an example: a utility that reads lines from
      <literal>stdin</literal>, computing a frequency count of the lines
      that have been read in. At the end, the 10 lines with the highest
      frequency counts are written out. Here's a simple implementation,
      which we'll save as the file <literal>freq.ml</literal>. Note that
      we're using several functions from the
      <literal>List.Assoc</literal> module, which provides utility
      functions for interacting with association lists,
      <emphasis>i.e.</emphasis>, lists of key/value pairs.
    </para>
    <programlisting language="ocaml">
(* freq.ml: basic implementation *)

open Core.Std

let build_counts () =
  In_channel.fold_lines stdin ~init:[] ~f:(fun counts line -&gt;
    let count =
      match List.Assoc.find counts line with
      | None -&gt; 0
      | Some x -&gt; x
    in
    List.Assoc.add counts line (count + 1)
  )

let () =
  build_counts ()
  |&gt; List.sort ~cmp:(fun (_,x) (_,y) -&gt; compare y x)
  |&gt; (fun l -&gt; List.take l 10)
  |&gt; List.iter ~f:(fun (line,count) -&gt; printf &quot;%3d: %s\n&quot; count line)
</programlisting>
    <para>
      The function <literal>build_counts</literal> reads in lines from
      <literal>stdin</literal>, constructing from those lines an
      associating list with the frequencies of each line. It does this
      by invoking <literal>In_channel.fold_lines</literal> (similar to
      the function <literal>List.fold</literal> described in
      <xref linkend="lists-options-and-patterns"/>), which
      reads through the lines one by one, calling the provided fold
      function for each line to update the accumulator. That accumulator
      is initialized to the empty list.
    </para>
    <para>
      With <literal>build_counts</literal> defined, we then call the
      function to build the associating list, sort that list be
      frequency in descending order, grab the first 10 elements off the
      list, and the iterate over those ten elements and print them to
      the screen. These operations are tied together using the operator,
      as described in
      <xref linkend="variables-and-functions"/>.
    </para>
    <note><title>
    Where is the main function?
    </title>
    <para>
      Unlike C, programs in OCaml do not have a unique
      <literal>main</literal> function. When an OCaml program is
      evaluated, all the statements in the implementation files are
      evaluated in order. These implementation files can contain
      arbitrary expressions, not just function definitions. In this
      example, the declaration starting with <literal>let () =</literal>
      plays the role of the <literal>main</literal> declaration, kicking
      off the processing. But really the entire file is evaluated at
      startup, and so in some sense the full codebase is one big
      <literal>main</literal> function.
    </para>
    </note>
    <para>
      If we weren't using Core or any other external libraries, we could
      build the executable like this:
    </para>
    <programlisting>
ocamlc freq.ml -o freq
</programlisting>
    <para>
      But in this case, this command will fail with the error
      <literal>Unbound module Core</literal>. We need a somewhat more
      complex invocation to get Core linked in:
    </para>
    <programlisting>
ocamlfind ocamlc -linkpkg -thread -package core freq.ml -o freq
</programlisting>
    <para>
      Here we're using <literal>ocamlfind</literal>, a tool which itself
      invokes other parts of the ocaml toolchain (in this case,
      <literal>ocamlc</literal>) with the appropriate flags to link in
      particular libraries and packages. Here,
      <literal>-package core</literal> is asking
      <literal>ocamlfind</literal> to link in the Core library,
      <literal>-linkpkg</literal> is required to do the final linking in
      of packages for building a runnable executable, and
      <literal>-thread</literal> turns on threading support, which is
      required for Core.
    </para>
    <para>
      While this works well enough for a one-file project, more
      complicated builds will require a tool to orchestrate the build.
      One great tool for this task is <literal>ocamlbuild</literal>,
      which is shipped with the OCaml compiler. We'll talk more about
      <literal>ocamlbuild</literal> in
      <xref linkend="packaging"/>, but for now, we'll just
      walk through the steps required for this simple application.
      First, create a <literal>_tags</literal> file, containing the
      following lines.
    </para>
    <programlisting>
true:package(core),thread,annot,debugging
</programlisting>
    <para>
      The purpose of the <literal>_tags</literal> file is to specify
      which compilation options are required for which files. In this
      case, we're telling <literal>ocamlbuild</literal> to link in the
      <literal>core</literal> package and to turn on threading, output
      of annotation files, and debugging support for all files (the
      condition <literal>true</literal> causes the options to be applied
      to every file in the project.)
    </para>
    <para>
      We can then invoke <literal>ocamlbuild</literal> to build the
      executable in question.
    </para>
    <programlisting>
$ ocamlbuild -use-ocamlfind freq.byte
</programlisting>
    <para>
      If we'd invoked <literal>ocamlbuild</literal> with a target of
      <literal>freq.native</literal> instead of
      <literal>freq.byte</literal>, we would have gotten native-code
      instead.
    </para>
    <para>
      We can now run the our program from the command-line. The
      following line extracts strings from the
      <literal>ocamlopt</literal> executable, and then reports the most
      frequently occurring ones.
    </para>
    <programlisting>
$ strings `which ocamlopt` | ./freq.byte
 13: movq
 10: cmpq
  8: &quot;, &amp;
  7: .globl
  6: addq
  6: leaq
  5: &quot;, $
  5: .long
  5: .quad
  4: &quot;, '
</programlisting>
    <note><title>
    Byte-code vs native-code
    </title>
    <para>
      OCaml ships with two compilers---the <literal>ocamlc</literal>
      byte-code compiler, and the <literal>ocamlopt</literal>
      native-code compiler. Programs compiled with
      <literal>ocamlc</literal> are interpreted by a virtual machine,
      while programs compiled with <literal>ocamlopt</literal> are
      compiled to native machine code to be run on a specific operating
      system and processor architecture.
    </para>
    <para>
      Aside from performance, executables generated by the two compilers
      have nearly identical behavior. There are a few things to be aware
      of. First, the byte-code compiler can be used on more
      architectures, and has some better tool support; in particular,
      the OCaml debugger only works with byte-code. Also, the byte-code
      compiler compiles faster than the native code compiler. Also, in
      order to run a bytecode executable you typically need to have
      OCaml installed on the system in question. That's not strictly
      required, though, since you can build a byte-code executable with
      an embedded runtime, using the <literal>-custom</literal> compiler
      flag.
    </para>
    <para>
      As a general matter, production executables should usually be
      built using the native-code compiler, but it sometimes makes sense
      to use bytecode for development builds. And, of course, bytecode
      makes sense when targeting a platform not supported by the native
      code compiler.
    </para>
    </note>

  </sect1>
  <sect1 id="multi-file-programs-and-modules">
    <title>Multi-file programs and modules</title>
    <para>
      Source files in OCaml are tied into the module system, with each
      file compiling down into a module whose name is derived from the
      name of the file. We've encountered modules before, for example,
      when we used functions like <literal>find</literal> and
      <literal>add</literal> from the <literal>List.Assoc</literal>
      module. At it's simplest, you can think of a module as a
      collection of definitions that are stored within a namespace.
    </para>
    <para>
      Let's consider how we can use modules to refactor the
      implementation of <literal>freq.ml</literal>. Remember that the
      variable <literal>counts</literal> contains an association list
      representing the counts of the lines seen so far. But updating an
      association list takes time linear in the length of the list,
      meaning that the time complexity of processing a file is quadratic
      in the number of distinct lines in the file.
    </para>
    <para>
      We can fix this problem by replacing association lists with a more
      efficient data structure. To do that, we'll first factor out the
      key functionality into a separate module with an explicit
      interface. We can consider alternative (and more efficient)
      implementations once we have a clear interface to program against.
    </para>
    <para>
      We'll start by creating a file, <literal>counter.ml</literal>,
      that contains the logic for maintaining the association list used
      to describe the counts. The key function, called
      <literal>touch</literal>, updates the association list with the
      information that a given line should be added to the frequency
      counts.
    </para>
    <programlisting language="ocaml">
(* counter.ml: first version *)

open Core.Std

let touch t s =
  let count =
    match List.Assoc.find t s with
    | None -&gt; 0
    | Some x -&gt; x
  in
  List.Assoc.add t s (count + 1)
</programlisting>
    <para>
      We can now rewrite <literal>freq.ml</literal> to use
      <literal>Counter</literal>. Note that the resulting code can still
      be built with <literal>ocamlbuild</literal>, which will discover
      dependencies and realize that <literal>counter.ml</literal> needs
      to be compiled.
    </para>
    <programlisting language="ocaml">
(* freq.ml: using Counter *)
open Core.Std

let build_counts () =
  In_channel.fold_lines stdin ~init:[] ~f:Counter.touch

let () =
  build_counts ()
  |&gt; List.sort counts ~cmp:(fun (_,x) (_,y) -&gt; Int.descending x y)
  |&gt; (fun l -&gt; List.take l 10)
  |&gt; List.iter ~f:(fun (line,count) -&gt; printf &quot;%3d: %s\n&quot; count line)
</programlisting>
    <sect2 id="signatures-and-abstract-types">
      <title>Signatures and Abstract Types</title>
      <para>
        While we've pushed some of the logic to the
        <literal>Counter</literal> module, the code in
        <literal>freq.ml</literal> can still depend on the details of
        the implementation of <literal>Counter</literal>. Indeed, if you
        look at the definition of build_counts:
      </para>
      <programlisting language="ocaml">
let build_counts () =
  In_channel.fold_lines stdin ~init:[] ~f:Counter.touch
</programlisting>
      <para>
        you'll see that it depends on the fact that the empty set of
        frequency counts is represented as an empty list. We'd like to
        prevent this kind of dependency, so that we can change the
        implementation of <literal>Counter</literal> without needing to
        change client code like that in <literal>freq.ml</literal>.
      </para>
      <para>
        The first step towards hiding the implementation details of
        <literal>Counter</literal> is to create an interface file,
        <literal>counter.mli</literal>, which controls how
        <literal>counter</literal> is accessed. Let's start by writing
        down a simple descriptive interface, <emphasis>i.e.</emphasis>,
        an interface that describes what's currently available in
        <literal>Counter</literal> without hiding anything. We'll use
        <literal>val</literal> declarations in the
        <literal>mli</literal>, which have the following syntax
      </para>
      <programlisting>
val &lt;identifier&gt; : &lt;type&gt;
</programlisting>
      <para>
        and are used to expose the existence of a given value in the
        module. Here's an interface that describes the current contents
        of <literal>Counter</literal>. We can save this as
        <literal>counter.mli</literal> and compile, and the program will
        build as before.
      </para>
      <programlisting language="ocaml">
(* counter.mli: descriptive interface *)
open Core.Std

val touch : (string * int) list -&gt; string -&gt; (string * int) list
</programlisting>
      <note><title>
      Auto-generating <literal>mli</literal> files
      </title>
      <para>
        In general, if you don't want to construct an mli entirely by
        hand, you can ask OCaml to autogenerate one for you from the
        source, which you can then adjust to fit your needs. In this
        case, we can write:
      </para>
      <programlisting>
$ ocamlbuild -use-ocamlfind counter.inferred.mli
</programlisting>
      <para>
        Which will generate the file
        <literal>_build/counter.inferred.mli</literal>, with the
        following contents.
      </para>
      <programlisting>
$ cat _build/counter.inferred.mli
val touch :
  ('a, int) Core.Std.List.Assoc.t -&gt; 'a -&gt; ('a, int) Core.Std.List.Assoc.t
</programlisting>
      <para>
        This is equivalent to the mli that we generated, but is a little
        more verbose. In general, you want to use autogenerated
        <literal>mli</literal>'s as a starting point only. There's no
        replacement for a careful consideration of what should be
        included in the interface of your module and of how that should
        be organized, documented and formatted.
      </para>
      </note>

      <para>
        To actually hide the fact that frequency counts are represented
        as association lists, we need to make the type of frequency
        counts <emphasis>abstract</emphasis>. A type is abstract if its
        name is exposed in the interface, but its definition is not.
        Here's an abstract interface for <literal>Counter</literal>:
      </para>
      <programlisting language="ocaml">
(* counter.mli: abstract interface *)

open Core.Std

type t

val empty : t
val to_list : t -&gt; (string * int) list
val touch : t -&gt; string -&gt; t
</programlisting>
      <para>
        Note that we needed to add <literal>empty</literal> and
        <literal>to_list</literal> to <literal>Counter</literal>, since
        otherwise, there would be no way to create a
        <literal>Counter.t</literal> or get data out of one.
      </para>
      <para>
        Here's a rewrite of <literal>counter.ml</literal> to match this
        signature.
      </para>
      <programlisting language="ocaml">
(* counter.ml: implementation matching abstract interface *)

open Core.Std

type t = (string * int) list

let empty = []

let to_list x = x

let touch t s =
  let count =
    match List.Assoc.find t s with
    | None -&gt; 0
    | Some x -&gt; x
  in
  List.Assoc.add t s (count + 1)
</programlisting>
      <para>
        If we now try to compile <literal>freq.ml</literal>, we'll get
        the following error:
      </para>
      <programlisting>
File &quot;freq.ml&quot;, line 11, characters 20-22:
Error: This expression has type 'a list
       but an expression was expected of type Counter.t
</programlisting>
      <para>
        This is because <literal>freq.ml</literal> depends on the fact
        that frequency counts are represented as association lists, a
        fact that we've just hidden. We just need to fix the code to use
        <literal>Counter.empty</literal> instead of
        <literal>[]</literal> and <literal>Counter.to_list</literal> to
        get the association list out at the end for processing and
        printing.
      </para>
      <para>
        Now we can turn to optimizing the implementation of
        <literal>Counter</literal>. Here's an alternate and far more
        efficient implementation, based on the <literal>Map</literal>
        datastructure in Core.
      </para>
      <programlisting language="ocaml">
(* counter.ml: efficient version *)

open Core.Std

type t = int String.Map.t

let empty = String.Map.empty

let to_list t = Map.to_alist t

let touch t s =
  let count =
    match Map.find t s with
    | None -&gt; 0
    | Some x -&gt; x
  in
  Map.add t ~key:s ~data:(count + 1)
</programlisting>
    </sect2>
  </sect1>
  <sect1 id="more-on-modules-and-signatures">
    <title>More on modules and signatures</title>
    <sect2 id="concrete-types-in-signatures">
      <title>Concrete types in signatures</title>
      <para>
        In our frequency-count example, the module
        <literal>Counter</literal> had an abstract type
        <literal>Counter.t</literal> for representing a collection of
        frequency counts. Sometimes, you'll want to make a type in your
        interface <emphasis>concrete</emphasis>, by including the type
        definition in the interface.
      </para>
      <para>
        For example, imagine we wanted to add a function to
        <literal>Counter</literal> for returning the line with the
        median frequency count. If the number of lines is even, then
        there is no precise median, so the function would return the two
        lines before and after the median instead. We'll use a custom
        type to represent the fact that there are two possible return
        values. Here's a possible implementation.
      </para>
      <programlisting language="ocaml">
type median = | Median of string
              | Before_and_after of string * string

let median t =
  let sorted_strings = List.sort (Map.to_alist t)
      ~cmp:(fun (_,x) (_,y) -&gt; Int.descending x y)
  in
  let len = List.length sorted_strings in
  if len = 0 then failwith &quot;median: empty frequency count&quot;;
  let nth n = fst (List.nth_exn sorted_strings n) in
  if len mod 2 = 1
  then Median (nth (len/2))
  else Before_and_after (nth (len/2 - 1), nth (len/2));;
</programlisting>
      <para>
        Now, to expose this usefully in the interface, we need to expose
        both the function and the type <literal>median</literal> with
        its definition. We'd do that by adding these lines to the
        <literal>counter.mli</literal>:
      </para>
      <programlisting language="ocaml">
type median = | Median of string
              | Before_and_after of string * string

val median : t -&gt; median
</programlisting>
      <para>
        The decision of whether a given type should be abstract or
        concrete is an important one. Abstract types give you more
        control over how values are created and accessed, and makes it
        easier to enforce invariants beyond the what is enforced by the
        type itself; concrete types let you expose more detail and
        structure to client code in a lightweight way. The right choice
        depends very much on the context.
      </para>
    </sect2>
    <sect2 id="the-include-statement">
      <title>The <literal>include</literal> statement</title>
      <para>
        OCaml provides a number of tools for manipulating modules. One
        particularly useful one is the <literal>include</literal>
        statement, which is used to include the contents of one module
        into another.
      </para>
      <para>
        One natural application of <literal>include</literal> is to
        create one module which is an extension of another one. For
        example, imagine you wanted to build an extended version of the
        <literal>List</literal> module, where you've added some
        functionality not present in the module as distributed in Core.
        We can do this easily using <literal>include</literal>:
      </para>
      <programlisting language="ocaml">
(* ext_list.ml: an extended list module *)

open Core.Std

(* The new function we're going to add *)
let rec intersperse list el =
  match list with
  | [] | [ _ ]   -&gt; list
  | x :: y :: tl -&gt; x :: el :: intersperse (y::tl) el

(* The remainder of the list module *)
include List
</programlisting>
      <para>
        Now, what about the interface of this new module? It turns out
        that include works on the signature language as well, so we can
        pull essentially the same trick to write an
        <literal>mli</literal> for this new module. The only trick is
        that we need to get our hands on the signature for the list
        module, which can be done using
        <literal>module type of</literal>.
      </para>
      <programlisting language="ocaml">
(* ext_list.mli: an extended list module *)

open Core.Std

(* Include the interface of the list module from Core *)
include (module type of List)

(* Signature of function we're adding *)
val intersperse : 'a list -&gt; 'a -&gt; 'a list
</programlisting>
      <para>
        And we can now use <literal>Ext_list</literal> as a replacement
        for <literal>List</literal>. If we want to use
        <literal>Ext_list</literal> in preference to
        <literal>List</literal> in our project, we can create a file of
        common definitions:
      </para>
      <programlisting language="ocaml">
(* common.ml *)

module List = Ext_list
</programlisting>
      <para>
        And if we then put <literal>open Common</literal> after
        <literal>open Core.Std</literal> at the top of each file in our
        project, then references to <literal>List</literal> will
        automatically go to <literal>Ext_list</literal> instead.
      </para>
    </sect2>
    <sect2 id="modules-within-a-file">
      <title>Modules within a file</title>
      <para>
        Up until now, we've only considered modules that correspond to
        files, like <literal>counter.ml</literal>. But modules (and
        module signatures) can be nested inside other modules. As a
        simple example, consider a program that needs to deal with
        multiple identifier like usernames and hostnames. If you just
        represent these as strings, then it becomes easy to confuse one
        with the other.
      </para>
      <para>
        A better approach is to mint new abstract types for each
        identifier, where those types are under the covers just
        implemented as strings. That way, the type system will prevent
        you from confusing a username with a hostname, and if you do
        need to convert, you can do so using explicit conversions to and
        from the string type.
      </para>
      <para>
        Here's how you might create such an abstract type, within a
        sub-module:
      </para>
      <programlisting language="ocaml">
open Core.Std

module Username : sig
  type t
  val of_string : string -&gt; t
  val to_string : t -&gt; string
end = struct
  type t = string
  let of_string x = x
  let to_string x = x
end
</programlisting>
      <para>
        Note that the <literal>to_string</literal> and
        <literal>of_string</literal> functions above are implemented
        simply as the identity function, which means they have no
        runtime effect. They are there purely as part of the discipline
        that they enforce on the code through the type system.
      </para>
      <para>
        The basic structure of a module declaration like this is:
      </para>
      <programlisting language="ocaml">
module &lt;name&gt; : &lt;signature&gt; = &lt;implementation&gt;
</programlisting>
      <para>
        We could have written this slightly differently, by giving the
        signature its own toplevel <literal>module type</literal>
        declaration, making it possible to create multiple distinct
        types with the same underlying implementation in a lightweight
        way.
      </para>
      <programlisting language="ocaml">
open Core.Std

module type ID = sig
  type t
  val of_string : string -&gt; t
  val to_string : t -&gt; string
end

module String_id = struct
  type t = string
  let of_string x = x
  let to_string x = x
end

module Username : ID = String_id
module Hostname : ID = String_id

type session_info = { user: Username.t;
                      host: Hostname.t;
                      when_started: Time.t;
                    }

let sessions_have_same_user s1 s2 =
  s1.user = s2.host
</programlisting>
      <para>
        The above code has a fairly obvious bug, and indeed, the
        compiler will refuse to compile it, spitting out the following
        error.
      </para>
      <programlisting>
File &quot;buggy.ml&quot;, line 25, characters 12-19:
Error: This expression has type Hostname.t
       but an expression was expected of type Username.t
Command exited with code 2.
</programlisting>
      <para>
        We can also combine this with the use of the include statement
        to add some extra functionality to such a module. Thus, we could
        have rewritten the definition of <literal>Hostname</literal>
        above as follows to add a function
        <literal>Hostname.mine</literal> that returns the hostname of
        the present machine.
      </para>
      <programlisting language="ocaml">
module Hostname : sig
  include ID
  val mine : unit -&gt; t
end = struct
  include String_id
  let mine = Unix.gethostname
end
</programlisting>
    </sect2>
    <sect2 id="opening-modules">
      <title>Opening modules</title>
      <para>
        One useful primitive in OCaml's module language is the
        <literal>open</literal> statement. We've seen that already in
        the <literal>open Core.Std</literal> that has been at the top of
        our source files.
      </para>
      <para>
        The basic purpose of <literal>open</literal> is to extend the
        namespaces that OCaml searches when trying to resolve an
        identifier. Roughly, if you open a module <literal>M</literal>,
        then every subsequent time you look for an identifier
        <literal>foo</literal>, the module system will look in
        <literal>M</literal> for a value named <literal>foo</literal>.
        This is true for all kinds of identifiers, including types, type
        constructors, values and modules.
      </para>
      <para>
        <literal>open</literal> is essential when dealing with something
        like a standard library, but it's generally good style to keep
        opening of modules to a minimum. Opening a module is basically a
        tradeoff between terseness and explicitness - the more modules
        you open, the harder it is to look at an identifier and figure
        out where it's defined.
      </para>
      <para>
        Here's some general advice on how to deal with opens.
      </para>
      <itemizedlist>
        <listitem>
          <para>
            Opening modules at the toplevel of a module should be done
            quite sparingly, and generally only with modules that have
            been specifically designed to be opened, like
            <literal>Core.Std</literal> or
            <literal>Option.Monad_infix</literal>.
          </para>
        </listitem>
        <listitem>
          <para>
            If you do need to do an open, it's better to do a
            <emphasis>local open</emphasis>. There are two syntaxes for
            local opens. For example, you can write:
          </para>
          <programlisting language="ocaml">
let average x y =
  let open Int64 in
  x + y / of_int 2
</programlisting>
          <para>
            In the above, <literal>of_int</literal> and the infix
            operators are the ones from <literal>Int64</literal> module.
          </para>
          <para>
            There's another even more lightweight syntax for local
            opens, which is particularly useful for small expressions:
          </para>
          <programlisting language="ocaml">
let average x y =
  Int64.(x + y / of_int 2)
</programlisting>
        </listitem>
        <listitem>
          <para>
            An alternative to local opens that makes your code terser
            without giving up on explicitness is to locally rebind the
            name of a module. So, instead of writing:
          </para>
          <programlisting language="ocaml">
let print_median m =
   match m with
   | Counter.Median string -&gt; printf &quot;True median:\n   %s\n&quot;
   | Counter.Before_and_after of before * after -&gt;
     printf &quot;Before and after median:\n   %s\n   %s\n&quot; before after
</programlisting>
          <para>
            ...you could write
          </para>
          <programlisting language="ocaml">
let print_median m =
   let module C = Counter in
   match m with
   | C.Median string -&gt; printf &quot;True median:\n   %s\n&quot;
   | C.Before_and_after of before * after -&gt;
     printf &quot;Before and after median:\n   %s\n   %s\n&quot; before after
</programlisting>
          <para>
            Because the module name <literal>C</literal> only exists for
            a short scope, it's easy to read and remember what
            <literal>C</literal> stands for. Rebinding modules to very
            short names at the toplevel of your module is usually a
            mistake.
          </para>
        </listitem>
      </itemizedlist>
    </sect2>
    <sect2 id="common-errors-with-modules">
      <title>Common errors with modules</title>
      <para>
        When OCaml compiles a program with an <literal>ml</literal> and
        an <literal>mli</literal>, it will complain if it detects a
        mismatch between the two. Here are some of the common errors
        you'll run into.
      </para>
      <sect3 id="type-mismatches">
        <title>Type mismatches</title>
        <para>
          The simplest kind of error is where the type specified in the
          signature does not match up with the type in the
          implementation of the module. As an example, if we replace the
          <literal>val</literal> declaration in
          <literal>counter.mli</literal> by swapping the types of the
          first two arguments:
        </para>
        <programlisting language="ocaml">
val touch : string -&gt; t -&gt; t
</programlisting>
        <para>
          and then try to compile <literal>Counter</literal> (by writing
          <literal>ocamlbuild -use-ocamlfind counter.cmo</literal>. The
          <literal>cmo</literal> file is a compiled object file,
          containing the bytecode-compiled version of a module), we'll
          get the following error:
        </para>
        <programlisting>
File &quot;counter.ml&quot;, line 1, characters 0-1:
Error: The implementation counter.ml
       does not match the interface counter.cmi:
       Values do not match:
         val touch :
           ('a, int) Core.Std.Map.t -&gt; 'a -&gt; ('a, int) Core.Std.Map.t
       is not included in
         val touch : string -&gt; t -&gt; t
</programlisting>
        <para>
          This error message is a bit intimidating at first, and it
          takes a bit of thought to see why the first type for touch
          (which comes from the implementation) doesn't match the second
          one (which comes from the interface). The key thing to
          remember is that <literal>t</literal> is a
          <literal>Core.Std.Map.t</literal>, at which point you can see
          that the error is a mismatch in the order of arguments to
          <literal>touch</literal>.
        </para>
        <para>
          There's no denying that learning to decode such error messages
          is difficult at first, and takes some getting used to. But in
          time, decoding these errors becomes second nature.
        </para>
      </sect3>
      <sect3 id="missing-definitions">
        <title>Missing definitions</title>
        <para>
          We might decide that we want a new function in
          <literal>Counter</literal> for pulling out the frequency count
          of a given string. We can update the <literal>mli</literal> by
          adding the following line.
        </para>
        <programlisting language="ocaml">
val count : t -&gt; string -&gt; int
</programlisting>
        <para>
          Now, if we try to compile without actually adding the
          implementation, we'll get this error:
        </para>
        <programlisting>
File &quot;counter.ml&quot;, line 1, characters 0-1:
Error: The implementation counter.ml
       does not match the interface counter.cmi:
       The field `count' is required but not provided
</programlisting>
        <para>
          A missing type definition will lead to a similar error.
        </para>
      </sect3>
      <sect3 id="type-definition-mismatches">
        <title>Type definition mismatches</title>
        <para>
          Type definitions that show up in an <literal>mli</literal>
          need to match up with corresponding definitions in the
          <literal>ml</literal>. Consider again the example of the type
          <literal>median</literal>. The order of the declaration of
          variants matters to the OCaml compiler so, if the definition
          of <literal>median</literal> in the implementation lists those
          options in a different order:
        </para>
        <programlisting language="ocaml">
type median = | Before_and_after of line * line
              | Median of line
</programlisting>
        <para>
          that will lead to a compilation error:
        </para>
        <programlisting>
File &quot;counter.ml&quot;, line 1, characters 0-1:
Error: The implementation counter.ml
       does not match the interface counter.cmi:
       Type declarations do not match:
         type median = Before_and_after of string * string | Median of string
       is not included in
         type median = Median of string | Before_and_after of string * string
       Their first fields have different names, Before_and_after and Median.
</programlisting>
        <para>
          Order is similarly important in other parts of the signature,
          including the order in which record fields are declared and
          the order of arguments (including labeled and optional
          arguments) to a function.
        </para>
      </sect3>
      <sect3 id="cyclic-dependencies">
        <title>Cyclic dependencies</title>
        <para>
          In most cases, OCaml doesn't allow circular dependencies,
          <emphasis>i.e.</emphasis>, a collection of definitions that
          all refer to each other. If you want to create such
          definitions, you typically have to mark them specially. For
          example, when defining a set of mutually recursive values
          (like the definition of <literal>is_even</literal> and
          <literal>is_odd</literal> in
          <xref linkend="recursive-functions"/>), you need to
          define them using <literal>let rec</literal> rather than
          ordinary <literal>let</literal>.
        </para>
        <para>
          The same is true at the module level. By default, circular
          dependencies between modules is not allowed, and indeed,
          circular dependencies among files is never allowed.
        </para>
        <para>
          The simplest case of this is that a module can not directly
          refer to itself (although definitions within a module can
          refer to each other in the ordinary way). So, if we tried to
          add a reference to <literal>Counter</literal> from within
          <literal>counter.ml</literal>:
        </para>
        <programlisting language="ocaml">
let singleton l = Counter.touch Counter.empty
</programlisting>
        <para>
          then when we try to build, we'll get this error:
        </para>
        <programlisting>
File &quot;counter.ml&quot;, line 17, characters 18-31:
Error: Unbound module Counter
Command exited with code 2.
</programlisting>
        <para>
          The problem manifests in a different way if we create circular
          references between files. We could create such a situation by
          adding a reference to Freq from <literal>counter.ml</literal>,
          <emphasis>e.g.</emphasis>, by adding the following line:
        </para>
        <programlisting language="ocaml">
let build_counts = Freq.build_counts
</programlisting>
        <para>
          In this case, <literal>ocamlbuild</literal> will notice the
          error and complain:
        </para>
        <programlisting>
Circular dependencies: &quot;freq.cmo&quot; already seen in
  [ &quot;counter.cmo&quot;; &quot;freq.cmo&quot; ]
</programlisting>
      </sect3>
    </sect2>
  </sect1>
</chapter><chapter id="records">
  <title>Records</title>
  <para>
    One of OCaml's best features is its concise and expressive system
    for declaring new datatypes. Two key elements of that system are
    <emphasis>records</emphasis> and <emphasis>variants</emphasis>, both
    of which we discussed briefly in
    <xref linkend="a-guided-tour"/>. In this chapter we'll
    cover records in more depth, covering more of the details of how
    they work, as well as advice on how to use them effectively in your
    software designs.
  </para>
  <para>
    A record represents a collection of values stored together as one,
    where each component is identified by a different field name. The
    basic syntax for a record type declaration is as follows.
  </para>
  <programlisting language="ocaml">
type &lt;record-name&gt; =
  { &lt;field-name&gt; : &lt;type-name&gt; ;
    &lt;field-name&gt; : &lt;type-name&gt; ;
    ...
  }
</programlisting>
  <para>
    Here's a simple example, a <literal>host_info</literal> record that
    summarizes information about a given computer.
  </para>
  <programlisting language="ocaml">
# type host_info =
    { hostname   : string;
      os_name    : string;
      os_release : string;
      cpu_arch   : string;
    };;
</programlisting>
  <para>
    We can construct a <literal>host_info</literal> just as easily. The
    following code uses the <literal>Shell</literal> module from
    <literal>Core_extended</literal> to dispatch commands to the shell
    to extract the information we need about the computer we're running
    on.
  </para>
  <programlisting language="ocaml">
# open Core_extended.Std;;
# let my_host =
    let sh = Shell.sh_one_exn in
    { hostname   = sh &quot;hostname&quot;;
      os_name    = sh &quot;uname -s&quot;;
      os_release = sh &quot;uname -r&quot;;
      cpu_arch   = sh &quot;uname -p&quot;;
    };;
val my_host : host_info =
  {hostname = &quot;Yarons-MacBook-Air.local&quot;; os_name = &quot;Darwin&quot;;
   os_release = &quot;11.4.0&quot;; cpu_arch = &quot;i386&quot;}
</programlisting>
  <para>
    You might wonder how the compiler inferred that
    <literal>my_host</literal> is of type <literal>host_info</literal>.
    The hook that the compiler uses in this case to figure out the type
    is the record field names. It turns out that, within a given scope,
    each record field name is associated with a unique record type.
    Later in the chapter, we'll talk about what to do when you want to
    have the same record fields in multiple records.
  </para>
  <para>
    Once we have a record value in hand, we can extract elements from
    the record field using dot-notation.
  </para>
  <programlisting language="ocaml">
# my_host.cpu_arch;;
- : string = &quot;i386&quot;
</programlisting>
  <sect1 id="patterns-and-exhaustiveness">
    <title>Patterns and exhaustiveness</title>
    <para>
      Another way of getting information out of a record is by using a
      pattern match, as in the definition of
      <literal>host_info_to_string</literal> below.
    </para>
    <programlisting language="ocaml">
# let host_info_to_string { hostname = h; os_name = os;
                            os_release = r; cpu_arch = c } =
       sprintf &quot;%s (%s %s / %s)&quot; h os r c;;
    val host_info_to_string : host_info -&gt; string = &lt;fun&gt;
# host_info_to_string my_host;;
- : string = &quot;Yarons-MacBook-Air.local (Darwin 11.4.0 / i386)&quot;
</programlisting>
    <para>
      Note that the pattern that we used had only a single case, rather
      than using several cases separated by <literal>|</literal>s. We
      only needed a single pattern because record patterns are
      <emphasis>irrefutable</emphasis>, meaning that, because the layout
      of a record is always the same, a record pattern match will never
      fail at runtime. In general, types with a fixed structure, like
      records and tuples, have irrefutable patterns, whereas types with
      variable structure, like lists and variants, do not.
    </para>
    <para>
      Another important characteristic of record patterns is that they
      don't need to be complete; a pattern can mention only a subset of
      the fields in the record. This can be convenient, but it can also
      be error prone. In particular, this means that when new fields are
      added to the record, code that should be updated to react to the
      presence of those new fields will not be flagged by the compiler.
    </para>
    <para>
      As an example, imagine that we wanted to add a new field to our
      <literal>host_info</literal> record called
      <literal>os_version</literal>, as shown below.
    </para>
    <programlisting language="ocaml">
# type host_info =
    { hostname   : string;
      os_name    : string;
      os_release : string;
      cpu_arch   : string;
      os_version : string;
    };;
</programlisting>
    <para>
      The code for <literal>host_info_to_string</literal> would continue
      to compile without change. In this particular case, it's pretty
      clear that you might want to update
      <literal>host_info_to_string</literal> in order to take into
      account the new field, and it would be nice if the type system
      would give you a warning about the change.
    </para>
    <para>
      Happily, OCaml does offer an optional warning for missing fields
      in a record pattern. With that warning turned on (which you can do
      in the toplevel by typing
      <literal>#warnings &quot;+9&quot;</literal>), the compiler will
      warn about the missing field.
    </para>
    <programlisting language="ocaml">
# warnings &quot;+9&quot;;;
# let host_info_to_string { hostname = h; os_name = os;
                            os_release = r; cpu_arch = c } =
       sprintf &quot;%s (%s %s / %s)&quot; h os r c;;
    Characters 24-112:
  ........................{ hostname = h; os_name = os;
                              os_release = r; cpu_arch = c }..
Warning 9: the following labels are not bound in this record pattern:
os_version
Either bind these labels explicitly or add '; _' to the pattern.
val host_info_to_string : host_info -&gt; string = &lt;fun&gt;
</programlisting>
    <para>
      We can disable the warning for a given pattern by explicitly
      acknowledging that we are ignoring extra fields. This is done by
      adding an underscore to the pattern, as shown below.
    </para>
    <programlisting language="ocaml">
# let host_info_to_string { hostname = h; os_name = os;
                            os_release = r; cpu_arch = c; _ } =
       sprintf &quot;%s (%s %s / %s)&quot; h os r c;;
    val host_info_to_string : host_info -&gt; string = &lt;fun&gt;
</programlisting>
    <para>
      Generally, the right default is to turn the warning for incomplete
      record matches on, and to explicitly disable it with an
      <literal>_</literal> where necessary.
    </para>
  </sect1>
  <sect1 id="field-punning">
    <title>Field punning</title>
    <para>
      When the name of a variable coincides with the name of a record
      field, OCaml provides some handy syntactic shortcuts. For example,
      the pattern in the following function binds all of the fields in
      question to variables of the same name. This is called
      <emphasis>field punning</emphasis>.
    </para>
    <programlisting language="ocaml">
# let host_info_to_string { hostname; os_name; os_release; cpu_arch } =
     sprintf &quot;%s (%s %s / %s)&quot; hostname os_name os_release cpu_arch;;
  val host_info_to_string : host_info -&gt; string = &lt;fun&gt;
</programlisting>
    <para>
      Field punning can also be used to construct a record. Consider the
      following code for generating a <literal>host_info</literal>
      record.
    </para>
    <programlisting language="ocaml">
# let my_host =
    let sh cmd = Shell.sh_one_exn cmd in
    let hostname   = sh &quot;hostname&quot; in
    let os_name    = sh &quot;uname -s&quot; in
    let os_release = sh &quot;uname -r&quot; in
    let cpu_arch   = sh &quot;uname -p&quot; in
    { hostname; os_name; os_release; cpu_arch };;
val my_host : host_info =
  {hostname = &quot;Yarons-MacBook-Air.local&quot;; os_name = &quot;Darwin&quot;;
   os_release = &quot;11.4.0&quot;; cpu_arch = &quot;i386&quot;}
</programlisting>
    <para>
      In the above code, we defined variables corresponding to the
      record fields first, and then the record declaration itself simply
      listed the fields that needed to be included.
    </para>
    <para>
      You can take advantage of both field punning and label punning
      when writing a function for constructing a record from labeled
      arguments, as shown below.
    </para>
    <programlisting language="ocaml">
# let create_host_info ~hostname ~os_name ~os_release ~cpu_arch =
    let hostname = String.lowercase hostname in
    { hostname; os_name; os_release; cpu_arch };;
</programlisting>
    <para>
      This is considerably more concise than what you would get without
      punning at all.
    </para>
    <programlisting language="ocaml">
let create_host_info ~hostname:hostname ~os_name:os_name
   ~os_release:os_release ~cpu_arch:cpu_arch =
    let hostname = String.lowercase hostname in
    { hostname = hostname ; os_name = os_name;
      os_release = os_release; cpu_arch = cpu_arch };;
</programlisting>
    <para>
      Together, labeled arguments, field names, and field and label
      punning, encourage a style where you propagate the same names
      throughout your code-base. This is generally good practice, since
      it encourages consistent naming, which makes it easier for new
      people to navigate your source.
    </para>
  </sect1>
  <sect1 id="reusing-field-names">
    <title>Reusing field names</title>
    <para>
      Defining records with the same field names can be problematic.
      Let's consider a simple example: building types to represent the
      protocol used for a logging server. The following types represent
      messages a server might receive from a client.
    </para>
    <para>
      Below, the <literal>log_entry</literal> message is used to deliver
      a log entry to the server for processing. The
      <literal>logon</literal> message is sent when a client initiates a
      connection, and includes the identity of the user connecting and
      credentials used for authentication. Finally, the
      <literal>heartbeat</literal> message is periodically sent by the
      client to demonstrate to the server that the client is alive and
      connected. All of these messages include a session id and the time
      the message was generated.
    </para>
    <programlisting language="ocaml">
# type log_entry =
    { session_id: string;
      time: Time.t;
      important: bool;
      message: string;
    }
  type heartbeat =
    { session_id: string;
      time: Time.t;
      status_message: string;
    }
  type logon =
    { session_id: string;
      time: Time.t;
      user: string;
      credentials: string;
    }
;;
</programlisting>
    <para>
      The fact that we reused field names will cause trouble when we try
      to construct a message.
    </para>
    <programlisting language="ocaml">
# let create_log_entry ~session_id ~important message =
     { time = Time.now (); session_id; important; message }
  ;;
    Characters 75-129:
       { time = Time.now (); session_id; important; message }
       ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
Error: The record field label important belongs to the type log_entry
       but is mixed here with labels of type logon
</programlisting>
    <para>
      The problem is that the declaration of <literal>logon</literal>
      (and <literal>heartbeat</literal>) shadowed some of the fields of
      <literal>log_entry</literal>. As a result, the fields
      <literal>time</literal> and <literal>session_id</literal> are
      assumed to be fields of <literal>logon</literal>, and
      <literal>important</literal> and <literal>message</literal>, which
      were not shadowed, are assumed to be fields of
      <literal>log_entry</literal>. The compiler therefore complains
      that we're trying to construct a record with fields from two
      different record types.
    </para>
    <para>
      There are two common solutions to this problem. The first is to
      add a prefix to each field name to make it unique, as shown below.
    </para>
    <programlisting language="ocaml">
# type log_entry =
    { log_entry_session_id: string;
      log_entry_time: Time.t;
      log_entry_important: bool;
      log_entry_message: string;
    }
  type heartbeat =
    { heartbeat_session_id: string;
      heartbeat_time: Time.t;
      heartbeat_status_message: string;
    }
  type logon =
    { logon_session_id: string;
      logon_time: Time.t;
      logon_user: string;
      logon_credentials: string;
    }
;;
</programlisting>
    <para>
      This eliminates the collisions and is simple enough to do. But it
      leaves you with awkwardly named record fields, and adds needless
      repetition and verbosity to your code.
    </para>
    <para>
      Another approach is to mint a module for each type. We'll talk
      more about modules more in
      <xref linkend="files-modules-and-programs"/>, but for
      now, you can think of a module as a way of collecting values and
      types together in a named package.
    </para>
    <para>
      Packing types into modules is actually a broadly useful idiom (and
      one used quite extensively by Core), providing for each type a
      name-space within which to put related values. When using this
      style, it is standard practice to name the type associated with
      the module <literal>t</literal>. Using this style we would write:
    </para>
    <programlisting language="ocaml">
# module Log_entry = struct
    type t =
      { session_id: string;
        time: Time.t;
        important: bool;
        message: string;
      }
  end
  module Heartbeat = struct
    type t =
      { session_id: string;
        time: Time.t;
        status_message: string;
      }
  end
  module Logon = struct
    type t =
      { session_id: string;
        time: Time.t;
        user: string;
        credentials: string;
      }
  end;;
</programlisting>
    <para>
      Now, our heartbeat-creation function can be rendered as follows.
    </para>
    <programlisting language="ocaml">
# let create_log_entry ~session_id ~important message =
     { Log_entry.time = Time.now (); Log_entry.session_id;
       Log_entry.important; Log_entry.message }
  ;;
val create_log_entry :
  session_id:string -&gt; important:bool -&gt; string -&gt; Log_entry.t = &lt;fun&gt;
</programlisting>
    <para>
      The module name <literal>Log_entry</literal> is required to
      qualify the fields, because this function is outside of the
      <literal>Log_entry</literal> module where the record was defined.
      OCaml only requires the module qualification for one record field,
      however, so we can write this more concisely.
    </para>
    <programlisting language="ocaml">
# let create_log_entry ~session_id ~important message =
     { Log_entry. time = Time.now (); session_id; important; message }
  ;;
val create_log_entry :
  session_id:string -&gt; important:bool -&gt; string -&gt; Log_entry.t = &lt;fun&gt;
</programlisting>
    <para>
      This is not restricted to constructing a record; we can use the
      same trick when pattern-matching:
    </para>
    <programlisting language="ocaml">
# let message_to_string { Log_entry. important; message; _ } =
    if important then String.uppercase message else message
  ;;
val message_to_string : Log_entry.t -&gt; string = &lt;fun&gt;
</programlisting>
    <para>
      However, when using dot-notation for accessing record fields, you
      need to specify the module explicitly.
    </para>
    <programlisting language="ocaml">
# let is_important t = t.Log_entry.important;;
val is_important : Log_entry.t -&gt; bool = &lt;fun&gt;
</programlisting>
    <para>
      For functions defined within the module where a given record is
      defined, the module qualification goes away entirely.
    </para>
  </sect1>
  <sect1 id="functional-updates">
    <title>Functional updates</title>
    <para>
      Fairly often, you will find yourself wanting to create a new
      record that differs from an existing record in only a subset of
      the fields. For example, imagine our logging server had a record
      type for representing the state of a given client, including when
      the last heartbeat was received from that client. The following
      defines a type for representing this information, as well as a
      function for updating the client information when a new heartbeat
      arrives.
    </para>
    <programlisting language="ocaml">
# type client_info =
   { addr: Unix.Inet_addr.t;
     port: int;
     user: string;
     credentials: string;
     last_heartbeat_time: Time.t;
   };;
# let register_heartbeat t hb =
      { addr = t.addr;
        port = t.port;
        user = t.user;
        credentials = t.credentials;
        last_heartbeat_time = hb.Heartbeat.time;
      };;
val register_heartbeat : client_info -&gt; Heartbeat.t -&gt; client_info = &lt;fun&gt;
</programlisting>
    <para>
      This is fairly verbose, given that there's only one field that we
      actually want to change, and all the others are just being copied
      over from <literal>t</literal>. We can use OCaml's
      <emphasis>functional update</emphasis> syntax to do this more
      tersely. The syntax of a functional update is as follows.
    </para>
    <programlisting language="ocaml">
{ &lt;record&gt; with &lt;field&gt; = &lt;value&gt;;
                &lt;field&gt; = &lt;value&gt;;
                ...
}
</programlisting>
    <para>
      The purpose of the functional update is to create a new record
      based on an existing one, with a set of field changes layered on
      top.
    </para>
    <para>
      Given this, we can rewrite <literal>register_heartbeat</literal>
      more concisely.
    </para>
    <programlisting language="ocaml">
# let register_heartbeat t hb =
    { t with last_heartbeat_time = hb.Heartbeat.time };;
</programlisting>
    <para>
      Functional updates make your code independent of the identity of
      the fields in the record that are not changing. This is often what
      you want, but it has downsides as well. In particular, if you
      change the definition of your record to have more fields, the type
      system will not prompt you to reconsider whether your update code
      should affect those fields. Consider what happens if we decided to
      add a field for the status message received on the last heartbeat.
    </para>
    <programlisting language="ocaml">
# type client_info =
   { addr: Unix.Inet_addr.t;
     port: int;
     user: string;
     credentials: string;
     last_heartbeat_time: Time.t;
     last_heartbeat_status: string;
   };;
</programlisting>
    <para>
      The original implementation of
      <literal>register_heartbeat</literal> would now be invalid, and
      thus the compiler would warn us to think about how to handle this
      new field. But the version using a functional update continues to
      compile as is, even though it incorrectly ignores the new field.
      The correct thing to do would be to update the code as follows.
    </para>
    <programlisting language="ocaml">
# let register_heartbeat t hb =
    { t with last_heartbeat_time   = hb.Heartbeat.time;
             last_heartbeat_status = hb.Heartbeat.status_message;
    };;
</programlisting>
  </sect1>
  <sect1 id="mutable-fields">
    <title>Mutable fields</title>
    <para>
      Like most OCaml values, records are immutable by default. You can,
      however, declare individual record fields as mutable. For example,
      we could take the <literal>client_info</literal> type and make the
      fields that may need to change over time mutable, as follows.
    </para>
    <programlisting language="ocaml">
# type client_info =
   { addr: Unix.Inet_addr.t;
     port: int;
     user: string;
     credentials: string;
     mutable last_heartbeat_time: Time.t;
     mutable last_heartbeat_status: string;
   };;
</programlisting>
    <para>
      We then use the <literal>&lt;-</literal> operator for actually
      changing the state. The side-effecting version of
      <literal>register_heartbeat</literal> would be written as follows.
    </para>
    <programlisting language="ocaml">
# let register_heartbeat t hb =
    t.last_heartbeat_time   &lt;- hb.Heartbeat.time;
    t.last_heartbeat_status &lt;- hb.Heartbeat.status_message
  ;;
val register_heartbeat : client_info -&gt; Heartbeat.t -&gt; unit = &lt;fun&gt;
</programlisting>
    <para>
      Note that <literal>&lt;-</literal> is not needed for
      initialization, because all fields of a record, including mutable
      ones, must be specified when the record is created.
    </para>
    <para>
      OCaml's policy of immutable-by-default is a good one, but
      imperative programming does have its place. We'll discuss more
      about how (and when) to use OCaml's imperative features in
      <xref linkend="imperative-programming"/>.
    </para>
  </sect1>
  <sect1 id="first-class-fields">
    <title>First-class fields</title>
    <para>
      Consider the following function for extracting the usernames from
      a list of <literal>Logon</literal> messages.
    </para>
    <programlisting language="ocaml">
# let get_users logons =
     List.dedup (List.map logons ~f:(fun x -&gt; x.Logon.user));;
val get_users : Logon.t list -&gt; string list = &lt;fun&gt;
</programlisting>
    <para>
      Here, we wrote a small function
      <literal>(fun x -&gt; x.Logon.user)</literal> to access the
      <literal>user</literal> field. This kind of accessor function is a
      common enough pattern that that it would be convenient to generate
      them automatically. The <literal>fieldslib</literal> syntax
      extension that ships with <literal>Core</literal> does just that.
    </para>
    <para>
      <literal>fieldslib</literal> is invoked by putting the
      <literal>with fields</literal> annotation at the end of the
      declaration of a record type. So, for example, we could have
      defined <literal>Logon</literal> as follows.
    </para>
    <programlisting language="ocaml">
# module Logon = struct
    type t =
      { session_id: string;
        time: Time.t;
        user: string;
        credentials: string;
      }
    with fields
  end;;
</programlisting>
    <para>
      Given that definition, we can use the function
      <literal>Logon.user</literal> to extract the user field from a
      logon message.
    </para>
    <programlisting language="ocaml">
# let get_users logons = List.dedup (List.map logons ~f:Logon.user);;
val get_users : Logon.t list -&gt; string list = &lt;fun&gt;
</programlisting>
    <para>
      In addition to generating field accessor functions,
      <literal>fieldslib</literal> also creates a sub-module called
      <literal>Fields</literal> that contains a first class
      representative of each field, in the form of a value of type
      <literal>Field.t</literal>. The <literal>Field</literal> module
      provides the following functions:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          <literal>Field.name</literal>, which returns the name of a
          field
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>Field.get</literal>, which returns the content of a
          field
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>Field.fset</literal>, which does a functional update
          of a field
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>Field.setter</literal>, which returns
          <literal>None</literal> if the field is not mutable or
          <literal>Some f</literal> if it is, where <literal>f</literal>
          is a function for mutating that field.
        </para>
      </listitem>
    </itemizedlist>
    <para>
      A <literal>Field.t</literal> has two type parameters: the first
      for the type of the record, and the second for the type of the
      field in question. Thus, the type of
      <literal>Logon.Fields.session_id</literal> is
      <literal>(Logon.t, string) Field.t</literal>, whereas the type of
      <literal>Logon.Fields.time</literal> is
      <literal>(Logon.t, Time.t) Field.t</literal>. Accordingly, the
      function <literal>Field.get</literal> has type
    </para>
    <programlisting language="ocaml">
('r, 'a) Field.t -&gt; 'r -&gt; 'a
</programlisting>
    <para>
      As you can see, the first parameter of the
      <literal>Field.t</literal> corresponds to the record you pass to
      <literal>get</literal>, and the second argument corresponds to the
      value contained in the field, which is also the return type of
      <literal>get</literal>.
    </para>
    <para>
      We can use first class fields to do things like write a generic
      function for displaying a record field.
    </para>
    <programlisting language="ocaml">
# let show_field field to_string record =
     let name = Field.name field in
     let field_string = to_string (Field.get field record) in
     name ^ &quot;: &quot; ^ field_string
  ;;
val show_field : ('a, 'b) Field.t -&gt; ('b -&gt; string) -&gt; 'a -&gt; string = &lt;fun&gt;
</programlisting>
    <para>
      This takes three arguments: the <literal>Field.t</literal>, a
      function for converting the contents of the field in question to a
      string, and a record from which the field can be grabbed..
    </para>
    <para>
      Here's an example of <literal>show_field</literal> in action.
    </para>
    <programlisting language="ocaml">
# let logon = { Logon.
                session_id = &quot;26685&quot;;
                time = Time.now ();
                user = &quot;yminsky&quot;;
                credentials = &quot;Xy2d9W&quot;; }
  ;;
# show_field Logon.Fields.user Fn.id logon;;
- : string = &quot;user: yminsky&quot;
# show_field Logon.Fields.time Time.to_string logon;;
- : string = &quot;time: 2012-06-26 18:44:13.807826&quot;
</programlisting>
    <para>
      As a side note, the above is our first use of the
      <literal>Fn</literal> module (short for &quot;function&quot;)
      which provides a collection of useful primitives for dealing with
      functions. <literal>Fn.id</literal> is the identity function.
    </para>
    <para>
      <literal>fieldslib</literal> also provides higher-level operators,
      like <literal>Fields.fold</literal> and
      <literal>Fields.iter</literal>, which let you iterate over all the
      fields of a record. The following function uses
      <literal>Logon.Fields.iter</literal> and
      <literal>show_field</literal> to print out all the fields of a
      <literal>Logon</literal> record.
    </para>
    <programlisting language="ocaml">
# let print_logon logon =
    let print to_string field =
      printf &quot;%s\n&quot; (show_field field to_string logon)
    in
    Logon.Fields.iter
      ~session_id:(print Fn.id)
      ~time:(print Time.to_string)
      ~user:(print Fn.id)
      ~credentials:(print Fn.id)
  ;;
val print_logon : Logon.t -&gt; unit = &lt;fun&gt;
# print_logon logon;;
session_id: 26685
time: 2012-06-26 18:44:13.807826
user: yminsky
credentials: Xy2d9W
- : unit = ()
</programlisting>
    <para>
      One nice side effect of this approach is that it helps you
      refactor your code when changing the fields of a record. In
      particular, if you add a field to <literal>Logon</literal>, the
      type of <literal>iter</literal> will change with it, acquiring a
      new argument. Any code using <literal>iter</literal> won't be able
      to compile until it's fixed to take this new argument into
      account, and these compilation failiures will point out places you
      need to adapt your code.
    </para>
    <para>
      Field iterators are useful for a variety of record-related tasks,
      from building record validation functions to scaffolding the
      definition of a web-form from a record type, all with a guarantee
      that you've considered all fields of the record type in question.
    </para>
  </sect1>
</chapter><chapter id="variants">
  <title>Variants</title>
  <para>
    Variant types are one of the most useful features of OCaml, and also
    one of the most unusual. They let you represent data that may take
    on multiple different forms, where each form is marked by an
    explicit tag. As we'll see, when combined with pattern matching,
    variants give you a powerful way of representing complex data and of
    organizing the case-analysis on that information.
  </para>
  <para>
    Let's consider a concrete example of how variants can be useful.
    Almost all terminals support a set of 8 basic colors, and we can
    represent those colors using a variant. Each color is declared as a
    simple tag, with pipes used to separate the different cases.
  </para>
  <programlisting language="ocaml">
# type basic_color =
    Black | Red | Green | Yellow | Blue | Magenta | Cyan | White ;;
# Cyan ;;
- : basic_color = Cyan
# [Blue; Magenta; Red] ;;
- : basic_color list = [Blue; Magenta; Red]
</programlisting>
  <para>
    The following function uses pattern matching to convert a
    <literal>basic_color</literal> to a corresponding integer. Note that
    the exhaustiveness checking on pattern matches means that the
    compiler will warn us if we miss a color.
  </para>
  <programlisting language="ocaml">
# let basic_color_to_int = function
  | Black -&gt; 0 | Red     -&gt; 1 | Green -&gt; 2 | Yellow -&gt; 3
  | Blue  -&gt; 4 | Magenta -&gt; 5 | Cyan  -&gt; 6 | White  -&gt; 7 ;;
val basic_color_to_int : basic_color -&gt; int = &lt;fun&gt;
# List.map ~f:basic_color_to_int [Blue;Red];;
- : int list = [4; 1]
</programlisting>
  <para>
    Using the above, we can generate escape codes to change the color of
    a given string displayed in a terminal.
  </para>
  <programlisting language="ocaml">
# let color_by_number number text =
    sprintf &quot;\027[38;5;%dm%s\027[0m&quot; number text;;
  val color_by_number : int -&gt; string -&gt; string = &lt;fun&gt;
# let blue = color_by_number (basic_color_to_int Blue) &quot;Blue&quot;;;
val blue : string = &quot;\027[38;5;4mBlue\027[0m&quot;
# printf &quot;Hello %s World!\n&quot; blue;;
Hello Blue World!
- : unit = ()
</programlisting>
  <para>
    On most terminals, that word &quot;Blue&quot; will be rendered in
    blue.
  </para>
  <para>
    In this example, the cases of the variant are simple tag with no
    associated data. This is substantively the same as the enumerations
    found in languages like C and Java. But as we'll see, variants can
    do considerably more than represent a simple enumeration. Indeed, an
    enumeration isn't enough to effectively describe the full set of
    colors that a modern terminal can display. Many terminals, including
    the venerable <literal>xterm</literal>, support 256 different
    colors, broken up into the following groups.
  </para>
  <itemizedlist>
    <listitem>
      <para>
        The 8 basic colors, in regular and bold versions.
      </para>
    </listitem>
    <listitem>
      <para>
        A 6 × 6 × 6 RGB color cube
      </para>
    </listitem>
    <listitem>
      <para>
        A 24-level grayscale ramp
      </para>
    </listitem>
  </itemizedlist>
  <para>
    We'll also represent this more complicated color-space as a variant,
    but this time, the different tags will have arguments which describe
    the data available in each case. Note that variants can have
    multiple arguments, which are separated by <literal>*</literal>'s.
  </para>
  <programlisting language="ocaml">
# type weight = Regular | Bold
  type color =
  | Basic of basic_color * weight (* basic colors, regular and bold *)
  | RGB   of int * int * int       (* 6x6x6 color cube *)
  | Gray  of int                   (* 24 grayscale levels *)
;;
# [RGB (250,70,70); Basic (Green, Regular)];;
- : color list = [RGB (250, 70, 70); Basic (Green, Regular)]
</programlisting>
  <para>
    Once again, we'll use pattern matching to convert a color to a
    corresponding integer. But in this case, the pattern matching does
    more than separate out the different cases; it also allows us to
    extract the data associated with each tag.
  </para>
  <programlisting language="ocaml">
# let color_to_int = function
    | Basic (basic_color,weight) -&gt;
      let base = match weight with Bold -&gt; 8 | Regular -&gt; 0 in
      base + basic_color_to_int basic_color
    | RGB (r,g,b) -&gt; 16 + b + g * 6 + r * 36
    | Gray i -&gt; 232 + i ;;
val color_to_int : color -&gt; int = &lt;fun&gt;
</programlisting>
  <para>
    Now, we can print text using the full set of available colors.
  </para>
  <programlisting language="ocaml">
# let color_print color s =
     printf &quot;%s\n&quot; (color_by_number (color_to_int color) s);;
val color_print : color -&gt; string -&gt; unit = &lt;fun&gt;
# color_print (Basic (Red,Bold)) &quot;A bold red!&quot;;;
A bold red!
- : unit = ()
# color_print (Gray 4) &quot;A muted gray...&quot;;;
A muted gray...
- : unit = ()
</programlisting>
  <note><title>
  Catch-all cases and refactoring
  </title>
  <para>
    OCaml's type system can act as a refactoring tool, by warning you of
    places where your code needs to be updated to match an interface
    change. This is particularly valuable in the context of variants.
  </para>
  <para>
    Consider what would happen if we were to change the definition of
    <literal>color</literal> to the following.
  </para>
  <programlisting language="ocaml">
# type color =
  | Basic of basic_color     (* basic colors *)
  | Bold  of basic_color     (* bold basic colors *)
  | RGB   of int * int * int (* 6x6x6 color cube *)
  | Gray  of int             (* 24 grayscale levels *)
;;
</programlisting>
  <para>
    We've essentially broken out the <literal>Basic</literal> case into
    two cases, <literal>Basic</literal> and <literal>Bold</literal>, and
    <literal>Basic</literal> has changed from having two arguments to
    one. <literal>color_to_int</literal> as we wrote it still expects
    the old structure of the variant, and if we try to compile that same
    code again, the compiler will notice the discrepancy.
  </para>
  <programlisting language="ocaml">
# let color_to_int = function
    | Basic (basic_color,weight) -&gt;
      let base = match weight with Bold -&gt; 8 | Regular -&gt; 0 in
      base + basic_color_to_int basic_color
    | RGB (r,g,b) -&gt; 16 + b + g * 6 + r * 36
    | Gray i -&gt; 232 + i ;;
Characters 40-60:
Error: This pattern matches values of type 'a * 'b
       but a pattern was expected which matches values of type basic_color
</programlisting>
  <para>
    Here, the compiler is complaining that the <literal>Basic</literal>
    tag is used with the wrong number of arguments. If we fix that,
    however, the compiler flag will flag a second problem, which is that
    we haven't handled the new <literal>Bold</literal> tag.
  </para>
  <programlisting language="ocaml">
# let color_to_int = function
    | Basic basic_color -&gt; basic_color_to_int basic_color
    | RGB (r,g,b) -&gt; 16 + b + g * 6 + r * 36
    | Gray i -&gt; 232 + i ;;
Characters 19-154:
Warning 8: this pattern-matching is not exhaustive.
Here is an example of a value that is not matched:
Bold _
val color_to_int : color -&gt; int = &lt;fun&gt;
</programlisting>
  <para>
    Fixing this now leads us to the correct implementation.
  </para>
  <programlisting language="ocaml">
# let color_to_int = function
    | Basic basic_color -&gt; basic_color_to_int basic_color
    | Bold  basic_color -&gt; 8 + basic_color_to_int basic_color
    | RGB (r,g,b) -&gt; 16 + b + g * 6 + r * 36
    | Gray i -&gt; 232 + i ;;
val color_to_int : color -&gt; int = &lt;fun&gt;
</programlisting>
  <para>
    As we've seen, the type errors identified the things that needed to
    be fixed to complete the refactoring of the code. This is
    fantastically useful, but for it to work well and reliably, you need
    to write your code in a way that maximizes the compiler's chances of
    helping you find the bugs. One important rule of thumb to follow to
    maximize what the compiler can do for you is to avoid catch-all
    cases in pattern matches.
  </para>
  <para>
    Here's an example of how a catch-all case plays in. Imagine we
    wanted a version of <literal>color_to_int</literal> that works on
    older terminals by rendering the first 16 colors (the 8
    <literal>basic_color</literal>s in regular and bold) in the normal
    way, but rendering everything else as white. We might have written
    the function as follows.
  </para>
  <programlisting language="ocaml">
# let oldschool_color_to_int = function
    | Basic (basic_color,weight) -&gt;
      let base = match weight with Bold -&gt; 8 | Regular -&gt; 0 in
      base + basic_color_to_int basic_color
    | _ -&gt; basic_color_to_int White;;
val oldschool_color_to_int : color -&gt; int = &lt;fun&gt;
</programlisting>
  <para>
    But because the catch-all case encompasses all possibilities, the
    type system will no longer warn us that we have missed the new
    <literal>Bold</literal> case when we change the type to include it.
    We can get this check back by being more explicit about what we're
    ignoring. We haven't changed the behavior of the code, but we have
    improved our robustness to change.
  </para>
  </note>
  <sect1 id="combining-records-and-variants">
    <title>Combining records and variants</title>
    <para>
      Records and variants are most effective when used in concert.
      Consider again the type <literal>Log_entry.t</literal> from
      <xref linkend="records"/>:
    </para>
    <programlisting language="ocaml">
module Log_entry = struct
  type t =
    { session_id: string;
      time: Time.t;
      important: bool;
      message: string;
    }
end
</programlisting>
    <para>
      This record type combines multiple pieces of data into one value.
      In particular, a single <literal>Log_entry.t</literal> has a
      <literal>session_id</literal> <emphasis>and</emphasis> a
      <literal>time</literal> <emphasis>and</emphasis> an
      <literal>important</literal> flag <emphasis>and</emphasis> a
      <literal>message</literal>. More generally, you can think of
      record types as acting as conjunctions. Variants, on the other
      hand, are disjunctions, letting you represent multiple
      possibilities, as in the following example.
    </para>
    <programlisting language="ocaml">
type client_message = | Logon of Logon.t
                      | Heartbeat of Heartbeat.t
                      | Log_entry of Log_entry.t
</programlisting>
    <para>
      A <literal>client_message</literal> is a <literal>Logon</literal>
      <emphasis>or</emphasis> a <literal>Heartbeat</literal>
      <emphasis>or</emphasis> a <literal>Log_entry</literal>. If we want
      to write code that processes messages generically, rather than
      code specialized to a fixed message type, we need something like
      <literal>client_message</literal> to act as one overarching type
      for the different possible messages.
    </para>
    <para>
      You can increase the precision of your types by using variants to
      represent differences between types, and records to represent
      shared structure. Consider the following function that takes a
      list of <literal>client_message</literal>s and returns all
      messages generated by a given user. The code in question is
      implemented by folding over the list of messages, where the
      accumulator is a pair of:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          the set of session identifiers for the user that have been
          seen thus far.
        </para>
      </listitem>
      <listitem>
        <para>
          the set of messages so far that are associated with the user.
        </para>
      </listitem>
    </itemizedlist>
    <para>
      Here's the concrete code.
    </para>
    <programlisting language="ocaml">
let messages_for_user user messages =
  let (user_messages,_) =
    List.fold messages ~init:([],String.Set.empty)
      ~f:(fun ((messages,user_sessions) as acc) message -&gt;
        match message with
        | Logon m -&gt;
          if m.Logon.user = user then
            (message::messages, Set.add user_sessions m.Logon.session_id)
          else acc
        | Heartbeat _ | Log_entry _ -&gt;
          let session_id = match message with
            | Logon     m -&gt; m.Logon.session_id
            | Heartbeat m -&gt; m.Heartbeat.session_id
            | Log_entry m -&gt; m.Log_entry.session_id
          in
          if Set.mem user_sessions session_id then
            (message::messages,user_sessions)
          else acc
      )
  in
  List.rev user_messages
</programlisting>
    <para>
      There's one awkward bit about the code above, which is the
      calculation of the session ids. In particular, we have the
      following repetitive snippet of code:
    </para>
    <programlisting language="ocaml">
  let session_id = match message with
    | Logon     m -&gt; m.Logon.session_id
    | Heartbeat m -&gt; m.Heartbeat.session_id
    | Log_entry m -&gt; m.Log_entry.session_id
  in
</programlisting>
    <para>
      This code effectively computes the session id for each underlying
      message type. The repetition in this case isn't that bad, but
      would become problematic in larger and more complicated examples.
      Also, we had to include code for the <literal>Logon</literal>
      case, even though it can't actually come up.
    </para>
    <para>
      We can improve the code by refactoring our types to explicitly
      separate the parts that are shared from those that are common. The
      first step is to cut down the definitions of the per-message
      records to just contain the unique components of each message.
    </para>
    <programlisting language="ocaml">
module Log_entry = struct
  type t = { important: bool;
             message: string;
           }
end

module Heartbeat = struct
  type t = { status_message: string; }
end

module Logon = struct
  type t = { user: string;
             credentials: string;
           }
end
</programlisting>
    <para>
      We can then define a variant type that covers the different
      possible unique components.
    </para>
    <programlisting language="ocaml">
type details =
| Logon of Logon.t
| Heartbeat of Heartbeat.t
| Log_entry of Log_entry.t
</programlisting>
    <para>
      Separately, we need a record that contains the fields that are
      common across all messages.
    </para>
    <programlisting language="ocaml">
module Common = struct
  type t = { session_id: string;
             time: Time.t;
           }
end
</programlisting>
    <para>
      A full message can then be represented as a pair of a
      <literal>Common.t</literal> and a <literal>details</literal>.
      Using this, we can rewrite our example above as follows:
    </para>
    <programlisting language="ocaml">
let messages_for_user user messages =
  let (user_messages,_) =
    List.fold messages ~init:([],String.Set.empty)
      ~f:(fun ((messages,user_sessions) as acc) ((common,details) as message) -&gt;
        let session_id = common.Common.session_id in
        match details with
        | Logon m -&gt;
          if m.Logon.user = user then
            (message::messages, Set.add user_sessions session_id)
          else acc
        | Heartbeat _ | Log_entry _ -&gt;
          if Set.mem user_sessions session_id then
            (message::messages,user_sessions)
          else acc
      )
  in
  List.rev user_messages
</programlisting>
    <para>
      Note that the more complex match statement for computing the
      session id has been replaced with the simple expression
      <literal>common.Common.session_id</literal>.
    </para>
    <para>
      In addition, this design allows us to essentially downcast to the
      specific message type once we know what it is, and then dispatch
      code to handle just that message type. In particular, while we use
      the type <literal>Common.t * details</literal> to represent an
      arbitrary message, we can use
      <literal>Common.t * Logon.t</literal> to represent a logon
      message. Thus, if we had functions for handling individual message
      types, we could write a dispatch function as follows.
    </para>
    <programlisting language="ocaml">
let handle_message server_state (common,details) =
  match details with
  | Log_entry m -&gt; handle_log_entry server_state (common,m)
  | Logon     m -&gt; handle_logon     server_state (common,m)
  | Heartbeat m -&gt; handle_heartbeat server_state (common,m)
</programlisting>
    <para>
      And it's explicit at the type level that
      <literal>handle_log_entry</literal> sees only
      <literal>Log_entry</literal> messages,
      <literal>handle_logon</literal> sees only <literal>Logon</literal>
      messages, etc.
    </para>
  </sect1>
  <sect1 id="variants-and-recursive-data-structures">
    <title>Variants and recursive data structures</title>
    <para>
      Another common application of variants is to represent tree-like
      recursive data-structures. We'll show how this can be done by
      walking through the design of a simple Boolean expression
      language. Such a language can be useful anywhere you need to
      specify filters, which are used in everything from packet
      analyzers to mail clients.
    </para>
    <para>
      An expression in this language will be defined by the variant
      <literal>blang</literal> (short for &quot;boolean language&quot;)
      with one tag for each kind of expression we want to support.
    </para>
    <programlisting language="ocaml">
# type 'a blang =
  | Base  of 'a
  | Const of bool
  | And   of 'a blang list
  | Or    of 'a blang list
  | Not   of 'a blang
  ;;
</programlisting>
    <para>
      Note that the definition of the type <literal>blang</literal> is
      recursive, meaning that a <literal>blang</literal> may contain
      other <literal>blang</literal>s.
    </para>
    <para>
      The purpose of each tag is pretty straightforward.
      <literal>And</literal>, <literal>Or</literal> and
      <literal>Not</literal> are the basic operators for building up
      boolean expression, and <literal>Const</literal> lets you enter
      constants <literal>true</literal> and <literal>false</literal>.
    </para>
    <para>
      The <literal>Base</literal> tag is what allows you to tie the
      <literal>blang</literal> to your application, by letting you
      specify an element of some base predicate type, whose truth or
      falsehood is determined by your application. If you were writing a
      filter language for an email processor, your base predicates might
      specify the tests you would run against an email, as in the
      following example.
    </para>
    <programlisting language="ocaml">
# type mail_field = To | From | CC | Date | Subject
  type mail_predicate = { field: mail_field;
                          contains: string }
  ;;
</programlisting>
    <para>
      Using the above, we can construct a simple expression with
      <literal>mail_predicate</literal> as its base predicate.
    </para>
    <programlisting language="ocaml">
# let test field contains = Base { field; contains };;
val test : mail_field -&gt; string -&gt; mail_predicate blang = &lt;fun&gt;
# And [ Or [ test To &quot;doligez&quot;; test CC &quot;doligez&quot; ];
        test Subject &quot;runtime&quot;;
      ]
  ;;
- : mail_predicate blang =
And
 [Or
   [Base {field = To; contains = &quot;doligez&quot;};
    Base {field = CC; contains = &quot;doligez&quot;}];
  Base {field = Subject; contains = &quot;runtime&quot;}]
</programlisting>
    <para>
      Being able to construct such expressions isn't enough: we also
      need to be able to evaluate such an expression. The following code
      shows how you could write a general-purpose evaluator for
      <literal>blang</literal>'s.
    </para>
    <programlisting language="ocaml">
# let rec eval blang base_eval =
    (* a shortcut, so we don't need to repeatedly pass [base_eval]
       explicitly to [eval] *)
    let eval' blang = eval blang base_eval in
    match blang with
    | Base  base   -&gt; base_eval base
    | Const bool   -&gt; bool
    | And   blangs -&gt; List.for_all blangs ~f:eval'
    | Or    blangs -&gt; List.exists  blangs ~f:eval'
    | Not   blang  -&gt; not (eval' blang)
  ;;
val eval : 'a blang  -&gt; ('a -&gt; bool) -&gt; bool = &lt;fun&gt;
</programlisting>
    <para>
      The structure of the code is pretty straightforward --- we're just
      pattern-matching over the structure of the data, doing the
      appropriate calculation based on which tag we see. To use this
      evaluator on a concrete example, we just need to write the
      <literal>base_eval</literal> function which is capable of
      evaluating a base predicate.
    </para>
    <para>
      Another useful operation to be able to do on expressions is
      simplification. The following function applies some basic
      simplification rules, most of the simplifications being driven by
      the presence of constants.
    </para>
    <programlisting language="ocaml">
# let rec simplify = function
    | Base _ | Const _ as x -&gt; x
    | And blangs -&gt;
      let blangs =
        List.map ~f:simplify blangs
        |&gt; List.filter ~f:(fun x -&gt; x &lt;&gt; Const true)
      in
      if List.is_empty blangs then Const true
      else if List.exists blangs ~f:(fun x -&gt; x = Const false)
      then Const false
      else And blangs
    | Or blangs -&gt;
      let blangs =
        List.map ~f:simplify blangs
        |&gt; List.filter ~f:(fun x -&gt; x &lt;&gt; Const false)
      in
      if List.is_empty blangs then Const false
      else if List.exists blangs ~f:(fun x -&gt; x = Const true)
      then Const true
      else Or blangs
    | Not blang -&gt;
      match simplify blang with
      | Const bool -&gt; Const (not bool)
      | blang -&gt; Not blang
  ;;
val simplify : 'a blang -&gt; 'a blang = &lt;fun&gt;
</programlisting>
    <para>
      One thing to notice about the above code is that it uses a
      catch-all case in the very last line within the
      <literal>Not</literal> case. It's generally better to be explicit
      about the cases you're ignoring. Indeed, if we change this snippet
      of code to be more explicit:
    </para>
    <programlisting language="ocaml">
    | Not blang -&gt;
      match simplify blang with
      | Const bool -&gt; Const (not bool)
      | (And _ | Or _ | Base _ | Not _) -&gt; Not blang
</programlisting>
    <para>
      it's easy to see that we've missed an important case:
      double-negation.
    </para>
    <programlisting language="ocaml">
    | Not blang -&gt;
      match simplify blang with
      | Const b -&gt; Const (not b)
      | Not blang -&gt; blang
      | (And _ | Or _ | Base _ ) -&gt; Not blang
</programlisting>
    <para>
      This example is more than a toy. There's a module in Core very
      much in this spirit in Core called <literal>Blang</literal>, and
      it gets a lot of practical use in a variety of applications.
    </para>
    <para>
      More generally, using variants to build recursive data-structures
      is a common technique, and shows up everywhere from designing
      little languages to building efficient data-structures.
    </para>
  </sect1>
  <sect1 id="polymorphic-variants">
    <title>Polymorphic variants</title>
    <para>
      In addition to the ordinary variants we've seen so far, OCaml also
      supports so-called <emphasis>polymorphic variants</emphasis>. As
      we'll see, polymorphic variants are more flexible and
      syntactically more lightweight than ordinary variants, but that
      extra power comes at a cost.
    </para>
    <para>
      Syntactically, polymorphic variants are distinguished from
      ordinary variants by the leading backtick. And unlike ordinary
      variants, polymorphic variants can be used without an explicit
      type declaration.
    </para>
    <programlisting language="ocaml">
# let three = `Int 3;;
val three : [&gt; `Int of int ] = `Int 3
# let four = `Float 4.;;
val four : [&gt; `Float of float ] = `Float 4.
# let nan = `Not_a_number;;
val nan : [&gt; `Not_a_number ] = `Not_a_number
# [three; four; nan];;
- : [&gt; `Float of float | `Int of int | `Not_a_number ] list =
[`Int 3; `Float 4.; `Not_a_number]
</programlisting>
    <para>
      As you can see, polymorphic variant types are inferred
      automatically, and when we combine variants with different tags,
      the compiler infers a new type that knows about all of those tags.
    </para>
    <para>
      The type system will complain, however, if it sees incompatible
      uses of the same tag:
    </para>
    <programlisting language="ocaml">
# let five = `Int &quot;five&quot;;;
val five : [&gt; `Int of string ] = `Int &quot;five&quot;
# [three; four; five];;
Characters 14-18:
  [three; four; five];;
                ^^^^
Error: This expression has type [&gt; `Int of string ]
       but an expression was expected of type
         [&gt; `Float of float | `Int of int ]
       Types for tag `Int are incompatible
</programlisting>
    <para>
      The <literal>&gt;</literal> at the beginning of the variant types
      above is critical, because it marks the types as being open to
      combination with other variant types. We can read the type
      <literal>[&gt; `Int of string | `Float of float]</literal> as
      describing a variant whose tags include
      <literal>`Int of string</literal> and
      <literal>`Float of float</literal>, but may include more tags as
      well. In other words, you can roughly translate
      <literal>&gt;</literal> to mean: &quot;these tags or more&quot;.
    </para>
    <para>
      OCaml will in some cases infer a variant type with
      <literal>&lt;</literal>, to indicate &quot;these tags or
      less&quot;, as in the following example.
    </para>
    <programlisting language="ocaml">
# let is_positive = function
     | `Int   x -&gt; x &gt; 0
     | `Float x -&gt; x &gt; 0.
  ;;
val is_positive : [&lt; `Float of float | `Int of int ] -&gt; bool = &lt;fun&gt;
</programlisting>
    <para>
      The <literal>&lt;</literal> is there because
      <literal>is_positive</literal> has no way of dealing with values
      that have tags other than <literal>`Float of float</literal> or
      <literal>`Int of int</literal>.
    </para>
    <para>
      We can think of these <literal>&lt;</literal> and
      <literal>&gt;</literal> markers as indications of upper and lower
      bounds on the tags involved. If the same set of tags are both an
      upper and a lower bound, we end up with an
      <emphasis>exact</emphasis> polymorphic variant type, which has
      neither marker. For example:
    </para>
    <programlisting language="ocaml">
# let exact = List.filter ~f:is_positive [three;four];;
val exact : [ `Float of float | `Int of int ] list
   = [`Int 3; `Float 4.]
</programlisting>
    <para>
      Perhaps surprisingly, we can also create polymorphic variant types
      that have different upper and lower bounds.
    </para>
    <programlisting language="ocaml">
let is_positive = function
     | `Int   x -&gt; Ok (x &gt; 0)
     | `Float x -&gt; Ok (x &gt; 0.)
     | `Not_a_number -&gt; Error &quot;not a number&quot;;;
val is_positive :
  [&lt; `Float of float | `Int of int | `Not_a_number ] -&gt;
  (bool, string) Result.t = &lt;fun&gt;
# List.filter [three; four] ~f:(fun x -&gt;
     match is_positive x with Error _ -&gt; false | Ok b -&gt; b);;
  - : [&lt; `Float of float | `Int of int | `Not_a_number &gt; `Float `Int ]
    List.t
= [`Int 3; `Float 4.]
</programlisting>
    <para>
      Here, the inferred type states that the tags can be no more than
      <literal>`Float</literal>, <literal>`Int</literal> and
      <literal>`Not_a_number</literal>, and must contain at least
      <literal>`Float</literal> and <literal>`Int</literal>. As you can
      already start to see, polymorphic variants can lead to fairly
      complex inferred types.
    </para>
    <sect2 id="example-terminal-colors-redux">
      <title>Example: Terminal colors redux</title>
      <para>
        To see how to use polymorphic variants in practice, we'll return
        to terminal colors. Imagine that we have a new terminal type
        that adds yet more colors, say, by adding an alpha channel so
        you can specify translucent colors. We could model this extended
        set of colors as follows, using an ordinary variant.
      </para>
      <programlisting language="ocaml">
# type extended_color =
  | Basic of basic_color * weight  (* basic colors, regular and bold *)
  | RGB   of int * int * int       (* 6x6x6 color space *)
  | Gray  of int                   (* 24 grayscale levels *)
  | RGBA  of int * int * int * int (* 6x6x6x6 color space *)
  ;;
</programlisting>
      <para>
        We want to write a function
        <literal>extended_color_to_int</literal>, that works like
        <literal>color_to_int</literal> for all of the old kinds of
        colors, with new logic only for handling colors that include an
        alpha channel. One might try to write such a function as
        follows.
      </para>
      <programlisting language="ocaml">
# let extended_color_to_int = function
    | RGBA (r,g,b,a) -&gt; 256 + a + b * 6 + g * 36 + r * 216
    | (Basic _ | RGB _ | Gray _) as color -&gt; color_to_int color
  ;;
</programlisting>
      <para>
        This looks reasonable enough, but it leads to the following type
        error.
      </para>
      <programlisting language="ocaml">
Characters 93-98:
    | (Basic _ | RGB _ | Gray _) as color -&gt; color_to_int color
                                                          ^^^^^
Error: This expression has type extended_color
       but an expression was expected of type color
</programlisting>
      <para>
        The problem is that <literal>extended_color</literal> and
        <literal>color</literal> are in the compiler's view distinct and
        unrelated types. The compiler doesn't, for example, recognize
        any equality between the <literal>Basic</literal> tag in the two
        types.
      </para>
      <para>
        What we want to do is to share tags between two different
        variant types, and polymorphic variants let us do this in a
        natural way. First, let's rewrite
        <literal>basic_color_to_int</literal> and
        <literal>color_to_int</literal> using polymorphic variants. The
        translation here is pretty straightforward.
      </para>
      <programlisting language="ocaml">
# let basic_color_to_int = function
    | `Black -&gt; 0 | `Red     -&gt; 1 | `Green -&gt; 2 | `Yellow -&gt; 3
    | `Blue  -&gt; 4 | `Magenta -&gt; 5 | `Cyan  -&gt; 6 | `White  -&gt; 7

  let color_to_int = function
    | `Basic (basic_color,weight) -&gt;
      let base = match weight with `Bold -&gt; 8 | `Regular -&gt; 0 in
      base + basic_color_to_int basic_color
    | `RGB (r,g,b) -&gt; 16 + b + g * 6 + r * 36
    | `Gray i -&gt; 232 + i
 ;;
val basic_color_to_int :
  [&lt; `Black | `Blue | `Cyan | `Green | `Magenta | `Red | `White | `Yellow ] -&gt;
  int = &lt;fun&gt;
val color_to_int :
  [&lt; `Basic of
       [&lt; `Black | `Blue | `Cyan | `Green | `Magenta | `Red
        | `White | `Yellow ] *
       [&lt; `Bold | `Regular ]
   | `Gray of int
   | `RGB of int * int * int ] -&gt;
  int = &lt;fun&gt;
</programlisting>
      <para>
        Now we can try writing <literal>extended_color_to_int</literal>.
        The key issue with this code is that
        <literal>extended_color_to_int</literal> needs to invoke
        <literal>color_to_int</literal> with a narrower type,
        <emphasis>i.e.</emphasis>, one that includes fewer tags. Written
        properly, this narrowing can be done via a pattern match. In
        particular, in the following code, the type of the variable
        <literal>color</literal> includes only the tags
        <literal>`Basic</literal>, <literal>`RGB</literal> and
        <literal>`Gray</literal>, and not <literal>`RGBA</literal>.
      </para>
      <programlisting language="ocaml">
# let extended_color_to_int = function
    | `RGBA (r,g,b,a) -&gt; 256 + a + b * 6 + g * 36 + r * 216
    | (`Basic _ | `RGB _ | `Gray _) as color -&gt; color_to_int color
  ;;
val extended_color_to_int :
  [&lt; `Basic of
       [&lt; `Black | `Blue | `Cyan | `Green | `Magenta | `Red
        | `White | `Yellow ] *
       [&lt; `Bold | `Regular ]
   | `Gray of int
   | `RGB of int * int * int
   | `RGBA of int * int * int * int ] -&gt;
  int = &lt;fun&gt;
</programlisting>
      <para>
        The above code is more delicately balanced than one might
        imagine. In particular, if we use a catch-all case instead of an
        explicit enumeration of the cases, the type is no longer
        narrowed, and so compilation fails.
      </para>
      <programlisting language="ocaml">
# let extended_color_to_int = function
    | `RGBA (r,g,b,a) -&gt; 256 + a + b * 6 + g * 36 + r * 216
    | color -&gt; color_to_int color
  ;;
      Characters 125-130:
      | color -&gt; color_to_int color
                              ^^^^^
Error: This expression has type [&gt; `RGBA of int * int * int * int ]
       but an expression was expected of type
         [&lt; `Basic of
              [&lt; `Black | `Blue | `Cyan | `Green | `Magenta | `Red
               | `White | `Yellow ] *
              [&lt; `Bold | `Regular ]
          | `Gray of int
          | `RGB of int * int * int ]
       The second variant type does not allow tag(s) `RGBA
</programlisting>
      <note>
      <title>
      Polymorphic variants and catch-all cases
      </title>
      <para>
        As we saw with the definition of <literal>is_positive</literal>,
        a match statement can lead to the inference of an upper bound on
        a variant type, limiting the possible tags to those that can be
        handled by the match. If we add a catch-all case to our match
        statement, we end up with a function with a lower bound.
      </para>
      <programlisting language="ocaml">
# let is_positive_permissive = function
     | `Int   x -&gt; Ok (x &gt; 0)
     | `Float x -&gt; Ok (x &gt; 0.)
     | _ -&gt; Error &quot;Unknown number type&quot;
  ;;
val is_positive_permissive :
  [&gt; `Float of float | `Int of int ] -&gt; (bool, string) Core.Std._result =
  &lt;fun&gt;
# is_positive_permissive (`Int 0);;
- : (bool, string) Result.t = Ok false
# is_positive_permissive (`Ratio (3,4));;
- : (bool, string) Result.t = Error &quot;Unknown number type&quot;
</programlisting>
      <para>
        Catch-all cases are error-prone even with ordinary variants, but
        they are especially so with polymorphic variants. That's because
        you have no way of bounding what tags your function might have
        to deal with. Such code is particularly vulnerable to typos. For
        instance, if code that uses
        <literal>is_positive_permissive</literal> passes in
        <literal>Float</literal> misspelled as <literal>Floot</literal>,
        the erroneous code will compile without complaint.
      </para>
      <programlisting language="ocaml">
# is_positive_permissive (`Floot 3.5);;
- : (bool, string) Result.t = Error &quot;Unknown number type&quot;
</programlisting>
      <para>
        With ordinary variants, such a typo would have been caught as an
        unknown tag. As a general matter, one should be wary about
        mixing catch-all cases and polymorphic variants.
      </para>
      </note>
      <para>
        Let's consider how we might turn our code into a proper library
        with an <literal>mli</literal>. Here's what the interface to
        this file might look like.
      </para>
      <programlisting language="ocaml">
(* file: terminal_color.mli *)

open Core.Std

type basic_color =
  [ `Black   | `Blue | `Cyan  | `Green
  | `Magenta | `Red  | `White | `Yellow ]

type color =
  [ `Basic of basic_color * [ `Bold | `Regular ]
  | `Gray of int
  | `RGB  of int * int * int ]

type extended_color =
  [ color
  | `RGBA of int * int * int * int ]

val color_to_int          : color -&gt; int
val extended_color_to_int : extended_color -&gt; int
</programlisting>
      <para>
        Here, <literal>extended_color</literal> is defined as an
        explicit extension of <literal>color</literal>. Also, notice
        that we defined all of these types as exact variants. We can
        implement this library as follows.
      </para>
      <programlisting language="ocaml">
open Core.Std

type basic_color =
  [ `Black   | `Blue | `Cyan  | `Green
  | `Magenta | `Red  | `White | `Yellow ]

type color =
  [ `Basic of basic_color * [ `Bold | `Regular ]
  | `Gray of int
  | `RGB  of int * int * int ]

type extended_color =
  [ color
  | `RGBA of int * int * int * int ]

let basic_color_to_int = function
  | `Black -&gt; 0 | `Red     -&gt; 1 | `Green -&gt; 2 | `Yellow -&gt; 3
  | `Blue  -&gt; 4 | `Magenta -&gt; 5 | `Cyan  -&gt; 6 | `White  -&gt; 7

let color_to_int = function
  | `Basic (basic_color,weight) -&gt;
    let base = match weight with `Bold -&gt; 8 | `Regular -&gt; 0 in
    base + basic_color_to_int basic_color
  | `RGB (r,g,b) -&gt; 16 + b + g * 6 + r * 36
  | `Gray i -&gt; 232 + i

let extended_color_to_int = function
  | `RGBA (r,g,b,a) -&gt; 256 + a + b * 6 + g * 36 + r * 216
  | `Grey x -&gt; 2000 + x
  | (`Basic _ | `RGB _ | `Gray _) as color -&gt; color_to_int color
</programlisting>
      <para>
        In the above code, we did something funny to the definition of
        <literal>extended_color_to_int</literal>, that underlines some
        of the downsides of polymorphic variants. In particular, we
        added some special-case handling for the color gray, rather than
        using <literal>color_to_int</literal>. Unfortunately, we
        misspelled <literal>Gray</literal> as <literal>Grey</literal>.
        This is exactly the kind of error that the compiler would catch
        with ordinary variants, but with polymorphic variants, this
        compiles without issue. All that happened was that the compiler
        inferred a wider type for
        <literal>extended_color_to_int</literal>, which happens to be
        compatible with the narrower type that was listed in the mli.
      </para>
      <para>
        If we add an explicit type annotation to the code itself (rather
        than just in the mli), then the compiler has enough information
        to warn us.
      </para>
      <programlisting language="ocaml">
let extended_color_to_int : extended_color -&gt; int = function
  | `RGBA (r,g,b,a) -&gt; 256 + a + b * 6 + g * 36 + r * 216
  | `Grey x -&gt; 2000 + x
  | (`Basic _ | `RGB _ | `Gray _) as color -&gt; color_to_int color
</programlisting>
      <para>
        In particular, the compiler will complain that the
        <literal>`Grey</literal> case as unused.
      </para>
      <programlisting language="ocaml">
File &quot;terminal_color.ml&quot;, line 29, characters 4-11:
Warning 11: this match case is unused.
</programlisting>
      <para>
        Once we have type definitions at our disposal, we can revisit
        the question of how we write the pattern-match that narrows the
        type. In particular, we can explicitly use the type name as part
        of the pattern match, by prefixing it with a
        <literal>#</literal>.
      </para>
      <programlisting language="ocaml">
let extended_color_to_int : extended_color -&gt; int = function
  | `RGBA (r,g,b,a) -&gt; 256 + a + b * 6 + g * 36 + r * 216
  | #color as color -&gt; color_to_int color
</programlisting>
      <para>
        This is useful when you want to narrow down to a type whose
        definition is long, and you don't want the verbosity of writing
        the tags down explicitly in the match.
      </para>
    </sect2>
    <sect2 id="when-to-use-polymorphic-variants">
      <title>When to use polymorphic variants</title>
      <para>
        At first glance, polymorphic variants look like a strict
        improvement over ordinary variants. You can do everything that
        ordinary variants can do, plus it's more flexible and more
        concise. What's not to like?
      </para>
      <para>
        In reality, regular variants are the more pragmatic choice most
        of the time. That's because the flexibility of polymorphic
        variants comes at a price. Here are some of the downsides.
      </para>
      <itemizedlist>
        <listitem>
          <para>
            <emphasis>Complexity:</emphasis> As we've seen, the typing
            rules for polymorphic variants are a lot more complicated
            than they are for regular variants. This means that heavy
            use of polymorphic variants can leave you scratching your
            head trying to figure out why a given piece of code did or
            didn't compile. It can also lead to absurdly long and hard
            to decode error messages. Indeed, concision at the value
            level is often balanced out by more verbosity at the type
            level.
          </para>
        </listitem>
        <listitem>
          <para>
            <emphasis>Error-finding:</emphasis> Polymorphic variants are
            type-safe, but the typing discipline that they impose is, by
            dint of its flexibility, less likely to catch bugs in your
            program.
          </para>
        </listitem>
        <listitem>
          <para>
            <emphasis>Efficiency:</emphasis> This isn't a huge effect,
            but polymorphic variants are somewhat heavier than regular
            variants, and OCaml can't generate code for matching on
            polymorphic variants that is quite as efficient as what is
            generated for regular variants.
          </para>
        </listitem>
      </itemizedlist>
      <para>
        All that said, polymorphic variants are still a useful and
        powerful feature, but it's worth understanding their
        limitations, and how to use them sensibly and modestly.
      </para>
      <para>
        Probably the safest and most common use-case for polymorphic
        variants is for cases where ordinary variants would be
        sufficient, but are syntactically too heavyweight. For example,
        you often want to create a variant type for encoding the inputs
        or outputs to a function, where it's not worth declaring a
        separate type for it. Polymorphic variants are very useful here,
        and as long as there are type annotations that constrain these
        to have explicit, exact types, this tends to work well.
      </para>
      <para>
        Variants are most problematic exactly where you take full
        advantage of their power; in particular, when you take advantage
        of the ability of polymorphic variant types to overlap in the
        tags they support. This ties into OCaml's support for subtyping.
        As we'll discuss further when we cover objects in
        <xref linkend="object-oriented-programming"/>,
        subtyping brings in a lot of complexity, and most of the time,
        that's complexity you want to avoid.
      </para>
    </sect2>
  </sect1>
</chapter><chapter id="error-handling">
  <title>Error Handling</title>
  <para>
    Nobody likes dealing with errors. It's tedious, it's easy to get
    wrong, and it's usually just not as fun as planning out how your
    program is going to succeed. But error handling is important, and
    however much you don't like thinking about it, having your software
    fail due to poor error handling code is worse.
  </para>
  <para>
    Thankfully, OCaml has powerful tools for handling errors reliably
    and with a minimum of pain. In this chapter we'll discuss some of
    the different approaches in OCaml to handling errors, and give some
    advice on how to design interfaces that make error handling easier.
  </para>
  <para>
    We'll start by describing the two basic approaches for reporting
    errors in OCaml: error-aware return types and exceptions.
  </para>
  <sect1 id="error-aware-return-types">
    <title>Error-aware return types</title>
    <para>
      The best way in OCaml to signal an error is to include that error
      in your return value. Consider the type of the
      <literal>find</literal> function in the <literal>List</literal>
      module.
    </para>
    <programlisting language="ocaml">
# List.find;;
- : 'a list -&gt; f:('a -&gt; bool) -&gt; 'a option
</programlisting>
    <para>
      The option in the return type indicates that the function may not
      succeed in finding a suitable element, as you can see below.
    </para>
    <programlisting language="ocaml">
# List.find [1;2;3] ~f:(fun x -&gt; x &gt;= 2) ;;
- : int option = Some 2
# List.find [1;2;3] ~f:(fun x -&gt; x &gt;= 10) ;;
- : int option = None
</programlisting>
    <para>
      Having errors be explicit in the return values of your functions
      tells the caller that there is an error that needs to be handled.
      The caller can then handle the error explicitly, either recovering
      from the error or propagating it onward.
    </para>
    <para>
      Consider the <literal>compute_bounds</literal> function defined
      below. The function takes a list and a comparison function, and
      returns upper and lower bounds for the list by finding the
      smallest and largest element on the list.
      <literal>List.hd</literal> and <literal>List.last</literal>, which
      return <literal>None</literal> when they encounter an empty list,
      are used to extract the largest and smallest element of the list.
    </para>
    <programlisting language="ocaml">
# let compute_bounds ~cmp list =
    let sorted = List.sort ~cmp list in
    match List.hd sorted, List.last sorted with
    | None,_ | _, None -&gt; None
    | Some x, Some y -&gt; Some (x,y)
  ;;
val compute_bounds :
  cmp:('a -&gt; 'a -&gt; int) -&gt; 'a list -&gt; ('a * 'a) option = &lt;fun&gt;
</programlisting>
    <para>
      The match statement is used to handle the error cases, propagating
      a None in <literal>hd</literal> or <literal>last</literal> into
      the return value of <literal>compute_bounds</literal>.
    </para>
    <para>
      On the other hand, in <literal>find_mismatches</literal> below,
      errors encountered during the computation do not propagate to the
      return value of the function. <literal>find_mismatches</literal>
      takes two hash tables as arguments, and searches for keys that
      have different data in one table than in the other. As such, the
      failure to find a key in one table isn't a failure of any sort.
    </para>
    <programlisting language="ocaml">
# let find_mismatches table1 table2 =
     Hashtbl.fold table1 ~init:[] ~f:(fun ~key ~data mismatches -&gt;
        match Hashtbl.find table2 key with
        | Some data' when data' &lt;&gt; data -&gt; key :: mismatches
        | _ -&gt; mismatches
     )
 ;;
val find_mismatches :
  ('a, 'b) Hashtbl.t -&gt; ('a, 'b) Hashtbl.t -&gt; 'a list = &lt;fun&gt;
</programlisting>
    <para>
      The use of options to encode errors underlines the fact that it's
      not clear whether a particular outcome, like not finding something
      on a list, is really an error, or just another valid outcome of
      your function. This turns out to be very context-dependent, and
      error-aware return types give you a uniform way of handling the
      result that works well for both situations.
    </para>
    <sect2 id="encoding-errors-with-result">
      <title>Encoding errors with <literal>Result</literal></title>
      <para>
        Options aren't always a sufficiently expressive way to report
        errors. Specifically, when you encode an error as
        <literal>None</literal>, there's nowhere to say anything about
        the nature of the error.
      </para>
      <para>
        <literal>Result.t</literal> is meant to address this deficiency.
        The type is defined as follows.
      </para>
      <programlisting language="ocaml">
module Result : sig
   type ('a,'b) t = | Ok of 'a
                    | Error of 'b
end
</programlisting>
      <para>
        A <literal>Result.t</literal> is essentially an option augmented
        with the ability to store other information in the error case.
        Like <literal>Some</literal> and <literal>None</literal> for
        options, the constructors <literal>Ok</literal> and
        <literal>Error</literal> are promoted to the toplevel by
        <literal>Core.Std</literal>. As such, we can write:
      </para>
      <programlisting language="ocaml">
# [ Ok 3; Error &quot;abject failure&quot;; Ok 4 ];;
- : (int, string) Core.Result.t list = [Ok 3; Error &quot;abject failure&quot;; Ok 4]
</programlisting>
      <para>
        without first opening the <literal>Result</literal> module.
      </para>
    </sect2>
    <sect2 id="error-and-or_error">
      <title><literal>Error</literal> and
      <literal>Or_error</literal></title>
      <para>
        <literal>Result.t</literal> gives you complete freedom to choose
        the type of value you use to represent errors, but it's often
        useful to standardize on an error type. Among other things, this
        makes it easier to write utility functions to automate common
        error handling patterns.
      </para>
      <para>
        But which type to choose? Is it better to represent errors as
        strings? Some more structured representation like XML or
        s-expressions? Or something else entirely?
      </para>
      <para>
        Core's answer to this question is the <literal>Error.t</literal>
        type, which tries to forge a good compromise between efficiency,
        convenience, and control over the presentation of errors.
      </para>
      <para>
        It might not be obvious at first why efficiency is an issue at
        all. But generating error messages is an expensive business. An
        ASCII representation of a value can be quite time-consuming to
        construct, particularly if it includes expensive-to-convert
        numerical data.
      </para>
      <para>
        <literal>Error</literal> gets around this issue through
        laziness. In particular, an <literal>Error.t</literal> allows
        you to put off generation of the error string until you need it,
        which means a lot of the time you never have to construct it at
        all. You can of course construct an error directly from a
        string:
      </para>
      <programlisting language="ocaml">
# Error.of_string &quot;something went wrong&quot;;;
- : Core.Std.Error.t = &quot;something went wrong&quot;
</programlisting>
      <para>
        A more interesting construction message from a performance point
        of view is to construct an <literal>Error.t</literal> from a
        <emphasis>thunk</emphasis>, <emphasis>i.e.</emphasis>, a
        function that takes a single argument of type
        <literal>unit</literal>.
      </para>
      <programlisting language="ocaml">
# Error.of_thunk (fun () -&gt;
    sprintf &quot;something went wrong: %f&quot; 32.3343);;
  - : Core.Std.Error.t = &quot;something went wrong: 32.334300&quot;
</programlisting>
      <para>
        In this case, we can benefit from the laziness of
        <literal>Error</literal>, since the thunk won't be called until
        the <literal>Error.t</literal> is converted to a string.
      </para>
      <para>
        We can also create an <literal>Error.t</literal> based on an
        s-expression converter. This is probably the most common idiom
        in Core.
      </para>
      <programlisting language="ocaml">
# Error.create &quot;Something failed a long time ago&quot; Time.epoch Time.sexp_of_t;;
- : Core.Std.Error.t =
&quot;Something failed a long time ago: (1969-12-31 19:00:00.000000)&quot;
</programlisting>
      <para>
        Here, the value <literal>Time.epoch</literal> is included in the
        error, but that value isn't converted into an s-expression until
        the error is printed out. Using the Sexplib syntax-extension,
        which is discussed in more detail in chapter
        <ulink url="data-serialization-with-json-xml-and-s-expressions">xref</ulink>,
        we can create an s-expression converter for a new type, thus
        allowing us to conveniently register multiple pieces of data in
        an <literal>Error.t</literal> as a tuple.
      </para>
      <programlisting language="ocaml">
# Error.create &quot;Something went terribly wrong&quot;
    (3.5, [&quot;a&quot;;&quot;b&quot;;&quot;c&quot;], 6034)
    &lt;:sexp_of&lt;float * string list * int&gt;&gt; ;;
- : Core.Std.Error.t = &quot;Something went terribly wrong: (3.5(a b c)6034)&quot;
</programlisting>
      <para>
        The above declaration of
        <literal>&lt;:sexp_of&lt;float * string list * int&gt;&gt;</literal>
        is interpreted by sexplib as a sexp-converter for the tuple.
      </para>
      <para>
        <literal>Error</literal> also supports operations for
        transforming errors. For example, it's often useful to augment
        an error with some extra information about the context of the
        error, or to combine multiple errors together.
        <literal>Error.tag</literal> and
        <literal>Error.of_list</literal> fulfill these roles.
      </para>
      <para>
        The type <literal>'a Or_error.t</literal> is just a shorthand
        for <literal>('a,Error.t) Result.t</literal>, and it is, after
        <literal>option</literal>, the most common way of returning
        errors in Core.
      </para>
    </sect2>
    <sect2 id="bind-and-other-error-handling-idioms">
      <title><literal>bind</literal> and other error-handling
      idioms</title>
      <para>
        As you write more error handling code in OCaml, you'll discover
        that certain patterns start to emerge. A number of these common
        patterns have been codified by functions in modules like
        <literal>Option</literal> and <literal>Result</literal>. One
        particularly useful pattern is built around the function
        <literal>bind</literal>, which is both an ordinary function and
        an infix operator <literal>&gt;&gt;=</literal>. Here's the
        definition of <literal>bind</literal> for options.
      </para>
      <programlisting language="ocaml">
# let bind option f =
    match option with
    | None -&gt; None
    | Some x -&gt; f x
  ;;
val bind : 'a option -&gt; ('a -&gt; 'b option) -&gt; 'b option = &lt;fun&gt;
</programlisting>
      <para>
        As you can see, <literal>bind None f</literal> returns
        <literal>None</literal> without calling <literal>f</literal>,
        and <literal>bind (Some x) f</literal> returns
        <literal>f x</literal>. Perhaps surprisingly,
        <literal>bind</literal> can be used as a way of sequencing
        together error-producing functions so that the first one to
        produce an error terminates the computation. Here's a rewrite of
        <literal>compute_bounds</literal> to use a nested series of
        <literal>bind</literal>s.
      </para>
      <programlisting language="ocaml">
# let compute_bounds ~cmp list =
    let sorted = List.sort ~cmp list in
    Option.bind (List.hd sorted) (fun first -&gt;
      Option.bind (List.last sorted) (fun last -&gt;
        Some (first,last)))
  ;;
val compute_bounds : cmp:('a -&gt; 'a -&gt; int) -&gt; 'a list -&gt; ('a * 'a) option =
  &lt;fun&gt;
</programlisting>
      <para>
        The above code is a little bit hard to swallow, however, on a
        syntactic level. We can make it a bit easier to read, and drop
        some of the parenthesis, by using the infix operator form of
        bind. Note that we locally open the
        <literal>Option.Monad_infix</literal> module to get access to
        the operators. (The module is called
        <literal>Monad_infix</literal> because the bind operator is part
        of a sub-interface called <literal>Monad</literal>, which we'll
        talk about more in
        <xref linkend="concurrent-programming-with-async"/>.
      </para>
      <programlisting language="ocaml">
# let compute_bounds ~cmp list =
    let open Option.Monad_infix in
    let sorted = List.sort ~cmp list in
    List.hd sorted   &gt;&gt;= fun first -&gt;
    List.last sorted &gt;&gt;= fun last  -&gt;
    Some (first,last)
</programlisting>
      <para>
        There are other useful idioms encoded in the functions in
        <literal>Option</literal>. Another example is
        <literal>Option.both</literal>, which takes two optional values
        and produces a new optional pair that is <literal>None</literal>
        if either of its arguments are <literal>None</literal>. Using
        <literal>Option.both</literal>, we can make
        <literal>compute_bounds</literal> even shorter.
      </para>
      <programlisting language="ocaml">
# let compute_bounds ~cmp list =
    let sorted = List.sort ~cmp list in
    Option.both (List.hd sorted) (List.last sorted)
</programlisting>
      <para>
        These error-handling functions are valuable because they let you
        express your error handling both explicitly and concisely. We've
        only discussed these functions in the context of the
        <literal>Option</literal> module, but similar functionality is
        available in both <literal>Result</literal> and
        <literal>Or_error</literal>.
      </para>
    </sect2>
  </sect1>
  <sect1 id="exceptions">
    <title>Exceptions</title>
    <para>
      Exceptions in OCaml are not that different from exceptions in many
      other languages, like Java, C# and Python. Exceptions are a way to
      terminate a computation and report an error, while providing a
      mechanism to catch and handle (and possibly recover from)
      exceptions that are triggered by sub-computations.
    </para>
    <para>
      You can trigger an exception by, for example, dividing an integer
      by zero:
    </para>
    <programlisting language="ocaml">
# 3 / 0;;
Exception: Division_by_zero.
</programlisting>
    <para>
      And an exception can terminate a computation even if it happens
      nested a few levels deep in a computation.
    </para>
    <programlisting language="ocaml">
# List.map ~f:(fun x -&gt; 100 / x) [1;3;0;4];;
Exception: Division_by_zero.
</programlisting>
    <para>
      If we put a <literal>printf</literal> in the middle of the
      computation, we can see that the <literal>List.map</literal> is
      interrupted part way through it's execution:
    </para>
    <programlisting language="ocaml">
# List.map ~f:(fun x -&gt; printf &quot;%d\n%!&quot; x; 100 / x) [1;3;0;4];;
1
3
0
Exception: Division_by_zero.
</programlisting>
    <para>
      In addition to built-in exceptions like
      <literal>Divide_by_zero</literal>, OCaml lets you define your own.
    </para>
    <programlisting language="ocaml">
# exception Key_not_found of string;;
exception Key_not_found of string
# raise (Key_not_found &quot;a&quot;);;
Exception: Key_not_found(&quot;a&quot;).
</programlisting>
    <para>
      Here's an example of a function for looking up a key in an
      <emphasis>association list</emphasis>, <emphasis>i.e.</emphasis> a
      list of key/value pairs which uses this newly-defined exception:
    </para>
    <programlisting language="ocaml">
# let rec find_exn alist key = match alist with
    | [] -&gt; raise (Key_not_found key)
    | (key',data) :: tl -&gt; if key = key' then data else find_exn tl key
  ;;
val find_exn : (string * 'a) list -&gt; string -&gt; 'a = &lt;fun&gt;
# let alist = [(&quot;a&quot;,1); (&quot;b&quot;,2)];;
val alist : (string * int) list = [(&quot;a&quot;, 1); (&quot;b&quot;, 2)]
# find_exn alist &quot;a&quot;;;
- : int = 1
# find_exn alist &quot;c&quot;;;
Exception: Key_not_found(&quot;c&quot;).
</programlisting>
    <para>
      Note that we named the function <literal>find_exn</literal> to
      warn the user that the function routinely throws exceptions, a
      convention that is used heavily in Core.
    </para>
    <para>
      In the above example, <literal>raise</literal> throws the
      exception, thus terminating the computation. The type of raise is
      a bit surprising when you first see it:
    </para>
    <programlisting language="ocaml">
# raise;;
- : exn -&gt; 'a = &lt;fun&gt;
</programlisting>
    <para>
      The return type of <literal>'a</literal> suggests that
      <literal>raise</literal> could return a value of any type. That
      seems impossible, and it is. Really, <literal>raise</literal> has
      this type because it never returns at all. This behavior isn't
      restricted to functions like <literal>raise</literal> that
      terminate by throwing exceptions. Here's another example of a
      function that doesn't return a value.
    </para>
    <programlisting language="ocaml">
# let rec forever () = forever ();;
val forever : unit -&gt; 'a = &lt;fun&gt;
</programlisting>
    <para>
      <literal>forever</literal> doesn't return a value for a different
      reason: it is an infinite loop.
    </para>
    <para>
      This all matters because it means that the return type of
      <literal>raise</literal> can be whatever it needs to be to fit in
      to the context it is called in. Thus, the type system will let us
      throw an exception anywhere in a program.
    </para>
    <note>
    <title>
    Declaring exceptions with <literal>with sexp</literal>
    </title>
    <para>
      OCaml can't always generate a useful textual representation of an
      exception. For example:
    </para>
    <programlisting language="ocaml">
# exception Wrong_date of Date.t;;
exception Wrong_date of Date.t
# Wrong_date (Date.of_string &quot;2011-02-23&quot;);;
- : exn = Wrong_date(_)
</programlisting>
    <para>
      But if we declare the exception using <literal>with sexp</literal>
      (and the constituent types have sexp converters), we'll get
      something with more information.
    </para>
    <programlisting language="ocaml">
# exception Wrong_date of Date.t with sexp;;
exception Wrong_date of Core.Std.Date.t
# Wrong_date (Date.of_string &quot;2011-02-23&quot;);;
- : exn = (.Wrong_date 2011-02-23)
</programlisting>
    <para>
      The period in front of <literal>Wrong_date</literal> is there
      because the representation generated by
      <literal>with sexp</literal> includes the full module path of the
      module where the exception in question is defined. We'll talk more
      about modules in
      <xref linkend="files-modules-and-programs"/>, but for
      now, it's enough to know that this is useful in tracking down
      which precise exception is being reported. In this case, since
      we've declared the exception at the toplevel, that module path is
      trivial.
    </para>
    </note>
    <sect2 id="helper-functions-for-throwing-exceptions">
      <title>Helper functions for throwing exceptions</title>
      <para>
        A number of helper functions that are provided to simplify the
        task of throwing exceptions. The simplest one is
        <literal>failwith</literal>, which could be defined as follows:
      </para>
      <programlisting language="ocaml">
# let failwith msg = raise (Failure msg);;
val failwith : string -&gt; 'a = &lt;fun&gt;
</programlisting>
      <para>
        There are several other useful functions for raising exceptions,
        which can be found in the API documentation for the
        <literal>Common</literal> and <literal>Exn</literal> modules in
        Core.
      </para>
      <para>
        Another important way of throwing an exception is the
        <literal>assert</literal> directive. <literal>assert</literal>
        is used for situations where violation of condition in question
        is a bug. Consider the following piece of code for zipping
        together two lists.
      </para>
      <programlisting language="ocaml">
# let merge_lists xs ys ~f =
    if List.length xs &lt;&gt; List.length ys then None
    else
      let rec loop xs ys =
        match xs,ys with
        | [],[] -&gt; []
        | x::xs, y::ys -&gt; f x y :: loop xs ys
        | _ -&gt; assert false
      in
      Some (loop xs ys)
   ;;
 val merge_lists : 'a list -&gt; 'b list -&gt; f:('a -&gt; 'b -&gt; 'c) -&gt; 'c list option =
  &lt;fun&gt;
# merge_lists [1;2;3] [-1;1;2] ~f:(+);;
- : int list option = Some [0; 3; 5]
# merge_lists [1;2;3] [-1;1] ~f:(+);;
- : int list option = None
</programlisting>
      <para>
        Here we use <literal>assert false</literal>, which means that
        the assert is guaranteed to trigger. In general, one can put an
        arbirary condition in the assertion.
      </para>
      <para>
        In this case, the assert can never be triggered because we have
        a check that makes sure that the lists are of the same length
        before we call <literal>loop</literal>. If we change the code so
        that we drop this test, then we can trigger the assert.
      </para>
      <programlisting language="ocaml">
# let merge_lists xs ys ~f =
      let rec loop xs ys =
        match xs,ys with
        | [],[] -&gt; []
        | x::xs, y::ys -&gt; f x y :: loop xs ys
        | _ -&gt; assert false
      in
      loop xs ys
   ;;
val merge_lists : 'a list -&gt; 'b list -&gt; f:('a -&gt; 'b -&gt; 'c) -&gt; 'c list = &lt;fun&gt;
# merge_lists [1;2;3] [-1] ~f:(+);;
Exception: (Assert_failure //toplevel// 25 15).
</programlisting>
      <para>
        This shows what's special about <literal>assert</literal>, which
        is that it captures the source location.
      </para>
    </sect2>
    <sect2 id="exception-handlers">
      <title>Exception handlers</title>
      <para>
        So far, we've only seen exceptions fully terminate the execution
        of a computation. But often, we want a program to be able to
        respond to and recover from an exception. This is achieved
        through the use of <emphasis>exception handlers</emphasis>.
      </para>
      <para>
        In OCaml, an exception handler is declared using a
        <literal>try</literal>/<literal>with</literal> statement. Here's
        the basic syntax.
      </para>
      <programlisting language="ocaml">
try &lt;expr&gt; with
| &lt;pat1&gt; -&gt; &lt;expr1&gt;
| &lt;pat2&gt; -&gt; &lt;expr2&gt;
...
</programlisting>
      <para>
        A <literal>try/with</literal> clause first evaluates it's body,
        <literal>&lt;expr&gt;</literal>. If no exception is thrown, then
        the result of evaluating the body is what the entire
        <literal>try/with</literal> clause evaluates to.
      </para>
      <para>
        But if the evaluation of the body throws an exception, then the
        exception will be fed to the pattern match statements following
        the <literal>with</literal>. If the exception matches a pattern,
        then we consider the exception caught, and the
        <literal>try/with</literal> clause evaluates to the expression
        on the right-hand side of the matching pattern.
      </para>
      <para>
        Otherwise, the original exception continues up the stack of
        function calls, to be handled by the next outer exception
        handler. If the exception is never caught, it terminates the
        program.
      </para>
    </sect2>
    <sect2 id="cleaning-up-in-the-presence-of-exceptions">
      <title>Cleaning up in the presence of exceptions</title>
      <para>
        One headache with exceptions is that they can terminate your
        execution at unexpected places, leaving your program in an
        awkward state. Consider the following code snippet:
      </para>
      <programlisting language="ocaml">
let load_config filename =
  let inc = In_channel.create filename in
  let config = Config.t_of_sexp (Sexp.input_sexp inc) in
  In_channel.close inc;
  config
</programlisting>
      <para>
        The problem with this code is that the function that loads the
        s-expression and parses it into a <literal>Config.t</literal>
        might throw an exception if the config file in question is
        malformed. Unfortunately, that means that the
        <literal>In_channel.t</literal> that was opened will never be
        closed, leading to a file-descriptor leak.
      </para>
      <para>
        We can fix this using Core's <literal>protect</literal>
        function. The basic purpose of <literal>protect</literal> is to
        ensure that the <literal>finally</literal> thunk will be called
        when <literal>f</literal> exits, whether it exited normally or
        with an exception. This is similar to the
        <literal>try/finally</literal> construct available in many
        programming languages, but it is implemented in a library,
        rather than being a built-in primitive. Here's how it could be
        used to fix <literal>load_config</literal>.
      </para>
      <programlisting language="ocaml">
let load_config filename =
  let inc = In_channel.create filename in
  protect ~f:(fun () -&gt; Config.t_of_sexp (Sexp.input_sexp inc)
    ~finally:(fun () -&gt; In_channel.close inc)
</programlisting>
    </sect2>
    <sect2 id="catching-specific-exceptions">
      <title>Catching specific exceptions</title>
      <para>
        OCaml's exception-handling system allows you to tune your
        error-recovery logic to the particular error that was thrown.
        For example, <literal>List.find_exn</literal> always throws
        <literal>Not_found</literal>. You can take advantage of this in
        your code, for example, let's define a function called
        <literal>lookup_weight</literal>, with the following signature:
      </para>
      <programlisting language="ocaml">
(** [lookup_weight ~compute_weight alist key] Looks up a
    floating-point weight by applying [compute_weight] to the data
    associated with [key] by [alist].  If [key] is not found, then
    return 0.
*)
val lookup_weight :
  compute_weight:('data -&gt; float) -&gt; ('key * 'data) list -&gt; 'key -&gt; float
</programlisting>
      <para>
        We can implement such a function using exceptions as follows:
      </para>
      <programlisting language="ocaml">
# let lookup_weight ~compute_weight alist key =
    try
      let data = List.Assoc.find_exn alist key in
      compute_weight data
    with
      Not_found -&gt; 0. ;;
val lookup_weight :
  compute_weight:('a -&gt; float) -&gt; ('b * 'a) list -&gt; 'b -&gt; float =
  &lt;fun&gt;
</programlisting>
      <para>
        This implementation is more problematic than it looks. In
        particular, what happens if <literal>compute_weight</literal>
        itself throws an exception? Ideally,
        <literal>lookup_weight</literal> should propagate that exception
        on, but if the exception happens to be
        <literal>Not_found</literal>, then that's not what will happen:
      </para>
      <programlisting language="ocaml">
# lookup_weight ~compute_weight:(fun _ -&gt; raise Not_found)
    [&quot;a&quot;,3; &quot;b&quot;,4] &quot;a&quot; ;;
- : float = 0.
</programlisting>
      <para>
        This kind of problem is hard to detect in advance, because the
        type system doesn't tell us what kinds of exceptions a given
        function might throw. Because of this kind of confusion, it's
        usually better to avoid catching specific exceptions. In this
        case, we can improve the code by catching the exception in a
        narrower scope.
      </para>
      <programlisting language="ocaml">
# let lookup_weight ~compute_weight alist key =
    match
      try Some (List.Assoc.find_exn alist key)
      with Not_found -&gt; None
    with
    | None -&gt; 0.
    | Some data -&gt; compute_weight data ;;
val lookup_weight :
  compute_weight:('a -&gt; float) -&gt;
  ('b, 'a) Core.Std.List.Assoc.t -&gt; 'b -&gt; float = &lt;fun&gt;
</programlisting>
      <para>
        At which point, it makes sense to simply use the non-exception
        throwing function, <literal>List.Assoc.find</literal>, instead.
      </para>
      <programlisting language="ocaml">
# let lookup_weight ~compute_weight alist key =
    match List.Assoc.find alist key with
    | None -&gt; 0.
    | Some data -&gt; compute_weight data ;;
val lookup_weight :
  compute_weight:('a -&gt; float) -&gt;
  ('b, 'a) Core.Std.List.Assoc.t -&gt; 'b -&gt; float = &lt;fun&gt;
</programlisting>
    </sect2>
    <sect2 id="backtraces">
      <title>Backtraces</title>
      <para>
        A big part of the point of exceptions is to give useful
        debugging information. But at first glance, OCaml's exceptions
        can be less than informative. Consider the following simple
        program.
      </para>
      <programlisting language="ocaml">
(* exn.ml *)

open Core.Std
exception Empty_list

let list_max = function
  | [] -&gt; raise Empty_list
  | hd :: tl -&gt; List.fold tl ~init:hd ~f:(Int.max)

let () =
  printf &quot;%d\n&quot; (list_max [1;2;3]);
  printf &quot;%d\n&quot; (list_max [])
</programlisting>
      <para>
        If we build and run this program, we'll get a pretty
        uninformative error:
      </para>
      <programlisting language="bash">
$ ./exn
3
Fatal error: exception Exn.Empty_list
</programlisting>
      <para>
        The example in question is short enough that it's quite easy to
        see where the error came from. But in a complex program, simply
        knowing which exception was thrown is usually not enough
        information to figure out what went wrong.
      </para>
      <para>
        We can get more information from OCaml if we turn on stack
        traces. This can be done by setting the
        <literal>OCAMLRUNPARAM</literal> environment variable, as shown:
      </para>
      <programlisting language="bash">
exn $ export OCAMLRUNPARAM=b
exn $ ./exn
3
Fatal error: exception Exn.Empty_list
Raised at file &quot;exn.ml&quot;, line 7, characters 16-26
Called from file &quot;exn.ml&quot;, line 12, characters 17-28
</programlisting>
      <para>
        Backtraces can also be obtained at runtime. In particular,
        <literal>Exn.backtrace</literal> will return the backtrace of
        the most recently thrown exception.
      </para>
    </sect2>
    <sect2 id="from-exceptions-to-error-aware-types-and-back-again">
      <title>From exceptions to error-aware types and back again</title>
      <para>
        Both exceptions and error-aware types are necessary parts of
        programming in OCaml. As such, you often need to move between
        these two worlds. Happily, Core comes with some useful helper
        functions to help you do just that. For example, given a piece
        of code that can throw an exception, you can capture that
        exception into an option as follows:
      </para>
      <programlisting language="ocaml">
# let find alist key =
    Option.try_with (fun () -&gt; find_exn alist key) ;;
val find : (string * 'a) list -&gt; string -&gt; 'a option = &lt;fun&gt;
# find [&quot;a&quot;,1; &quot;b&quot;,2] &quot;c&quot;;;
- : int Core.Std.Option.t = None
# find [&quot;a&quot;,1; &quot;b&quot;,2] &quot;b&quot;;;
- : int Core.Std.Option.t = Some 2
</programlisting>
      <para>
        And <literal>Result</literal> and <literal>Or_error</literal>
        have similar <literal>try_with</literal> functions. So, we could
        write:
      </para>
      <programlisting language="ocaml">
# let find alist key =
    Result.try_with (fun () -&gt; find_exn alist key) ;;
val find : (string * 'a) list -&gt; string -&gt; ('a, exn) Result.t = &lt;fun&gt;
# find [&quot;a&quot;,1; &quot;b&quot;,2] &quot;c&quot;;;
- : (int, exn) Result.t = Result.Error Key_not_found(&quot;c&quot;)
</programlisting>
      <para>
        And then we can re-raise that exception:
      </para>
      <programlisting language="ocaml">
# Result.ok_exn (find [&quot;a&quot;,1; &quot;b&quot;,2] &quot;b&quot;);;
- : int = 2
# Result.ok_exn (find [&quot;a&quot;,1; &quot;b&quot;,2] &quot;c&quot;);;
Exception: Key_not_found(&quot;c&quot;).
</programlisting>
    </sect2>
  </sect1>
</chapter><chapter id="functors">
  <title>Functors</title>
  <para>
    Up until now, we've seen OCaml's module system play an important but
    limited role. In particular, we've seen them as a mechanism for
    organizing code into units with specified interfaces. But modules
    can do much more than that, acting as a powerful toolset for
    building generic code and structuring large-scale systems. Much of
    that power comes from functors.
  </para>
  <para>
    Functors are, roughly speaking, functions from modules to modules,
    and they can be used to solve a variety of code-structuring
    problems, including:
  </para>
  <itemizedlist>
    <listitem>
      <para>
        <emphasis>Dependency injection</emphasis>, or making the
        implementations of some components of a system swappable. This
        is particularly useful when you want to mock up parts of your
        system for testing and simulation purposes.
      </para>
    </listitem>
    <listitem>
      <para>
        <emphasis>Auto-extension of modules</emphasis>. Sometimes, there
        is some functionality that you want to build in a standard way
        for different types, in each case based on a some piece of
        type-specific logic. For example, you might want to add a slew
        of comparison operators derived from a base comparison function.
        To do this by hand would require a lot of repetitive code for
        each type, but functors let you write this logic just once and
        apply it to many different types.
      </para>
    </listitem>
    <listitem>
      <para>
        <emphasis>Instantiating modules with state</emphasis>. Modules
        can contain mutable state, and that means that you'll
        occasionally want to have multiple instantiations of a
        particular module, each with its own separate and independent
        mutable state. Functors let you automate the construction of
        such modules.
      </para>
    </listitem>
  </itemizedlist>
  <sect1 id="a-trivial-example">
    <title>A trivial example</title>
    <para>
      We'll start by considering the simplest possible example: a
      functor for incrementing an integer.
    </para>
    <para>
      More precisely, we'll create a functor that takes a module
      containing a single integer variable <literal>x</literal>, and
      returns a new module with <literal>x</literal> incremented by one.
    </para>
    <programlisting language="ocaml">
# module type X_int = sig val x : int end;;
module type X_int = sig val x : int end
# module Increment (M:X_int) : X_int = struct
    let x = M.x + 1
  end;;
module Increment : functor (M : X_int) -&gt; X_int
</programlisting>
    <para>
      One thing that immediately jumps out about functors is that
      they're considerably more heavyweight syntactically than ordinary
      functions. For one thing, functors require explicit (module) type
      annotations, which ordinary functions do not. Here, we've
      specified the module type <literal>X_int</literal> for both the
      input and output of the functor. Technically, only the type on the
      input is mandatory, although in practice, one often specifies
      both.
    </para>
    <para>
      The following shows what happens when we omit the module type for
      the output of the functor.
    </para>
    <programlisting language="ocaml">
# module Increment (M:X_int) = struct
    let x = M.x + 1
  end;;
module Increment : functor (M : X_int) -&gt; sig val x : int end
</programlisting>
    <para>
      We can see that the inferred module type of the output is now
      written out explicitly, rather than being a reference to the named
      signature <literal>X_int</literal>.
    </para>
    <para>
      We can now use <literal>Increment</literal> to define new modules.
    </para>
    <programlisting language="ocaml">
# module Three = struct let x = 3 end;;
  module Three : sig val x : int end
# module Four = Increment(Three);;
module Four : sig val x : int end
# Four.x - Three.x;;
- : int = 1
</programlisting>
    <para>
      In this case, we applied <literal>Increment</literal> to a module
      whose signature is exactly equal to <literal>X_int</literal>. But
      we can apply <literal>Increment</literal> to any module that
      satisfies the interface <literal>X_int</literal>, in the same way
      that a the contents of an <literal>ml</literal> file can satisfy
      the <literal>mli</literal>. That means that the module type can
      omit some information available in the module, either by dropping
      fields or by leaving some fields abstract. Here's an example:
    </para>
    <programlisting language="ocaml">
# module Three_and_more = struct
    let x = 3
    let y = &quot;three&quot;
  end;;
module Three_and_more : sig val x : int val x_string : string end
# module Four = Increment(Three_and_more);;
module Four : sig val x : int end
</programlisting>
  </sect1>
  <sect1 id="a-bigger-example-computing-with-intervals">
    <title>A bigger example: computing with intervals</title>
    <para>
      Let's now consider a more realistic example of how to use
      functors: a library for computing with intervals. This library
      will be functorized over the type of the endpoints of the
      intervals and the ordering of those endpoints.
    </para>
    <para>
      First we'll define a module type that captures the information
      we'll need about the endpoint type. This interface, which we'll
      call <literal>Comparable</literal>, contains just two things: a
      comparison function, and the type of the values to be compared.
    </para>
    <programlisting language="ocaml">
# module type Comparable = sig
    type t
    val compare : t -&gt; t -&gt; int
  end ;;
module type Comparable = sig type t val compare : t -&gt; t -&gt; int end
</programlisting>
    <para>
      The comparison function follows the standard OCaml idiom for such
      functions, returning <literal>0</literal> if the two elements are
      equal, a positive number if the first element is larger than the
      second, and a negative number if the first element is smaller than
      the second. Thus, we could rewrite the standard comparison
      functions on top of <literal>compare</literal> as shown below.
    </para>
    <programlisting language="ocaml">
compare x y &lt; 0     (* x &lt; y *)
compare x y = 0     (* x = y *)
compare x y &gt; 0     (* x &gt; y *)
</programlisting>
    <para>
      The functor for creating the interval module is shown below. We
      represent an interval with a variant type, which is either
      <literal>Empty</literal> or <literal>Interval (x,y)</literal>,
      where <literal>x</literal> and <literal>y</literal> are the bounds
      of the interval.
    </para>
    <programlisting language="ocaml">
# module Make_interval(Endpoint : Comparable) = struct

    type t = | Interval of Endpoint.t * Endpoint.t
             | Empty

    let create low high =
      if Endpoint.compare low high &gt; 0 then Empty
      else Interval (low,high)

    let is_empty = function
      | Empty -&gt; true
      | Interval _ -&gt; false

    let contains t x =
      match t with
      | Empty -&gt; false
      | Interval (l,h) -&gt;
        Endpoint.compare x l &gt;= 0 &amp;&amp; Endpoint.compare x h &lt;= 0

    let intersect t1 t2 =
      let min x y = if Endpoint.compare x y &lt;= 0 then x else y in
      let max x y = if Endpoint.compare x y &gt;= 0 then x else y in
      match t1,t2 with
      | Empty, _ | _, Empty -&gt; Empty
      | Interval (l1,h1), Interval (l2,h2) -&gt;
        create (max l1 l2) (min h1 h2)

  end ;;
module Make_interval :
  functor (Endpoint : Comparable) -&gt;
    sig
      type t = Interval of Endpoint.t * Endpoint.t | Empty
      val create : Endpoint.t -&gt; Endpoint.t -&gt; t
      val is_empty : t -&gt; bool
      val contains : t -&gt; Endpoint.t -&gt; bool
      val intersect : t -&gt; t -&gt; t
    end
</programlisting>
    <para>
      We can instantiate the functor by applying it to a module with the
      right signature. In the following, we provide the functor input as
      an anonymous module.
    </para>
    <programlisting language="ocaml">
# module Int_interval =
    Make_interval(struct
      type t = int
      let compare = Int.compare
    end);;
module Int_interval :
  sig
    type t = Interval of int * int | Empty
    val create : int -&gt; int -&gt; t
    val is_empty : t -&gt; bool
    val contains : t -&gt; int -&gt; bool
    val intersect : t -&gt; t -&gt; t
  end
</programlisting>
    <para>
      When we choose our interfaces that are aligned with the standards
      of our libraries, we don't need to construct a custom module to
      feed to the functor. In this case, for example, we can directly
      use the <literal>Int</literal> or <literal>String</literal>
      modules provided by Core.
    </para>
    <programlisting language="ocaml">
# module Int_interval = Make_interval(Int) ;;
# module String_interval = Make_interval(String) ;;
</programlisting>
    <para>
      This works because many modules in Core, including
      <literal>Int</literal> and <literal>String</literal>, satisfy an
      extended version of the <literal>Comparable</literal> signature
      described above. Standardized signatures are generally good
      practice, both because they makes functors easier to use, and
      because they make the codebase generally easier to navigate.
    </para>
    <para>
      Now we can use the newly defined <literal>Int_interval</literal>
      module like any ordinary module.
    </para>
    <programlisting language="ocaml">
# let i1 = Int_interval.create 3 8;;
val i1 : Int_interval.t = Int_interval.Interval (3, 8)
# let i2 = Int_interval.create 4 10;;
val i2 : Int_interval.t = Int_interval.Interval (4, 10)
# Int_interval.intersect i1 i2;;
- : Int_interval.t = Int_interval.Interval (4, 8)
</programlisting>
    <para>
      This design gives us the freedom to use any comparison function we
      want for comparing the endpoints. We could, for example, create a
      type of int interval with the order of the comparison reversed, as
      follows:
    </para>
    <programlisting language="ocaml">
# module Rev_int_interval =
    Make_interval(struct
      type t = int
      let compare x y = Int.compare y x
    end);;
</programlisting>
    <para>
      The behavior of <literal>Rev_int_interval</literal> is of course
      different from <literal>Int_interval</literal>, as we can see
      below.
    </para>
    <programlisting language="ocaml">
# let interval = Int_interval.create 4 3;;
val interval : Int_interval.t = Int_interval.Empty
# let rev_interval = Rev_int_interval.create 4 3;;
val rev_interval : Rev_int_interval.t = Rev_int_interval.Interval (4, 3)
</programlisting>
    <para>
      Importantly, <literal>Rev_int_interval.t</literal> is a different
      type than <literal>Int_interval.t</literal>, even though its
      physical representation is the same. Indeed, the type system will
      prevent us from confusing them.
    </para>
    <programlisting language="ocaml">
# Int_interval.contains rev_interval 3;;
Characters 22-34:
  Int_interval.contains rev_interval 3;;
                        ^^^^^^^^^^^^
Error: This expression has type Rev_int_interval.t
       but an expression was expected of type
         Int_interval.t = Make_interval(Int).t
</programlisting>
    <para>
      This is important, because confusing the two kinds of intervals
      would be a semantic error, and it's an easy one to make. The
      ability of functors to mint new types is a useful trick that comes
      up a lot.
    </para>
    <sect2 id="making-the-functor-abstract">
      <title>Making the functor abstract</title>
      <para>
        There's a problem with <literal>Make_interval</literal>. The
        code we wrote depends on the invariant that the upper bound of
        an interval is greater than its lower bound, but that invariant
        can be violated. The invariant is enforced by the create
        function, but because <literal>Interval.t</literal> is not
        abstract, we can bypass the <literal>create</literal> function.
      </para>
      <programlisting language="ocaml">
# Int_interval.create 4 3;; (* going through create *)
- : Int_interval.t = Int_interval.Empty
# Int_interval.Interval (4,3);; (* bypassing create *)
- : Int_interval.t = Int_interval.Interval (4, 3)
</programlisting>
      <para>
        To make <literal>Int_interval.t</literal> abstract, we need to
        apply an interface to the output of the
        <literal>Make_interval</literal>. Here's an explicit interface
        that we can use for that purpose.
      </para>
      <programlisting language="ocaml">
# module type Interval_intf = sig
   type t
   type endpoint
   val create : endpoint -&gt; endpoint -&gt; t
   val is_empty : t -&gt; bool
   val contains : t -&gt; endpoint -&gt; bool
   val intersect : t -&gt; t -&gt; t
  end;;
</programlisting>
      <para>
        This interface includes the type <literal>endpoint</literal> to
        represent the type of the endpoints of the interval. Given this
        interface, we can redo our definition of
        <literal>Make_interval</literal>. Notice that we added the type
        <literal>endpoint</literal> to the implementation of the module
        to make the implementation match
        <literal>Interval_intf</literal>.
      </para>
      <programlisting language="ocaml">
# module Make_interval(Endpoint : Comparable) : Interval_intf = struct

    type endpoint = Endpoint.t
    type t = | Interval of Endpoint.t * Endpoint.t
             | Empty

    ....

  end ;;
module Make_interval : functor (Endpoint : Comparable) -&gt; Interval_intf
</programlisting>
    </sect2>
    <sect2 id="sharing-constraints">
      <title>Sharing constraints</title>
      <para>
        The resulting module is abstract, but unfortunately, it's too
        abstract. In particular, we haven't exposed the type
        <literal>endpoint</literal>, which means that we can't even
        construct an interval anymore.
      </para>
      <programlisting language="ocaml">
# module Int_interval = Make_interval(Int);;
module Int_interval : Interval_intf
# Int_interval.create 3 4;;
Characters 20-21:
  Int_interval.create 3 4;;
                      ^
Error: This expression has type int but an expression was expected of type
         Int_interval.endpoint
</programlisting>
      <para>
        To fix this, we need to expose the fact that
        <literal>endpoint</literal> is equal to <literal>Int.t</literal>
        (or more generally, <literal>Endpoint.t</literal>, where
        <literal>Endpoint</literal> is the argument to the functor). One
        way of doing this is through a <emphasis>sharing
        constraint</emphasis>, which allows you to tell the compiler to
        expose the fact that a given type is equal to some other type.
        The syntax for a sharing constraint on a module type is as
        follows.
      </para>
      <programlisting language="ocaml">
S with type s = t
</programlisting>
      <para>
        where <literal>S</literal> is a module type,
        <literal>s</literal> is a type inside of <literal>S</literal>,
        and <literal>t</literal> is a type defined outside of
        <literal>S</literal>. The result of this expression is a new
        signature that's been modified so that it exposes the fact that
        <literal>s</literal> is equal to <literal>t</literal>. We can
        use a sharing constraint to create a specialized version of
        <literal>Interval_intf</literal> for integer intervals.
      </para>
      <programlisting language="ocaml">
# module type Int_interval_intf = Interval_intf with type endpoint = int;;
module type Int_interval_intf =
  sig
    type t
    type endpoint = int
    val create : endpoint -&gt; endpoint -&gt; t
    val is_empty : t -&gt; bool
    val contains : t -&gt; endpoint -&gt; bool
    val intersect : t -&gt; t -&gt; t
  end
</programlisting>
      <para>
        And we can also use it in the context of a functor, where the
        right-hand side of the sharing constraint is an element of the
        functor argument. Thus, we expose an equality between a type in
        the output of the functor (in this case, the type
        <literal>endpoint</literal>) and a type in its input
        (<literal>Endpoint.t</literal>).
      </para>
      <programlisting language="ocaml">
# module Make_interval(Endpoint : Comparable)
      : Interval_intf with type endpoint = Endpoint.t = struct

    type endpoint = Endpoint.t
    type t = | Interval of Endpoint.t * Endpoint.t
             | Empty

    ...

  end ;;
module Make_interval :
  functor (Endpoint : Comparable) -&gt;
    sig
      type t
      type endpoint = Endpoint.t
      val create : endpoint -&gt; endpoint -&gt; t
      val is_empty : t -&gt; bool
      val contains : t -&gt; endpoint -&gt; bool
      val intersect : t -&gt; t -&gt; t
    end
</programlisting>
      <para>
        So now, the interface is as it was, except that
        <literal>endpoint</literal> is now known to be equal to
        <literal>Endpoint.t</literal>. As a result of that type
        equality, we can now do things like construct intervals again.
      </para>
      <programlisting language="ocaml">
# let i = Int_interval.create 3 4;;
val i : Int_interval.t = &lt;abstr&gt;
# Int_interval.contains i 5;;
- : bool = false
</programlisting>
    </sect2>
    <sect2 id="destructive-substitution">
      <title>Destructive substitution</title>
      <para>
        Sharing constraints basically do the job, but they have some
        downsides. In particular, we've now been stuck with the useless
        type declaration of <literal>endpoint</literal> that clutters up
        both the interface and the implementation. A better solution
        would be to modify the <literal>Interval_intf</literal>
        signature by replacing <literal>endpoint</literal> with
        <literal>Endpoint.t</literal> everywhere it shows up, and
        deleting the definition of <literal>endpoint</literal> from the
        signature. We can do just this using what's called
        <emphasis>destructive substitution</emphasis>. Here's the basic
        syntax.
      </para>
      <programlisting language="ocaml">
S with type s := t
</programlisting>
      <para>
        The following shows how we could use this with
        <literal>Make_interval</literal>.
      </para>
      <para>
        Here's an example of what we get if we use destructive
        substitution to specialize the <literal>Interval_intf</literal>
        interface to integer intervals.
      </para>
      <programlisting language="ocaml">
# module type Int_interval_intf = Interval_intf with type endpoint := int;;
module type Int_interval_intf =
  sig
    type t
    val create : int -&gt; int -&gt; t
    val is_empty : t -&gt; bool
    val contains : t -&gt; int -&gt; bool
    val intersect : t -&gt; t -&gt; t
  end
</programlisting>
      <para>
        There's now no mention of n <literal>endpoint</literal>, all
        occurrences of that type having been replaced by
        <literal>int</literal>. As with sharing constraints, we can also
        use this in the context of a functor.
      </para>
      <programlisting language="ocaml">
# module Make_interval(Endpoint : Comparable)
    : Interval_intf with type endpoint := Endpoint.t =
  struct

    type t = | Interval of Endpoint.t * Endpoint.t
             | Empty

    ....

  end ;;
module Make_interval :
  functor (Endpoint : Comparable) -&gt;
    sig
      type t
      val create : Endpoint.t -&gt; Endpoint.t -&gt; t
      val is_empty : t -&gt; bool
      val contains : t -&gt; Endpoint.t -&gt; bool
      val intersect : t -&gt; t -&gt; t
    end
</programlisting>
      <para>
        The interface is precisely what we want, and we didn't need to
        define the <literal>endpoint</literal> type alias in the body of
        the module. If we instantiate this module, we'll see that it
        works properly: we can construct new intervals, but
        <literal>t</literal> is abstract, and so we can't directly
        access the constructors and violate the invariants of the data
        structure.
      </para>
      <programlisting language="ocaml">
# module Int_interval = Make_interval(Int);;
# Int_interval.create 3 4;;
- : Int_interval.t = &lt;abstr&gt;
# Int_interval.Interval (4,3);;
Characters 0-27:
  Int_interval.Interval (4,3);;
  ^^^^^^^^^^^^^^^^^^^^^^^^^^^
Error: Unbound constructor Int_interval.Interval
</programlisting>
    </sect2>
    <sect2 id="using-multiple-interfaces">
      <title>Using multiple interfaces</title>
      <para>
        Another feature that we might want for our interval module is
        the ability to serialize the type, in particular, by converting
        to s-expressions. If we simply invoke the
        <literal>sexplib</literal> macros by adding
        <literal>with sexp</literal> to the definition of
        <literal>t</literal>, though, we'll get an error:
      </para>
      <programlisting language="ocaml">
# module Make_interval(Endpoint : Comparable)
    : Interval_intf with type endpoint := Endpoint.t = struct

    type t = | Interval of Endpoint.t * Endpoint.t
             | Empty
    with sexp

    ....

  end ;;
Characters 120-123:
        type t = | Interval of Endpoint.t * Endpoint.t
                               ^^^^^^^^^^
Error: Unbound value Endpoint.t_of_sexp
</programlisting>
      <para>
        The problem is that <literal>with sexp</literal> adds code for
        defining the s-expression converters, and that code assumes that
        <literal>Endpoint</literal> has the appropriate sexp-conversion
        functions for <literal>Endpoint.t</literal>. But all we know
        about <literal>Endpoint</literal> is that it satisfies the
        <literal>Comparable</literal> interface, which doesn't say
        anything about s-expressions.
      </para>
      <para>
        Happily, Core comes with a built in interface for just this
        purpose called <literal>Sexpable</literal>, which is defined as
        follows:
      </para>
      <programlisting language="ocaml">
module type Sexpable = sig
  type t = int
  val sexp_of_t : t -&gt; Sexp.t
  val t_of_sexp : Sexp.t -&gt; t
end
</programlisting>
      <para>
        We can modify <literal>Make_interval</literal> to use the
        <literal>Sexpable</literal> interface, for both its input and
        its output. Note the use of destructive substitution to combine
        multiple signatures together. This is important because it stops
        the <literal>type t</literal>'s from the different signatures
        from interfering with each other.
      </para>
      <para>
        Also note that we have been careful to override the
        sexp-converter here to ensure that the data structures
        invariants are still maintained when reading in from an
        s-expression.
      </para>
      <programlisting language="ocaml">
# module type Interval_intf_with_sexp = sig
   type t
   include Interval_intf with type t := t
   include Sexpable      with type t := t
  end;;
# module Make_interval(Endpoint : sig
    type t
    include Comparable with type t := t
    include Sexpable   with type t := t
  end) : Interval_intf_with_sexp with type endpoint := Endpoint.t =
  struct

      type t = | Interval of Endpoint.t * Endpoint.t
               | Empty
      with sexp

      let create low high =
         ...

      (* put a wrapper round the auto-generated sexp_of_t to enforce
         the invariants of the data structure *)
      let t_of_sexp sexp =
        match t_of_sexp sexp with
        | Empty -&gt; Empty
        | Interval (x,y) -&gt; create x y

      ....

     end ;;
module Make_interval :
  functor
    (Endpoint : sig
           type t
           val compare : t -&gt; t -&gt; int
           val sexp_of_t : t -&gt; Sexplib.Sexp.t
           val t_of_sexp : Sexplib.Sexp.t -&gt; t
         end) -&gt;
    sig
      type t
      val create : Endpoint.t -&gt; Endpoint.t -&gt; t
      val is_empty : t -&gt; bool
      val contains : t -&gt; Endpoint.t -&gt; bool
      val intersect : t -&gt; t -&gt; t
      val sexp_of_t : t -&gt; Sexplib.Sexp.t
      val t_of_sexp : Sexplib.Sexp.t -&gt; t
    end
</programlisting>
      <para>
        And now, we can use that sexp-converter in the ordinary way:
      </para>
      <programlisting language="ocaml">
# module Int = Make_interval(Int) ;;
# Int_interval.sexp_of_t (Int_interval.create 3 4);;
- : Sexplib.Sexp.t = (Interval 3 4)
# Int_interval.sexp_of_t (Int_interval.create 4 3);;
- : Sexplib.Sexp.t = Empty
</programlisting>
    </sect2>
  </sect1>
  <sect1 id="extending-modules">
    <title>Extending modules</title>
    <para>
      Another common use of functors is to generate type-specific
      functionality for a given module in a standardized way. Let's see
      how this works in the context of a functional queue, which is just
      a functional version of a FIFO (first-in, first-out) queue. Being
      functional, operations on the queue return new queues, rather than
      modifying the queues that were passed in.
    </para>
    <para>
      Here's a reasonable mli
    </para>
    <programlisting language="ocaml">
(* file: fqueue.mli *)

type 'a t

val empty : 'a t

val enqueue : 'a t -&gt; 'a -&gt; 'a t

(** [dequeue q] returns None if the [q] is empty *)
val dequeue : 'a t -&gt; ('a * 'a t) option

val fold : 'a t -&gt; init:'acc -&gt; f:('acc -&gt; 'a -&gt; 'acc) -&gt; 'acc
</programlisting>
    <para>
      Now let's implement <literal>Fqueue</literal>. A standard trick is
      for the <literal>Fqueue</literal> to maintain an input and an
      output list, so that one can efficiently
      <literal>enqueue</literal> on the first list, and can efficiently
      dequeue from the out list. If you attempt to dequeue when the
      output list is empty, the input list is reversed and becomes the
      new output list. Here's an implementation that uses that trick.
    </para>
    <programlisting language="ocaml">
(* file: fqueue.ml *)
open Core.Std

type 'a t = 'a list * 'a list

let empty = ([],[])

let enqueue (l1,l2) x = (x :: l1,l2)

let dequeue (in_list,out_list) =
  match out_list with
  | hd :: tl -&gt; Some (hd, (in_list,tl))
  | [] -&gt;
    match List.rev in_list with
    | [] -&gt; None
    | hd::tl -&gt; Some (hd, ([], tl))

let fold (in_list,out_list) ~init ~f =
  List.fold ~init:(List.fold ~init ~f out_list) ~f
    (List.rev in_list)
</programlisting>
    <para>
      One problem with our <literal>Fqueue</literal> is that the
      interface is quite skeletal. There are lots of useful helper
      functions that one might want that aren't there. For example, for
      lists we have <literal>List.iter</literal> which runs a function
      on each node; and a <literal>List.find</literal> that finds the
      first element on the list that matches a given predicate. Such
      helper functions come up for pretty much every container type, and
      implementing them over and over is a bit of a dull and repetitive
      affair.
    </para>
    <para>
      As it happens, many of these helper functions can be derived
      mechanically from just the fold function we already implemented.
      Rather than write all of these helper functions by hand for every
      new container type, we can instead use a functor that will let us
      add this functionality to any container that has a
      <literal>fold</literal> function.
    </para>
    <para>
      We'll create a new module, <literal>Foldable</literal> that
      automates the process of adding helper functions to a
      fold-supporting container. As you can see,
      <literal>Foldable</literal> contains a module signature
      <literal>S</literal> which defines the signature that is required
      to support folding; and a functor <literal>Extend</literal> that
      allows one to extend any module that matches
      <literal>Foldable.S</literal>.
    </para>
    <programlisting language="ocaml">
(* file: foldable.ml *)

open Core.Std

module type S = sig
  type 'a t
  val fold : 'a t -&gt; init:'acc -&gt; f:('acc -&gt; 'a -&gt; 'acc) -&gt; 'acc
end

module type Extension = sig
  type 'a t
  val iter    : 'a t -&gt; f:('a -&gt; unit) -&gt; unit
  val length  : 'a t -&gt; int
  val count   : 'a t -&gt; f:('a -&gt; bool) -&gt; int
  val for_all : 'a t -&gt; f:('a -&gt; bool) -&gt; bool
  val exists  : 'a t -&gt; f:('a -&gt; bool) -&gt; bool
end

(* For extending a Foldable module *)
module Extend(Arg : S)
  : Extension with type 'a t := 'a Arg.t =
struct
  open Arg

  let iter t ~f =
    fold t ~init:() ~f:(fun () a -&gt; f a)

  let length t =
    fold t ~init:0  ~f:(fun acc _ -&gt; acc + 1)

  let count t ~f =
    fold t ~init:0  ~f:(fun count x -&gt; count + if f x then 1 else 0)

  exception Short_circuit

  let for_all c ~f =
    try iter c ~f:(fun x -&gt; if not (f x) then raise Short_circuit); true
    with Short_circuit -&gt; false

  let exists c ~f =
    try iter c ~f:(fun x -&gt; if f x then raise Short_circuit); false
    with Short_circuit -&gt; true
end
</programlisting>
    <para>
      Now we can apply this to <literal>Fqueue</literal>. We can rewrite
      the interface of <literal>Fqueue</literal> as follows.
    </para>
    <programlisting language="ocaml">
(* file: fqueue.mli *)
open Core.Std

type 'a t
val empty : 'a t
val enqueue : 'a t -&gt; 'a -&gt; 'a t
val dequeue : 'a t -&gt; ('a * 'a t) option
val fold : 'a t -&gt; init:'acc -&gt; f:('acc -&gt; 'a -&gt; 'acc) -&gt; 'acc

include Foldable.Extension with type 'a t := 'a t
</programlisting>
    <para>
      In order to apply the functor, we'll put the definition of
      <literal>Fqueue</literal> in a sub-module called
      <literal>T</literal>, and then call
      <literal>Foldable.Extend</literal> on <literal>T</literal>.
    </para>
    <programlisting language="ocaml">
open Core.Std

module T = struct
  type 'a t = 'a list * 'a list

  let empty = [],[]

  let enqueue (l1,l2) x =
    (x :: l1,l2)

  let rec dequeue (in_list,out_list) =
    match out_list with
    | hd :: tl -&gt; Some (hd, (in_list,tl))
    | [] -&gt; dequeue ([], List.rev in_list)

  let fold (in_list,out_list) ~init ~f =
    List.fold ~init:(List.fold ~init ~f out_list) ~f
      (List.rev in_list)
end
include T
include Foldable.Extend(T)
</programlisting>
    <para>
      This pattern comes up quite a bit in Core, and is used to for a
      variety of purposes.
    </para>
    <itemizedlist>
      <listitem>
        <para>
          Adding comparison-based data structures like maps and sets,
          based on the <literal>Comparable</literal> interface.
        </para>
      </listitem>
      <listitem>
        <para>
          Adding hash-based data structures like hash sets and hash
          heaps.
        </para>
      </listitem>
      <listitem>
        <para>
          Support for so-called monadic libraries, like the ones
          discussed in <xref linkend="error-handling"/> and
          <xref linkend="concurrent-programming-with-async"/>.
          Here, the functor is used to provide a collection of standard
          helper functions based on the core <literal>bind</literal> and
          <literal>return</literal> operators.
        </para>
      </listitem>
    </itemizedlist>
  </sect1>
</chapter><chapter id="imperative-programming-1">
  <title>Imperative Programming</title>
  <para>
    Most of the code shown so far in this book, and indeed, most OCaml
    code in general, is <emphasis>pure</emphasis>. Pure code works
    without mutating the program's internal state, performing I/O,
    reading the clock, or in any other way interacting with changeable
    parts of the world. Thus, a pure function behaves like a
    mathematical function, always returning the same results when given
    the same inputs, and never affecting the world except insofar as it
    returns the value of its computation.
    <emphasis>Imperative</emphasis> code, on the other hand, operates by
    side-effects that modify a program's internal state or interact with
    the outside world, and so can have a new effect, and return
    different results, every time they're called.
  </para>
  <para>
    Pure code is the default in OCaml, and for good reason --- it's
    generally easier to reason about, less error prone and more
    composable. But imperative code is of fundamental importance to any
    practical programming language because real-world tasks require that
    you interact with the outside world, which is by its nature
    imperative. Imperative programming can also be important for
    performance. While pure code is quite efficient in OCaml, there are
    many algorithms that can only be implemented efficiently using
    imperative techniques.
  </para>
  <para>
    OCaml offers a happy compromise here, making it easy and natural to
    program in a pure style, but also providing great support for
    imperative programming where you need it. This chapter will walk you
    through OCaml's imperative features, and help you use them to their
    fullest.
  </para>
  <sect1 id="example-imperative-dictionaries">
    <title>Example: Imperative dictionaries</title>
    <para>
      We'll start with the implementation of a simple imperative
      dictionary, <emphasis>i.e.</emphasis>, a mutable mapping from keys
      to values. This is really for illustration purposes; both Core and
      the standard library provide imperative dictionaries, and for most
      real world tasks, you should use one of those implementations.
    </para>
    <para>
      Our dictionary, like those in Core and the standard library, will
      be implemented as a hash table. In particular, we'll use a
      <emphasis>open hashing</emphasis> scheme, which is to say the hash
      table will be an array of buckets, each bucket containing a list
      of key/value pairs that have been hashed into that bucket.
    </para>
    <para>
      Here's the interface we'll match, provided as an
      <literal>mli</literal>.
    </para>
    <programlisting language="ocaml">
(* file: dictionary.mli *)
open Core.Std

type ('a, 'b) t

val create : unit -&gt; ('a, 'b) t
val length : ('a, 'b) t -&gt; int
val add    : ('a, 'b) t -&gt; key:'a -&gt; data:'b -&gt; unit
val find   : ('a, 'b) t -&gt; 'a -&gt; 'b option
val iter   : ('a, 'b) t -&gt; f:(key:'a -&gt; data:'b -&gt; unit) -&gt; unit
val remove : ('a, 'b) t -&gt; 'a -&gt; unit
</programlisting>
    <para>
      The type <literal>('a, 'b) t</literal> is used for a dictionary
      with keys of type <literal>'a</literal> and data of type
      <literal>'b</literal>. The <literal>mli</literal> also includes a
      collection of helper functions whose purpose and behavior should
      be largely inferrable from their names and type signatures. Notice
      that a number of the functions, in particular, ones like
      <literal>add</literal> that modify the dictionary, return unit.
      This is typical of functions that act by side-effect.
    </para>
    <para>
      We'll now walk through the implementation (contained in the
      corresponding <literal>ml</literal> file) piece by piece,
      explaining different imperative constructs as they come up.
    </para>
    <para>
      Our first step is to define the type of a dictionary as a record
      with two fields.
    </para>
    <programlisting language="ocaml">
(* file: dictionary.ml *)
open Core.Std

type ('a, 'b) t = { mutable length: int;
                    buckets: ('a * 'b) list array;
                  }
</programlisting>
    <para>
      The first field, <literal>length</literal> is declared as mutable.
      In OCaml, records are immutable by default, but individual fields
      are mutable when marked as such. The second field,
      <literal>buckets</literal>, is immutable, but contains an array,
      which is itself a mutable data structure, as we'll see.
    </para>
    <para>
      Now we'll start putting together the basic functions for
      manipulating a dictionary.
    </para>
    <programlisting language="ocaml">
let num_buckets = 17

let hash_bucket key = (Hashtbl.hash key) mod num_buckets

let create () =
  { length = 0;
    buckets = Array.create ~len:num_buckets [];
  }

let length t = t.length

let find t key =
  List.find_map t.buckets.(hash_bucket key)
    ~f:(fun (key',data) -&gt; if key' = key then Some data else None)
</programlisting>
    <para>
      Note that <literal>num_buckets</literal> is a constant. That's
      because, for simplicity's sake, we're using a fixed-length bucket
      array. For a practical implementation, the length of the array
      would have to be able to grow as the number of elements in the
      dictionary increases.
    </para>
    <para>
      The function <literal>hash_bucket</literal> is used throughout the
      rest of the module to choose the position in the array that a
      given key should be stored at. It is implemented on top of
      <literal>Hashtbl.hash</literal>, which is a hash function provided
      by the OCaml runtime that can be applied to values of any type.
      Thus, its own type is polymorphic:
      <literal>'a -&gt; int</literal>.
    </para>
    <para>
      While <literal>Hashtbl.hash</literal> can be used with any type,
      it won't necessarily succeed for all values.
      <literal>Hashtbl.hash</literal> will throw an exception if it
      encounters a value it can't handle, like a function or a value
      from a C libraries that lives outside the OCaml heap.
    </para>
    <para>
      The other functions defined above are fairly straightforward:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          <literal>create</literal> creates an empty dictionary.
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>length</literal> grabs the length from the
          corresponding record field, thus returning the number of
          entries stored in the dictionary.
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>find</literal> looks for a matching key in the table
          and returns the corresponding value if found as an option.
        </para>
      </listitem>
    </itemizedlist>
    <para>
      A new piece of syntax has also popped up in
      <literal>find</literal>: we write <literal>array.(index)</literal>
      to grab a value from an array.
    </para>
    <para>
      Now we'll look at the implementation of <literal>iter</literal>:
    </para>
    <programlisting language="ocaml">
let iter t ~f =
  for i = 0 to Array.length t.buckets - 1 do
    List.iter t.buckets.(i) ~f:(fun (key, data) -&gt; f ~key ~data)
  done
</programlisting>
    <para>
      <literal>iter</literal> is designed to walk over all the entries
      in the dictionary. In particular, <literal>iter d ~f</literal>
      will call <literal>f</literal> for each key/value pair in
      dictionary <literal>d</literal>. Note that <literal>f</literal>
      must return <literal>unit</literal>, since it is expected to work
      by side effect rather than by returning a value, and the overall
      <literal>iter</literal> function returns <literal>unit</literal>
      as well.
    </para>
    <para>
      The code for <literal>iter</literal> uses two forms of iteration:
      a <literal>for</literal> loop to walk over the array of buckets;
      and within that loop, and a call to <literal>List.iter</literal>
      to walk over the list of values in a given bucket. We could have
      done the outer loop with a recursive function instead of a
      <literal>for</literal> loop, but <literal>for</literal> loops are
      syntactically convenient, and are more familiar and idiomatic in
      the context of imperative code.
    </para>
    <para>
      The following code is for adding and removing mappings from the
      dictionary.
    </para>
    <programlisting language="ocaml">
let bucket_has_key t i key =
  List.exists t.buckets.(i) ~f:(fun (key',_) -&gt; key' = key)

let add t ~key ~data =
  let i = hash_bucket key in
  let replace = bucket_has_key t i key in
  let filtered_bucket =
    if replace then
      List.filter t.buckets.(i) ~f:(fun (key',_) -&gt; key' &lt;&gt; key)
    else
      t.buckets.(i)
  in
  t.buckets.(i) &lt;- (key, data) :: filtered_bucket;
  if not replace then t.length &lt;- t.length + 1

let remove t key =
  let i = hash_bucket key in
  if not (bucket_has_key t i key) then ()
  else (
    let filtered_bucket =
      List.filter t.buckets.(i) ~f:(fun (key',_) -&gt; key' &lt;&gt; key)
    in
    t.buckets.(i) &lt;- filtered_bucket;
    t.length &lt;- t.length - 1
  )
</programlisting>
    <para>
      This above code is made more complicated by the fact that we need
      to detect whether we are overwriting or removing an existing
      binding, so we can decide whether <literal>t.length</literal>
      needs to be changed. The helper function
      <literal>bucket_has_key</literal> is used for this purpose.
    </para>
    <para>
      Another new piece of syntax shows up in both
      <literal>add</literal> and <literal>remove</literal>: the use of
      the <literal>&lt;-</literal> operator to update elements of an
      array (<literal>array.(i) &lt;- expr</literal>) and for updating a
      record field (<literal>record.field &lt;- expression</literal>).
    </para>
    <para>
      We also use a single semicolon, <literal>;</literal>, as a
      sequencing operator, to allow us to do a sequence of
      side-effecting operations in a row: first, update the bucket, then
      update the count. We could have done this using let bindings:
    </para>
    <programlisting language="ocaml">
    let () = t.buckets.(i) &lt;- filtered_bucket in
    t.length &lt;- t.length - 1
</programlisting>
    <para>
      but <literal>;</literal> is more concise and idiomatic. More
      generally,
    </para>
    <programlisting language="ocaml">
&lt;expr1&gt;;
&lt;expr2&gt;;
...
&lt;exprN&gt;
</programlisting>
    <para>
      is eqivalent to
    </para>
    <programlisting language="ocaml">
let () = &lt;expr1&gt; in
let () = &lt;expr2&gt; in
...
&lt;exprN&gt;
</programlisting>
    <para>
      When a sequence expression <literal>expr1; expr2</literal> is
      evaluated, <literal>expr1</literal> is evaluated first, and then
      <literal>expr2</literal>. The expression <literal>expr1</literal>
      should have type <literal>unit</literal> (though this is a warning
      rather than a hard restriction), and the value of
      <literal>expr2</literal> is returned as the value of the entire
      sequence. For example, the sequence
      <literal>print_string &quot;hello world&quot;; 1 + 2</literal>
      first prints the string
      <literal>&quot;hello world&quot;</literal>, then returns the
      integer <literal>3</literal>.
    </para>
    <para>
      Note also that we do all of the side-effecting operations at the
      very end of each function. This is good practice because it
      minimizes the chance that such operations will be interrupted with
      an exception, leaving the data structure in an inconsistent state.
    </para>
  </sect1>
  <sect1 id="primitive-mutable-data">
    <title>Primitive mutable data</title>
    <para>
      Now that we've looked at a complete example, let's take a more
      systematic look at imperative programming in OCaml. We encountered
      two different forms of mutable data above: records with mutable
      fields and arrays. We'll now discuss these in more detail, along
      with the other primitive forms of mutable data that are available
      in OCaml.
    </para>
    <sect2 id="array-like-data">
      <title>Array-like data</title>
      <para>
        OCaml supports a number of array-like data structures;
        <emphasis>i.e.</emphasis>, mutable integer-indexed containers
        that provide constant-time access to their elements. We'll
        discuss several of them below.
      </para>
      <sect3 id="ordinary-arrays">
        <title>Ordinary arrays</title>
        <para>
          The <literal>array</literal> type is used for general purpose
          polymorphic arrays. The <literal>Array</literal> module has a
          variety of utility functions for interacting with arrays,
          including a number of mutating operations. These include
          <literal>Array.set</literal>, for setting an individual
          element, and <literal>Array.blit</literal>, for efficiently
          copying values from one range of indices to another.
        </para>
        <para>
          Arrays also come with special syntax for retrieving an element
          from an array:
        </para>
        <programlisting language="ocaml">
array.(index)
</programlisting>
        <para>
          and for setting an element in an array:
        </para>
        <programlisting language="ocaml">
array.(index) &lt;- expr
</programlisting>
        <para>
          Array literals are written using <literal>[|</literal> and
          <literal>|]</literal> as delimiters. Thus,
          <literal>[| 1; 2; 3 |]</literal> is a literal integer array.
        </para>
      </sect3>
      <sect3 id="strings">
        <title>Strings</title>
        <para>
          Strings are essentially byte-arrays which are often used for
          textual data. The main advantage of using a
          <literal>string</literal> in place of a
          <literal>Char.t array</literal> (a <literal>Char.t</literal>
          is an 8-bit character) is that the former is considerably more
          space efficient; an array uses one word --- 8 bytes on a
          64-bit machine --- to store a single entry, whereas strings
          use one byte per character.
        </para>
        <para>
          Strings also come with their own syntax for getting and
          setting values: <literal>string.[index]</literal> and
          <literal>string.[index] &lt;- expr</literal> respectively, and
          string literals are bounded by quotes. There's also a module
          <literal>String</literal> where you'll find useful functions
          for working with strings. We'll talk more about the
          <literal>String</literal> module in
          <xref linkend="text-processing-and-unicode"/>.
        </para>
      </sect3>
      <sect3 id="bigarrays">
        <title>Bigarrays</title>
        <para>
          A <literal>Bigarray.t</literal> is a handle to a block of
          memory stored outside of the OCaml heap. These are mostly
          useful for interacting with C or Fortran libraries, and are
          discussed in
          <xref linkend="managing-external-memory-with-bigarrays"/>.
          Bigarrays too have their own getting and setting syntax:
          <literal>bigarray.{index}</literal> and
          <literal>bigarray.{index} &lt;- expr</literal>. There is no
          literal syntax for bigarrays.
        </para>
      </sect3>
    </sect2>
    <sect2 id="mutable-record-and-object-fields-and-ref-cells">
      <title>Mutable record and object fields and ref cells</title>
      <para>
        As we've seen, records are immutable by default, but individual
        record fields can be declared as mutable. These mutable fields
        can be set using the <literal>&lt;-</literal> operator,
        <emphasis>i.e.</emphasis>,
        <literal>record.field &lt;- expr</literal>.
      </para>
      <para>
        As we'll see in
        <xref linkend="object-oriented-programming"/>, fields
        of an object can similarly be declared as mutable, and can then
        be modified in much the same way as record fields.
      </para>
      <sect3 id="ref-cells">
        <title>Ref Cells</title>
        <para>
          Variables in OCaml are never mutable --- they can refer to
          mutable data, but what the variable points to can't be
          changed. Sometimes, though, you want to do exactly what you
          would do with a mutable variable in another language: define a
          single, mutable value. In OCaml this is typically achieved
          using a <literal>ref</literal>, which is essentially a
          container with a single mutable polymorphic field.
        </para>
        <para>
          The definition for the ref type is as follows:
        </para>
        <programlisting language="ocaml">
type 'a ref = { mutable contents : 'a }
</programlisting>
        <para>
          The standard library defines the following operators for
          working with refs.
        </para>
        <itemizedlist>
          <listitem>
            <para>
              <literal>ref expr</literal> constructs a reference cell
              containing the value defined by the expression
              <literal>expr</literal>.
            </para>
          </listitem>
          <listitem>
            <para>
              <literal>!refcell</literal> returns the contents of the
              reference cell.
            </para>
          </listitem>
          <listitem>
            <para>
              <literal>refcell := expr</literal> replaces the contents
              of the reference cell.
            </para>
          </listitem>
        </itemizedlist>
        <para>
          You can see these in action below.
        </para>
        <programlisting language="ocaml">
# let x = ref 1;;
val x : int ref = {contents = 1}
# !x;;
- : int = 1
# x := !x + 1;;
- : unit = ()
# !x;;
- : int = 2
</programlisting>
        <para>
          All of these are operations are just ordinary OCaml functions,
          and could be defined as follows.
        </para>
        <programlisting language="ocaml">
let ref x = { contents = x }
let (!) r = r.contents
let (:=) r x = r.contents &lt;- x
</programlisting>
      </sect3>
    </sect2>
    <sect2 id="foreign-functions">
      <title>Foreign functions</title>
      <para>
        Another source of imperative operations in OCaml is resources
        that come from interfacing with external libraries through
        OCaml's foreign function interface (FFI). The FFI opens OCaml up
        to imperative constructs that are exported by system calls or
        other external libraries. Many of these come built in, like
        access to the <literal>write</literal> system call, or to the
        <literal>clock</literal>; while others come from user libraries,
        like LAPACK bindings.
      </para>
    </sect2>
  </sect1>
  <sect1 id="for-and-while-loops-1">
    <title><literal>for</literal> and <literal>while</literal>
    loops</title>
    <para>
      OCaml provides support for traditional imperative looping
      constructs, in particular, <literal>for</literal> and
      <literal>while</literal> loops, even though neither of them is
      strictly necessary. Anything you can do with such a loop you can
      also do with a recursive function, and you can also write
      higher-order functions like <literal>Array.iter</literal> that
      cover much of the same ground.
    </para>
    <para>
      Nonetheless, explicit <literal>for</literal> and
      <literal>while</literal> loops are both more idiomatic for
      imperative programming and often more concise.
    </para>
    <para>
      The <literal>for</literal> loop is the simpler of the two. Indeed,
      we've already seen the <literal>for</literal> loop in action ---
      the <literal>iter</literal> function in
      <literal>Dictionary</literal> is built using it. Here's a simple
      example of <literal>for</literal>.
    </para>
    <programlisting language="ocaml">
# for i = 0 to 3 do Printf.printf &quot;i = %d\n&quot; i done;;
i = 0
i = 1
i = 2
i = 3
- : unit = ()
</programlisting>
    <para>
      As you can see, the upper and lower bounds are inclusive. We can
      also use <literal>downto</literal> to iterate in the other
      direction.
    </para>
    <programlisting language="ocaml">
# for i = 3 downto 0 do Printf.printf &quot;i = %d\n&quot; i done;;
i = 3
i = 2
i = 1
i = 0
- : unit = ()
</programlisting>
    <para>
      OCaml also supports <literal>while</literal> loops, which include
      a condition and a body. The loop first evaluates the condition,
      and then, if it evaluates to true, evaluates the body and starts
      the loop again. Here's a simple example of a function for
      reversing an array in-place.
    </para>
    <programlisting language="ocaml">
# let rev_inplace ar =
    let i = ref 0 in
    let j = ref (Array.length ar - 1) in
    (* terminate when the upper and lower indices meet *)
    while !i &lt; !j do
      (* swap the two elements *)
      let tmp = ar.(!i) in
      ar.(!i) &lt;- ar.(!j);
      ar.(!j) &lt;- tmp;
      (* bump the indices *)
      incr i;
      decr j
    done
  ;;
val rev_inplace : 'a array -&gt; unit = &lt;fun&gt;
# let nums = [|1;2;3;4;5|];;
val nums : int array = [|1; 2; 3; 4; 5|]
# rev_inplace nums;;
- : unit = ()
# nums;;
- : int array = [|5; 4; 3; 2; 1|]
</programlisting>
    <para>
      In the above, we used <literal>incr</literal> and
      <literal>decr</literal>, which are build-in functions for
      incrementing and decrementing an <literal>int ref</literal> by
      one, respectively.
    </para>
  </sect1>
  <sect1 id="example-doubly-linked-lists">
    <title>Example: Doubly-linked lists</title>
    <para>
      Another common imperative data structure is the doubly-linked
      list. Doubly-linked lists can be traversed in both directions and
      elements can be added and removed from the list in constant time.
      Core defines a doubly-linked list (the module is called
      <literal>Doubly_linked</literal>), but we'll define our own linked
      list library as an illustration.
    </para>
    <para>
      Here's the <literal>mli</literal> of the module we'll build.
    </para>
    <programlisting language="ocaml">
(* file: dlist.mli *)
open Core.Std

type 'a t
type 'a element

(** Basic list operations  *)
val create   : unit -&gt; 'a t
val is_empty : 'a t -&gt; bool

(** Navigation using [element]s *)
val first : 'a t -&gt; 'a element option
val next  : 'a element -&gt; 'a element option
val prev  : 'a element -&gt; 'a element option
val value : 'a element -&gt; 'a

(** Whole-data-structure iteration *)
val iter    : 'a t -&gt; f:('a -&gt; unit) -&gt; unit
val find_el : 'a t -&gt; f:('a -&gt; bool) -&gt; 'a element option

(** Mutation *)
val insert_first : 'a t -&gt; 'a -&gt; 'a element
val insert_after : 'a element -&gt; 'a -&gt; 'a element
val remove : 'a t -&gt; 'a element -&gt; unit
</programlisting>
    <para>
      Note that there are two types defined here:
      <literal>'a t</literal>, the type of a list, and
      <literal>'a element</literal>, the type of an element. Elements
      act as pointers to the interior of a list, and allow us to
      navigate the list and give us a point at which to apply mutating
      operations.
    </para>
    <para>
      Now let's look at the implementation. We'll start by defining
      <literal>'a element</literal> and <literal>'a t</literal>.
    </para>
    <programlisting language="ocaml">
(* file: dlist.ml *)
open Core.Std

type 'a element =
  { value : 'a;
    mutable next : 'a element option;
    mutable previous : 'a element option
  }

type 'a t = 'a element option ref
</programlisting>
    <para>
      An <literal>'a element</literal> is a record containing the value
      to be stored in that node as well as optional (and mutable) fields
      pointing to the previous and next elements. At the beginning of
      the list, the <literal>prev</literal> field is
      <literal>None</literal>, and at the end of the list, the
      <literal>next</literal> field is <literal>None</literal>.
    </para>
    <para>
      The type of the list itself, <literal>'a t</literal>, is an
      mutable reference to an optional <literal>element</literal>. This
      reference is <literal>None</literal> if the list is empty, and
      <literal>Some</literal> otherwise.
    </para>
    <para>
      Now we can define a few basic functions that operate on lists and
      elements.
    </para>
    <programlisting language="ocaml">
let create () = ref None
let is_empty l = !l = None

let value elt = elt.value

let first l = !l
let next elt = elt.next
let prev elt = elt.prev
</programlisting>
    <para>
      These all follow relatively straight-forwardly from our type
      definitions.
    </para>
    <note><title> 
    Cyclic data structures
    </title>
    <para>
      Doubly-linked lists are a cyclic data structure, meaning that it
      is possible to follow a nontrivial sequence of pointers that
      closes in on itself. In general, building cyclic data structures
      requires the use of side-effects. This is done by constructing the
      data elements first, and then adding cycles using assignment
      afterwards.
    </para>
    <para>
      There is an exception to this, though: you can construct
      fixed-size cyclic data-structures using
      <literal>let rec</literal>.
    </para>
    <programlisting language="ocaml">
# let rec endless_loop = 1 :: 2 :: 3 :: endless_loop;;
val endless_loop : int list =
  [1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1;
   2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2; 3; 1; 2;
   ...]
</programlisting>
    <para>
      This approach is quite limited, however. General purpose cyclic
      data structures require mutation.
    </para>
    </note>
    <sect2 id="modifying-the-list">
      <title>Modifying the list</title>
      <para>
        Now, we'll start considering operations that mutate the list,
        starting with <literal>insert_first</literal>, which inserts an
        element at the front of the list.
      </para>
      <programlisting language="ocaml">
let insert_first l value =
  let new_elt = { prev = None; next = !l; value } in
  begin match !l with
  | Some old_first -&gt; old_first.prev &lt;- Some new_elt
  | None -&gt; ()
  end;
  l := Some new_elt;
  new_elt
</programlisting>
      <para>
        <literal>insert_first</literal> first defines a new element
        <literal>new_elt</literal>, and then links it into the list,
        finally setting the the list itself to point to
        <literal>new_elt</literal>. Note that the precedence of a
        <literal>match</literal> expression is very low, so to separate
        it from the following assignment
        <literal>l := Some new_front</literal>, we surround the match in
        a <literal>begin ... end</literal> bracketing (we could also use
        parentheses). If we did not, the final assignment would become
        part of the <literal>None -&gt; ...</literal> case, which is not
        what we want.
      </para>
      <para>
        In order to add elements later in the list, we can use
        <literal>insert_after</literal>, which takes an
        <literal>element</literal> as an argument, after which it
        inserts a new element.
      </para>
      <programlisting language="ocaml">
let insert_after elt value =
  let new_elt = { value; prev = Some elt; next = elt.next } in
  begin match elt.next with
  | Some old_next -&gt; old_next.prev &lt;- Some new_elt
  | None -&gt; ()
  end;
  elt.next &lt;- Some new_elt;
  new_elt
</programlisting>
      <para>
        Finally, we need a <literal>remove</literal> function.
      </para>
      <programlisting language="ocaml">
let remove l elt =
  let { prev; next; _ } = elt in
  begin match prev with
  | Some prev -&gt; prev.next &lt;- next
  | None -&gt; l := next
  end;
  begin match next with
  | Some next -&gt; next.prev &lt;- prev;
  | None -&gt; ()
  end;
  elt.prev &lt;- None;
  elt.next &lt;- None
</programlisting>
      <para>
        Note that the above code is careful to change the
        <literal>prev</literal> pointer of the following element, and
        the <literal>next</literal> pointer of the previous element, if
        they exist. If there's no previous element, then the list
        pointer itself is updated. In any case, the next and previous
        pointers of the element itself are set to
        <literal>None</literal>.
      </para>
      <para>
        These functions are more fragile than they may seem. In
        particular, misuse of the interface may lead to corrupted data.
        For example, double-removing an element will cause the main list
        reference to be set to <literal>None</literal>, thus emptying
        the list. Similar problems arise from removing an element from a
        list it doesn't belong to.
      </para>
      <para>
        This shouldn't be a big surprise. Complex imperative data
        structures can be quite tricky; considerably trickier than their
        pure equivalents. The issues described above can be dealt with
        by more careful error detection, and such error correction is
        taken care of in modules like Core's
        <literal>Doubly_linked</literal>. You should use imperative data
        structures from a well-designed library when you can. And when
        you can't, you should make sure that the code you write is
        careful about error detection.
      </para>
    </sect2>
    <sect2 id="iteration-functions">
      <title>Iteration functions</title>
      <para>
        When defining containers like lists, dictionaries and trees,
        you'll typically want to define a set of iteration functions,
        like <literal>iter</literal>, <literal>map</literal>, and
        <literal>fold</literal>, which let you concisely express common
        iteration patterns.
      </para>
      <para>
        <literal>Dlist</literal> has two such iterators:
        <literal>iter</literal>, the goal of which is to call a
        <literal>unit</literal> producing function on every element of
        the list, in order; and <literal>find_el</literal>, which runs a
        provided test function on each values stored in the list,
        returning the first <literal>element</literal> that passes the
        test. Both <literal>iter</literal> and
        <literal>find_el</literal> are implemented using simple
        recursive loops that use <literal>next</literal> to walk from
        element to element, and <literal>value</literal> to extract the
        element from a given node.
      </para>
      <programlisting language="ocaml">
let iter l ~f =
  let rec loop = function
    | None -&gt; ()
    | Some el -&gt; f (value el); loop (next el)
  in
  loop !l

let find_el l ~f =
  let rec loop = function
    | None -&gt; None
    | Some elt -&gt;
      if f (value elt) then Some elt
      else loop (next elt)
  in
  loop !l
</programlisting>
    </sect2>
  </sect1>
  <sect1 id="laziness-and-other-unobservable-effects">
    <title>Laziness and other unobservable effects</title>
    <para>
      There are many instances where you basically want to program in a
      pure style, but you want to make limited use of side-effects to
      improve the performance of your code, without really changing
      anything else. Such side effects are sometimes called
      <emphasis>unobservable effects</emphasis>, and they are a useful
      way of leveraging OCaml's imperative features while still
      maintaining most of the benefits of pure programming.
    </para>
    <para>
      One of the simplest unobservable effect is
      <emphasis>laziness</emphasis>. A lazy value is one that is not
      computed until it is actually needed. In OCaml, lazy values are
      created using the <literal>lazy</literal> keyword, which can be
      used to prefix any expression, returning a value of type
      <literal>'a Lazy.t</literal>. The evaluation of that expression is
      delayed until forced with the <literal>Lazy.force</literal>
      function.
    </para>
    <programlisting language="ocaml">
# let v = lazy (print_string &quot;performing lazy computation\n&quot;; sqrt 16.);;
val v : float lazy_t = &lt;lazy&gt;
# Lazy.force v;;
performing lazy computation
- : float = 4.
# Lazy.force v;;
- : float = 4.
</programlisting>
    <para>
      You can see from the print statement that the actual computation
      was performed only once, and only after <literal>force</literal>
      had been called.
    </para>
    <para>
      To better understand how laziness works, let's walk through the
      implementation of our own lazy type. We'll start by declaring
      types to represent a lazy value.
    </para>
    <programlisting language="ocaml">
# type 'a lazy_state =
  | Delayed of (unit -&gt; 'a)
  | Value of 'a
  | Exn of exn
type 'a lazy_state = Delayed of (unit -&gt; 'a) | Value of 'a | Exn of exn
</programlisting>
    <para>
      A <literal>lazy_state</literal> represents the possible states of
      a lazy value. A lazy value is <literal>Delayed</literal> before it
      has been run, where <literal>Delayed</literal> holds a function
      for computing the value in question. A lazy value is in the
      <literal>Value</literal> state when it has been forced and the
      computation ended normally. The <literal>Exn</literal> case is for
      when the lazy value has been forced, but the computation ended
      with an exception. A lazy value is just a reference to a
      <literal>lazy_state</literal>.
    </para>
    <para>
      We can create a lazy value based on a thunk,
      <emphasis>i.e.</emphasis>, a function that takes a unit argument.
      Wrapping an expression in a thunk is another way to suspend the
      computation of an expression.
    </para>
    <programlisting language="ocaml">
# let create_lazy f = ref (Delayed f);;
val create_lazy : (unit -&gt; 'a) -&gt; 'a lazy_state ref = &lt;fun&gt;
# let v = create_lazy
    (fun () -&gt; print_string &quot;performing lazy computation\n&quot;; sqrt 16.);;
  val v : float lazy_state ref = {contents = Delayed &lt;fun&gt;}
</programlisting>
    <para>
      Now we just need a way to force a lazy value. The following
      function does just that.
    </para>
    <programlisting language="ocaml">
# let force v =
    match !v with
    | Value x -&gt; x
    | Exn e -&gt; raise e
    | Delayed f -&gt;
      try
        let x = f () in
        v := Value x;
        x
      with exn -&gt;
        v := Exn exn;
        raise exn
   ;;
val force : 'a lazy_state ref -&gt; 'a = &lt;fun&gt;
</programlisting>
    <para>
      Which we can use in the same way we used
      <literal>Lazy.force</literal>:
    </para>
    <programlisting language="ocaml">
# force v;;
performing lazy computation
- : float = 4.
# force v;;
- : float = 4.
</programlisting>
    <para>
      The main difference between our implementation of laziness and the
      built-in version is syntax. Rather than writing
      <literal>create_lazy (fun () -&gt; sqrt 16.)</literal>, we can
      just write <literal>lazy (sqrt 16.)</literal>.
    </para>
    <sect2 id="memoization-and-dynamic-programming">
      <title>Memoization and Dynamic Programming</title>
      <para>
        Another unobservable effect is <emphasis>memoization</emphasis>.
        A memoized function remembers the result of previous invocations
        of the function so that they can be returned without further
        computation when the same arguments are presented again.
      </para>
      <para>
        Here's a function that takes as an argument an arbitrary
        single-argument function and returns a memoized version of that
        function. Here we'll use Core's <literal>Hashtbl</literal>
        module, rather than our toy <literal>Dictionary</literal>.
      </para>
      <programlisting language="ocaml">
# let memoize f =
    let table = Hashtbl.Poly.create () in
    (fun x -&gt;
      match Hashtbl.find table x with
      | Some y -&gt; y
      | None -&gt;
        let y = f x in
        Hashtbl.add_exn table ~key:x ~data:y;
        y
    );;
val memoize : ('a -&gt; 'b) -&gt; 'a -&gt; 'b = &lt;fun&gt;
</programlisting>
      <para>
        Note that we use <literal>Hashtbl.Poly.create</literal> to
        create a hash table using OCaml's built-in polymorphic hash
        function.
      </para>
      <para>
        Memoization can be useful whenever you have a function that is
        expensive to recompute, and you don't mind caching old values
        indefinitely. One important caution: every time you create a
        memoized function, there's something of a built-in memory leak.
        As long as you hold on to the memoized function, you're holding
        every result it has returned thus far.
      </para>
      <para>
        Memoization is also useful for efficiently implementing some
        recursive algorithms. One good example is the algorithm for
        computing the <emphasis>edit distance</emphasis> (also called
        the Levenshtein distance) between two strings. The edit distance
        is the number of single-character changes (including letter
        switches, insertions and deletions) required to convert one
        string to the other. This kind of distance metric can be useful
        for a variety of approximate string matching problems, like
        spell checkers.
      </para>
      <para>
        Consider the following code for computing the edit distance.
        Understanding the algorithm isn't important here, but you should
        pay attention to the structure of the recursive calls.
      </para>
      <programlisting language="ocaml">
# let rec edit_distance s t =
    match String.length s, String.length t with
    | (0,x) | (x,0) -&gt; x
    | (len_s,len_t) -&gt;
      let s' = String.drop_suffix s 1 in
      let t' = String.drop_suffix t 1 in
      let cost_to_drop_both =
        if s.[len_s - 1] = t.[len_t - 1] then 0 else 1
      in
      List.reduce_exn ~f:Int.min
        [ edit_distance s' t  + 1
        ; edit_distance s  t' + 1
        ; edit_distance s' t' + cost_to_drop_both
        ]
  ;;
val edit_distance : string -&gt; string -&gt; int = &lt;fun&gt;
# edit_distance &quot;OCaml&quot; &quot;ocaml&quot;;;
- : int = 2
</programlisting>
      <para>
        The thing to note is that if you call
        <literal>edit_distance &quot;OCaml&quot; &quot;ocaml&quot;</literal>,
        then that will in turn dispatch the following calls:
      </para>
      <programlisting language="ocaml">
edit_distance &quot;OCam&quot; &quot;ocaml&quot;
edit_distance &quot;OCaml&quot; &quot;ocam&quot;
edit_distance &quot;OCam&quot; &quot;ocam&quot;
</programlisting>
      <para>
        And these calls will in turn dispatch other calls:
      </para>
      <programlisting language="ocaml">
edit_distance &quot;OCam&quot; &quot;ocaml&quot;
   edit_distance &quot;OCa&quot; &quot;ocaml&quot;
   edit_distance &quot;OCam&quot; &quot;ocam&quot;
   edit_distance &quot;OCa&quot; &quot;ocam&quot;
edit_distance &quot;OCaml&quot; &quot;ocam&quot;
   edit_distance &quot;OCam&quot; &quot;ocam&quot;
   edit_distance &quot;OCaml&quot; &quot;oca&quot;
   edit_distance &quot;OCam&quot; &quot;oca&quot;
edit_distance &quot;OCam&quot; &quot;ocam&quot;
   edit_distance &quot;OCa&quot; &quot;ocam&quot;
   edit_distance &quot;OCam&quot; &quot;oca&quot;
   edit_distance &quot;OCa&quot; &quot;oca&quot;
</programlisting>
      <para>
        As you can see, some of these calls are repeats. For example,
        there are two different calls to
        <literal>edit_distance &quot;OCam&quot; &quot;oca&quot;</literal>.
        The number of redundant calls grows exponentially with the size
        of the strings, meaning that our implementation of
        <literal>edit_distance</literal> is brutally slow for large
        strings. We can see this by writing a small timing function.
      </para>
      <programlisting language="ocaml">
# let time f =
    let start = Time.now () in
    let x = f () in
    let stop = Time.now () in
    printf &quot;Time: %s\n&quot; (Time.Span.to_string (Time.diff stop start));
    x ;;
val time : (unit -&gt; 'a) -&gt; 'a = &lt;fun&gt;
</programlisting>
      <para>
        And now we can use this to try out some examples.
      </para>
      <programlisting language="ocaml">
# time (fun () -&gt; edit_distance &quot;OCaml&quot; &quot;ocaml&quot;);;
Time: 5.11003ms
- : int = 2
# time (fun () -&gt; edit_distance &quot;OCaml 4.01&quot; &quot;ocaml 4.01&quot;);;
Time: 19.3322s
- : int = 2
</programlisting>
      <para>
        Just those few extra characters made it almost four thousand
        times slower!
      </para>
      <para>
        Memoization would be a huge help here, but to fix the problem,
        we need to memoize the calls that
        <literal>edit_distance</literal> makes to itself. This technique
        is sometimes referred to as <emphasis>dynamic
        programming</emphasis>. To see how to do this, let's step away
        from <literal>edit_distance</literal>, and instead consider a
        much simpler example: computing the nth element of the Fibonacci
        sequence. The Fibonacci sequence by definition starts out with
        two <literal>1</literal>'s, with every subsequent element being
        the sum of the previous two. The classic recursive definition of
        Fibonacci is as follows:
      </para>
      <programlisting language="ocaml">
# let rec fib i =
    if i &lt;= 1 then 1 else fib (i - 1) + fib (i - 2);;
</programlisting>
      <para>
        This is, however, exponentially slow, for the same reason that
        <literal>edit_distance</literal> was slow: we end up making many
        redundant calls to <literal>fib</literal>. It shows up quite
        dramatically in the performance.
      </para>
      <programlisting language="ocaml">
# time (fun () -&gt; fib 20);;
Time: 5.17392ms
- : int = 10946
# time (fun () -&gt; fib 40);;
Time: 51.4205s
- : int = 165580141
</programlisting>
      <para>
        Here, <literal>fib 40</literal> takes almost a minute to
        compute, as opposed to five <emphasis>milliseconds</emphasis>
        for <literal>fib 20</literal>.
      </para>
      <para>
        So, how can we use memoization to make this faster? The tricky
        bit is that we need to insert the memoization before the
        recursive calls within <literal>fib</literal>. We can't just
        define <literal>fib</literal> in the ordinary way and memoize it
        after the fact and expect the first call to
        <literal>fib</literal> to be improved (though of course repeated
        calls will be improved).
      </para>
      <programlisting language="ocaml">
# let fib = memoize fib;;
val fib : int -&gt; int = &lt;fun&gt;
# time (fun () -&gt; fib 40);;
Time: 52.6s
- : int = 165580141
# time (fun () -&gt; fib 40);;
Time: 0.00596046ms
- : int = 165580141
</programlisting>
      <para>
        In order to make <literal>fib</literal> fast, our first step
        will be to rewrite <literal>fib</literal> in a way that unwinds
        the recursion. The following version expects as its first
        argument a function (called <literal>fib</literal>) that will be
        called in lieu of the usual recursive call.
      </para>
      <programlisting language="ocaml">
# let fib_norec fib i =
    if i &lt;= 1 then i
    else fib (i - 1) + fib (i - 2) ;;
val fib_norec : (int -&gt; int) -&gt; int -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        We can now turn this back into an ordinary Fibonacci function by
        tying the recursive knot, as shown below.
      </para>
      <programlisting language="ocaml">
# let rec fib i = fib_norec fib i
val fib : int -&gt; int = &lt;fun&gt;
# fib 5;;
- : int = 8
</programlisting>
      <para>
        We can even write a polymorphic function that we'll call
        <literal>make_rec</literal> that can tie the recursive not for
        any function of this form.
      </para>
      <programlisting language="ocaml">
# let make_rec f_norec =
    let rec f x = f_norec f x in
    f
  ;;
val make_rec : (('a -&gt; 'b) -&gt; 'a -&gt; 'b) -&gt; 'a -&gt; 'b = &lt;fun&gt;
# let fib = make_rec fib_norec;;
val fib : int -&gt; int = &lt;fun&gt;
# fib 5;;
- : int = 8
</programlisting>
      <para>
        This is a pretty strange piece of code, and it may take a few
        minutes of thought to figure out what's going on. Like
        <literal>fib_norec</literal>, the function
        <literal>f_norec</literal> passed into
        <literal>make_rec</literal> is a function that isn't recursive,
        but takes as an argument a function that it will call. What
        <literal>make_rec</literal> does is to essentially feed
        <literal>f_norec</literal> to itself, thus making it a true
        recursive function.
      </para>
      <para>
        This is clever enough, but all we've really done is find a new
        way to implement the same old slow Fibonacci function. To make
        it faster, we need variant on <literal>make_rec</literal> that
        inserts memoization when it ties the recursive knot. We'll call
        that function <literal>memo_rec</literal>.
      </para>
      <programlisting language="ocaml">
# let memo_rec f_norec x =
     let fref = ref (fun _ -&gt; assert false) in
     let f = memoize (fun x -&gt; f_norec !fref x) in
     fref := f;
     f x
  ;;
val memo_rec : (('a -&gt; 'b) -&gt; 'a -&gt; 'b) -&gt; 'a -&gt; 'b = &lt;fun&gt;
</programlisting>
      <para>
        Note that <literal>memo_rec</literal> has the same signature as
        <literal>make_rec</literal>.
      </para>
      <para>
        We're using the reference here as a way of tying the recursive
        knot without using a <literal>let rec</literal>, which for
        reasons we'll describe later wouldn't work here.
      </para>
      <para>
        Using <literal>memo_rec</literal>, we can now build an efficient
        version of <literal>fib</literal>.
      </para>
      <programlisting language="ocaml">
# let fib = memo_rec fib_norec;;
val fib : int -&gt; int = &lt;fun&gt;
# time (fun () -&gt; fib 40);;
Time: 0.236034ms
</programlisting>
      <para>
        And as you can see, the exponential time complexity is now gone.
      </para>
      <para>
        The memory behavior here is important. If you look back at the
        definition of <literal>memo_rec</literal>, you'll see that the
        call to memo_rec does not trigger a call to
        <literal>memoize</literal>. Only when the final argument to
        <literal>fib</literal> is presented does
        <literal>memoize</literal> get called, and the result of that
        call falls out of scope when the <literal>fib</literal> call
        returns. That means that, unlike ordinary memoization, calling
        <literal>memo_rec</literal> on a function does not create a
        memory leak --- the memoization table is collected after the
        computation completes.
      </para>
      <para>
        We can use <literal>memo_rec</literal> as part of a single
        declaration that makes this look like it's little more than a
        special form of <literal>let rec</literal>.
      </para>
      <programlisting language="ocaml">
# let fib = memo_rec (fun fib i -&gt;
    if i &lt;= 1 then 1 else fib (i - 1) + fib (i - 2));;
val fib : int -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        This same approach will work for
        <literal>edit_distance</literal>. The one change we'll need to
        make is that <literal>edit_distance</literal> will now take a
        pair of strings as a single argument, since
        <literal>memoize</literal> only works sensibly for
        single-argument functions. We can always recover the original
        interface with a wrapper function.
      </para>
      <programlisting language="ocaml">
# let edit_distance = memo_rec (fun edit_distance (s,t) -&gt;
    match String.length s, String.length t with
    | (0,x) | (x,0) -&gt; x
    | (len_s,len_t) -&gt;
      let s' = String.drop_suffix s 1 in
      let t' = String.drop_suffix t 1 in
      let cost_to_drop_both =
        if s.[len_s - 1] = t.[len_t - 1] then 0 else 1
      in
      List.reduce_exn ~f:Int.min
        [ edit_distance (s',t ) + 1
        ; edit_distance (s ,t') + 1
        ; edit_distance (s',t') + cost_to_drop_both
        ]) ;;
val edit_distance : string * string -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        And this new version of <literal>edit_distance</literal> is
        indeed much more efficient than the one we started with.
      </para>
      <programlisting language="ocaml">
# time (fun () -&gt; edit_distance (&quot;OCaml 4.01&quot;,&quot;ocaml 4.01&quot;));;
Time: 2.14601ms
- : int = 2
</programlisting>
      <para>
        This is about ten thousand times faster than our original
        implementation.
      </para>
      <note> <title> 
      Limitations of <literal>let rec</literal>
      </title>
      <para>
        You might wonder why we didn't tie the recursive knot in
        <literal>memo_rec</literal> using <literal>let rec</literal>, as
        we did for <literal>make_rec</literal> earlier. Here's code that
        tries to do just that:
      </para>
      <programlisting language="ocaml">
# let memo_rec f_norec =
     let rec f = memoize (fun x -&gt; f_norec f x) in
     f
  ;;
      Characters 41-72:
       let rec f = memoize (fun x -&gt; f_norec f x) in
                   ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
Error: This kind of expression is not allowed as right-hand side of `let rec'
</programlisting>
      <para>
        OCaml rejects the definition because OCaml, as a strict
        language, has limits on what it can put on the right hand side
        of a <literal>let rec</literal>. In particular, imagine how the
        following code snippet would be compiled.
      </para>
      <programlisting language="ocaml">
let rec x = x + 1
</programlisting>
      <para>
        Note that <literal>x</literal> is an ordinary value, not a
        function. As such, it's not clear how to execute this code. In
        some sense, you could imagine it compiling down to an infinite
        loop, but there's no looping control structure to make that
        happen.
      </para>
      <para>
        To avoid such cases, the compiler only allow three possible
        constructs to show up on the right-hand side of a
        <literal>let rec</literal>: a function definition, a
        constructor, or the lazy keyword. This excludes some reasonable
        things, like our definition of <literal>memo_rec</literal>, but
        it also blocks things that don't make sense, like our definition
        of <literal>x</literal>.
      </para>
      <para>
        It's worth noting that these restrictions don't show up in a
        lazy language like Haskell. Indeed, we can make something like
        our definition of <literal>x</literal> work if we use OCaml's
        laziness:
      </para>
      <programlisting language="ocaml">
# let rec x = lazy (Lazy.force x + 1);;
val x : int lazy_t = &lt;lazy&gt;
</programlisting>
      <para>
        Of course, actually trying to compute this will fail. OCaml's
        <literal>lazy</literal> throws an exception when a lazy value
        tries to force itself as part of its own evaluation.
      </para>
      <programlisting language="ocaml">
# Lazy.force x;;
Exception: Lazy.Undefined.
</programlisting>
      <para>
        But we can create useful recursive definitions possible with
        <literal>lazy</literal> as well. In particular, we can use
        laziness to make our definition of <literal>memo_rec</literal>
        work without explicit mutation.
      </para>
      <programlisting language="ocaml">
# let lazy_memo_rec f_norec x =
     let rec f = lazy (memoize (fun x -&gt; f_norec (Lazy.force f) x)) in
     (Lazy.force f) x
  ;;
val lazy_memo_rec : (('a -&gt; 'b) -&gt; 'a -&gt; 'b) -&gt; 'a -&gt; 'b = &lt;fun&gt;
# time (fun () -&gt; lazy_memo_rec fib_norec 40);;
Time: 0.298977ms
- : int = 102334155
</programlisting>
      <para>
        Laziness is more constrained than explicit mutation, and so in
        some cases can lead to code whose behavior is easier to think
        about.
      </para>
      </note>
    </sect2>
  </sect1>
  <sect1 id="input-and-output">
    <title>Input and Output</title>
    <para>
      Imperative programming is about more than modifying in-memory
      data-structures. Any function that doesn't boil down to a
      deterministic transformation from its arguments to its return
      value is imperative in nature. That includes not only things that
      mutate your program's data, but also operations that interact with
      the world outside of your program. An important example of this
      kind of interaction is I/O, <emphasis>i.e.</emphasis>, operations
      for reading or writing data to things like files, terminal input
      and output, and network sockets.
    </para>
    <para>
      There are multiple I/O libraries in OCaml. In this section we'll
      discuss OCaml's buffered I/O library that can be used through the
      <literal>In_channel</literal> and <literal>Out_channel</literal>
      modules in Core. Other I/O primitives are also available through
      the <literal>Unix</literal> module in Core as well as
      <literal>Async</literal>, the asynchronous I/O library that is
      covered in
      <xref linkend="concurrent-programming-with-async"/>.
      Most of the functionality in Core's <literal>In_channel</literal>,
      <literal>Out_channel</literal> (and in Core's
      <literal>Unix</literal> module) derives from the standard library,
      but we'll use Core's interfaces here.
    </para>
    <sect2 id="terminal-io">
      <title>Terminal I/O</title>
      <para>
        OCaml's buffered I/O library is organized around two types:
        <literal>in_channel</literal>, for channels you read from, and
        <literal>out_channel</literal>, for channels you write to.
        <literal>In_channel</literal> and <literal>Out_channel</literal>
        modules only have direct support for channels corresponding to
        files and terminals; other kinds of channels can be created
        through the <literal>Unix</literal> module.
      </para>
      <para>
        We'll start our discussion of I/O by focusing on the terminal.
        Following the UNIX model, communication with the terminal is
        organized around three channels, which correspond to the three
        standard file descriptors in Unix:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            <literal>In_channel.stdin</literal>. The &quot;standard
            input&quot; channel. By default, input comes from the
            terminal, which handles keyboard input.
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>In_channel.stdout</literal>. The &quot;standard
            output&quot; channel. By default, output written to
            <literal>stdout</literal> appears on the user terminal.
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>In_channel.stderr</literal>. The &quot;standard
            error&quot; channel. This is similar to
            <literal>stdout</literal>, but is intended for error
            messages.
          </para>
        </listitem>
      </itemizedlist>
      <para>
        The values <literal>stdin</literal>, <literal>stdout</literal>
        and <literal>stderr</literal> are useful enough that they are
        also available in the global name-space directly, without having
        to go through the <literal>In_channel</literal> and
        <literal>Out_channel</literal> modules.
      </para>
      <para>
        Let's see this in action in a simple interactive application.
        The following program, <literal>time_converter</literal>,
        prompts the user for a timezone, and then prints out the current
        time in that timezone. Here, we use Core's
        <literal>Zone</literal> module for looking up a timezone, and
        the <literal>Time</literal> module for computing the current
        time and printing it out in the timezone in question.
      </para>
      <programlisting language="ocaml">
(* file: time_converter.ml *)
open Core.Std

let () =
  Out_channel.output_string stdout &quot;Pick a timezone: &quot;;
  Out_channel.flush stdout;
  match In_channel.input_line stdin with
  | None -&gt; failwith &quot;No timezone provided&quot;
  | Some zone_string -&gt;
    let zone = Zone.find_exn zone_string in
    let time_string = Time.to_localized_string (Time.now ()) zone in
    Out_channel.output_string stdout
      (String.concat
         [&quot;The time in &quot;;Zone.to_string zone;&quot; is &quot;; time_string;&quot;\n&quot;]);
    Out_channel.flush stdout
</programlisting>
      <para>
        We can build this program (using <literal>ocamlbuild</literal>
        with the <literal>_tags</literal> file described in
        <xref linkend="single-file-programs"/>) and run it,
        you'll see that it prompts you for input, as follows:
      </para>
      <programlisting>
$ ./time_converter.byte
Pick a timezone:
</programlisting>
      <para>
        You can then type in the name of a timezone and hit return, and
        it will print out the current time in the timezone in question.
      </para>
      <programlisting>
Pick a timezone: Europe/London
The time in Europe/London is 2013-03-06 02:15:13.602033
</programlisting>
      <para>
        We called <literal>Out_channel.flush</literal> on
        <literal>stdout</literal> because
        <literal>out_channel</literal>s are buffered, which is to say
        that OCaml doesn't immediately do a write every time you call
        <literal>output_string</literal>. Instead, writes are buffered
        until either enough has been written to trigger the flushing of
        the buffers, or until a flush is explicitly requested. This
        greatly increases the efficiency of the writing process, by
        reducing the number of system calls.
      </para>
      <para>
        Note that <literal>In_channel.input_line</literal> returns a
        <literal>string option</literal>, with <literal>None</literal>
        indicating that the input stream has ended
        (<emphasis>i.e.</emphasis>, an end-of-file condition).
        <literal>Out_channel.output_string</literal> is used to print
        the final output, and <literal>Out_channel.flush</literal> is
        called to flush that output to the screen. The final flush is
        not technically required, since the program ends after that
        instruction, at which point all remaining output will be flushed
        anyway, but the flush is nonetheless good practice.
      </para>
    </sect2>
    <sect2 id="formatted-output-with-printf">
      <title>Formatted output with <literal>printf</literal></title>
      <para>
        Generating output with functions like
        <literal>Out_channel.output_string</literal> is simple and easy
        to understand, but can be a bit verbose. OCaml also supports
        formatted output using the <literal>printf</literal> function,
        which is modeled after <literal>printf</literal> in the C
        standard library. <literal>printf</literal> takes a
        <emphasis>format string</emphasis> that describe what to print
        and how to format it, as well as arguments to be printed, as
        determined by the formatting directives embedded in the format
        string. So, for example, we can write:
      </para>
      <programlisting language="ocaml">
# printf &quot;%i is an integer, %F is a float, \&quot;%s\&quot; is a string\n&quot;
    3 4.5 &quot;five&quot;;;
3 is an integer, 4.5 is a float, &quot;five&quot; is a string
- : unit = ()
</programlisting>
      <para>
        Importantly, and unlike C's <literal>printf</literal>, the
        <literal>printf</literal> in OCaml is type-safe. In particular,
        if we provide an argument whose type doesn't match what's
        presented in the format string, we'll get a type error.
      </para>
      <programlisting language="ocaml">
# printf &quot;An integer: %i\n&quot; 4.5;;
Characters 26-29:
  printf &quot;An integer: %i\n&quot; 4.5;;
                            ^^^
Error: This expression has type float but an expression was expected of type
         int
</programlisting>
      <note> <title> 
      Understanding format strings
      </title>
      <para>
        The format strings used by <literal>printf</literal> turn out to
        be quite different from ordinary strings. This difference ties
        to the fact that OCaml format strings, unlike their equivalent
        in C, are type-safe. In particular, the compiler checks that the
        types referred to by the format string match the types of the
        rest of the arguments passed to <literal>printf</literal>.
      </para>
      <para>
        To check this, OCaml needs to analyze the contents of the format
        string at compile time, which means the format string needs to
        be available as a string literal at compile time. Indeed, if you
        try to pass an ordinary string to <literal>printf</literal>, the
        compiler will complain.
      </para>
      <programlisting language="ocaml">
# let fmt = &quot;%i is an integer, %F is a float, \&quot;%s\&quot; is a string\n&quot;;;
val fmt : string = &quot;%i is an integer, %F is a float, \&quot;%s\&quot; is a string\n&quot;
# printf fmt 3 4.5 &quot;five&quot;;;
Characters 7-10:
  printf fmt 3 4.5 &quot;five&quot;;;
         ^^^
Error: This expression has type string but an expression was expected of type
         ('a -&gt; 'b -&gt; 'c -&gt; 'd, out_channel, unit) format =
           ('a -&gt; 'b -&gt; 'c -&gt; 'd, out_channel, unit, unit, unit, unit)
           format6
</programlisting>
      <para>
        If OCaml infers that a given string literal is a format string,
        then it parses it at compile time as such, choosing its type in
        accordance with the formatting directives it finds. Thus, if we
        add a type-annotation indicating that the string we're defining
        is actually a format string, it will be interpreted as such:
      </para>
      <programlisting language="ocaml">
# let fmt : ('a, 'b, 'c) format =
    &quot;%i is an integer, %F is a float, \&quot;%s\&quot; is a string\n&quot;;;
  val fmt : (int -&gt; float -&gt; string -&gt; 'c, 'b, 'c) format = &lt;abstr&gt;
</programlisting>
      <para>
        And accordingly, we can pass it to <literal>printf</literal>.
      </para>
      <programlisting language="ocaml">
# printf fmt 3 4.5 &quot;five&quot;;;
3 is an integer, 4.5 is a float, &quot;five&quot; is a string
- : unit = ()
</programlisting>
      <para>
        If this looks different from everything else you've seen so far,
        that's because it is. This is really a special case in the
        type-system. Most of the time, you don't need to worry about
        this special handling of format strings --- you can just use
        <literal>printf</literal> and not worry about the details. But
        it's useful to keep the broad outlines of the story in the back
        of your head.
      </para>
      </note>
      <para>
        Now let's see how we can rewrite our time conversion program to
        be a little more concise using <literal>printf</literal>.
      </para>
      <programlisting language="ocaml">
(* file: time_converter.ml *)
open Core.Std

let () =
  printf &quot;Pick a timezone: %!&quot;;
  match In_channel.input_line stdin with
  | None -&gt; failwith &quot;No timezone provided&quot;
  | Some zone_string -&gt;
    let zone = Zone.find_exn zone_string in
    let time_string = Time.to_localized_string (Time.now ()) zone in
    printf &quot;The time in %s is %s.\n%!&quot; (Zone.to_string zone) time_string
</programlisting>
      <para>
        In the above example, we've used only two formatting directives:
        <literal>%s</literal>, for including a string, and
        <literal>%!</literal> which causes <literal>printf</literal> to
        flush the channel.
      </para>
      <para>
        <literal>printf</literal>'s formatting directives offer a
        significant amount of control, allowing you to specify things
        like:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            alignment and padding
          </para>
        </listitem>
        <listitem>
          <para>
            escaping rules for strings
          </para>
        </listitem>
        <listitem>
          <para>
            whether numbers should be formatted in decimal, hex or
            binary
          </para>
        </listitem>
        <listitem>
          <para>
            precision of float conversions
          </para>
        </listitem>
      </itemizedlist>
      <para>
        There are also <literal>printf</literal>-style functions that
        target outputs other than <literal>stdout</literal>, including:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            <literal>eprintf</literal>, which prints to
            <literal>stderr</literal>.
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>fprintf</literal>, which prints to an arbitrary
            <literal>out_channel</literal>
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>sprintf</literal>, which returns a formatted string
          </para>
        </listitem>
      </itemizedlist>
      <para>
        All of this, and a good deal more, is described in the API
        documentation for the <literal>Printf</literal> module in the
        OCaml Manual.
      </para>
    </sect2>
    <sect2 id="file-io">
      <title>File I/O</title>
      <para>
        Another common use of <literal>in_channel</literal>s and
        <literal>out_channel</literal>s is for working with files.
        Here's a couple of functions, one that creates a file full of
        numbers, and the other that reads in such a file and returns the
        sum of those numbers.
      </para>
      <programlisting language="ocaml">
# let create_number_file filename numbers =
    let outc = Out_channel.create filename in
    List.iter numbers ~f:(fun x -&gt; fprintf outc &quot;%d\n&quot; x);
    Out_channel.close outc
  ;;
 val create_number_file : string -&gt; int Core.Std.List.t -&gt; unit = &lt;fun&gt;
# let sum_file filename =
     let file = In_channel.create filename in
     let numbers = List.map ~f:Int.of_string (In_channel.input_lines file) in
     let sum = List.fold ~init:0 ~f:(+) numbers in
     In_channel.close file;
     sum
  ;;
val sum_file : string -&gt; int = &lt;fun&gt;
# create_number_file &quot;numbers.txt&quot; [1;2;3;4;5];;
- : unit = ()
# sum_file &quot;numbers.txt&quot;;;
- : int = 15
</programlisting>
      <para>
        For both of these functions we followed the same basic sequence:
        we first create the channel, then use the channel, and finally
        close the channel. The closing of the channel is important,
        since without it, we won't release resources associated with the
        file back to the operating system.
      </para>
      <para>
        One problem with the code above is that if it throws an
        exception in the middle of its work, it won't actually close the
        file. If we try to read a file that doesn't actually contain
        numbers, we'll see such an error:
      </para>
      <programlisting language="ocaml">
# sum_file &quot;/etc/hosts&quot;;;
Exception: (Failure &quot;Int.of_string: \&quot;##\&quot;&quot;).
</programlisting>
      <para>
        And if we do this over and over in a loop, we'll eventually run
        out of file descriptors.
      </para>
      <programlisting language="ocaml">
# for i = 1 to 10000 do try ignore (sum_file &quot;/etc/hosts&quot;) with _ -&gt; () done;;
- : unit = ()
# sum_file &quot;numbers.txt&quot;;;
Exception: (Sys_error &quot;numbers.txt: Too many open files&quot;).
</programlisting>
      <para>
        And now, you'll need to restart your toplevel if you want to
        open any more files!
      </para>
      <para>
        To avoid this, we need to make sure that our code cleans up
        after itself. We can do this using the
        <literal>protect</literal> function described in
        <xref linkend="error-handling"/>, as follows.
      </para>
      <programlisting language="ocaml">
# let sum_file filename =
     let file = In_channel.create filename in
     protect ~f:(fun () -&gt;
         let numbers = List.map ~f:Int.of_string (In_channel.input_lines file) in
         List.fold ~init:0 ~f:(+) numbers)
       ~finally:(fun () -&gt; In_channel.close file)
  ;;
val sum_file : string -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        And now, the file descriptor leak is gone:
      </para>
      <programlisting language="ocaml">
# for i = 1 to 10000 do try ignore (sum_file &quot;/etc/hosts&quot;) with _ -&gt; () done;;
- : unit = ()
# sum_file &quot;numbers.txt&quot;;;
- : int = 15
</programlisting>
      <para>
        This is really an example of a more general complexity of
        imperative programming. When programming imperatively, you need
        to be quite careful to make sure that exceptions don't leave you
        in an awkward state.
      </para>
      <para>
        <literal>In_channel</literal> also supports some idioms that
        handle some of the details of this for you. For example, the
        <literal>with_file</literal> function takes a filename and a
        function for processing that file, and takes care of the opening
        and closing of the file transparently.
      </para>
      <programlisting language="ocaml">
# let sum_file filename =
     In_channel.with_file filename ~f:(fun file -&gt;
       let numbers = List.map ~f:Int.of_string (In_channel.input_lines file) in
       List.fold ~init:0 ~f:(+) numbers)
  ;;
val sum_file : string -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        Another misfeature of our implementation of
        <literal>sum_file</literal> is that we read the entire file into
        memory before processing it. For a large file, it's more
        efficient to process a line at a time. You can use the
        <literal>In_channel.fold_lines</literal> function to do just
        that.
      </para>
      <programlisting language="ocaml">
# let sum_file filename =
     In_channel.with_file filename ~f:(fun file -&gt;
       In_channel.fold_lines file ~init:0 ~f:(fun sum line -&gt;
         sum + Int.of_string line))
  ;;
val sum_file : string -&gt; int = &lt;fun&gt;
</programlisting>
      <para>
        This is just a taste of the functionality of
        <literal>In_channel</literal> and
        <literal>Out_channel</literal>. To get a fuller understanding
        you should review the API documentation for those modules.
      </para>
    </sect2>
  </sect1>
  <sect1 id="order-of-evaluation">
    <title>Order of evaluation</title>
    <para>
      The order in which expressions are evaluated is an important part
      of the definition of a programming language, and it is
      particularly important when programming imperatively. Most
      programming languages you're likely to have encountered are
      <emphasis>strict</emphasis>, and OCaml is too. In a strict
      language, when you bind an identifier to the result of some
      expression, the expression is evaluated before the variable is
      defined. Similarly, if you call a function on a set of arguments,
      those arguments are evaluated before they are passed to the
      function.
    </para>
    <para>
      Consider the following simple example. Here, we have a collection
      of angles and we want to determine if any of them have a negative
      `sin. The following snippet of code would answer that question.
    </para>
    <programlisting language="ocaml">
# let x = sin 120. in
  let y = sin 75.  in
  let z = sin 128. in
  List.exists ~f:(fun x -&gt; x &lt; 0.) [x;y;z]
  ;;
- : bool = true
</programlisting>
    <para>
      In some sense, we don't really need to compute the
      <literal>sin 128</literal>, because <literal>sin 75.</literal> is
      negative, so we could know the answer before even computing
      <literal>sin 128</literal>.
    </para>
    <para>
      It doesn't have to be this way. Using the <literal>lazy</literal>
      keyword, we can write the original computation so that
      <literal>sin 128.</literal> won't ever be computed.
    </para>
    <programlisting language="ocaml">
# let x = lazy (sin 120.) in
  let y = lazy (sin 75.)  in
  let z = lazy (sin 128.) in
  List.exists ~f:(fun x -&gt; Lazy.force x &lt; 0.) [x;y;z]
  ;;
- : bool = true
</programlisting>
    <para>
      We can confirm that fact by a few well placed
      <literal>printf</literal>s.
    </para>
    <programlisting language="ocaml">
# let x = lazy (printf &quot;1\n&quot;; sin 120.) in
  let y = lazy (printf &quot;2\n&quot;; sin 75.)  in
  let z = lazy (printf &quot;3\n&quot;; sin 128.) in
  List.exists ~f:(fun x -&gt; Lazy.force x &lt; 0.) [x;y;z]
  ;;
1
2
- : bool = true
</programlisting>
    <para>
      OCaml is strict by default for a good reason: Lazy evaluation and
      imperative programming generally don't mix well, because laziness
      makes it harder to reason about when a given side effect is going
      to occur. Understanding the order of side-effects is essential to
      reasoning about the behavior of an imperative program.
    </para>
    <para>
      In a strict language, we know that expressions that are bound by a
      sequence of let-bindings will be evaluated in the order that
      they're defined. But what about the evaluation order within a
      single expression? Officially, the answer is that evaluation order
      within an expression is undefined. In practice, OCaml has only one
      compiler, and that behavior is a kind of <emphasis>de
      facto</emphasis> standard. Unfortunately, the evaluation order in
      this case is often the oppose of what one might expect.
    </para>
    <para>
      Consider the following example.
    </para>
    <programlisting language="ocaml">
# List.exists ~f:(fun x -&gt; x &lt; 0.)
    [ (printf &quot;1\n&quot;; sin 120.);
      (printf &quot;2\n&quot;; sin 75.);
      (printf &quot;3\n&quot;; sin 128.); ]
  ;;
3
2
1
- : bool = true
</programlisting>
    <para>
      Here, you can see that the sub-expression that came last was
      actually evaluated first! This is generally the case for many
      different kinds of expressions. If you want to make sure of the
      evaluation order of different sub-expressions, you should express
      them as a series of <literal>let</literal> bindings.
    </para>
  </sect1>
  <sect1 id="side-effects-and-weak-polymorphism">
    <title>Side-effects and weak polymorphism</title>
    <para>
      Consider the following simple imperative function.
    </para>
    <programlisting language="ocaml">
# let remember =
    let cache = ref None in
    (fun x -&gt;
       match !cache with
       | Some y -&gt; y
       | None -&gt; cache := Some x; x)
  ;;
</programlisting>
    <para>
      <literal>remember</literal> simply caches the first value that's
      passed to it, returning that value on every call. It's not a
      terribly useful function, but it raises an interesting question:
      what type should it have?
    </para>
    <para>
      On its first call, <literal>remember</literal> returns the same
      value its passed, which means its input type and return type
      should match. Accordingly, <literal>remember</literal> should have
      the type <literal>t -&gt; t</literal> for some type
      <literal>t</literal>. There's nothing about
      <literal>remember</literal> that ties the choice of
      <literal>t</literal> to any particular type, so you might expect
      OCaml to generalize, replacing <literal>t</literal> with a
      polymorphic type variable. It's this kind of generalization that
      gives us polymorphic types in the first place. The identity
      function, as an example, gets a polymorphic type in this way.
    </para>
    <programlisting language="ocaml">
# let identity x = x;;
val identity : 'a -&gt; 'a = &lt;fun&gt;
# identity 3;;
- : int = 3
# identity &quot;five&quot;;;
- : string = &quot;five&quot;
</programlisting>
    <para>
      As you can see, the polymorphic type of
      <literal>identity</literal> lets it operate on values with
      different types.
    </para>
    <para>
      This is not what happens with <literal>remember</literal>, though.
      Here's the type that OCaml infers for remember, which looks
      almost, but not quite, like the type of the identity function.
    </para>
    <programlisting language="ocaml">
val remember : '_a -&gt; '_a = &lt;fun&gt;
</programlisting>
    <para>
      The underscore in the type variable <literal>'_a</literal> tells
      us that the variable is only <emphasis>weakly
      polymorphic</emphasis>, which is to say that it can be used with
      any <emphasis>single</emphasis> type. That makes sense, because,
      unlike <literal>identity</literal>, <literal>remember</literal>
      always returns the value it was passed on its first invocation,
      which means it can only be used with one type.
    </para>
    <para>
      OCaml will convert a weakly polymorphic variable to a concrete
      type as soon as it gets a clue as to what concrete type it is to
      be used as, as you can see below.
    </para>
    <programlisting language="ocaml">
# let remember_three () = remember 3;;
val remember_three : unit -&gt; int = &lt;fun&gt;
# remember;;
- : int -&gt; int = &lt;fun&gt;
# remember &quot;avocado&quot;;;
Characters 9-18:
  remember &quot;avocado&quot;;;
           ^^^^^^^^^
Error: This expression has type string but an expression was expected of type
         int
</programlisting>
    <para>
      Note that the type of <literal>remember</literal> was settled by
      the definition of <literal>remember_three</literal>, even though
      <literal>remember_three</literal> was never called!
    </para>
    <sect2 id="the-value-restriction">
      <title>The Value Restriction</title>
      <para>
        So, when does the compiler infer weakly polymorphic types? As
        we've seen, we need weakly polymorphic types when a value of
        unknown type is stored in a persistent mutable cell. Because the
        type-system isn't precise enough to determine all cases where
        this might happen, OCaml uses a rough rule to flag cases where
        it's sure there are no persistent refs, and to only infer
        polymorphic types in those cases. This rule is called
        <emphasis>the value restriction</emphasis>.
      </para>
      <para>
        The core of the value restriction is the observation that some
        kinds of simple values by their nature can't contain refs,
        including:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            Constants (<emphasis>i.e.</emphasis>, things like integer
            and floating point literals)
          </para>
        </listitem>
        <listitem>
          <para>
            Constructors that contain only other simple values
          </para>
        </listitem>
        <listitem>
          <para>
            Function declarations, <emphasis>i.e.</emphasis>,
            expressions that begin with <literal>fun</literal> or
            <literal>function</literal>, or, the equivalent let binding,
            <literal>let f x = ...</literal>.
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>let</literal> bindings of the form
            <literal>let var = &lt;expr1&gt; in &lt;expr2&gt;</literal>,
            where both <literal>&lt;expr1&gt;</literal> and
            <literal>&lt;expr2&gt;</literal> are simple values.
          </para>
        </listitem>
      </itemizedlist>
      <para>
        Thus, the following expression is a simple value, and as a
        result, the types of values contained within it are allowed to
        be polymorphic.
      </para>
      <programlisting language="ocaml">
# (fun x -&gt; [x;x]);;
- : 'a -&gt; 'a list = &lt;fun&gt;
</programlisting>
      <para>
        But, if we write down an expression that isn't a simple value by
        the above definition, we'll get different results. For example,
        consider what happens if we try to memoize the function defined
        above.
      </para>
      <programlisting language="ocaml">
# memoize (fun x -&gt; [x;x]);;
- : '_a -&gt; '_a list = &lt;fun&gt;
</programlisting>
      <para>
        The memoized version of the function does in fact need to be
        restricted to a single type, because it uses mutable state
        behind the scenes to cache previous invocations of the function
        it has passed. But OCaml would make the same determination even
        if the function in question did no such thing. Consider this
        example:
      </para>
      <programlisting language="ocaml">
# identity (fun x -&gt; [x;x]);;
- : '_a -&gt; '_a list = &lt;fun&gt;
</programlisting>
      <para>
        It would be safe to infer a weakly polymorphic variable here,
        but because OCaml's type system doesn't distinguish between pure
        and impure functions, it can't separate those two cases.
      </para>
      <para>
        The value restriction doesn't require that there is no mutable
        state, only that there is no <emphasis>persistent</emphasis>
        mutable state that could share values between uses of the same
        function. Thus, a function that produces a fresh reference every
        time it's called can have a fully polymorphic type:
      </para>
      <programlisting language="ocaml">
# let f () = ref None;;
val f : unit -&gt; 'a option ref = &lt;fun&gt;
</programlisting>
      <para>
        But a function that has a mutable cache that persists across
        calls, like memoize, can only be weakly polymorphic.
      </para>
    </sect2>
    <sect2 id="partial-application-and-the-value-restriction">
      <title>Partial application and the value restriction</title>
      <para>
        Most of the time, when the value restriction kicks in, it's for
        a good reason, <emphasis>i.e.</emphasis>, it's because the value
        in question can actually only safely be used with a single type.
        But sometimes, the value restriction kicks in when you don't
        want it. The most common such case is partially applied
        functions. A partially applied function, like any function
        application, is not a simple value, and as such, functions
        created by partial application are sometimes less general than
        you might expect.
      </para>
      <para>
        Consider the <literal>List.init</literal> function, which is
        used for creating lists where each element is created by calling
        a function on the index of that element.
      </para>
      <programlisting language="ocaml">
# List.init;;
- : int -&gt; f:(int -&gt; 'a) -&gt; 'a list = &lt;fun&gt;
# List.init 10 ~f:Int.to_string;;
- : string list = [&quot;0&quot;; &quot;1&quot;; &quot;2&quot;; &quot;3&quot;; &quot;4&quot;; &quot;5&quot;; &quot;6&quot;; &quot;7&quot;; &quot;8&quot;; &quot;9&quot;]
</programlisting>
      <para>
        Imagine we wanted to create a specialized version of
        <literal>List.init</literal> that always created lists of length
        10. We could do that using partial application, as follows.
      </para>
      <programlisting language="ocaml">
# let list_init_10 = List.init 10;;
val list_init_10 : f:(int -&gt; '_a) -&gt; '_a list = &lt;fun&gt;
</programlisting>
      <para>
        As you can see, we now infer a weakly polymorphic type for the
        resulting function, and for good reason. There's nothing that
        tells us that <literal>List.init</literal> isn't creating a
        persistent <literal>ref</literal> somewhere inside of it that
        would be shared across multiple calls to
        <literal>list_init_10</literal>. We can eliminate this
        possibility, and at the same time get the compiler to infer a
        polymorphic type, by using explicit variables rather than
        partial application.
      </para>
      <programlisting language="ocaml">
# let list_init_10 ~f = List.init 10 ~f;;
val list_init_10 : f:(int -&gt; 'a) -&gt; 'a list = &lt;fun&gt;
</programlisting>
      <para>
        This transformation is referred to as <emphasis>eta
        expansion</emphasis>, and is often useful to resolve problems
        that arise from the value restriction.
      </para>
    </sect2>
    <sect2 id="relaxing-the-value-restriction">
      <title>Relaxing the value restriction</title>
      <para>
        OCaml is actually a little better at inferring polymorphic types
        than is implied above. The value restriction as we described it
        above is basically a syntactic check: there are a few operations
        that you can do that count as simple values, and anything that's
        a simple value can be generalized.
      </para>
      <para>
        But OCaml actually has a relaxed version of the value
        restriction that can make some use of type information to allow
        polymorphic types for things that are not simple values.
      </para>
      <para>
        For example, we saw above that a function application, even a
        simple application of the identity function, is not a simple
        value and thus can turn a polymorphic value into a weakly
        polymorphic one.
      </para>
      <programlisting language="ocaml">
# identity (fun x -&gt; [x;x]);;
- : '_a -&gt; '_a list = &lt;fun&gt;
</programlisting>
      <para>
        But that's not always the case. When the type of the returned
        value is immutable, then OCaml can typically infer a fully
        polymorphic type.
      </para>
      <programlisting language="ocaml">
# identity [];;
- : 'a list = []
</programlisting>
      <para>
        On the other hand, if the returned type is potentially mutable,
        then the result will be weakly polymorphic.
      </para>
      <programlisting language="ocaml">
# [||];;
- : 'a array = [||]
# identity [||];;
- : '_a array = [||]
</programlisting>
      <para>
        A more important example of this comes up when defining abstract
        data types. Consider the following simple data-structure for an
        immutable list type that supports constant-time concatenation.
      </para>
      <programlisting language="ocaml">
# module Concat_list : sig
    type 'a t
    val empty : 'a t
    val singleton : 'a -&gt; 'a t
    val concat  : 'a t -&gt; 'a t -&gt; 'a t  (* constant time *)
    val to_list : 'a t -&gt; 'a list       (* linear time   *)
  end = struct

    type 'a t = Empty | Singleton of 'a | Concat of 'a t * 'a t

    let empty = Empty
    let singleton x = Singleton x
    let concat x y = Concat (x,y)

    let rec to_list_with_tail t tail =
      match t with
      | Empty -&gt; tail
      | Singleton x -&gt; x :: tail
      | Concat (x,y) -&gt; to_list_with_tail x (to_list_with_tail y tail)

    let to_list t =
      to_list_with_tail t []

  end;;
 module Concat_list :
  sig
    type 'a t
    val empty : 'a t
    val singleton : 'a -&gt; 'a t
    val concat : 'a t -&gt; 'a t -&gt; 'a t
    val to_list : 'a t -&gt; 'a list
  end
</programlisting>
      <para>
        The details of the implementation don't matter so much, but it's
        important to note that a <literal>Concat_list.t</literal> is
        unquestionably an immutable value. However, when it comes to the
        value restriction, OCaml treats it as if it were mutable.
      </para>
      <programlisting language="ocaml">
# Concat_list.empty;;
- : 'a Concat_list.t = &lt;abstr&gt;
# identity Concat_list.empty;;
- : '_a Concat_list.t = &lt;abstr&gt;
</programlisting>
      <para>
        The issue here is that the signature, by virtue of being
        abstract, has obscured the fact that
        <literal>Concat_list.t</literal> is in fact an immutable
        data-type. We can resolve this in one of two ways: either by
        making the type concrete (<emphasis>i.e.</emphasis>, exposing
        the implementation in the <literal>mli</literal>), which is
        often not desirable; or by marking the type variable in question
        as <emphasis>covariant</emphasis>. We'll learn more about
        variance and covariance in
        <xref linkend="object-oriented-programming"/>, but for
        now, you can think of it as an annotation which can be put in
        the interface of a pure data structure.
      </para>
      <para>
        Thus, if we replace <literal>type 'a t</literal> in the
        interface with <literal>type +'a t</literal>, that will make it
        explicit in the interface that the data-structure doesn't
        contain any persistent references to values of type
        <literal>'a</literal>, at which point, OCaml can infer
        polymorphic types for expressions of this type that are not
        simple values.
      </para>
      <programlisting language="ocaml">
# identity Concat_list.empty;;
- : 'a Concat_list.t = &lt;abstr&gt;
</programlisting>
    </sect2>
  </sect1>
</chapter><chapter id="object-oriented-programming">
  <title>Object Oriented Programming</title>
  <para>
    We've already seen several tools that OCaml provides for organizing
    programs, particularly first-class modules. In addition, OCaml also
    supports object-oriented programming. There are objects, classes,
    and their associated types. Objects are good for encapsulation and
    abstraction, and classes are good for code re-use.
  </para>
  <note>
  <title>
  What is Object-Oriented Programming?
  </title>
  <para>
    Object-oriented programming (often shorted to OOP) is a programming
    style that encapsulates computation and data within logical
    <emphasis>objects</emphasis>. Each object contains some data stored
    in <emphasis>fields</emphasis>, and has <emphasis>method</emphasis>
    functions that can be invoked against the data within the object.
    The code definition behind an object is called a
    <emphasis>class</emphasis>, and objects are constructed from a class
    definition by calling a constructor with the data that the object
    will use to build itself.
  </para>
  <para>
    There are four fundamental properties that differentiate OOP from
    other styles:
  </para>
  <itemizedlist>
    <listitem>
      <para>
        <emphasis>Abstraction</emphasis>: the details of the
        implementation are hidden in the object, and the external
        interface is just the set of publically-accessible methods.
      </para>
    </listitem>
    <listitem>
      <para>
        <emphasis>Dynamic lookup</emphasis>: when a message is sent to
        an object, the method to be executed is determined by the
        implementation of the object, not by some static property of the
        program. In other words, different objects may react to the same
        message in different ways.
      </para>
    </listitem>
    <listitem>
      <para>
        <emphasis>Subtyping</emphasis>: if an object
        <literal>a</literal> has all the functionality of an object
        <literal>b</literal>, then we may use <literal>a</literal> in
        any context where <literal>b</literal> is expected.
      </para>
    </listitem>
    <listitem>
      <para>
        <emphasis>Inheritance</emphasis>: the definition of one kind of
        object can be re-used to produce a new kind of object. This new
        definition can override some behaviour, but also share code with
        its parent.
      </para>
    </listitem>
  </itemizedlist>
  <para>
    Almost every notable modern programming language has been influenced
    by OOP, and you'll have run across these terms if you've ever used
    C++, Java, C#, Ruby, Python or Javascript.
  </para>
  </note>
  <sect1 id="when-to-use-objects">
    <title>When to use objects</title>
    <para>
      You might wonder when to use objects in OCaml, which has a
      multitude of alternative mechanisms to express the same concept.
      First-class modules are more expressive (a module can include
      types, while classes and objects cannot). Modules, functors, and
      algebraic data types also offer a wide range of ways to express
      program structure. In fact, many seasoned OCaml programmers rarely
      use classes and objects, if at all.
    </para>
    <para>
      Modules already provide these features in some form, but the main
      focus of classes is on code re-use through inheritance and late
      binding of methods. This is a critical property of classes: the
      methods that implement an object are determined when the object is
      instantiated, a form of <emphasis>dynamic</emphasis> binding. In
      the meantime, while classes are being defined, it is possible (and
      necessary) to refer to methods without knowing statically how they
      will be implemented.
    </para>
    <para>
      In contrast, modules use static (lexical) scoping. If you want to
      parameterize your module code so that some part of it can be
      implemented later, you would write a function or functor. This is
      more explicit, but often more verbose than overriding a method in
      a class.
    </para>
    <para>
      In general, a rule of thumb is: use classes and objects in
      situations where dynamic binding is a big win, for example if you
      have many similar variations in the implementation of a concept.
      Two good examples are Xavier Leroy's
      <ulink url="http://gallium.inria.fr/~xleroy/software.html#cryptokit">Cryptokit</ulink>,
      which provides a variety of cryptographic primitives that can be
      combined in building-block style, and the
      <ulink url="http://cristal.inria.fr/camlimages/">Camlimages</ulink>
      library which manipulates various graphical file formats.
    </para>
    <para>
      In this chapter, we'll introduce you to the basics of object
      definition and use in OCaml, and then demonstrate their use with
      an example using Cryptokit. We'll return to the more advanced
      areas of object use later on in the book in
      <xref linkend="object-subtyping-and-inheritance"/>.
    </para>
  </sect1>
  <sect1 id="ocaml-objects">
    <title>OCaml objects</title>
    <para>
      If you already know about object oriented programming in a
      language like Java or C++, the OCaml object system may come as a
      surprise. Foremost is the complete separation of subtyping and
      inheritance in OCaml. In a language like Java, a class name is
      also used as the type of objects created by instantiating it, and
      the subtyping rule corresponds to inheritance. For example, if we
      implement a class <literal>Stack</literal> in Java by inheriting
      from a class <literal>Deque</literal>, we would be allowed to pass
      a stack anywhere a deque is expected (this is a silly example of
      course, practitioners will point out that we shouldn't do it).
    </para>
    <para>
      OCaml is entirely different. Classes are used to construct objects
      and support inheritance, including non-subtyping inheritance.
      Classes are not types. Instead, objects have <emphasis>object
      types</emphasis>, and if you want to use objects, you aren't
      required to use classes at all. Here's an example of a simple
      object.
    </para>
    <programlisting language="ocaml">
# let p =
  object
    val mutable x = 0
    method get = x
    method set i = x &lt;- i
  end;;
val p : &lt; get : int; set : int -&gt; unit &gt; = &lt;obj&gt;
</programlisting>
    <para>
      The object has an integer value <literal>x</literal>, a method
      <literal>get</literal> that returns x, and a method
      <literal>set</literal> that updates the value of
      <literal>x</literal>.
    </para>
    <para>
      The object type is enclosed in angle brackets
      <literal>&lt; ... &gt;</literal>, containing just the types of the
      methods. Fields, like <literal>x</literal>, are not part of the
      public interface of an object. All interaction with an object is
      through its methods. The syntax for a method invocation (also
      called &quot;sending a message&quot; to the object) uses the
      <literal>#</literal> character.
    </para>
    <programlisting language="ocaml">
# p#get;
- : int = 0
# p#set 17;;
- : unit = ()
# p#get;;
- : int = 17
</programlisting>
    <para>
      Objects can also be constructed by functions. If we want to
      specify the initial value of the object, we can define a function
      that takes the value and returns an object.
    </para>
    <programlisting language="ocaml">
# let make i =
  object
    val mutable x = i
    method get = x
    method set y = x &lt;- y
  end;;
val make : 'a -&gt; &lt; get : 'a; set : 'a -&gt; unit &gt; = &lt;fun&gt;
# let p = make 5;;
val p : &lt; get : int; set : int -&gt; unit &gt; = &lt;obj&gt;
# p#get;;
- : int = 5
</programlisting>
    <para>
      Note that the types of the function <literal>make</literal> and
      the returned object now use the polymorphic type
      <literal>'a</literal>. When make is invoked on a concrete value
      <literal>5</literal>, we get the same object type as before, with
      type <literal>int</literal> for the value.
    </para>
  </sect1>
  <sect1 id="object-polymorphism">
    <title>Object Polymorphism</title>
    <para>
      Functions can also take object arguments. Let's construct a new
      object <literal>average</literal> that returns the average of any
      two objects with a <literal>get</literal> method.
    </para>
    <programlisting language="ocaml">
# let average p1 p2 =
  object
    method get = (p1#get + p2#get) / 2
  end;;
val average :
  &lt; get : int; .. &gt; -&gt;
  &lt; get : int; .. &gt; -&gt;
  &lt; get : int &gt; = &lt;fun&gt;
</programlisting>
    <para>
      There's some new syntax in the type that's been inferred for
      <literal>average</literal> here. The parameters have the object
      type <literal>&lt; get : int; .. &gt;</literal>. The
      <literal>..</literal> are ellipsis, standing for any other
      methods. The type <literal>&lt; get : int; .. &gt;</literal>
      specifies an object that must have at least a
      <literal>get</literal> method, and possibly some others as well.
    </para>
    <para>
      We can use the <literal>average</literal> using the normal object
      invocation syntax:
    </para>
    <programlisting language="ocaml">
# let p1 = make 5;;
# let p2 = make 15;;
# let a = average p1 p2;;
# a#get;;
- : int = 10
# p2#set 25;;
# a#get;;
- : int = 15
</programlisting>
    <para>
      The potential extra parameters defined by the object are carefully
      tracked by the OCaml type checker. If we manually try and
      constrain the exact type <literal>&lt; get : int &gt;</literal>
      for an object with more methods, type inference will fail.
    </para>
    <programlisting language="ocaml">
# let (p : &lt; get : int &gt;) = make 5;;
Error: This expression has type &lt; get : int; set : int -&gt; unit &gt;
       but an expression was expected of type &lt; get : int &gt;
       The second object type has no method set
</programlisting>
    <note>
    <title>
    Elisions are polymorphic
    </title>
    <para>
      The <literal>..</literal> in an object type is an elision,
      standing for &quot;possibly more methods.&quot; It may not be
      apparent from the syntax, but an elided object type is actually
      polymorphic. If we try to write a type definition, we get an
      obscure error.
    </para>
    <programlisting language="ocaml">
# type point = &lt; get:int; .. &gt;;;
Error: A type variable is unbound in this type declaration.
In type &lt; get : int; .. &gt; as 'a the variable 'a is unbound
</programlisting>
    <para>
      A <literal>..</literal> in an object type is called a
      <emphasis>row variable</emphasis> and this typing scheme is called
      <emphasis>row polymorphism</emphasis>. Even though
      <literal>..</literal> doesn't look like a type variable, it
      actually is. The error message suggests a solution, which is to
      add the <literal>as 'a</literal> type constraint.
    </para>
    <programlisting language="ocaml">
# type 'a point = &lt; get:int; .. &gt; as 'a;;
type 'a point = 'a constraint 'a = &lt; get : int; .. &gt;
</programlisting>
    <para>
      In other words, the type <literal>'a point</literal> is equal to
      <literal>'a</literal>, where
      <literal>'a = &lt; get : int; .. &gt;</literal>. That may seem
      like an odd way to say it, and in fact, this type definition is
      not really an abbreviation because <literal>'a</literal> refers to
      the entire type.
    </para>
    </note>
    <para>
      An object of type <literal>&lt; get:int; .. &gt;</literal> can be
      any object with a method <literal>get:int</literal>, it doesn't
      matter how it is implemented. So far, we've constructed two
      objects with that type; the function <literal>make</literal>
      constructed one, and so did <literal>average</literal>. When the
      method <literal>#get</literal> is invoked, the actual method that
      is run is determined by the object.
    </para>
    <programlisting language="ocaml">
# let print_point p = Printf.printf &quot;Point: %d\n&quot; p#get;;
val print_point : &lt; get : int; .. &gt; -&gt; unit = &lt;fun&gt;
# print_point (make 5);;
Point: 5
# print_point (average (make 5) (make 15));;
Point: 10
</programlisting>
  </sect1>
  <sect1 id="classes">
    <title>Classes</title>
    <para>
      Programming with objects directly is great for encapsulation, but
      one of the main goals of object-oriented programming is code
      re-use through inheritance. For inheritance, we need to introduce
      <emphasis>classes</emphasis>. In object-oriented programming, a
      class is a &quot;recipe&quot; for creating objects. The recipe can
      be changed by adding new methods and fields, or it can be changed
      by modifying existing methods.
    </para>
    <para>
      In OCaml, class definitions must be defined as toplevel statements
      in a module. A class is not an object, and a class definition is
      not an expression. The syntax for a class definition uses the
      keyword <literal>class</literal>.
    </para>
    <programlisting language="ocaml">
# class point =
  object
    val mutable x = 0
    method get = x
    method set y = x &lt;- y
  end;;
class point :
  object
    val mutable x : int
    method get : int
    method set : int -&gt; unit
  end
</programlisting>
    <para>
      The type <literal>class point : ... end</literal> is a
      <emphasis>class type</emphasis>. This particular type specifies
      that the <literal>point</literal> class defines a mutable field
      <literal>x</literal>, a method <literal>get</literal> that returns
      an <literal>int</literal>, and a method <literal>set</literal>
      with type <literal>int -&gt; unit</literal>.
    </para>
    <para>
      To produce an object, classes are instantiated with the keyword
      <literal>new</literal>.
    </para>
    <programlisting language="ocaml">
# let p = new point;;
val p : point = &lt;obj&gt;
# p#get;;
- : int = 0
# p#set 5;;
- : unit = ()
# p#get;;
- : int = 5
</programlisting>
    <para>
      Inheritance uses an existing class to define a new one. For
      example, the following class definition supports an addition
      method <literal>moveby</literal> that moves the point by a
      relative amount.
    </para>
    <programlisting language="ocaml">
# class movable_point =
  object (self : 'self)
    inherit point
    method moveby dx = self#set (self#get + dx)
  end;;
class movable_point :
  object
    val mutable x : int
    method get : int
    method moveby : int -&gt; unit
    method set : int -&gt; unit
  end
</programlisting>
    <para>
      This new <literal>movable_point</literal> class also makes use of
      the <literal>(self : 'self)</literal> binding after the
      <literal>object</literal> keyword. The variable
      <literal>self</literal> stands for the current object, allowing
      self-invocation, and the type variable <literal>'self</literal>
      stands for the type of the current object (which in general is a
      subtype of <literal>movable_point</literal>).
    </para>
  </sect1>
  <sect1 id="an-example-cryptokit">
    <title>An Example: Cryptokit</title>
    <para>
      Let's take a break from describing the object system with a more
      practical example that uses the OCaml cryptographic library.
    </para>
    <note>
    <title>
    Installing the Cryptokit library
    </title>
    <para>
      The Cryptokit library can be installed via OPAM via
      <literal>opam install cryptokit</literal>. Once that's finished
      compiling and installing, you just need to
      <literal>#require &quot;cryptokit&quot;</literal> in your toplevel
      to load the library and make the modules available.
    </para>
    </note>
    <para>
      Our first example mimics the <literal>md5</literal> command, which
      reads in an input file and returns a hexadecimal representation of
      its MD5 cryptographic hash. Cryptokit defines a number of
      different functions and collects them together under the
      <literal>Cryptokit.hash</literal> class type:
    </para>
    <programlisting language="ocaml">
class type hash = object
  method add_byte : int -&gt; unit
  method add_char : char -&gt; unit
  method add_string : string -&gt; unit
  method add_substring : string -&gt; int -&gt; int -&gt; unit
  method hash_size : int
  method result : string
  method wipe : unit
end

val hash_string : hash -&gt; string -&gt; string
</programlisting>
    <para>
      Concrete hash objects can be instantiated from various sub-modules
      in Cryptokit. The simplest ones such as MD5 or SHA1 do not take
      any special input parameters to build the object. The
      <literal>hmac_sha1</literal> takes a string key to initialise the
      Message Authenticate Code for that particular hash function.
    </para>
    <programlisting language="ocaml">
# Cryptokit.Hash.md5;;
- : unit -&gt; Cryptokit.hash = &lt;fun&gt;
# Cryptokit.Hash.sha1;;
- : unit -&gt; Cryptokit.hash = &lt;fun&gt;
# Cryptokit.MAC.hmac_sha1;;
- : string -&gt; Cryptokit.hash = &lt;fun&gt;
</programlisting>
    <para>
      Hash objects hold state and are thus naturally imperative. Once
      instantiated, data is fed into them by the addition functions, the
      <literal>result</literal> is computed and finally the contents
      erased via <literal>wipe</literal>. The
      <literal>hash_string</literal> convenience function applies the
      hash function fully to a string, and returns the result. The
      <literal>md5</literal> command is quite straight-forward now:
    </para>
    <programlisting language="ocaml">
open Core.Std
open Cryptokit

let _ =
  In_channel.(input_all stdin)
  |&gt; hash_string (Hash.md5 ())
  |&gt; transform_string (Hexa.encode ())
  |&gt; print_endline
</programlisting>
    <para>
      After opening the right modules, we read in the entire standard
      input into an OCaml string. This is then passed onto the MD5 hash
      function, which returns a binary string. This binary is passed
      through the <literal>Hexa</literal> hexadecimal encoder, which
      returns an ASCII representation of the input. The output of this
      command will be the same as the <literal>md5</literal> command (or
      <literal>md5sum</literal> in some systems).
    </para>
    <para>
      We can extend this simple example by selecting either the
      <literal>md5</literal> or <literal>sha1</literal> hash function at
      runtime depending on the name of our binary.
      <literal>Sys.argv</literal> is an array containing the arguments
      the command was invoked with, and the first entry is the name of
      the binary itself.
    </para>
    <programlisting language="ocaml">
open Core.Std
open Cryptokit

let _ =
  let hash_fn =
    match Filename.basename Sys.argv.(0) with
    |&quot;md5&quot; -&gt; Hash.md5 ()
    |&quot;sha1&quot; -&gt; Hash.sha1 ()
    |_ -&gt; Hash.md5 ()
  in
  In_channel.(input_all stdin)
  |&gt; hash_string hash_fn
  |&gt; transform_string (Hexa.encode ())
  |&gt; print_endline
</programlisting>
    <para>
      Now let's try something more advanced. The
      <literal>openssl</literal> library is installed on most systems,
      and can be used to encrypt plaintext using several encryption
      strategies. At its simplest, it will take a secret phrase and
      derive an appropriate key and initialisation vector.
    </para>
    <programlisting>
$ openssl enc -nosalt -aes-128-cbc -base64 -k &quot;ocaml&quot; -P
key=6217C07FF169F6AB2EB2731F855095F1
iv =8164D5477E66E6A9EC99A8D58ACAADAF
</programlisting>
    <para>
      We've selected the <literal>-nosalt</literal> option here to make
      the output deterministic, and the <literal>-P</literal> option
      prints out the derived key and IV and exits. The algorithm used to
      derive these results is described in the
      <literal>man EVP_BytesToKey</literal> manual page (you may need to
      install the OpenSSL documentation packages on your system first).
      We can implement this derivation function using an imperative
      style:
    </para>
    <programlisting language="ocaml">
let md5 s = hash_string (Hash.md5 ()) s

let evp_byte_to_key password tlen =
  let o = Hexa.encode () in
  let v = ref (md5 password) in
  o#put_string !v;
  while o#available_output/2 &lt; tlen do
    let n = md5 (!v ^ password) in
    o#put_string n;
    v := n;
  done;
  String.uppercase o#get_string

let _ =
  let secret = &quot;ocaml&quot; in
  let key_len = 16 * 2 in
  let iv_len = 16 * 2 in
  let x = evp_byte_to_key secret (key_len+iv_len) in
  let key = String.sub x ~pos:0 ~len:key_len in
  let iv = String.sub x ~pos:key_len ~len:iv_len in
  Printf.printf &quot;key=%s\niv =%s\n%!&quot; key iv
</programlisting>
    <para>
      The derivation algorithm takes an input password and desired total
      length (the addition of the key and IV length). It initialises a
      <literal>Hexa.encode</literal> transformer, which will accept
      arbitrary binary data and output a hexadecimal string (with two
      output bytes per input byte). A reference stores the last digest
      that's been calculated, and then the algorithm iterates until it
      has sufficient data to satisfy the required key length.
    </para>
    <para>
      Notice how the encoder object is used as an accumulator, by using
      the <literal>put_string</literal> and
      <literal>available_output</literal> to keep track of progress.
      Objects don't <emphasis>require</emphasis> an imperative style
      though, and the same algorithm can be written more functionally:
    </para>
    <programlisting language="ocaml">
let evp_byte_to_key password tlen =
  let rec aux acc v =
    match String.length acc &lt; tlen with
    | true -&gt;
      let v = md5 (v ^ password) in
      aux (acc^v) v
    | false -&gt; acc
  in
  let v = md5 password in
  String.uppercase (transform_string (Hexa.encode ()) (aux v v))
</programlisting>
    <para>
      In this version, we don't use any references, and instead a
      recursive function keeps track of the last digest in use and the
      accumulated result string. This version isn't quite as efficient
      as the previous one due to the careless use of string
      concatenation for the accumulator, but this can easily be fixed by
      using the <literal>Buffer</literal> module instead.
    </para>
  </sect1>
  <sect1 id="class-parameters-and-polymorphism">
    <title>Class parameters and polymorphism</title>
    <para>
      A class definition serves as the <emphasis>constructor</emphasis>
      for the class. In general, a class definition may have parameters
      that must be provided as arguments when the object is created with
      <literal>new</literal>.
    </para>
    <para>
      Let's build an example of an imperative singly-linked list using
      object-oriented techniques. First, we'll want to define a class
      for a single element of the list. We'll call it a
      <literal>node</literal>, and it will hold a value of type
      <literal>'a</literal>. When defining the class, the type
      parameters are placed in square brackets before the class name in
      the class definition. We also need a parameter
      <literal>x</literal> for the initial value.
    </para>
    <programlisting language="ocaml">
class ['a] node x =
object
  val mutable value : 'a = x
  val mutable next_node : 'a node option = None

  method get = value
  method set x = value &lt;- x

  method next = next_node
  method set_next node = next_node &lt;- node
end;;
</programlisting>
    <para>
      The <literal>value</literal> is the value stored in the node, and
      it can be retrieved and changed with the <literal>get</literal>
      and <literal>set</literal> methods. The
      <literal>next_node</literal> field is the link to the next element
      in the stack. Note that the type parameter <literal>['a]</literal>
      in the definition uses square brackets, but other uses of the type
      can omit them (or use parentheses if there is more than one type
      parameter).
    </para>
    <para>
      The type annotations on the <literal>val</literal> declarations
      are used to constrain type inference. If we omit these
      annotations, the type inferred for the class will be &quot;too
      polymorphic,&quot; <literal>x</literal> could have some type
      <literal>'b</literal> and <literal>next_node</literal> some type
      <literal>'c option</literal>.
    </para>
    <programlisting language="ocaml">
  class ['a] node x =
  object
    val mutable value = x
    val mutable next_node = None

    method get = value
    method set x = value &lt;- x

    method next = next_node
    method set_next node = next_node &lt;- node
  end;;
Error: Some type variables are unbound in this type:
         class ['a] node :
           'b -&gt;
           object
             val mutable next_node : 'c option
             val mutable value : 'b
             method get : 'b
             method next : 'c option
             method set : 'b -&gt; unit
             method set_next : 'c option -&gt; unit
           end
       The method get has type 'b where 'b is unbound
</programlisting>
    <para>
      In general, we need to provide enough constraints so that the
      compiler will infer the correct type. We can add type constraints
      to the parameters, to the fields, and to the methods. It is a
      matter of preference how many constraints to add. You can add type
      constraints in all three places, but the extra text may not help
      clarity. A convenient middle ground is to annotate the fields
      and/or class parameters, and add constraints to methods only if
      necessary.
    </para>
    <para>
      Next, we can define the list itself. We'll keep a field
      <literal>head</literal> the refers to the first element in the
      list, and <literal>last</literal> refers to the final element in
      the list. The method <literal>insert</literal> adds an element to
      the end of the list.
    </para>
    <programlisting language="ocaml">
class ['a] slist =
object
   val mutable first : ('a) node option = None
   val mutable last : ('a) node option = None

   method is_empty = first = None

   method insert x =
      let new_node = Some (new node x) in
      match last with
         Some last_node -&gt;
            last_node#set_next new_node;
            last &lt;- new_node
       | None -&gt;
            first &lt;- new_node;
            last &lt;- new_node
end;;
</programlisting>
  </sect1>
  <sect1 id="object-types">
    <title>Object types</title>
    <para>
      This definition of the class <literal>slist</literal> is not
      complete, we can construct lists, but we also need to add the
      ability to traverse the elements in the list. One common style for
      doing this is to define a class for an <literal>iterator</literal>
      object. An iterator provides a generic mechanism to inspect and
      traverse the elements of a collection. This pattern isn't
      restricted to lists, it can be used for many different kinds of
      collections.
    </para>
    <para>
      There are two common styles for defining abstract interfaces like
      this. In Java, an iterator would normally be specified with an
      interface, which specifies a set of method types. In languages
      without interfaces, like C++, the specification would normally use
      <emphasis>abstract</emphasis> classes to specify the methods
      without implementing them (C++ uses the &quot;= 0&quot; definition
      to mean &quot;not implemented&quot;).
    </para>
    <programlisting language="java">
// Java-style iterator, specified as an interface.
interface &lt;T&gt; iterator {
  T Get();
  boolean HasValue();
  void Next();
};

// Abstract class definition in C++.
template&lt;typename T&gt;
class Iterator {
 public:
  virtual ~Iterator() {}
  virtual T get() const = 0;
  virtual bool has_value() const = 0;
  virtual void next() = 0;
};
</programlisting>
    <para>
      OCaml support both styles. In fact, OCaml is more flexible than
      these approaches because an object type can be implemented by any
      object with the appropriate methods, it does not have to be
      specified by the object's class <emphasis>a priori</emphasis>.
      We'll leave abstract classes for later. Let's demonstrate the
      technique using object types.
    </para>
    <para>
      First, we'll define an object type <literal>iterator</literal>
      that specifies the methods in an iterator.
    </para>
    <programlisting language="ocaml">
type 'a iterator = &lt; get : 'a; has_value : bool; next : unit &gt;;;`
</programlisting>
    <para>
      Next, we'll define an actual iterator for the class
      <literal>slist</literal>. We can represent the position in the
      list with a field <literal>current</literal>, following links as
      we traverse the list.
    </para>
    <programlisting language="ocaml">
class ['a] slist_iterator cur =
object
  val mutable current : 'a node option = cur

  method has_value = current &lt;&gt; None

  method get =
     match current with
        Some node -&gt; node#get
      | None -&gt; raise (Invalid_argument &quot;no value&quot;)

  method next =
     match current with
        Some node -&gt; current &lt;- node#next
      | None -&gt; raise (Invalid_argument &quot;no value&quot;)
end;;
</programlisting>
    <para>
      Finally, we add a method <literal>iterator</literal> to the slist
      class to produce an iterator. To do so, we construct an
      <literal>slist_iterator</literal> that refers to the first node in
      the list, but we want to return a value with the object type
      <literal>iterator</literal>. This requires an explicit coercion
      using the <literal>:&gt;</literal> operator.
    </para>
    <programlisting language="ocaml">
class ['a] slist = object
...
   method iterator = (new slist_iterator first :&gt; 'a iterator)
end

# let l = new slist;;
# l.insert 5;;
# l.insert 4;;
# let it = l#iterator;;
# it#get;;
- : int = 5
# it#next;;
- : unit = ()
# it#get;;
- : int = 4
# it#next;;
- : unit = ()
# it#has_value;;
- : bool = false
</programlisting>
    <para>
      We may also wish to define functional-style methods,
      <literal>iter f</literal> takes a function <literal>f</literal>
      and applies it to each of the elements of the list.
    </para>
    <programlisting language="ocaml">
method iter f =
  let it = self#iterator in
  while it#has_value do
    f it#get
    it#next
  end
</programlisting>
    <para>
      What about functional operations similar to
      <literal>List.map</literal> or <literal>List.fold</literal>? In
      general, these methods take a function that produces a value of
      some other type than the elements of the set. For example, the
      function <literal>List.fold</literal> has type
      <literal>'a list -&gt; ('b -&gt; 'a -&gt; 'b) -&gt; 'b -&gt; 'b</literal>,
      where <literal>'b</literal> is an arbitrary type. To replicate
      this in the <literal>slist</literal> class, we need a method type
      <literal>('b -&gt; 'a -&gt; 'b) -&gt; 'b -&gt; 'b</literal>, where
      the method type is polymorphic over <literal>'b</literal>.
    </para>
    <para>
      The solution is to use a type quantifier, as shown in the
      following example. The method type must be specified directly
      after the method name, which means that method parameters must be
      expressed using a <literal>fun</literal> or
      <literal>function</literal> expression.
    </para>
    <programlisting language="ocaml">
method fold : 'b. ('b -&gt; 'a -&gt; 'b) -&gt; 'b -&gt; 'b =
   (fun f x -&gt;
         let y = ref x in
         let it = self#iterator in
         while it#has_value do
            y := f !y it#get;
            it#next
         done;
         !y)
</programlisting>
  </sect1>
  <sect1 id="immutable-objects">
    <title>Immutable objects</title>
    <para>
      Many people consider object-oriented programming to be
      intrinsically imperative, where an object is like a state machine.
      Sending a message to an object causes it to change state,
      possibily sending messages to other objects.
    </para>
    <para>
      Indeed, in many programs, this makes sense, but it is by no means
      required. Let's define an object-oriented version of lists similar
      to the imperative list above. We'll implement it with a regular
      list type <literal>'a list</literal>, and insertion will be to the
      beginning of the list instead of to the end.
    </para>
    <programlisting language="ocaml">
class ['a] flist =
object (self : 'self)
   val elements : 'a list = []

   method is_empty = elements = []

   method insert x : 'self = {&lt; elements = x :: elements &gt;}

   method iterator =
      (new flist_iterator elements :&gt; 'a iterator)

   method iter (f : 'a -&gt; unit) = List.iter f elements

   method fold : 'b. ('b -&gt; 'a -&gt; 'b) -&gt; 'b -&gt; 'b =
      (fun f x -&gt; List.fold_left f x elements)
end;;
</programlisting>
    <para>
      A key part of the implementation is the definition of the method
      <literal>insert</literal>. The expression
      <literal>{&lt; ... &gt;}</literal> produces a copy of the current
      object, with the same type, and the specified fields updated. In
      other words, the <literal>new_fst new_x</literal> method produces
      a copy of the object, with <literal>x</literal> replaced by
      <literal>new_x</literal>. The original object is not modified, and
      the value of <literal>y</literal> is also unaffected.
    </para>
    <para>
      There are some restriction on the use of the expression
      <literal>{&lt; ... &gt;}</literal>. It can be used only within a
      method body, and only the values of fields may be updated. Method
      implementations are fixed at the time the object is created, they
      cannot be changed dynamically.
    </para>
    <para>
      We use the same object type <literal>iterator</literal> for
      iterators, but implement it differently.
    </para>
    <programlisting language="ocaml">
class ['a] flist_iterator l =
object
   val mutable elements : 'a list = l

   method has_value = l &lt;&gt; []

   method get =
      match l with
         h :: _ -&gt; h
       | [] -&gt; raise (Invalid_argument &quot;list is empty&quot;)

   method next =
      match l with
         _ :: l -&gt; elements &lt;- l
       | [] -&gt; raise (Invalid_argument &quot;list is empty&quot;)
end;;
</programlisting>
  </sect1>
  <sect1 id="class-types">
    <title>Class types</title>
    <para>
      Once we have defined the list implementation, the next step is to
      wrap it in a module or <literal>.ml</literal> file and give it a
      type so that it can be used in the rest of our code. What is the
      type?
    </para>
    <para>
      Before we begin, let's wrap up the implementation in an explicit
      module (we'll use explicit modules for illustration, but the
      process is similar when we want to define a
      <literal>.mli</literal> file). In keeping with the usual style for
      modules, we define a type <literal>'a t</literal> to represent the
      type of list values.
    </para>
    <programlisting language="ocaml">
module SList = struct
   type 'a iterator = &lt; get : 'a; has_value : bool; next : unit &gt;
   type 'a t = &lt; is_empty : bool; insert : 'a -&gt; unit; iterator : 'a iterator &gt;

   class ['a] node x = object ... end
   class ['a] slist_iterator cur = object ... end
   class ['a] slist = object ... end

   let make () = new slist
end;;
</programlisting>
    <para>
      We have multiple choices in defining the module type, depending on
      how much of the implementation we want to expose. At one extreme,
      a maximally-abstract signature would completely hide the class
      definitions.
    </para>
    <programlisting language="ocaml">
module AbstractSList : sig
   type 'a iterator = &lt; get : 'a; has_value : bool; next : unit &gt;
   type 'a t = &lt; is_empty : bool; insert : 'a -&gt; unit; iterator : 'a iterator &gt;

   val make : unit -&gt; 'a t
end = SList
</programlisting>
    <para>
      The abstract signature is simple because we ignore the classes.
      But what if we want to include them in the signature, so that
      other modules can inherit from the class definitions? For this, we
      need to specify types for the classes, called <emphasis>class
      types</emphasis>. Class types do not appear in mainstream
      object-oriented programming languages, so you may not be familiar
      with them, but the concept is pretty simple. A class type
      specifies the type of each of the visible parts of the class,
      including both fields and methods. Just like for module types, you
      don't have to give a type for everything; anything you omit will
      be hidden.
    </para>
    <programlisting language="ocaml">
module VisibleSList : sig
  type 'a iterator = &lt; get : 'a; has_value : bool; next : unit &gt;
  type 'a t = &lt; is_empty : bool; insert : 'a -&gt; unit; iterator : 'a iterator &gt;

  class ['a] node : 'a -&gt;
  object
     method get : 'a
     method set : 'a -&gt; unit
     method next : 'a node option
     method set_next : 'a node option -&gt; unit
  end

  class ['a] slist_iterator : 'a node option -&gt;
  object
     method has_value : bool
     method get : 'a
     method next : unit
  end

  class ['a] slist :
  object
    val mutable first : 'a node option
    val mutable last : 'a node option
    method is_empty : bool
    method insert : 'a -&gt; unit
    method iterator : 'a iterator
  end

  val make : unit -&gt; 'a slist
end = SList
</programlisting>
    <para>
      In this signature, we've chosen to make nearly everything visible.
      The class type for <literal>slist</literal> specifies the types of
      the fields <literal>first</literal> and <literal>last</literal>,
      as well ad the types of each of the methods. We've also included a
      class type for <literal>slist_iterator</literal>, which is of
      somewhat more questionable value, since the type doesn't appear in
      the type for <literal>slist</literal> at all.
    </para>
    <para>
      One more thing, in this example the function
      <literal>make</literal> has type
      <literal>unit -&gt; 'a slist</literal>. But wait, we've stressed
      <emphasis>classes are not types</emphasis>, so what's up with
      that? In fact, what we've said is entirely true, classes and class
      names <emphasis>are not</emphasis> types. However, class names can
      be used to stand for types. When the compiler sees a class name in
      type position, it automatically constructs an object type from it
      by erasing all the fields and keeping only the method types. In
      this case, the type expression <literal>'a slist</literal> is
      exactly equivalent to <literal>'a t</literal>.
    </para>
  </sect1>
</chapter></part><part label="II"><title>Practical Examples</title><chapter id="data-serialization-with-s-expressions">
  <title>Data Serialization with S-Expressions</title>
  <para>
    Data serialization, <emphasis>i.e.</emphasis> converting data to and
    from a sequence of bytes that's suitable for writing to disk or
    sending across the network, is an important and common programming
    task. Sometimes you need to match someone else's data format (such
    as XML), sometimes you need a highly efficient format, and sometimes
    you just want something that is easy for humans to read and edit. To
    this end, OCaml comes with several techniques for data serialization
    depending on what your problem is.
  </para>
  <para>
    We'll start by considering the question of how to serialize data in
    a human-readable and editable form when you're not constrained to
    using a particular third-party format. Core's solution to this
    problem is to use s-expressions
  </para>
  <para>
    S-expressions are nested paranthetical expressions whose atomic
    values are strings. They were first popularized by the Lisp
    programming language in the 1960s, and have remained one of the
    simplest and most effective ways to encode structured data. An
    example s-expression might look like this:
  </para>
  <programlisting language="scheme">
(this (is an) (s expression))
</programlisting>
  <para>
    The OCaml type of an s-expression is quite simple:
  </para>
  <programlisting language="ocaml">
module Sexp : sig
  type t = Atom of string | List of t list
end
</programlisting>
  <para>
    An s-expression can be thought of as a tree where each node contains
    a list of its children, and where the leaves of the tree are
    strings.
  </para>
  <para>
    The <literal>Sexp</literal> module in Core comes with functionality
    for parsing and printing s-expressions.
  </para>
  <programlisting language="ocaml">
# let sexp =
    let a x = Sexp.Atom x and l x = Sexp.List x in
    l [a &quot;this&quot;;l [a &quot;is&quot;; a &quot;an&quot;]; l [a &quot;s&quot;; a &quot;expression&quot;]];;
val sexp : Sexp.t = (this (is an) (s expression))
</programlisting>
  <para>
    In addition, most of the base types in Core support conversion to
    and from s-expressions. For example, we can write:
  </para>
  <programlisting language="ocaml">
# Int.sexp_of_t 3;;
- : Sexp.t = 3
# List.sexp_of_t;;
- : ('a -&gt; Sexp.t) -&gt; 'a List.t -&gt; Sexp.t = &lt;fun&gt;
# List.sexp_of_t Int.sexp_of_t [1;2;3];;
- : Sexp.t = (1 2 3)
</programlisting>
  <para>
    Notice that <literal>List.sexp_of_t</literal> is polymorphic, and
    takes as its first argument another conversion function to handle
    the elements of the list to be converted. Core uses this scheme more
    generally for defining sexp-converters for polymorphic types.
  </para>
  <para>
    But what if you want a function to convert some brand new type to an
    s-expression? You can of course write it yourself manually:
  </para>
  <programlisting language="ocaml">
# type t = { foo: int; bar: float };;
# let sexp_of_t t =
    let a x = Sexp.Atom x and l x = Sexp.List x in
    l [ l [a &quot;foo&quot;; Int.sexp_of_t t.foo  ];
        l [a &quot;bar&quot;; Float.sexp_of_t t.bar]; ]
  ;;
val sexp_of_t : t -&gt; Core.Std.Sexp.t = &lt;fun&gt;
# sexp_of_t { foo = 3; bar = -5.5 };;
- : Core.Std.Sexp.t = ((foo 3) (bar -5.5))
</programlisting>
  <para>
    This is somewhat tiresome to write, and it gets more so when you
    consider the parser, <emphasis>i.e.</emphasis>,
    <literal>t_of_sexp</literal>, which is considerably more complex.
    Writing this kind of parsing and printing code by hand is mechanical
    and error prone, not to mention a drag.
  </para>
  <para>
    Given how mechanical the code is, you could imagine writing a
    program that inspected the type definition and auto-generated the
    conversion code for you. As it turns out, we can do just that using
    <literal>Sexplib</literal>. The <literal>Sexplib</literal> package,
    which is included with Core, provides both a library for
    manipulating s-exressions and a syntax extension for generating such
    conversion functions. With that synax extension enabled, any type
    that has <literal>with sexp</literal> as an annotation will trigger
    the generation of the functions we want for free.
  </para>
  <programlisting language="ocaml">
# type t = { foo: int; bar: float } with sexp;;
type t = { foo : int; bar : float; }
val t_of_sexp__ : Sexplib.Sexp.t -&gt; t = &lt;fun&gt;
val t_of_sexp : Sexplib.Sexp.t -&gt; t = &lt;fun&gt;
val sexp_of_t : t -&gt; Sexplib.Sexp.t = &lt;fun&gt;
# t_of_sexp (Sexp.of_string &quot;((bar 35) (foo 3))&quot;);;
- : t = {foo = 3; bar = 35.}
</programlisting>
  <para>
    The <literal>with sexp</literal> is detected by a
    <literal>Sexplib</literal> syntax extension and replaced with the
    extra conversion functions you see above. You can ignore
    <literal>t_of_sexp__</literal>, which is a helper function that is
    needed in very rare cases.
  </para>
  <para>
    The syntax extensions in Core almost all have this same basic
    structure: they auto-generate code based on type definitions,
    implementing functionality that you could in theory have implemented
    by hand, but with far less programmer effort.
  </para>
  <note>
  <title>
  The <literal>camlp4</literal> preprocessor and
  <literal>type_conv</literal>
  </title>
  <para>
    OCaml doesn't directly support converting static type definitions to
    and from other data formats. Instead, it supplies a powerful syntax
    extension mechanism known as <literal>camlp4</literal>. This lets
    you extend the grammar of the language to mark types as requiring
    special action, and then mechanically generate boilerplate code over
    those types (such as converting to and from other data formats).
  </para>
  <para>
    Many of the examples in the subsequent chapters depend on
    <literal>camlp4</literal>, but the examples all invoke it
    automatically for you via the <literal>-pp</literal> flag to the
    OCaml compiler. If you're interested in building your own
    generators, investigate the <literal>type_conv</literal> library
    which provides the basic extension mechanism used by the rest of
    this chapter.
  </para>
  </note>
  <sect1 id="the-sexp-format">
    <title>The Sexp format</title>
    <para>
      The textual representation of s-expressions is pretty
      straightforward. An s-expression is written down as a nested
      parenthetical expression, with whitespace-separated strings as the
      atoms. Quotes are used for atoms that contain parenthesis or
      spaces themselves; backslash is the escape character; and
      semicolons are used to introduce single-line comments. Thus, the
      following file, <literal>example.scm</literal>:
    </para>
    <programlisting language="scheme">
;; example.scm

((foo 3.3) ;; This is a comment
 (bar &quot;this is () an \&quot; atom&quot;))
</programlisting>
    <para>
      can be loaded using sexplib. As you can see, the commented data is
      not part of the resulting s-expression.
    </para>
    <programlisting language="ocaml">
# Sexp.load_sexp &quot;example.scm&quot;;;
- : Core.Std.Sexp.t = ((foo 3.3) (bar &quot;this is () an \&quot; atom&quot;))
</programlisting>
    <para>
      All in, the s-expression format actually supports three comment
      syntaxes:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          <literal>;</literal>, which comments out everything to the end
          of a line
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>#|</literal> and <literal>|#</literal>, which are
          delimiters for commenting out a block
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>#;</literal>, which comments out the first complete
          s-expression that follows.
        </para>
      </listitem>
    </itemizedlist>
    <para>
      The following example shows all of these in action.
    </para>
    <programlisting language="scheme">
;; comment_heavy_example.scm
((this is included)
 ; (this is commented out
 (this stays)
 #; (all of this is commented
     out (even though it crosses lines.))
  (and #| block delimiters #| which can be nested #|
     will comment out
    an arbitrary multi-line block))) |#
   now we're done
   ))
</programlisting>
    <para>
      Again, loading the file as an s-expression drops the comments.
    </para>
    <programlisting language="ocaml">
# Sexp.load_sexp &quot;comment_heavy_example.scm&quot;;;
- : Core.Std.Sexp.t = ((this is included) (this stays) (and now we're done))
</programlisting>
    <para>
      Note that the comments were dropped from the file upon reading.
      This is expected, since there's no place in the
      <literal>Sexp.t</literal> type to store comments.
    </para>
    <para>
      If we introduce an error into our s-expression, by, say, deleting
      the open-paren in front of <literal>bar</literal>, we'll get a
      parse error:
    </para>
    <programlisting language="ocaml">
# Exn.handle_uncaught ~exit:false (fun () -&gt;
    ignore (Sexp.load_sexp &quot;example.scm&quot;));;
  Uncaught exception:

  (Sexplib.Sexp.Parse_error
   ((location parse) (err_msg &quot;unexpected character: ')'&quot;) (text_line 4)
    (text_char 29) (global_offset 94) (buf_pos 94)))
</programlisting>
    <para>
      In the above, we use <literal>Exn.handle_uncaught</literal> to
      make sure that the exception gets printed out in full detail. You
      should generally wrap every Core program in this handler to get
      good error messages for any unexpected exceptions.
    </para>
  </sect1>
  <sect1 id="sexp-converters">
    <title>Sexp converters</title>
    <para>
      The most important functionality provided by Sexplib is the
      auto-generation of converters for new types. We've seen a bit of
      how this works already, but let's walk through a complete example.
      Here's the source for the beginning of a library for representing
      integer intervals.
    </para>
    <programlisting language="ocaml">
(* file: int_interval.ml *)
(* Module for representing closed integer intervals *)

open Core.Std

(* Invariant: For any Range (x,y), y &gt;= x *)
type t = | Range of int * int
         | Empty
with sexp

let is_empty = function Empty -&gt; true | Range _ -&gt; false
let create x y = if x &gt; y then Empty else Range (x,y)
let contains i x = match i with
   | Empty -&gt; false
   | Range (low,high) -&gt; x &gt;= low &amp;&amp; x &lt;= high
</programlisting>
    <para>
      We can now use this module as follows:
    </para>
    <programlisting language="ocaml">
(* file: test_interval.ml *)

open Core.Std

let intervals =
  let module I = Int_interval in
  [ I.create 3 4;
    I.create 5 4; (* should be empty *)
    I.create 2 3;
    I.create 1 6;
  ]

let () =
  intervals
  |&gt; List.sexp_of_t Int_interval.sexp_of_t
  |&gt; Sexp.to_string_hum
  |&gt; print_endline
</programlisting>
    <para>
      But we're still missing something: we haven't created an
      <literal>mli</literal> signature for
      <literal>Int_interval</literal> yet. Note that we need to
      explicitly export the s-expression converters that were created
      within the ml. If we don't:
    </para>
    <programlisting language="ocaml">
(* file: int_interval.mli *)
(* Module for representing closed integer intervals *)

type t

val is_empty : t -&gt; bool
val create : int -&gt; int -&gt; t
val contains : t -&gt; int -&gt; bool
</programlisting>
    <para>
      then we'll get the following error:
    </para>
    <programlisting>
File &quot;test_interval.ml&quot;, line 15, characters 20-42:
Error: Unbound value Int_interval.sexp_of_t
Command exited with code 2.
</programlisting>
    <para>
      We could export the types by hand in the signature:
    </para>
    <programlisting language="ocaml">
type t
val sexp_of_t : Sexp.t -&gt; t
val t_of_sexp : t -&gt; Sexp.t
</programlisting>
    <para>
      But Sexplib has a shorthand for this as well, so that we can just
      use the same <literal>with</literal> shorthand in the
      <literal>mli</literal>.
    </para>
    <programlisting language="ocaml">
type t with sexp
</programlisting>
    <para>
      at which point <literal>test_interval.ml</literal> will compile
      again, and if we run it, we'll get the following output:
    </para>
    <programlisting>
$ ./test_interval.native
((Range 3 4) Empty (Range 2 3) (Range 1 6))
</programlisting>
    <sect2 id="preserving-invariants">
      <title>Preserving invariants</title>
      <para>
        One easy mistake to make when dealing with sexp converters is to
        ignore the fact that those converters can violate the invariants
        of your code. For example, the <literal>Int_interval</literal>
        module depends for the correctness of the
        <literal>is_empty</literal> check on the fact that for any value
        <literal>Range (x,y)</literal>, <literal>y</literal> is greater
        than or equal to <literal>x</literal>. The
        <literal>create</literal> function preserves this invariant, but
        the <literal>t_of_sexp</literal> function does not.
      </para>
      <para>
        We can fix this problem by overriding the autogenerated function
        and writing a custom sexp-converter that is based on the
        auto-generated converter.
      </para>
      <programlisting language="ocaml">
type t = | Range of int * int
         | Empty
with sexp

let create x y = if x &gt; y then Empty else Range (x,y)

let t_of_sexp sexp =
  let t = t_of_sexp sexp in
  begin match t with
  | Empty -&gt; ()
  | Range (x,y) -&gt;
    if y &lt; x then of_sexp_error &quot;Upper and lower bound of Range swapped&quot; sexp
  end;
  t
</programlisting>
      <para>
        This trick of overriding an existing function definition with a
        new one is perfectly acceptable in OCaml. Function definitions
        are only recursive if the <literal>rec</literal> keyword is
        specified, and so in this case the inner
        <literal>t_of_sexp</literal> call will go to the earlier
        auto-generated definition that resulted from the
        <literal>type t with sexp</literal> definition.
      </para>
      <para>
        We call the function <literal>of_sexp_error</literal> to raise
        an exception because that improves the error reporting that
        Sexplib can provide when a conversion fails.
      </para>
    </sect2>
    <sect2 id="getting-good-error-messages">
      <title>Getting good error messages</title>
      <para>
        There are two steps to deserializing a type from an
        s-expression: first, converting the bytes in a file to an
        s-expression, and the second, converting that s-expression into
        the type in question. One problem with this is that it can be
        hard to localize errors to the right place using this scheme.
        Consider the following example:
      </para>
      <programlisting language="ocaml">
(* file: read_foo.ml *)

open Core.Std

type t = { a: string; b: int; c: float option } with sexp

let run () =
  let t =
    Sexp.load_sexp &quot;example.scm&quot;
    |&gt; t_of_sexp
  in
  printf &quot;b is: %d\n%!&quot; t.b

let () =
  Exn.handle_uncaught ~exit:true run
</programlisting>
      <para>
        If you were to run this on a malformatted file, say, this one:
      </para>
      <programlisting>
;; example.scm
((a not-an-integer)
 (b not-an-integer)
 (c ()))
</programlisting>
      <para>
        you'll get the following error:
      </para>
      <programlisting>
read_foo $ ./read_foo.native
Uncaught exception:

  (Sexplib.Conv.Of_sexp_error
   (Failure &quot;int_of_sexp: (Failure int_of_string)&quot;) not-an-integer)
</programlisting>
      <para>
        If all you have is the error message and the string, it's not
        terribly informative. In particular, you know that the parsing
        error-ed out on the atom &quot;not-an-integer&quot;, but you
        don't know which one! In a large file, this kind of bad error
        message can be pure misery.
      </para>
      <para>
        But there's hope! If we make small change to the
        <literal>run</literal> function as follows:
      </para>
      <programlisting language="ocaml">
let run () =
  let t = Sexp.load_sexp_conv_exn &quot;example.scm&quot; t_of_sexp in
  printf &quot;b is: %d\n%!&quot; t.b
</programlisting>
      <para>
        and run it again, we'll get the following much more helpful
        error message:
      </para>
      <programlisting language="ocaml">
read_foo $ ./read_foo.native
Uncaught exception:

  (Sexplib.Conv.Of_sexp_error
   (Sexplib.Sexp.Annotated.Conv_exn example.scm:3:4
    (Failure &quot;int_of_sexp: (Failure int_of_string)&quot;))
   not-an-integer)
</programlisting>
      <para>
        In the above error, &quot;example.scm:3:4&quot; tells us that
        the error occurred on &quot;example.scm&quot;, line 3, character
        4, which is a much better start for figuring out what has gone
        wrong.
      </para>
    </sect2>
  </sect1>
  <sect1 id="sexp-conversion-directives">
    <title>Sexp-conversion directives</title>
    <para>
      Sexplib supports a collection of directives for modifying the
      default behavior of the auto-generated sexp-converters. These
      directives allow you to customize the way in which types are
      represented as s-expressions without having to write a custom
      parser.
    </para>
    <sect2 id="sexp_opaque">
      <title><literal>sexp_opaque</literal></title>
      <para>
        The most commonly used directive is
        <literal>sexp_opaque</literal>, whose purpose is to mark a given
        component of a type as being unconvertible. Anything marked with
        <literal>sexp_opaque</literal> will be presented as the atom
        <literal>&lt;opaque&gt;</literal> by the to-sexp converter, and
        will trigger an exception from the from-sexp converter. Note
        that the type of a component marked as opaque doesn't need to
        have a sexp-converter defined. Here, if we define a type without
        a sexp-converter, and then try to use it another type with a
        sexp-converter, we'll error out:
      </para>
      <programlisting language="ocaml">
# type no_converter = int * int;;
type no_converter = int * int
# type t = { a: no_converter; b: string } with sexp;;
Characters 14-26:
  type t = { a: no_converter; b: string } with sexp;;
                ^^^^^^^^^^^^
Error: Unbound value no_converter_of_sexp
</programlisting>
      <para>
        But with <literal>sexp_opaque</literal>, we won't:
      </para>
      <programlisting language="ocaml">
# type t = { a: no_converter sexp_opaque; b: string } with sexp;;
type t = { a : no_converter Core.Std.sexp_opaque; b : string; }
val t_of_sexp__ : Sexplib.Sexp.t -&gt; t = &lt;fun&gt;
val t_of_sexp : Sexplib.Sexp.t -&gt; t = &lt;fun&gt;
val sexp_of_t : t -&gt; Sexplib.Sexp.t = &lt;fun&gt;
</programlisting>
      <para>
        And if we now convert a value of this type to an s-expression,
        we'll see the contents of field <literal>a</literal> marked as
        opaque:
      </para>
      <programlisting language="ocaml">
# sexp_of_t { a = (3,4); b = &quot;foo&quot; };;
- : Sexp.t = ((a &lt;opaque&gt;) (b foo))
</programlisting>
    </sect2>
    <sect2 id="sexp_list">
      <title><literal>sexp_list</literal></title>
      <para>
        Sometimes, sexp-converters have more parentheses than one would
        ideally like. Consider, for example, the following variant type:
      </para>
      <programlisting language="ocaml">
# type compatible_versions = | Specific of string list
                             | All
  with sexp;;
# sexp_of_compatible_versions (Specific [&quot;3.12.0&quot;; &quot;3.12.1&quot;; &quot;3.13.0&quot;]);;
- : Sexp.t = (Specific (3.12.0 3.12.1 3.13.0))
</programlisting>
      <para>
        You might prefer to make the syntax a bit less parenthesis-laden
        by dropping the parentheses around the list.
        <literal>sexp_list</literal> gives us this alternate syntax:
      </para>
      <programlisting language="ocaml">
# type compatible_versions = | Specific of string sexp_list
                             | All
  with sexp;;
# sexp_of_compatible_versions (Specific [&quot;3.12.0&quot;; &quot;3.12.1&quot;; &quot;3.13.0&quot;]);;
- : Sexp.t = (Specific 3.12.0 3.12.1 3.13.0)
</programlisting>
    </sect2>
    <sect2 id="sexp_option">
      <title><literal>sexp_option</literal></title>
      <para>
        Another common directive is <literal>sexp_option</literal>,
        which is used to to make a record field optional in the
        s-expressoin. Normally, optional values are represented either
        as <literal>()</literal> for <literal>None</literal>, or as
        <literal>(x)</literal> for <literal>Some x</literal>, and a
        record field containing an option would be rendered accordingly.
        For example:
      </para>
      <programlisting language="ocaml">
# type t = { a: int option; b: string } with sexp;;
# sexp_of_t { a = None; b = &quot;hello&quot; };;
- : Sexp.t = ((a ()) (b hello))
# sexp_of_t { a = Some 3; b = &quot;hello&quot; };;
- : Sexp.t = ((a (3)) (b hello))
</programlisting>
      <para>
        But what if we want a field to be optional,
        <emphasis>i.e.</emphasis>, we want to allow it to be omitted
        from the record entirely? In that case, we can mark it with
        <literal>sexp_option</literal>:
      </para>
      <programlisting language="ocaml">
# type t = { a: int sexp_option; b: string } with sexp;;
# sexp_of_t { a = Some 3; b = &quot;hello&quot; };;
- : Sexp.t = ((a 3) (b hello))
# sexp_of_t { a = None; b = &quot;hello&quot; };;
- : Sexp.t = ((b hello))
</programlisting>
    </sect2>
    <sect2 id="specifying-defaults">
      <title>Specifying defaults</title>
      <para>
        The <literal>sexp_option</literal> declaration is really just an
        example of how one might want to deal with default values. With
        <literal>sexp_option</literal>, your type on the OCaml side is
        an option, with <literal>None</literal> representing the case
        where no value is provided. But you might want to allow other
        ways of filling in default values.
      </para>
      <para>
        Consider the following type which represents the configuration
        of a very simple web-server.
      </para>
      <programlisting language="ocaml">
# type http_server_config = {
     web_root: string;
     port: int;
     addr: string;
  } with sexp;;
</programlisting>
      <para>
        One could imagine making some of these paramters optional; in
        particular, by default, we might want the web server to bind to
        port 80, and to listen as localhost. The sexp-syntax allows this
        to do this, as follows.
      </para>
      <programlisting language="ocaml">
# type http_server_config = {
     web_root: string;
     port: int with default(80);
     addr: string with default(&quot;localhost&quot;);
  } with sexp;;
type http_server_config = { web_root : string; port : int; addr : string; }
val http_server_config_of_sexp__ : Sexplib.Sexp.t -&gt; http_server_config =
  &lt;fun&gt;
val http_server_config_of_sexp : Sexplib.Sexp.t -&gt; http_server_config = &lt;fun&gt;
val sexp_of_http_server_config : http_server_config -&gt; Sexplib.Sexp.t = &lt;fun&gt;
# http_server_config_of_sexp (Sexp.of_string &quot;((web_root /var/www/html))&quot;;;
# let cfg = http_server_config_of_sexp (Sexp.of_string &quot;((web_root /var/www/html))&quot;);;
val cfg : http_server_config =
  {web_root = &quot;/var/www/html&quot;; port = 80; addr = &quot;localhost&quot;}
</programlisting>
      <para>
        When we convert that back out to an s-expression, you'll notice
        that no data is dropped.
      </para>
      <programlisting language="ocaml">
# sexp_of_http_server_config cfg;;
- : Sexplib.Sexp.t = ((web_root /var/www/html) (port 80) (addr localhost))
</programlisting>
      <para>
        We could make the generated s-expression also drop exported
        values, by using the <literal>sexp_drop_default</literal>
        directive.
      </para>
      <programlisting language="ocaml">
# type http_server_config = {
     web_root: string;
     port: int with default(80), sexp_drop_default;
     addr: string with default(&quot;localhost&quot;), sexp_drop_default;
  } with sexp;;
type http_server_config = { web_root : string; port : int; addr : string; }
val http_server_config_of_sexp__ : Sexplib.Sexp.t -&gt; http_server_config =
  &lt;fun&gt;
val http_server_config_of_sexp : Sexplib.Sexp.t -&gt; http_server_config = &lt;fun&gt;
val sexp_of_http_server_config : http_server_config -&gt; Sexplib.Sexp.t = &lt;fun&gt;
# let cfg = http_server_config_of_sexp (Sexp.of_string &quot;((web_root /var/www/html))&quot;);;
val cfg : http_server_config =
  {web_root = &quot;/var/www/html&quot;; port = 80; addr = &quot;localhost&quot;}
# sexp_of_http_server_config cfg;;
- : Sexplib.Sexp.t = ((web_root /var/www/html))
</programlisting>
      <para>
        As you can see, the fields that are at their default values are
        simply omitted from the s-expression. On the other hand, if we
        convert a config with other values, then those values will be
        included in the s-expression.
      </para>
      <programlisting language="ocaml">
# sexp_of_http_server_config { cfg with port = 8080 };;
- : Sexplib.Sexp.t = ((web_root /var/www/html) (port 8080))
# sexp_of_http_server_config { cfg with port = 8080; addr = &quot;192.168.0.1&quot; };;
- : Sexplib.Sexp.t =
((web_root /var/www/html) (port 8080) (addr 192.168.0.1))
</programlisting>
      <para>
        This can be very useful in designing config file formats that
        are both reasonably terse and easy to generate and maintain. It
        can also be useful for backwards compatibility: if you add a new
        field to your config record, but you make that field optiona,
        then you should still be able to parse older version of your
        config.
      </para>
    </sect2>
  </sect1>
</chapter><chapter id="handling-json-data">
  <title>Handling JSON data</title>
  <para>
    Now that you've seen how to convert OCaml values into s-expressions,
    it's time to look at other useful serialization formats. We'll cover
    JSON next, as it is a very common third-party data format on the
    Internet, and much easier to parse than alternatives. This chapter
    introduces you to a couple of new techniques that glue together the
    basic ideas from Part I of the book:
  </para>
  <itemizedlist>
    <listitem>
      <para>
        Using polymorphic variants to write more portable protocols (but
        still retain the ability to extend them if needed)
      </para>
    </listitem>
    <listitem>
      <para>
        The use of <emphasis>combinators</emphasis> to compose common
        operations over data structures in a type-safe way.
      </para>
    </listitem>
    <listitem>
      <para>
        Using external tools to generate boilerplate OCaml modules and
        signatures from external specification files.
      </para>
    </listitem>
  </itemizedlist>
  <sect1 id="json-basics">
    <title>JSON Basics</title>
    <para>
      JSON is a lightweight data-interchange format often used in web
      services and browsers. It's described in
      <ulink url="http://www.ietf.org/rfc/rfc4627.txt">RFC4627</ulink>,
      and is easier to parse and generate than alternatives such as XML.
      You'll run into JSON very often when working with modern web APIs,
      so we'll cover several different ways to manipulate it in this
      chapter.
    </para>
    <para>
      JSON consists of two basic structures: an unordered collection of
      key/value pairs, and an ordered list of values. Values can be
      strings, booleans, floats, integers or null. Let's see what a JSON
      record for an example book description looks like:
    </para>
    <programlisting language="json">
{
  &quot;title&quot;: &quot;Real World OCaml&quot;,
  &quot;tags&quot; : [ &quot;functional programming&quot;, &quot;ocaml&quot;, &quot;algorithms&quot; ],
  &quot;pages&quot;: 450,
  &quot;authors&quot;: [
    { &quot;name&quot;: &quot;Jason Hickey&quot;, &quot;affiliation&quot;: &quot;Google&quot; },
    { &quot;name&quot;: &quot;Anil Madhavapeddy&quot;, &quot;affiliation&quot;: &quot;Cambridge&quot;},
    { &quot;name&quot;: &quot;Yaron Minsky&quot;, &quot;affiliation&quot;: &quot;Jane Street&quot;}
  ],
  &quot;is_online&quot;: true
}
</programlisting>
    <para>
      The outermost JSON value is usually a record (delimited by the
      curly braces) and contains an unordered set of key/value pairs.
      The keys must be strings but values can be any JSON type. In the
      example above, <literal>tags</literal> is a string list, while the
      <literal>authors</literal> field contains a list of records.
      Unlike OCaml lists, JSON lists can contain multiple different JSON
      types within a single list.
    </para>
    <para>
      This free-form nature of JSON types is both a blessing and a
      curse. It's very easy to generate JSON values, but code that
      parses them also has to handling subtle variations in how values
      are represented. For example, what if the <literal>pages</literal>
      value above is actually represented as a string value of
      <literal>&quot;450&quot;</literal> instead of an integer?
    </para>
    <para>
      Our first task is to parse the JSON into a more structured OCaml
      type so that we can use static typing more effectively. When
      manipulating JSON in Python or Ruby, you might write unit tests to
      check that you have handled unusual inputs. The OCaml model
      prefers compile-time static checking as well as unit tests. For
      example, using pattern matching can warn you if you've not checked
      that a value can be <literal>Null</literal> as well as contain an
      actual value.
    </para>
    <note>
    <title>
    Installing the Yojson library
    </title>
    <para>
      There are several JSON libraries available for OCaml. For this
      chapter, we've picked the
      <ulink url="http://mjambon.com/yojson.html"><literal>Yojson</literal></ulink>
      library by Martin Jambon. It's easiest to install via OPAM.
    </para>
    <programlisting>
$ opam install yojson
</programlisting>
    <para>
      See <xref linkend="installation"/> for installation
      instructions if you haven't already got OPAM. Once installed, you
      can open it in the <literal>utop</literal> toplevel by:
    </para>
    <programlisting>
#require &quot;yojson&quot; ;;
open Yojson ;;
</programlisting>
    </note>
    <sect2 id="parsing-json-with-yojson">
      <title>Parsing JSON with Yojson</title>
      <para>
        The JSON specification has very few data types, and the
        <literal>Yojson.Basic.json</literal> type shown below is
        sufficient to express any valid JSON structure.
      </para>
      <programlisting language="ocaml">
type json = [
  | `Assoc of (string * json) list
  | `Bool of bool
  | `Float of float
  | `Int of int
  | `List of json list
  | `Null
  | `String of string ]
</programlisting>
      <para>
        Some interesting properties should leap out at you after reading
        this definition:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            Some of the type definitions are
            <emphasis>recursive</emphasis> (that is, one of the
            algebraic data types includes a reference to the name of the
            type being defined). Fields such as <literal>Assoc</literal>
            can contain references to more JSON fields, and thus
            precisely describe the underlying JSON data structure.
          </para>
        </listitem>
        <listitem>
          <para>
            The definition specifically includes a
            <literal>Null</literal> variant for empty fields. OCaml
            doesn't allow null values by default, so this must be
            encoded like any other value.
          </para>
        </listitem>
        <listitem>
          <para>
            The differences between certain OCaml and JSON data
            structures is more obvious. For example, a JSON
            <literal>List</literal> can contain more JSON fields,
            whereas an OCaml <literal>list</literal> must contain values
            that are all the same type.
          </para>
        </listitem>
        <listitem>
          <para>
            The type definition uses polymorphic variants and not normal
            variants. This will become significant later when we extend
            it with custom extensions to the JSON format.
          </para>
        </listitem>
      </itemizedlist>
      <para>
        Let's parse the earlier JSON example into this type now. The
        first stop is the <literal>Yojson.Basic</literal> documentation,
        where we find these helpful functions:
      </para>
      <programlisting language="ocaml">
val from_string : ?buf:Bi_outbuf.t -&gt; ?fname:string -&gt; ?lnum:int -&gt; string -&gt; json
(* Read a JSON value from a string.
   [buf]   : use this buffer at will during parsing instead of
             creating a new one. 
   [fname] : data file name to be used in error messages. It does not 
             have to be a real file. 
   [lnum]  : number of the first line of input. Default is 1.

val from_channel : ?buf:Bi_outbuf.t -&gt; ?fname:string -&gt; ?lnum:int -&gt; in_channel -&gt; json
(* Read a JSON value from a channel. See [from_string] for the meaning of the
   optional arguments. *)

val from_file : ?buf:Bi_outbuf.t -&gt; ?fname:string -&gt; ?lnum:int -&gt; string -&gt; json
(* Read a JSON value from a file. See [from_string] for the meaning of the optional
   arguments. *)
</programlisting>
      <para>
        When first reading these interfaces, you can generally ignore
        the optional arguments (which have the question marks in the
        type signature), as they will be filled in with sensible values.
        In the above signature, the optional arguments offer finer
        control over the memory buffer allocation and error messages
        from parsing incorrect JSON.
      </para>
      <para>
        The type signature for these functions with the optional
        elements removed makes their purpose much clearer:
      </para>
      <programlisting language="ocaml">
val from_string : string -&gt; json
val from_file : string -&gt; json
val from_channel : in_channel -&gt; json
</programlisting>
      <para>
        The <literal>in_channel</literal> constructor is from the
        original OCaml standard library, and its use is considered
        deprecated when using the Core standard library. This leaves us
        with two ways of parsing the JSON: either from a string or from
        a file on a filesystem. The next example shows both in action,
        assuming the JSON record is stored in a file called
        <literal>book.json</literal>:
      </para>
      <programlisting language="ocaml">
open Core.Std

let () =
  (* Read JSON file into an OCaml string *)
  let buf = In_channel.read_all &quot;book.json&quot; in

  (* Use the string JSON constructor *)
  let json1 = Yojson.Basic.from_string buf in

  (* Use the file JSON constructor *)
  let json2 = Yojson.Basic.from_file &quot;book.json&quot; in

  (* Test that the two values are the same *)
  print_endline (if json1 = json2 then &quot;OK&quot; else &quot;FAIL&quot;)
  print_endline (if phys_equal json1 json2 then &quot;FAIL&quot; else &quot;OK&quot;)
</programlisting>
      <para>
        The <literal>from_file</literal> function accepts an input
        filename and takes care of opening and closing it for you. It's
        far more common to use <literal>from_string</literal> to
        construct JSON values though, since these strings come in via a
        network connection (we'll see more of this in
        <xref linkend="concurrent-programming-with-async"/>)
        or a database. Finally, the example checks that the two input
        mechanisms actually resulted in the same OCaml data structure.
      </para>
      <sidebar>
      <title>
      The difference between <literal>=</literal> and
      <literal>==</literal>, and <literal>phys_equal</literal> in Core
      </title>
      <para>
        If you come from a C/C++ background, you will probably
        reflexively use <literal>==</literal> to test two values for
        equality. In OCaml, <literal>==</literal> tests for
        <emphasis>physical</emphasis> equality, and <literal>=</literal>
        tests for <emphasis>structural</emphasis> equality.
      </para>
      <para>
        The <literal>==</literal> physical equality test will match if
        two data structures have precisely the same pointer in memory.
        Two data structures that have identical contents, but are
        constructed separately, will not match using this operator. In
        the JSON example, the <literal>json1</literal> and
        <literal>json2</literal> values are not identical and so would
        fail the physical equality test.
      </para>
      <para>
        The <literal>=</literal> structural equality operator
        recursively inspects each field in the two values and tests them
        individually for equality. In the JSON parsing example, every
        field will be traversed and checked, and they will check out as
        equal. Crucially, if your data structure is cyclical (that is, a
        value recursively points back to another field within the same
        structure), the <literal>=</literal> operator will never
        terminate, and your program will hang! In this situation, you
        must use the physical equality operator, or write a custom
        comparison function that breaks the recursion.
      </para>
      <para>
        It's quite easy to mix up the use of <literal>=</literal> and
        <literal>==</literal>, so Core disables the
        <literal>==</literal> operator and provides the more explicit
        <literal>phys_equal</literal> function instead. You'll see a
        type error if you use <literal>==</literal> anywhere:
      </para>
      <programlisting language="ocaml">
# 1 == 2;;
Error: This expression has type int but an expression was expected of type
         [ `Consider_using_phys_equal ]
# phys_equal 1 2;;
- : bool = false
</programlisting>
      <para>
        If you feel like hanging your OCaml interpreter, you can verify
        what happens with recursive values and structural equality for
        yourself:
      </para>
      <programlisting language="ocaml">
# type t1 = { foo1:int; bar1:t2 } and t2 = { foo2:int; bar2:t1 } ;;
type t1 = { foo1 : int; bar1 : t2; }
and t2 = { foo2 : int; bar2 : t1; }
# let rec v1 = { foo1=1; bar1=v2 } and v2 = { foo2=2; bar2=v1 };;
&lt;lots of text&gt;
# v1 == v1;;
- : bool = true
# phys_equal v1 v1;;
- : bool = true
# v1 = v1 ;;
&lt;press ^Z and kill the process now&gt;
</programlisting>
      </sidebar>
      <sect3 id="selecting-values-from-json-structures">
        <title>Selecting values from JSON structures</title>
        <para>
          Now that we've figured out how to parse the example JSON into
          an OCaml value, let's manipulate it from OCaml code and
          extract specific fields.
        </para>
        <programlisting language="ocaml">
open Core.Std
open Printf

let () =
  (* Read the JSON file *)
  let json = Yojson.Basic.from_file &quot;book.json&quot; in

  (* Locally open the JSON manipulation functions *)
  let open Yojson.Basic.Util in
  let title = json |&gt; member &quot;title&quot; |&gt; to_string in
  let tags = json |&gt; member &quot;tags&quot; |&gt; to_list |&gt; filter_string in
  let pages = json |&gt; member &quot;pages&quot; |&gt; to_int in
  let is_online = json |&gt; member &quot;is_online&quot; |&gt; to_bool_option in
  let is_translated = json |&gt; member &quot;is_translated&quot; |&gt; to_bool_option in
  let authors = json |&gt; member &quot;authors&quot; |&gt; to_list in
  let names = List.map authors ~f:(fun json -&gt; member &quot;name&quot; json |&gt; to_string) in

  (* Print the results of the parsing *)
  printf &quot;Title: %s (%d)\n&quot; title pages;
  printf &quot;Authors: %s\n&quot; (String.concat ~sep:&quot;, &quot; names);
  printf &quot;Tags: %s\n&quot; (String.concat ~sep:&quot;, &quot; tags);
  let string_of_bool_option =
    function
    | None -&gt; &quot;&lt;none&gt;&quot;
    | Some true -&gt; &quot;yes&quot;
    | Some false -&gt; &quot;no&quot; in
  printf &quot;Online: %s\n&quot; (string_of_bool_option is_online);
  printf &quot;Translated: %s\n&quot; (string_of_bool_option is_translated)
</programlisting>
        <para>
          This introduces the <literal>Yojson.Basic.Util</literal>
          module, which contains <emphasis>combinator</emphasis>
          functions for JSON manipulation.
        </para>
        <sidebar>
        <title>
        Functional Combinators
        </title>
        <para>
          Combinators are a design pattern that crops up quite often in
          functional programming. John Hughes defines them as &quot;a
          function which builds program fragments from program
          fragments&quot;. In a functional language, this generally
          means higher-order functions that combine other functions to
          apply useful transformations over values.
        </para>
        <para>
          You've already run across several of these in the
          <literal>List</literal> module:
        </para>
        <programlisting language="ocaml">
val map  : 'a list -&gt; f:('a -&gt; 'b)   -&gt; 'b list
val iter : 'a list -&gt; f:('a -&gt; unit) -&gt; unit
val fold : 'a list -&gt; init:'accum -&gt; f:('accum -&gt; 'a -&gt; 'accum) -&gt; 'accum
</programlisting>
        <para>
          <literal>map</literal> and <literal>fold</literal> are
          extremely common combinators that transform an input list by
          applying a function to each value of the list. The
          <literal>map</literal> combinator is simplest, with the
          resulting list being output directly. <literal>fold</literal>
          applies each value in the input list to a function that
          accumulates a single result, and returns that instead.
        </para>
        <para>
          The final example above is a more specialised combinator that
          is only useful in OCaml due to side-effects being allowed. The
          input function is applied to every value, but no result is
          supplied. Instead, the function must directly result in some
          side-effect, such as printing to the console or updating a
          reference elsewhere in the environment.
        </para>
        </sidebar>
        <para>
          <literal>Yojson</literal> provides several combinators in the
          <literal>Yojson.Basic.Util</literal> module, such as:
        </para>
        <programlisting language="ocaml">
val member : string -&gt; json -&gt; json
val index : int -&gt; json -&gt; json
val to_string : json -&gt; string
val to_int : json -&gt; int
val filter_string : json list -&gt; string list
</programlisting>
        <para>
          We'll go through each of these uses one-by-one. Core provides
          the <literal>|&gt;</literal> pipe-forward which can chain
          combinators together, and the example code uses this to select
          and convert values out of the JSON structure. Let's examine
          some of them in more detail:
        </para>
        <programlisting language="ocaml">
  let open Yojson.Basic.Util in
  let title = json |&gt; member &quot;title&quot; |&gt; to_string in
</programlisting>
        <para>
          For the <literal>title</literal> field, the
          <literal>member</literal> combinator extracts the key from the
          <literal>json</literal> value, and converts it to an OCaml
          string. An exception is raised if the JSON value is not a
          string, so the caller must be careful to
          <literal>try/with</literal> the result.
        </para>
        <programlisting language="ocaml">
  let tags = json |&gt; member &quot;tags&quot; |&gt; to_list |&gt; filter_string in
  let pages = json |&gt; member &quot;pages&quot; |&gt; to_int in
</programlisting>
        <para>
          The <literal>tags</literal> field is similar to
          <literal>title</literal>, but the field is a list of strings
          instead of a single one. Converting this to an OCaml
          <literal>string list</literal> is a two stage process: first,
          we must convert it to a list of JSON values, and then filter
          out the <literal>String</literal> values. Remember that OCaml
          lists must have a single type, so any other JSON values will
          be skipped from the output of
          <literal>filter_string</literal>.
        </para>
        <programlisting language="ocaml">
  let is_online = json |&gt; member &quot;is_online&quot; |&gt; to_bool_option in
  let is_translated = json |&gt; member &quot;is_translated&quot; |&gt; to_bool_option in
</programlisting>
        <para>
          The <literal>is_online</literal> and
          <literal>is_translated</literal> fields are optional in our
          JSON schema, so no error should be raised if they are not
          present. The OCaml type is a <literal>string option</literal>
          to reflect this, and can be extracted via
          <literal>to_bool_option</literal>. In our example JSON, only
          <literal>is_online</literal> is present and
          <literal>is_translated</literal> will be
          <literal>None</literal>.
        </para>
        <programlisting language="ocaml">
  let authors = json |&gt; member &quot;authors&quot; |&gt; to_list in
  let names = List.map authors ~f:(fun json -&gt; member &quot;name&quot; json |&gt; to_string) in
</programlisting>
        <para>
          The final use of JSON combinators is to extract the
          <literal>name</literal> fields from the
          <literal>author</literal> list. We first construct the
          <literal>author</literal> list, and then
          <literal>map</literal> it into a
          <literal>string list</literal>. Notice that the example
          explicitly binds <literal>authors</literal> to a variable
          name. It can also be written more succinctly using the
          pipe-forward operator:
        </para>
        <programlisting language="ocaml">
let names =
  json
  |&gt; member &quot;authors&quot;
  |&gt; to_list
  |&gt; List.map ~f:(fun json -&gt; member &quot;name&quot; json |&gt; to_string)
</programlisting>
        <para>
          This style of programming which omits variable names and
          chains functions together is known as &quot;point-free
          programming&quot;. It's a succinct style, but shouldn't be
          overused due to the increased difficulty of debugging
          intermediate values. If an explicit name is assigned to each
          stage of the transformations, debuggers in particular have an
          easier time making the program flow easier to represent to the
          programer.
        </para>
        <para>
          This technique of using chained parsing functions is very
          powerful in combination with the OCaml type system. Many
          errors that don't make sense at runtime (for example, mixing
          up lists and objects) will be caught statically via a type
          error.
        </para>
      </sect3>
    </sect2>
    <sect2 id="constructing-json-values">
      <title>Constructing JSON values</title>
      <para>
        Building and printing JSON values is pretty straightforward
        given the <literal>Yojson.Basic.json</literal> type. You can
        just construct values of type <literal>json</literal> and call
        the <literal>to_string</literal> function] on them. There are
        also pretty-printing functions available that lay out the output
        in a more human-readable style:
      </para>
      <programlisting language="ocaml">
# let x = `Assoc [ (&quot;key&quot;, `String &quot;value&quot;) ] ;;
val x : [&gt; `Assoc of (string * [&gt; `String of string ]) list ] =
  `Assoc [(&quot;key&quot;, `String &quot;value&quot;)]

# Yojson.Basic.pretty_to_string x ;;
- : string = &quot;{ \&quot;key\&quot;: \&quot;value\&quot; }&quot;

# Yojson.Basic.pretty_to_channel stdout x ;;
{ &quot;key&quot;: &quot;value&quot; }
- : unit = ()
</programlisting>
      <para>
        In the example above, although the type of <literal>x</literal>
        is compatible with the type <literal>json</literal>, it's not
        explicitly defined as such. The type inference engine will
        figure out a type that is based on how the value
        <literal>x</literal> is used and in this case only the
        <literal>Assoc</literal> and <literal>String</literal> variants
        are present. This &quot;partial&quot; type signature is checked
        against the bigger <literal>json</literal> type it is applied to
        the <literal>pretty_to_string</literal> function, and determined
        to be compatible.
      </para>
      <sidebar>
      <title>
      Polymorphic variants and easier type checking
      </title>
      <para>
        One difficulty you will encounter is that type errors involving
        polymorphic variants can be quite verbose if you make a mistake
        in your code. For example, suppose you build an
        <literal>Assoc</literal> and mistakenly include a single value
        instead of a list of keys:
      </para>
      <programlisting language="ocaml">
# let x = `Assoc (&quot;key&quot;, `String &quot;value&quot;);;
val x : [&gt; `Assoc of string * [&gt; `String of string ] ] =
  `Assoc (&quot;key&quot;, `String &quot;value&quot;)

# Yojson.Basic.pretty_to_string x;;
Error: This expression has type
         [&gt; `Assoc of string * [&gt; `String of string ] ]
       but an expression was expected of type Yojson.Basic.json
       Types for tag `Assoc are incompatible
</programlisting>
      <para>
        The type error above isn't <emphasis>wrong</emphasis> as such,
        but can be inconvenient to wade through for larger values. An
        easy way to narrow down this sort of type error is to add
        explicit type annotations as a compiler hint about your
        intentions:
      </para>
      <programlisting language="ocaml">
# let (x:Yojson.Basic.json) = `Assoc (&quot;key&quot;, `String &quot;value&quot;);;
Error: This expression has type 'a * 'b
       but an expression was expected of type
         (string * Yojson.Basic.json) list
</programlisting>
      <para>
        In this case, we've marked the <literal>x</literal> as being of
        type <literal>Yojson.Basic.json</literal>, and the compiler
        immediately spots that the argument to the
        <literal>Assoc</literal> variant has the incorrect type. This
        illustrates the strengths and drawbacks of using polymorphic
        variants: they make it possible to easily subtype across module
        boundaries (<literal>Basic</literal> and <literal>Safe</literal>
        in Yojson's case), but the error messages can be more confusing.
        However, a bit of careful manual type annotation is all it takes
        to make tracking down such issues much easier.
      </para>
      </sidebar>
      <sect3 id="using-non-standard-json-extensions">
        <title>Using non-standard JSON extensions</title>
        <para>
          The standard JSON types are <emphasis>really</emphasis> basic,
          and OCaml types are far more expressive. Yojson supports an
          extended JSON format for those times when you're not
          interoperating with external systems and just want a
          convenient human-readable local format. The
          <literal>Yojson.Safe.json</literal> type is a superset of the
          <literal>Basic</literal> polymorphic variant, and looks like
          this:
        </para>
        <programlisting language="ocaml">
type json = [
  | `Assoc of (string * json) list
  | `Bool of bool
  | `Float of float
  | `Floatlit of string
  | `Int of int
  | `Intlit of string
  | `List of json list
  | `Null
  | `String of string
  | `Stringlit of string
  | `Tuple of json list
  | `Variant of string * json option ]
</programlisting>
        <para>
          You should immediately be able to spot a benefit of using
          polymorphic variants here. A standard JSON type such as a
          <literal>String</literal> will type-check against both the
          <literal>Basic</literal> module and also the non-standard
          <literal>Safe</literal> module. However, if you use extension
          values such as <literal>Tuple</literal> with the
          <literal>Basic</literal> module, they will not be a valid
          sub-type and the compiler will complain.
        </para>
        <para>
          The extensions includes with Yojson include:
        </para>
        <itemizedlist>
          <listitem>
            <para>
              The <literal>lit</literal> suffix denotes that the value
              is stored as a JSON string. For example, a
              <literal>Floatlit</literal> will be stored as
              <literal>&quot;1.234&quot;</literal> instead of
              <literal>1.234</literal>.
            </para>
          </listitem>
          <listitem>
            <para>
              The <literal>Tuple</literal> type is stored as
              <literal>(&quot;abc&quot;, 123)</literal> instead of a
              list.
            </para>
          </listitem>
          <listitem>
            <para>
              The <literal>Variant</literal> type encodes OCaml variants
              more explicitly, as
              <literal>&lt;&quot;Foo&quot;&gt;</literal> or
              <literal>&lt;&quot;Bar&quot;:123&gt;</literal> for a
              variant with parameters.
            </para>
          </listitem>
        </itemizedlist>
        <para>
          The only purpose of these extensions is to make the data
          representation more expressive without having to refer to the
          original OCaml types. You can always cast a
          <literal>Safe.json</literal> to a
          <literal>Basic.json</literal> type by using the
          <literal>to_basic</literal> function as follows:
        </para>
        <programlisting language="ocaml">
val to_basic : json -&gt; Yojson.Basic.json
(** Tuples are converted to JSON arrays, Variants are converted to
    JSON strings or arrays of a string (constructor) and a json value
    (argument). Long integers are converted to JSON strings.
    Examples:

    `Tuple [ `Int 1; `Float 2.3 ]   -&gt;    `List [ `Int 1; `Float 2.3 ]
    `Variant (&quot;A&quot;, None)            -&gt;    `String &quot;A&quot;
    `Variant (&quot;B&quot;, Some x)          -&gt;    `List [ `String &quot;B&quot;, x ]
    `Intlit &quot;12345678901234567890&quot;  -&gt;    `String &quot;12345678901234567890&quot;
 *)
</programlisting>
      </sect3>
    </sect2>
    <sect2 id="automatically-mapping-json-to-ocaml-types">
      <title>Automatically mapping JSON to OCaml types</title>
      <para>
        The combinators described earlier make it fairly easy to extract
        fields from JSON records, but the process is still pretty
        manual. We'll talk about how to do larger-scale JSON parsing
        now, using a domain-specific language known as
        <ulink url="http://oss.wink.com/atdgen/">ATD</ulink>.
      </para>
      <para>
        The idea behind ATD is to specify the format of the JSON in a
        separate file, and then run a compiler
        (<literal>atdgen</literal>) that outputs OCaml code to construct
        and parse JSON values. This means that you don't need to write
        any OCaml parsing code at all, as it will all be auto-generated
        for you.
      </para>
      <para>
        Let's go straight into looking at an example of how this works,
        by using a small portion of the Github API. Github is a popular
        code hosting and sharing website that provides a JSON-based web
        <ulink url="http://developer.github.com">API</ulink>. The ATD
        code fragment below describes the Github authorization API. It
        is based on a pseudo-standard web protocol known as OAuth, and
        is used to authorized users to access Github services.
      </para>
      <programlisting language="ocaml">
type scope = [
    User &lt;json name=&quot;user&quot;&gt;
  | Public_repo &lt;json name=&quot;public_repo&quot;&gt;
  | Repo &lt;json name=&quot;repo&quot;&gt;
  | Repo_status &lt;json name=&quot;repo_status&quot;&gt;
  | Delete_repo &lt;json name=&quot;delete_repo&quot;&gt;
  | Gist &lt;json name=&quot;gist&quot;&gt;
]

type app = {
  name: string;
  url: string;
}  &lt;ocaml field_prefix=&quot;app_&quot;&gt;

type authorization_request = {
  scopes: scope list;
  note: string;
} &lt;ocaml field_prefix=&quot;auth_req_&quot;&gt;

type authorization_response = {
  scopes: scope list;
  token: string;
  app: app;
  url: string;
  id: int;
  ?note: string option;
  ?note_url: string option;
}
</programlisting>
      <para>
        ATD is (deliberately) similar to OCaml type definitions. Each
        field can include extra annotations to customise the parsing
        code for a particular backend. For example, the Github
        <literal>scope</literal> field above is defined as a variant
        type, but with the actual JSON values being defined explicitly
        (as lower-case versions).
      </para>
      <para>
        The ATD spec can be compiled to a number of OCaml targets. Let's
        run the compiler twice, to generate some OCaml type definitions,
        and a JSON serialiser.
      </para>
      <programlisting language="bash">
$ atdgen -t github.atd
$ atdgen -j github.atd
</programlisting>
      <para>
        This will generate some new files in your current directory.
        <literal>Github_t.ml</literal> and
        <literal>Github_t.mli</literal> will contain an OCaml module
        with types defines that correspond to the ATD file. It looks
        like this:
      </para>
      <programlisting language="ocaml">
type scope = [
  | `User | `Public_repo | `Repo | `Repo_status
  | `Delete_repo | `Gist
]

type app = {
  app_name (*atd name *): string;
  app_url (*atd url *): string
}

type authorization_request = {
  auth_req_scopes (*atd scopes *): scope list;
  auth_req_note (*atd note *): string
}

type authorization_response = {
  scopes: scope list;
  token: string;
  app: app;
  url: string;
  id: int;
  note: string option;
  note_url: string option
}
</programlisting>
      <para>
        There is an obvious correspondence to the ATD definition. Note
        in particular that field names in separate OCaml records cannot
        shadow each other, and so we specifically prefix every field
        with a prefix to distinguish it from other records. For example,
        <literal>&lt;ocaml field_prefix=&quot;auth_req_&quot;&gt;</literal>
        in the ATD spec prefixes every field name in the generated
        <literal>authorization_request</literal> record with
        <literal>auth_req</literal>.
      </para>
      <para>
        The <literal>Github_t</literal> module only contains the type
        definitions, while <literal>Github_j</literal> has a concrete
        serialization module to and from JSON. You can read the
        <literal>github_j.mli</literal> to see the full interface, but
        the important functions for most uses are the conversion
        functions to and from a string. For our example above, this
        looks like:
      </para>
      <programlisting language="ocaml">
val string_of_authorization_response :
  ?len:int -&gt; authorization_response -&gt; string
  (** Serialize a value of type {!authorization_response}
      into a JSON string.
      @param len specifies the initial length
                 of the buffer used internally.
                 Default: 1024. *)

val authorization_response_of_string :
  string -&gt; authorization_response
</programlisting>
      <para>
        This is pretty convenient! We've written a single ATD file, and
        all the OCaml boilerplate to convert between JSON and a strongly
        typed record has been generated for us. You can control various
        aspects of the serializer by passing flags to
        <literal>atdgen</literal>. The important ones for JSON are:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            <literal>-j-std</literal>: work in standard JSON mode, and
            never print non-standard JSON extensions.
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>-j-custom-fields FUNCTION</literal>: call a custom
            function for every unknown field encountered, instead of
            raising a parsing exception.
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>-j-defaults</literal>: force the output a JSON
            value even if the specification defines it as the default
            value for that field.
          </para>
        </listitem>
      </itemizedlist>
      <para>
        The full ATD specification is quite sophisticated (and well
        documented online at its homepage). The ATD compiler can also
        target formats other than JSON, and also outputs code for other
        languages such as Java if you need more interoperability. There
        are also several similar projects you can investigate which
        automate the code generation process:
        <ulink url="http://piqi.org">Piqi</ulink> uses the Google
        protobuf format, and
        <ulink url="http://thrift.apache.org">Thrift</ulink> supports a
        huge variety of other programming languages.
      </para>
      <para>
        We'll also return to the Github example here later in the book
        when discussing the Async networking library, and you can find
        the full ATD specification for Github in the
        <ulink url="http://github.com/avsm/ocaml-github"><literal>ocaml-github</literal></ulink>
        repository.
      </para>
    </sect2>
  </sect1>
</chapter><chapter id="command-line-parsing">
  <title>Command Line Parsing</title>
  <para>
    Many of the OCaml programs you will write will end up as binaries
    that will be run directly from a command prompt. Any non-trivial
    program invoked as a command needs a few features:
  </para>
  <itemizedlist>
    <listitem>
      <para>
        program options and file inputs need to be parsed from the
        command line arguments.
      </para>
    </listitem>
    <listitem>
      <para>
        sensible error messages have to be generated in response to
        incorrect inputs.
      </para>
    </listitem>
    <listitem>
      <para>
        help needs to be shown for all the available options.
      </para>
    </listitem>
    <listitem>
      <para>
        interactive auto-completion of commands to assist the user.
      </para>
    </listitem>
  </itemizedlist>
  <para>
    Supporting all of this functionality manually is tedious and
    error-prone. Core provides the <literal>Command</literal> library
    that lets you declare your command-line options in one data
    structure, and the library takes care of parsing, help generation
    and auto-completion. Command is simple to use for smaller
    applications, and has a sophisticated sub-command mode that groups
    related commands together (you may be familiar with this style from
    <literal>git</literal> or <literal>hg</literal>).
  </para>
  <para>
    This chapter demonstrates how to use <literal>Command</literal> to
    extend the cryptographic utility from
    <xref linkend="object-oriented-programming"/> and builds a
    simple equivalent to the <literal>md5</literal> and
    <literal>shasum</literal> utilities. It also demonstrates how
    <emphasis>functional combinators</emphasis> can be used to declare
    complex data structures in a type-safe and elegant way.
  </para>
  <sect1 id="basic-command-line-parsing">
    <title>Basic command line parsing</title>
    <para>
      We'll begin by cloning the <literal>md5</literal> binary that is
      present on most Linux distributions and Mac OS X. It reads in the
      contents of a file, applies the MD5 one-way hash function to the
      data, and outputs an ASCII hex representation of the result.
    </para>
    <programlisting language="ocaml">
open Core.Std

let do_hash file =
  let open Cryptokit in
  In_channel.read_all file
  |&gt; hash_string (Hash.md5 ())
  |&gt; transform_string (Hexa.encode ())
  |&gt; print_endline

let command =
  Command.basic
    ~summary:&quot;Generate an MD5 hash of the input data&quot;
    ~readme:(fun () -&gt; &quot;More detailed information&quot;)
    Command.Spec.(
      empty
      +&gt; anon (&quot;filename&quot; %: string)
    )
  (fun file () -&gt; do_hash file)

let () = Command.run command
</programlisting>
    <para>
      The <literal>do_hash</literal> function accepts a filename
      parameter and prints the human-readable MD5 string to the console
      standard output. We want to control the inputs to this function
      via the command-line, and this is what the subsequent
      <literal>command</literal> value declares. If you compile this
      program and run it, the help screen looks like this:
    </para>
    <programlisting>
$ ./basic.byte
Generate an MD5 hash of the input data

  basic.byte filename

More detailed information

=== flags ===

  [-build-info]  print info about this build and exit
  [-version]     print the version of this build and exit
  [-help]        print this help text and exit
                 (alias: -?)

missing anonymous argument: filename
</programlisting>
    <para>
      If we invoke the binary without any arguments, it emits a help
      screen that informs you that a required argument
      <literal>filename</literal> is missing. Supplying the argument to
      the command results in <literal>do_hash</literal> being called,
      and the MD5 output being displayed to the standard output.
    </para>
    <programlisting>
$ ./basic.byte basic.byte
59562f5e4f790d16f1b2a095cd5de844
</programlisting>
    <para>
      So how does all this work? There are three parts to defining a
      command-line interface:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          <literal>Command.Spec.t</literal> defines the steps required
          to convert a command-line into an OCaml structure.
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>Command.basic</literal> takes a callback that is
          passed parameters parsed from the command-line according to
          the <literal>spec</literal> parameter. It takes a
          <literal>summary</literal> string for a one-line description
          of the command behavior, and an optional
          <literal>readme</literal> for longer help text.
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>Command.run</literal> actually executes a command and
          its specification, and runs the callback function with the
          resulting parameters.
        </para>
      </listitem>
    </itemizedlist>
    <para>
      Most of the interesting logic lies in how the specifications are
      defined. The <literal>Command.Spec</literal> module defines
      several combinators that can be chained together to define flags
      and anonymous arguments, what types they should map to, and
      whether to take special actions (such as interactive input) if
      certain fields are encountered.
    </para>
    <programlisting language="ocaml">
Command.Spec.(
  empty
  +&gt; anon (&quot;filename&quot; %: string)
)
</programlisting>
    <para>
      We begin the specification above with an <literal>empty</literal>
      value, and then chain more parameters via the
      <literal>+&gt;</literal> combinator. Our example defines a single
      <emphasis>anonymous</emphasis> parameter via the
      <literal>anon</literal> function (that is, a standalone token from
      the command-line). Anonymous functions can be assigned a name that
      is used in help text, and an OCaml type that they are mapped to.
      The parameters specified here are all eventually passed to a
      callback function which actually invokes the program logic.
    </para>
    <programlisting language="ocaml">
(fun file () -&gt; do_hash file)
</programlisting>
    <para>
      In our example, the function takes a <literal>file</literal>
      parameter that is a <literal>string</literal>, and maps it to the
      <literal>do_hash</literal> function. You aren't just limited to
      strings though, as <literal>Command.Spec</literal> defines several
      other conversion functions that validate and parse input into
      various types:
    </para>
    <informaltable>
      <tgroup cols="3">
        <colspec align="left"/>
        <colspec align="left"/>
        <colspec align="left"/>
        <thead>
          <row>
            <entry>
              Argument type
            </entry>
            <entry>
              OCaml type
            </entry>
            <entry>
              Example
            </entry>
          </row>
        </thead>
        <tbody>
          <row>
            <entry>
              <literal>string</literal>
            </entry>
            <entry>
              <literal>string</literal>
            </entry>
            <entry>
              <literal>foo</literal>
            </entry>
          </row>
          <row>
            <entry>
              <literal>int</literal>
            </entry>
            <entry>
              <literal>int</literal>
            </entry>
            <entry>
              <literal>123</literal>
            </entry>
          </row>
          <row>
            <entry>
              <literal>float</literal>
            </entry>
            <entry>
              <literal>float</literal>
            </entry>
            <entry>
              <literal>123.01</literal>
            </entry>
          </row>
          <row>
            <entry>
              <literal>bool</literal>
            </entry>
            <entry>
              <literal>bool</literal>
            </entry>
            <entry>
              <literal>true</literal>
            </entry>
          </row>
          <row>
            <entry>
              <literal>date</literal>
            </entry>
            <entry>
              <literal>Date.t</literal>
            </entry>
            <entry>
              <literal>2013-12-25</literal>
            </entry>
          </row>
          <row>
            <entry>
              <literal>time_span</literal>
            </entry>
            <entry>
              <literal>Span.t</literal>
            </entry>
            <entry>
              <literal>5s</literal>
            </entry>
          </row>
          <row>
            <entry>
              <literal>file</literal>
            </entry>
            <entry>
              <literal>string</literal>
            </entry>
            <entry>
              <literal>/etc/passwd</literal>
            </entry>
          </row>
        </tbody>
      </tgroup>
    </informaltable>
    <para>
      Anonymous arguments don't have to be declared individually. A more
      realistic <literal>md5</literal> function might also read from the
      standard input if a filename isn't specified. We can change our
      specification with a single line to reflect this by writing:
    </para>
    <programlisting language="ocaml">
Command.Spec.(
  empty
  +&gt; anon (maybe (&quot;filename&quot; %: string))
)
</programlisting>
    <para>
      The anonymous parameter has been prefixed with a
      <literal>maybe</literal> that indicates the value is now optional.
      If you compile the example, you'll get a type error though:
    </para>
    <programlisting>
File &quot;basic_broken.ml&quot;, line 18, characters 26-30:
Error: This expression has type string option
       but an expression was expected of type string
Command exited with code 2.
</programlisting>
    <para>
      This is because the type of the callback function has changed. It
      now wants a <literal>string option</literal> instead of a
      <literal>string</literal> since the value is optional. We can
      quickly adapt our example to use the new information and read from
      standard input if no file is specified.
    </para>
    <programlisting language="ocaml">
open Core.Std

let get_file_data = function
  | None
  | Some &quot;-&quot; -&gt; In_channel.(input_all stdin)
  | Some file -&gt; In_channel.read_all file

let do_hash file =
  let open Cryptokit in
  get_file_data file
  |&gt; hash_string (Hash.md5 ())
  |&gt; transform_string (Hexa.encode ())
  |&gt; print_endline

let command =
  Command.basic
    ~summary:&quot;Generate an MD5 hash of the input data&quot;
    Command.Spec.(
      empty
      +&gt; anon (maybe (&quot;filename&quot; %: string))
    )
  (fun file () -&gt; do_hash file)

let () = Command.run command
</programlisting>
    <para>
      There are several other transformations you can do on anonymous
      arguments. We've shown you <literal>maybe</literal>, and you can
      also obtain lists of arguments or supply default values. Try
      altering the example above to take a list of files and output
      checksums for all of them, just as the <literal>md5</literal>
      command does.
    </para>
    <informaltable>
      <tgroup cols="2">
        <colspec align="left"/>
        <colspec align="left"/>
        <thead>
          <row>
            <entry>
              Anonymous argument
            </entry>
            <entry>
              OCaml type
            </entry>
          </row>
        </thead>
        <tbody>
          <row>
            <entry>
              sequence
            </entry>
            <entry>
              <literal>list</literal> of arguments
            </entry>
          </row>
          <row>
            <entry>
              maybe
            </entry>
            <entry>
              <literal>option</literal> argument
            </entry>
          </row>
          <row>
            <entry>
              maybe_with_default
            </entry>
            <entry>
              argument with a default value if argument is missing
            </entry>
          </row>
        </tbody>
      </tgroup>
    </informaltable>
  </sect1>
  <sect1 id="using-flags-to-label-the-command-line">
    <title>Using flags to label the command line</title>
    <para>
      You aren't just limited to anonymous arguments on the
      command-line, of course. Flags (such as <literal>-v</literal>) can
      be specified in the same manner as anonymous arguments. These can
      appear in any order on the command-line, or multiple times,
      depending on how they're declared. Let's add two arguments to our
      <literal>md5</literal> command that mimic the Linux version: a
      <literal>-s</literal> flag to specify the string to be hashed
      directly, and a <literal>-t</literal> self-benchmark test.
    </para>
    <programlisting language="ocaml">
Command.Spec.(
  empty
  +&gt; flag &quot;-s&quot; (optional string) ~doc:&quot;string Checksum the given string&quot;
  +&gt; flag &quot;-t&quot; no_arg ~doc:&quot; run a built-in time trial&quot;
  +&gt; anon (maybe (&quot;filename&quot; %: string))
)
</programlisting>
    <para>
      The <literal>flag</literal> command is quite similar to
      <literal>anon</literal>. The first argument is the flag name, and
      aliases can be specified via an optional argument. The
      <literal>doc</literal> string should be formatted so that the
      first word is the short name that should appear in the usage text,
      with the remainder being the full help text. Notice that the
      <literal>-t</literal> flag has no argument, and so we prepend the
      doc text with a blank space. The help text for the above fragment
      looks like this:
    </para>
    <programlisting>
$ mlmd5 -s
Generate an MD5 hash of the input data

  mlmd5 [filename]

=== flags ===

  [-s string]    Checksum the given string
  [-t run]       a built-in time trial
  [-build-info]  print info about this build and exit
  [-version]     print the version of this build and exit
  [-help]        print this help text and exit
                 (alias: -?)

missing argument for flag -s

$ mlmd5 -s &quot;ocaml rocks&quot;
5a118fe92ac3b6c7854c595ecf6419cb
</programlisting>
    <para>
      The <literal>-s</literal> flag requires a
      <literal>string</literal> argument in our specification, and the
      parser outputs an error message if it isn't supplied. Here's a
      list of some of the functions that you can wrap flags in to
      control how they are parsed:
    </para>
    <informaltable>
      <tgroup cols="2">
        <colspec align="left"/>
        <colspec align="left"/>
        <thead>
          <row>
            <entry>
              Flag function
            </entry>
            <entry>
              OCaml type
            </entry>
          </row>
        </thead>
        <tbody>
          <row>
            <entry>
              <literal>required</literal> <emphasis>arg</emphasis>
            </entry>
            <entry>
              <emphasis>arg</emphasis> and error if not present
            </entry>
          </row>
          <row>
            <entry>
              <literal>optional</literal> <emphasis>arg</emphasis>
            </entry>
            <entry>
              <emphasis>arg</emphasis> <literal>option</literal>
            </entry>
          </row>
          <row>
            <entry>
              <literal>optional_with_default</literal>
            </entry>
            <entry>
              <emphasis>arg</emphasis> with a default if not present
            </entry>
          </row>
          <row>
            <entry>
              <literal>listed</literal> <emphasis>arg</emphasis>
            </entry>
            <entry>
              <emphasis>arg</emphasis> <literal>list</literal>, flag may
              appear multiple times
            </entry>
          </row>
          <row>
            <entry>
              <literal>no_arg</literal>
            </entry>
            <entry>
              <literal>bool</literal> that is true if flag is present.
            </entry>
          </row>
        </tbody>
      </tgroup>
    </informaltable>
    <para>
      The flags affect the type of the callback function in exactly the
      same way as anonymous arguments do. The full example of our
      <literal>md5</literal> function with flags is below.
    </para>
    <programlisting language="ocaml">
open Core.Std

let get_file_data file checksum =
  match file, checksum with
  | None, Some buf -&gt; buf
  | _, Some buf -&gt; eprintf &quot;Warning: ignoring file\n&quot;; buf
  | (None|Some &quot;-&quot;), None -&gt; In_channel.(input_all stdin)
  | Some file, None -&gt; In_channel.read_all file

let do_hash file checksum =
  let open Cryptokit in
  get_file_data file checksum
  |&gt; hash_string (Hash.md5 ())
  |&gt; transform_string (Hexa.encode ())
  |&gt; print_endline

let command =
  Command.basic
    ~summary:&quot;Generate an MD5 hash of the input data&quot;
    Command.Spec.(
      empty
      +&gt; flag &quot;-s&quot; (optional string) ~doc:&quot;string Checksum the given string&quot;
      +&gt; flag &quot;-t&quot; no_arg ~doc:&quot;run a built-in time trial&quot;
      +&gt; anon (maybe (&quot;filename&quot; %: string))
    )
  (fun checksum trial file () -&gt;
    match trial with
    | true -&gt; printf &quot;Running time trial\n&quot;
    | false -&gt; do_hash file checksum)

let () = Command.run command
</programlisting>
    <para>
      Notice how the <literal>get_file_data</literal> function now
      pattern matches across the <literal>checksum</literal> flag and
      the <literal>file</literal> anonymous argument. It selects the
      flag in preference to the file argument, but emits a warning if
      there's ambiguity.
    </para>
  </sect1>
  <sect1 id="grouping-sub-commands-together">
    <title>Grouping sub-commands together</title>
    <para>
      You can get pretty far by combining flags and anonymous arguments
      to assemble complex command-line interfaces. After a while though,
      too many options can make the program very confusing for newcomers
      to your application. One way to solve this is by grouping common
      operations together and adding some hierarchy to the command-line
      interface.
    </para>
    <para>
      You'll have run across this style already when using the OPAM
      package manager (or, in the non-OCaml world, the Git or Mercurial
      commands). OPAM exposes commands in this form:
    </para>
    <programlisting>
opam config env
opam remote list -kind git
opam install --help
opam install xmlm
</programlisting>
    <para>
      The <literal>config</literal>, <literal>remote</literal> and
      <literal>install</literal> keywords form a logical grouping of
      commands, and factor out flags and arguments that are specific to
      that particular operation. It's really simple to extend your
      application to do this in Command: just swap
      <literal>Command.basic</literal> for
      <literal>Command.group</literal>:
    </para>
    <programlisting language="ocaml">
val group :
  summary:string -&gt;
  ?readme:(unit -&gt; string) -&gt;
  (string * t) list -&gt; t
</programlisting>
    <para>
      The <literal>group</literal> signature accepts a list of basic
      <literal>Command.t</literal> values and their corresponding names.
      When executed, it looks for the appropriate sub-command from the
      name list, and dispatches it to the right command handler.
    </para>
    <para>
      Let's build the beginning of a calendar toool that does a few
      operations over dates from the command line. We first define a
      command that adds days to an input date and prints the resulting
      date.
    </para>
    <programlisting language="ocaml">
open Core.Std

let add =
  Command.basic
    ~summary:&quot;Add [days] to the [base] date and print day&quot;
    Command.Spec.(
      empty
      +&gt; anon (&quot;base&quot; %: date)
      +&gt; anon (&quot;days&quot; %: int)
    )
  (fun base span () -&gt;
    Date.add_days base span
    |&gt; Date.to_string
    |&gt; print_endline
  )

let () = Command.run add
</programlisting>
    <para>
      Once we've tested this and made sure it works, we can define
      another command that takes the difference of two dates. Both of
      the commands are now grouped as sub-commands using
      <literal>Command.group</literal>.
    </para>
    <programlisting language="ocaml">
open Core.Std

let add =
  Command.basic ~summary:&quot;Add [days] to the [base] date&quot;
    Command.Spec.(
      empty
      +&gt; anon (&quot;base&quot; %: date)
      +&gt; anon (&quot;days&quot; %: int)
    )
  (fun base span () -&gt;
    Date.add_days base span
    |&gt; Date.to_string
    |&gt; print_endline
  )

let diff =
  Command.basic ~summary:&quot;Show days between [date1] and [date2]&quot;
    Command.Spec.(
      empty
      +&gt; anon (&quot;date1&quot; %: date)
      +&gt; anon (&quot;date2&quot; %: date)
    )
  (fun date1 date2 () -&gt;
    Date.diff date1 date2
    |&gt; printf &quot;%d days\n&quot;
  )

let command =
  Command.group ~summary:&quot;Manipulate dates&quot;
    [ &quot;add&quot;, add; &quot;diff&quot;, diff ]

let () = Command.run command
</programlisting>
    <para>
      And that's all you need to add sub-command support! The help page
      for our calendar now reflects the two commands we just added:
    </para>
    <programlisting>
$ cal
Manipulate dates

  cal SUBCOMMAND

=== subcommands ===

  add      Add [days] to the [base] date
  diff     Show days between [date1] and [date2]
  version  print version information
  help     explain a given subcommand (perhaps recursively)

missing subcommand for command cal
</programlisting>
    <para>
      We can invoke the two commands we just defined to verify that they
      work and see the date parsing in action.
    </para>
    <programlisting>
$ cal add 2012-12-25 40
2013-02-03
$ cal diff 2012-12-25 2012-11-01
54 days
</programlisting>
  </sect1>
  <sect1 id="advanced-control-over-parsing">
    <title>Advanced control over parsing</title>
    <para>
      The use of the spec combinators has been somewhat magic so far: we
      just build them up with the '+&gt;' combinator and things seem to
      work. As your programs get larger and more complex, you'll want to
      factor out common functionality between specifications. Some other
      times, you'll need to interrupt the parsing to perform special
      processing, such as requesting an interactive passphrase from the
      user before proceeding. We'll show you some new combinators that
      let you do this now.
    </para>
    <sidebar>
    <title>
    The types behind <literal>Command.Spec</literal>
    </title>
    <para>
      The Command module's safety relies on the specification's output
      values precisely matching the callback function which invokes the
      main program. Any mismatch here will inevitably result in a
      dynamic failure, and so Command uses some interesting type
      abstraction to guarantee they remain in sync. You don't have to
      understand this section to use the more advanced combinators, but
      it'll help you debug type errors as you use
      <literal>Command</literal> more.
    </para>
    <para>
      The type of <literal>Command.t</literal> looks deceptively simple:
    </para>
    <programlisting language="ocaml">
type ('main_in, 'main_out) t
</programlisting>
    <para>
      You can think of <literal>('a, 'b) t</literal> as a function of
      type <literal>'a -&gt; 'b</literal>, but embellished with
      information about:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          how to parse the command line
        </para>
      </listitem>
      <listitem>
        <para>
          what the command does and how to call it
        </para>
      </listitem>
      <listitem>
        <para>
          how to auto-complete a partial command line
        </para>
      </listitem>
    </itemizedlist>
    <para>
      The type of a specification transforms a
      <literal>'main_in</literal> to a <literal>'main_out</literal>
      value. For instance, a value of <literal>Spec.t</literal> might
      have type:
    </para>
    <programlisting language="ocaml">
(arg1 -&gt; ... -&gt; argN -&gt; 'r, 'r) Spec.t
</programlisting>
    <para>
      Such a value transforms a main function of type
      <literal>arg1 -&gt; ... -&gt; argN -&gt; 'r</literal> by supplying
      all the argument values, leaving a main function that returns a
      value of type <literal>'r</literal>. Let's look at some examples
      of specs, and their types:
    </para>
    <programlisting language="ocaml">
# Command.Spec.empty ;;
- : ('m, 'm) Spec.t = &lt;abstr&gt;
# Command.Spec.(empty +&gt; anon (&quot;foo&quot; %: int)) ;;
- : (int -&gt; '_a, '_a) Command.Spec.t = &lt;abstr&gt;
</programlisting>
    <para>
      The empty specification is simple as it doesn't add any parameters
      to the callback type. The second example adds an
      <literal>int</literal> anonymous parameter that is reflected in
      the inferred type. Notice that the return value of this fragment
      has been inferred to be <literal>'_a</literal> instead of the
      usual <literal>'a</literal>. The underscore denotes a
      <emphasis>weakly polymorphic type</emphasis> which cannot be
      generalized further. You should never see this in normal use, but
      you may encounter this if you define specifications in the
      top-level of a module. You can work around this so-called
      <emphasis>value restriction</emphasis> by moving the definitions
      under a <literal>let</literal> binding.
    </para>
    <para>
      One forms a command by combining a spec of type
      <literal>('main, unit) Spec.t</literal> with a main function of
      type <literal>'main</literal>. All the combinators we've shown so
      far incrementally build up the type of <literal>'main</literal>
      according to the command-line parameters it expects, so the
      resulting type of <literal>'main</literal> is something like:
    </para>
    <programlisting language="ocaml">
arg1 -&gt; ... -&gt; argN -&gt; unit
</programlisting>
    <para>
      The type of <literal>Command.basic</literal> should make more
      sense now:
    </para>
    <programlisting language="ocaml">
val basic :
  summary:string -&gt;
  ?readme:(unit -&gt; string) -&gt;
  ('main, unit -&gt; unit) Spec.t -&gt; 'main -&gt; t
</programlisting>
    <para>
      The final line is the important one. It shows that the callback
      function for a spec should consume identical arguments to the
      supplied <literal>main</literal> function, except that there is an
      additional <literal>unit</literal> argument. This final
      <literal>unit</literal> is there in case there are no command-line
      arguments, as at least one parameter is required for the callback.
      That's why you have to supply an additional <literal>()</literal>
      to the callback function in the previous examples.
    </para>
    </sidebar>
    <sect2 id="composing-specification-fragments-together">
      <title>Composing specification fragments together</title>
      <para>
        If you want to factor out common command-line operations, the
        <literal>++</literal> operator will append two specifications
        together. Let's add some dummy verbosity and debug flags to our
        calendar application to illustrate this.
      </para>
      <programlisting language="ocaml">
open Core.Std

let add ~common =
  Command.basic ~summary:&quot;Add [days] to the [base] date&quot;
    Command.Spec.(
      empty
      +&gt; anon (&quot;base&quot; %: date)
      +&gt; anon (&quot;days&quot; %: int)
      ++ common
    )
  (fun base span debug verbose () -&gt;
    Date.add_days base span
    |&gt; Date.to_string
    |&gt; print_endline
  )

let diff ~common =
  Command.basic ~summary:&quot;Show days between [date2] and [date1]&quot;
    Command.Spec.(
      empty
      +&gt; anon (&quot;date1&quot; %: date)
      +&gt; anon (&quot;date2&quot; %: date)
      ++ common
    )
  (fun date1 date2 debug verbose () -&gt;
    Date.diff date1 date2
    |&gt; printf &quot;%d days\n&quot;
  )
</programlisting>
      <para>
        The definitions of the specifications are very similar to the
        earlier example, except that they append a
        <literal>common</literal> parameter after each specification. We
        can supply these flags when defining the groups:
      </para>
      <programlisting>
let () =
  let common =
    Command.Spec.(
      empty
      +&gt; flag &quot;-d&quot; (optional_with_default false bool) ~doc:&quot; Debug mode&quot;
      +&gt; flag &quot;-v&quot; (optional_with_default false bool) ~doc:&quot; Verbose output&quot;
    )
  in
  List.map ~f:(fun (name, cmd) -&gt; (name, cmd ~common))
    [ &quot;add&quot;, add; &quot;diff&quot;, diff ]
  |&gt; Command.group ~summary:&quot;Manipulate dates&quot;
  |&gt; Command.run
</programlisting>
      <para>
        Both of these flags will now be applied and passed to all the
        callback functions. This makes code refactoring a breeze by
        using the compiler to spot places where you use commands. Just
        add a parameter to the common definition, run the compiler, and
        fix type errors until everything works again.
      </para>
      <para>
        For example, if we remove the <literal>verbose</literal> flag
        above and compile, we'll get this impressively long type error:
      </para>
      <programlisting language="ocaml">
File &quot;cal_compose_error.ml&quot;, line 39, characters 38-45:
Error: This expression has type
         (bool -&gt; unit -&gt; unit -&gt; unit, unit -&gt; unit -&gt; unit)
         Command.Spec.t =
           (bool -&gt; unit -&gt; unit -&gt; unit, unit -&gt; unit -&gt; unit)
           Command.Spec.t
       but an expression was expected of type
         (bool -&gt; unit -&gt; unit -&gt; unit, unit -&gt; unit) Command.Spec.t
           = (bool -&gt; unit -&gt; unit -&gt; unit, unit -&gt; unit) Command.Spec.t
       Type unit -&gt; unit is not compatible with type unit
</programlisting>
      <para>
        While this does look scary, the key line to scan is the last
        one, where it's telling you that you have supplied too many
        arguments in the callback function
        (<literal>unit -&gt; unit</literal> vs <literal>unit</literal>).
        If you started with a working program and made this single
        change, you typically don't even need to read the type error, as
        the filename and location information is sufficient to make the
        obvious fix.
      </para>
    </sect2>
    <sect2 id="prompting-for-interactive-input">
      <title>Prompting for interactive input</title>
      <para>
        The <literal>step</literal> combinator lets you control the
        normal course of parsing by supplying a function that maps
        callback arguments to a new set of values. For instance, let's
        suppose we want our first calendar application to prompt for the
        number of days to add if a value wasn't supplied on the
        command-line.
      </para>
      <programlisting language="ocaml">
open Core.Std

let add =
  Command.basic
    ~summary:&quot;Add [days] to the [base] date and print day&quot;
    Command.Spec.(
      step (fun m base days -&gt;
        match days with
        | Some days -&gt; m base days
        | None -&gt;
            print_endline &quot;enter days: &quot;;
            m base (read_int ())
      )
      +&gt; anon (&quot;base&quot; %: date)
      +&gt; anon (maybe (&quot;days&quot; %: int))
    )
  (fun base span () -&gt;
    Date.add_days base span
    |&gt; Date.to_string
    |&gt; print_endline
  )

let () = Command.run add
</programlisting>
      <para>
        There are two main changes from the simple example that accepts
        a date and an integer. Firstly, the <literal>days</literal>
        argument is now an optional integer instead of a required
        argument. The <literal>step</literal> combinator takes the date
        and days parameters, and interactively reads an integer if no
        day value was supplied. It then returns an
        <literal>int</literal> instead of the
        <literal>int option</literal> that was passed in.
      </para>
      <programlisting>
$ cal_add_interactive 2013-12-01
enter days:
35
2014-01-05
</programlisting>
      <para>
        Notice that the &quot;program logic&quot; in the final callback
        doesn't see any of this, and is exactly the same as in our
        original version. The <literal>step</literal> combinator has
        transformed an <literal>int option</literal> argument into an
        <literal>int</literal>. This is reflected in the type of the
        specification:
      </para>
      <programlisting language="ocaml">
# open Core.Std ;;
# open Command.Spec ;;
# step (fun m (base:Date.t) days -&gt;
  match days with
  | Some days -&gt; m base days
  | None -&gt;
     print_endline &quot;enter days: &quot;;
     m base (read_int ()));;
- : (Date.t -&gt; int -&gt; '_a, Date.t -&gt; int option -&gt; '_a) Spec.t = &lt;abstr&gt;
</programlisting>
      <para>
        The first half of the <literal>Spec.t</literal> shows that the
        callback type is <literal>Date.t -&gt; int</literal>, whereas
        the resulting value that is expected from the next specification
        in the chain is a <literal>Date.t -&gt; int option</literal>.
      </para>
    </sect2>
    <sect2 id="adding-labelled-arguments-to-callbacks">
      <title>Adding labelled arguments to callbacks</title>
      <para>
        The <literal>step</literal> chaining combinator lets you control
        the types of your callbacks very easily. This can either help
        you fit in with existing interfaces, or make things more
        explicit by adding labelled arguments.
      </para>
      <programlisting language="ocaml">
open Core.Std

let add =
  Command.basic
    ~summary:&quot;Add [days] to the [base] date and print day&quot;
    Command.Spec.(
      step (fun m base days -&gt; m ~base ~days)
      +&gt; anon (&quot;base&quot; %: date)
      +&gt; anon (&quot;days&quot; %: int)
    )
  (fun ~base ~days () -&gt;
    Date.add_days base days
    |&gt; Date.to_string
    |&gt; print_endline
  )

let () = Command.run add
</programlisting>
      <para>
        This example goes back to our non-interactive calendar addition
        program, but adds a <literal>step</literal> combinator to turn
        the normal arguments into labelled ones. This is reflected in
        the callback function below, and can help prevent errors with
        command-line arguments with similar types but different names.
      </para>
    </sect2>
  </sect1>
  <sect1 id="command-line-auto-completion-with-bash">
    <title>Command-line auto-completion with
    <literal>bash</literal></title>
    <para>
      Modern UNIX shells usually have a tab-completion feature to
      interactively help you figure out how to build a command-line.
      These work by pressing the <literal>&lt;tab&gt;</literal> key in
      the middle of typing a command, and seeing the options that pop
      up. You've probably used this most often to find the files in the
      current directory, but it can actually be extended for other parts
      of the command too.
    </para>
    <para>
      The precise mechanism for autocompletion varies depending on what
      shell you are using, but we'll assume you are using the most
      common one: <literal>bash</literal>. This is the default
      interactive shell on most Linux distributions and Mac OS X, but
      you may need to switch to it on *BSD or Windows (when using
      Cygwin). The rest of this section assumes that you're using
      <literal>bash</literal>.
    </para>
    <para>
      Bash autocompletion isn't always installed by default, so check
      your OS package manager to see if you have it available.
    </para>
    <informaltable>
      <tgroup cols="3">
        <colspec align="left"/>
        <colspec align="left"/>
        <colspec align="left"/>
        <thead>
          <row>
            <entry>
              Operating System
            </entry>
            <entry>
              Package Manager
            </entry>
            <entry>
              Package
            </entry>
          </row>
        </thead>
        <tbody>
          <row>
            <entry>
              Debian Linux
            </entry>
            <entry>
              <literal>apt</literal>
            </entry>
            <entry>
              <literal>TODO</literal>
            </entry>
          </row>
          <row>
            <entry>
              CentOS
            </entry>
            <entry>
              <literal>yum</literal>
            </entry>
            <entry>
              <literal>TODO</literal>
            </entry>
          </row>
          <row>
            <entry>
              Mac OS X
            </entry>
            <entry>
              Homebrew
            </entry>
            <entry>
              <literal>bash-completion</literal>
            </entry>
          </row>
          <row>
            <entry>
              Mac OS X
            </entry>
            <entry>
              MacPorts
            </entry>
            <entry>
              <literal>TODO</literal>
            </entry>
          </row>
          <row>
            <entry>
              FreeBSD
            </entry>
            <entry>
              Ports System
            </entry>
            <entry>
              <literal>/usr/ports TODO</literal>
            </entry>
          </row>
          <row>
            <entry>
              OpenBSD
            </entry>
            <entry>
              <literal>pkg_add</literal>
            </entry>
            <entry>
              <literal>TODO</literal>
            </entry>
          </row>
        </tbody>
      </tgroup>
    </informaltable>
    <para>
      Once you have bash completion installed and configured, check that
      it works by typing the <literal>ssh</literal> command, and
      pressing <literal>tab</literal>. This should show you the list of
      known hosts from your <literal>~/.ssh/known_hosts</literal> file.
      If it lists those, then you can continue on, but if it lists the
      files in your current directory instead, then check your OS
      documentation to configure completion correctly.
    </para>
    <para>
      One last bit of information you'll need to find is the location of
      the <literal>bash_completion.d</literal> directory. This is where
      all the shell fragments that contain the completion logic are
      held. On Linux, this is often in
      <literal>/etc/bash_completion.d</literal>, and in Homebrew on Mac
      OS X it would be
      <literal>/usr/local/etc/bash_completion.d</literal> by default.
    </para>
    <sect2 id="generating-completion-fragments-from-command">
      <title>Generating completion fragments from Command</title>
      <para>
        The Command library has a declarative description of all the
        possible valid options, and it can use this information to
        generate a shell script which provides completion support for
        that command. To generate the fragment, just run the command
        with the <literal>COMMAND_OUTPUT_INSTALLATION_BASH</literal>
        environment variable set to any value.
      </para>
      <para>
        For example, let's try it on our calendar example from earlier,
        assuming that the binary is called <literal>cal</literal> in the
        current directory:
      </para>
      <programlisting>
$ COMMAND_OUTPUT_INSTALLATION_BASH=1 ./cal

function _jsautocom_41790 {
  export COMP_CWORD
  COMP_WORDS[0]=./cal
  COMPREPLY=($(&quot;${COMP_WORDS[@]}&quot;))
}
complete -F _jsautocom_41790 ./cal
</programlisting>
    </sect2>
    <sect2 id="installing-the-completion-fragment">
      <title>Installing the completion fragment</title>
      <para>
        You don't need to worry about what this script actually does
        (unless you have an unhealthy fascination with shell scripting,
        that is). Instead, redirect the output to a file in your
        <literal>bash_completion.d</literal> directory, named after the
        command you're installing.
      </para>
      <programlisting>
$ sudo env COMMAND_OUTPUT_INSTALLATION_BASH=1 ./cal \
    &gt; /etc/bash_completion.d/cal
$ bash -l
$ ./cal &lt;tab&gt;
add      diff     help     version
</programlisting>
      <para>
        The first line above redirects the earlier output into your
        <literal>bash_completion.d</literal> directory. The
        <literal>bash -l</literal> loads the new configuration as a
        fresh login shell, and then the final line shows the four valid
        commands by pressing the tab key.
      </para>
      <para>
        Command completion support works for flags and grouped commands,
        and is very useful when building larger command-line interfaces.
      </para>
      <note>
      <title>
      Installing a generic completion handler
      </title>
      <para>
        Sadly, <literal>bash</literal> doesn't support installing a
        generic handler for all Command-based applications. This means
        that you have to install the completion script for every
        application, but you should be able to automate this in the
        build and packaging system for your application.
      </para>
      <para>
        It will help to check out how other applications that install
        tab-completion scripts and following their lead, as the details
        are very OS-specific.
      </para>
      </note>
    </sect2>
  </sect1>
</chapter><chapter id="text-processing-and-unicode">
  <title>Text Processing and Unicode</title>
  <para>
    TODO
  </para>
</chapter><chapter id="xml-streams-and-trees">
  <title>XML Streams and Trees</title>
  <para>
    XML is a markup language designed to store tree-structured data in a
    format that is (somewhat) human- and machine-readable. Like JSON, it
    is a textual format commonly used in web technologies, with a
    complete
    <ulink url="http://www.w3.org/TR/REC-xml/">specification</ulink>
    available online.
  </para>
  <para>
    We're going to explain the basics of XML manipulation here, and also
    introduce the notion of a <emphasis>visitor pattern</emphasis> to
    manipulate fragments of XML trees.
  </para>
  <note>
  <title>
  Obtaining and installing XMLM
  </title>
  <para>
    The remainder of this chapter uses the freely available XMLM
    library. It's easiest to obtain it via OPAM. See
    <xref linkend="installation"/> for installation
    instructions if you don't have OPAM.
  </para>
  <programlisting language="ocaml">
$ opam install xmlm
</programlisting>
  <para>
    Once installed, the <literal>xmlm</literal> library will be
    available in your toplevel.
  </para>
  <programlisting language="ocaml">
$ utop
# #require &quot;xmlm&quot;;;
# open Xmlm ;;
</programlisting>
  <para>
    The library documentation is also available
    <ulink url="http://erratique.ch/software/xmlm/doc/Xmlm">online</ulink>.
  </para>
  </note>
  <para>
    Since XML is such a common web format, we've taken our example
    document from the
    <ulink url="http://duckduckgo.com">DuckDuckGo</ulink> search engine.
    This is a smaller search engine than the usual suspects, but has the
    advantage of a freely available API that doesn't require you to
    register before using it. We'll talk more about how to use the live
    API later in
    <xref linkend="concurrent-programming-with-async"/>, but
    for now here's what a shortened XML search response from DuckDuckGo
    looks like:
  </para>
  <programlisting language="xml">
&lt;DuckDuckGoResponse version=&quot;1.0&quot;&gt;
&lt;Heading&gt;DuckDuckGo&lt;/Heading&gt;
&lt;AbstractText&gt;DuckDuckGo is an Internet search engine.&lt;/AbstractText&gt;
&lt;AbstractURL&gt;https://en.wikipedia.org/wiki/DuckDuckGo&lt;/AbstractURL&gt;
&lt;AbstractSource&gt;Wikipedia&lt;/AbstractSource&gt;
&lt;Results&gt;
&lt;Result&gt;
  &lt;Text&gt;Official site&lt;/Text&gt;
  &lt;FirstURL&gt;https://duckduckgo.com/&lt;/FirstURL&gt;
  &lt;/Result&gt;
&lt;/Results&gt;
&lt;RelatedTopics&gt;
 &lt;RelatedTopic&gt;
   &lt;Text&gt;Companies based in Pennsylvania&lt;/Text&gt;
   &lt;FirstURL&gt;
     http://duckduckgo.com/c/Companies_based_in_Pennsylvania
   &lt;/FirstURL&gt;
 &lt;/RelatedTopic&gt;
 &lt;RelatedTopic&gt;
   &lt;Text&gt;Internet search engines&lt;/Text&gt;
   &lt;FirstURL&gt;
     http://duckduckgo.com/c/Internet_search_engines
   &lt;/FirstURL&gt;
 &lt;/RelatedTopic&gt;
&lt;/RelatedTopics&gt;
&lt;/DuckDuckGoResponse&gt;
</programlisting>
  <para>
    The XML document is structured as a set of opening
    <literal>&lt;tag&gt;</literal> tokens that are closed by a
    corresponding end <literal>&lt;/tag&gt;</literal> token. Opening
    tags can have an optional set of key/value attributes, for example
    <literal>&lt;tag name=&quot;foo&quot; id=&quot;bar&quot;&gt;</literal>.
    A tag usually contains data that can contain further tags, thus
    forming a tree structure.
  </para>
  <para>
    These XML documents can be very large, and we don't want to have to
    read it all into memory before starting to process it. Luckily there
    exists a low-level <emphasis>streaming</emphasis> interface that
    parses an XML document incrementally. This can be cumbersome to use
    for quick tasks, so we'll build a simpler tree API on top of it.
    We'll start with the streaming API first though.
  </para>
  <sect1 id="stream-parsing-xml">
    <title>Stream parsing XML</title>
    <para>
      The XMLM documentation is a good place to read about the overall
      layout of the library. It tells us that:
    </para>
    <blockquote>
      <para>
        A well-formed sequence of <literal>signal</literal>s represents
        an XML document tree traversal in depth-first order. Input pulls
        a well-formed sequence of <literal>signal</literal>s from a data
        source and output pushes a well-formed sequence of
        <literal>signal</literal>s to a data destination. Functions are
        provided to easily transform sequences of
        <literal>signal</literal>s to/from arborescent data structures.
      </para>
    </blockquote>
    <para>
      The <literal>signal</literal> type is at the heart of all XMLM
      functions:
    </para>
    <programlisting language="ocaml">
type signal = [
  | `Data of string
  | `Dtd of dtd
  | `El_end
  | `El_start of tag
]
</programlisting>
    <para>
      XMLM parses input XML documents into an ordered sequence of these
      <literal>signal</literal> values. The first
      <literal>signal</literal> that's received is always a
      <literal>Dtd</literal>. The Document Type Description (DTD)
      optionally defines which tags are allowed within the XML document.
      Some XML parsers can validate a document against a DTD, but XMLM
      is a <emphasis>non-validating</emphasis> parser that reads the DTD
      if present but disregards its contents.
    </para>
    <para>
      The <literal>El_start</literal> and <literal>El_end</literal>
      signals indicate the opening and closing of tags, and
      <literal>Data</literal> passes the data contained between tags.
    </para>
    <programlisting>
let i = Xmlm.make_input (`Channel (open_in &quot;ddg.xml&quot;)) ;;
let o = Xmlm.make_output (`Channel stdout) ;;
</programlisting>
    <para>
      We'll begin by defining the input source and output target for the
      XML data. The <literal>make_input</literal> and
      <literal>make_output</literal> functions use a polymorphic variant
      to define how the library should use to read and write the XML.
      <literal>Channel</literal> is used above, but there are several
      others defined in the library:
    </para>
    <programlisting language="ocaml">
type source = [
  | `Channel of in_channel
  | `Fun of unit -&gt; int
  | `String of int * string
]
</programlisting>
    <para>
      Each of these sources uses a different strategy for obtaining
      input data:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          <literal>Channel</literal> uses the OCaml standard library
          channel system. When more data is required, a blocking read is
          performed on that channel.
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>String</literal> accepts the whole document as an
          OCaml <literal>string</literal>, starting from an integer
          offset and continuing until the whole document has been
          parsed.
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>Fun</literal> is more general than the others, and
          supplies a function which can be called repeatedly to obtain
          the next character. The function can do this by any means it
          chooses: for example from the network (hopefully with
          buffering so it's not reading a single character at a time),
          or from a list of strings from elsewhere in the application.
        </para>
      </listitem>
    </itemizedlist>
    <para>
      Although we use <literal>Channel</literal> in this small example,
      real applications will tend to use either
      <literal>String</literal> or <literal>Fun</literal>. This is
      because the <literal>in_channel</literal> interface is deprecated
      in Core, and shouldn't be used in new code. <emphasis>(avsm: this
      is a somewhat unsatisfying explanation: what about adding a better
      Core interface?)</emphasis>.
    </para>
    <para>
      Regardless of which input method you choose, XMLM will convert it
      into a sequence of signals. For example, take this simplified XML
      fragment from our earlier search engine example.
    </para>
    <programlisting language="xml">
&lt;DuckDuckGoResponse version=&quot;1.0&quot;&gt;
&lt;Heading&gt;DuckDuckGo&lt;/Heading&gt;
&lt;/DuckDuckGoResponse&gt;
</programlisting>
    <para>
      This will be converted into the following sequence of signals:
    </para>
    <programlisting>
Dtd _
El_start ((&quot;&quot;,&quot;DuckDuckGoResponse&quot;), [(&quot;&quot;,&quot;version&quot;), &quot;1.0&quot;]
El_start ((&quot;&quot;,&quot;Heading&quot;), [])
Data &quot;DuckDuckGo&quot;
El_end
El_end
</programlisting>
    <para>
      Only the opening <literal>El_start</literal> defines the tag name,
      and the <literal>El_end</literal> signal closes the more recently
      opened tag. The tag names are a little complicated due to the XML
      facility for namespaces; just ignore the empty component of the
      tag if you don't care about these. Now, let's define the
      <literal>xml_id</literal> function that uses the input and output
      values we defined above and parses this document.
    </para>
    <programlisting language="ocaml">
let xml_id i o =
  let rec pull depth =
    Xmlm.output o (Xmlm.peek i);
    match Xmlm.input i with
    | `El_start _ -&gt; pull i o (depth + 1)
    | `El_end -&gt; if depth &gt; 1 then pull i o (depth - 1)
    | `Data _ -&gt; pull i o depth
    | `Dtd _ -&gt; assert false
  in
  Xmlm.output o (Xmlm.input i); (* `Dtd *)
  pull 0;
  if not (Xmlm.eoi i) then invalid_arg &quot;document not well-formed&quot;
</programlisting>
    <para>
      The <literal>xml_id</literal> function begins by defining a
      recursive helper <literal>pull</literal> function. The
      <literal>pull</literal> function iterates over the
      <literal>signal</literal> values until there are none left. It
      first uses <literal>Xmlm.peek</literal> to inspect the current
      input signal and immediately outputs it. The rest of the function
      is not strictly necessary, but tracks that all of the tags that
      have been started via the <literal>El_start</literal> signal are
      also closed by a corresponding <literal>El_end</literal> signal.
    </para>
    <para>
      The <literal>pull</literal> function isn't invoked immediately.
      The first thing we need to do is consume the
      <literal>Dtd</literal> signal, which is present in every
      well-formed XML input. Then we invoke <literal>pull</literal> with
      a depth of <literal>0</literal>, and it consumes the whole
      document. The final action is to call <literal>Xmlm.eoi</literal>
      to verify that the end of input has been reached, since the
      earlier <literal>pull</literal> should have consumed all of the
      XML signals.
    </para>
  </sect1>
  <sect1 id="tree-parsing-xml">
    <title>Tree parsing XML</title>
    <para>
      Signals enforce a very iterative style of parsing XML, as your
      program has to deal with signals arriving serially. It's often
      more convenient to deal with complete XML documents directly
      in-memory as an OCaml tree data structure. We can convert a signal
      stream into an OCaml structure by defining the following data type
      and helper functions:
    </para>
    <programlisting language="ocaml">
type tree =
  | Element of Xmlm.tag * tree list
  | Data of string

let in_tree i =
  let el tag children = Element (tag, children) in
  let data d = Data d in
  Xmlm.input_doc_tree ~el ~data i

let out_tree o t =
  let frag = function
  | Element (tag, childs) -&gt; `El (tag, childs)
  | Data d -&gt; `Data d
  in
  Xmlm.output_doc_tree frag o t
</programlisting>
    <para>
      The type <literal>tree</literal> can be pattern-matched and
      traversed like a normal OCaml data structure. Let's see how this
      works by extracting all the &quot;RelatedTopics&quot; in the
      example XML document. First, we'll need a few helper combinator
      functions to filter through tags and trees, with the following
      signature:
    </para>
    <programlisting language="ocaml">
(* Extract a textual name from an XML tag.
   Discards the namespace information. *)
val name : Xmlm.tag -&gt; string

(* Given a list of [trees], concatenate all of the data contents
   into a string, and discard any sub-tags within it *)
val concat_data : tree list -&gt; string

(* Filter out the contents of a tag [n] from a tagset,
   and return the concatenated contents of all of them *)
val filter_tag : string -&gt; tree list -&gt; tree list
</programlisting>
    <para>
      Let's look at the implementation of these functions in more
      detail.
    </para>
    <programlisting language="ocaml">
let name ((_,n),_) = n
</programlisting>
    <para>
      The <literal>name</literal> function is a good example of how
      pattern-matching can make data structure manipulation very
      succinct. An <literal>Xmlm.tag</literal> consists of a tuple of
      the tag name and its attributes. The tag name is itself a tuple of
      the namespace and the local name (which is what we actually want).
      The pattern matching in the <literal>name</literal> function binds
      the local name portion of these tuples to <literal>n</literal>,
      and ignores the rest of the argument input. The function body just
      returns <literal>n</literal> to the caller.
    </para>
    <programlisting language="ocaml">
let concat_data tl =
  List.fold_left ~init:&quot;&quot; ~f:(fun acc -&gt;
    function
    |Data s -&gt; acc ^ s
    |_ -&gt; acc
  ) tl
</programlisting>
    <para>
      The <literal>concat_data</literal> function accepts a
      <literal>tree list</literal> parameter and looks for
      <literal>Data</literal> tags that it concatenates into a single
      string. All other tags are ignored and discarded (a more
      sophisticated implementation would recurse into sub-tags and
      concatenate any data within them too).
    </para>
    <programlisting language="ocaml">
let filter_tag n =
  List.fold_left ~init:[] ~f:(fun acc -&gt;
    function
    |Element (tag, ts) when name tag = n -&gt;
      ts @ acc
    |_ -&gt; acc
  )
</programlisting>
    <para>
      The <literal>filter_tag</literal> function also folds over a
      <literal>tree list</literal>, but uses a more specialised pattern
      match. It looks for an <literal>Element</literal> value, but uses
      the <literal>when</literal> clause to also check that the name of
      the tag matches the <literal>n</literal> function argument. If it
      does match then that pattern is selected, and otherwise matching
      continues to the next option (in this case, a catch-all that
      simply returns the accumulator and continues the fold). This use
      of <literal>when</literal> in pattern matching is known as a
      <emphasis>guard pattern</emphasis>.
    </para>
    <para>
      Once we have these helper functions, the selection of all the
      <literal>&lt;Text&gt;</literal> tags is a matter of chaining the
      filter functions we just defined together.
    </para>
    <programlisting language="ocaml">
let topics trees =
  filter_tag &quot;DuckDuckGoResponse&quot; trees
  |&gt; filter_tag &quot;RelatedTopics&quot;
  |&gt; filter_tag &quot;RelatedTopic&quot;
  |&gt; filter_tag &quot;Text&quot;
  |&gt; List.iter ~f:(fun x -&gt; concat_data [x] |&gt; print_endline)

let _ =
  let i = Xmlm.make_input (`Channel (open_in &quot;ddg.xml&quot;)) in
  let (_,it) = in_tree i in
  topics [it]
</programlisting>
    <para>
      The <literal>filter_tag</literal> function accepts a
      <literal>tree list</literal> and also outputs a
      <literal>tree list</literal> that contains the sub-tags that
      match. This lets us chain together the results of one filter to
      another, and hence select hierarchical XML tags very easily. When
      we get to the <literal>&lt;Text&gt;</literal> tag, we iterate over
      all the results, concatenate the data contents, and print each one
      individually.
    </para>
  </sect1>
  <sect1 id="building-xml-using-syntax-extensions">
    <title>Building XML using syntax extensions</title>
    <para>
      In the earlier JSON chapter, we explained how to construct values
      by creating the data structures directly. While this works for
      small documents, it can get really confusing with bigger
      structures. For example, look at:
    </para>
    <programlisting language="ocaml">
let mk_tag n a c = Element(((&quot;&quot;,n),a),c)
let mk_data d = Data d

let response =
  mk_tag &quot;DuckDuckGoResponse&quot; [(&quot;&quot;,&quot;version&quot;),&quot;1.0&quot;] 
    (mk_tag &quot;Heading&quot; [] [mk_data &quot;DuckDuckGo&quot;])
</programlisting>
    <para>
      This defines a couple of helper functions to construct
      <literal>Element</literal> and <literal>Data</literal> values, and
      then builds a <literal>response</literal> value. Wouldn't it be
      nice if there were a way to write the XML we want directly?
      Happily, OCaml's syntax extension mechanism comes to the rescue
      via the <emphasis>quotation</emphasis> mechanism. It lets us write
      this equivalent code:
    </para>
    <programlisting language="ocaml">
let response =
  &lt;:xml
    &lt;DuckDuckGoResponse version=&quot;1.0&quot;&gt;
      &lt;Heading&gt;DuckDuckGo&lt;/Heading&gt;
    &lt;/DuckDuckGoResponse&gt;
  &gt;&gt;
</programlisting>
    <para>
      We use the Sexplib syntax extension earlier to generate
      boilerplate code from type definitions. The quotation shown above
      is a little different: it lets the syntax of an entire block of
      code to be completely different from OCaml's usual one. Camlp4
      loads a syntax extension module that transforms the Abstract
      Syntax Tree (AST) of the code fragment (in this case,
      <literal>xml</literal>), and converts it into the desired data
      structure.
    </para>
    <para>
      We'll use the Atom 1.0 syndication format as our example. Atom
      feeds allow web-based programs such as browsers to poll a website
      for updates. The website owner publishes a feed of content in a
      standardized XML format via HTTP. This feed is then parsed by
      clients and compared against previously downloaded versions to
      determine which contents are available.
    </para>
    <para>
      Here's an example of an Atom feed:
    </para>
    <programlisting language="xml">
&lt;?xml version=&quot;1.0&quot; encoding=&quot;utf-8&quot;?&gt;
&lt;feed xmlns=&quot;http://www.w3.org/2005/Atom&quot;&gt;
 &lt;title&gt;Example Feed&lt;/title&gt;
 &lt;subtitle&gt;A subtitle.&lt;/subtitle&gt;
 &lt;link href=&quot;http://example.org/feed/&quot; rel=&quot;self&quot; /&gt;
 &lt;link href=&quot;http://example.org/&quot; /&gt;
 &lt;id&gt;urn:uuid:60a76c80-d399-11d9-b91C-0003939e0af6&lt;/id&gt;
 &lt;updated&gt;2003-12-13T18:30:02Z&lt;/updated&gt;

 &lt;entry&gt;
  &lt;title&gt;Atom-Powered Robots Run Amok&lt;/title&gt;
  &lt;link href=&quot;http://example.org/2003/12/13/atom03&quot; /&gt;
  &lt;link rel=&quot;alternate&quot; type=&quot;text/html&quot; href=&quot;http://example.org/atom03.html&quot;/&gt;
  &lt;link rel=&quot;edit&quot; href=&quot;http://example.org/atom03/edit&quot;/&gt;
  &lt;id&gt;urn:uuid:1225c695-cfb8-4ebb-aaaa-80da344efa6a&lt;/id&gt;
  &lt;updated&gt;2003-12-13T18:30:02Z&lt;/updated&gt;
  &lt;summary&gt;Some text.&lt;/summary&gt;
  &lt;author&gt;
    &lt;name&gt;John Doe&lt;/name&gt;
    &lt;email&gt;johndoe@example.com&lt;/email&gt;
  &lt;/author&gt;
 &lt;/entry&gt;
&lt;/feed&gt;
</programlisting>
    <para>
      We want to build this by minimising the amount of repetitive XML
      generation code. The &quot;Caml on the Web&quot; (COW) library
      provides the syntax extension we need.
    </para>
    <note>
    <title>
    Installing Caml on the Web (COW)
    </title>
    <para>
      The COW library and syntax extension can be installed via OPAM via
      <literal>opam install cow</literal>. There are two OCamlfind
      packages installed: the library is called <literal>cow</literal>
      and the syntax extension is activated with the
      <literal>cow.syntax</literal> package.
    </para>
    <para>
      One caveat to bear in mind is that COW isn't fully compatible with
      Core yet, and so you must use the syntax extension before opening
      the Core modules. (<emphasis>avsm</emphasis>: we can fix this
      easily, but the note is here as a warning to reviewers).
    </para>
    </note>
    <para>
      Let's start to build up an Atom specification using Cow. First,
      the <literal>&lt;author&gt;</literal> tag can be represented with
      the following type:
    </para>
    <programlisting language="ocaml">
type author = {
  name: string;
  uri: string option;
  email: string option;
} with xml
</programlisting>
    <para>
      This is a standard record type definition with the addition of
      <literal>with xml</literal> at the end. This uses a syntax
      extension to signify that we wish to generate boilerplate code for
      handling this record as an XML document.
    </para>
    <sidebar>
    <title>
    Invoking <literal>camlp4</literal> syntax extensions
    </title>
    <para>
      The OCaml compiler can call <literal>camlp4</literal>
      automatically during a compilation to preprocess the source files.
      This is specified via the <literal>-pp</literal> flag to the
      compiler. You don't normally need to specify this flag yourself.
      Use the <literal>ocamlfind</literal> utility instead to generate
      the right command-line flags for you. Here's a small shell script
      which preprocesses a source file with the COW syntax extension:
    </para>
    <programlisting language="bash">
#!/bin/sh -x

file=$1
lib=cow.syntax
bin=ocamlfind
args=`$bin query -predicates syntax,preprocessor -r -format '-I %d %a' $lib`
camlp4o -printer o $args $file
</programlisting>
    <para>
      You can supply <literal>ocamlfind</literal> with a number of
      different predicates to define the type of build you are running
      (preprocessing, compilation or linking). The final part of the
      script invokes the <literal>camlp4o</literal> binary on your ML
      source file and outputs the transformed source code to your
      terminal.
    </para>
    </sidebar>
    <para>
      Let's see the OCaml code that has been generated for our
      <literal>author</literal> record after it has been preprocessed:
    </para>
    <programlisting language="ocaml">
type author = {
  name: string;
  uri: string option;
  email: string option;
}

let rec xml_of_author author : Cow.Xml.t =
  List.flatten
    [ (match match author.email with
             | None -&gt; []
             | Some var1 -&gt; [ `Data var1 ]
       with
       | [] -&gt; []
       | _ -&gt;
         [ `El ((((&quot;&quot;, &quot;email&quot;), []) : Cow.Xml.tag),
           (match author.email with
            | None -&gt; []
            | Some var1 -&gt; [ `Data var1 ])) ]);
      (match match author.uri with
        | None -&gt; [] | Some var2 -&gt; [ `Data var2 ]
       with
       | [] -&gt; []
       | _ -&gt;
         [ `El ((((&quot;&quot;, &quot;uri&quot;), []) : Cow.Xml.tag),
           (match author.uri with
             | None -&gt; []
             | Some var2 -&gt; [ `Data var2 ])) ]);
      (match [ `Data author.name ] with
       | [] -&gt; []
       | _ -&gt;
         [ `El ((((&quot;&quot;, &quot;name&quot;), []) : Cow.Xml.tag),
         [ `Data author.name ]) ]) ]
</programlisting>
    <para>
      Notice that the <literal>with xml</literal> clause has been
      replaced with a new <literal>xml_of_author</literal> function that
      has been generated for you. It accepts an
      <literal>author</literal> value and returns an
      <literal>Xml.t</literal> value. The generated code isn't really
      meant to be human-readable, but you don't normally see it when
      using the syntax extension (we've only dumped it out here to
      illustrate how <literal>camlp4</literal> works).
    </para>
    <para>
      If we run <literal>xml_of_author</literal> and convert the result
      to a human-readable string, our complete example looks like:
    </para>
    <programlisting language="ocaml">
type author = {
  name: string;
  uri: string option;
  email: string option;
} with xml

let anil = {
  name = &quot;Anil Madhavapeddy&quot;;
  uri = Some &quot;http://anil.recoil.org&quot;;
  email = Some &quot;anil@recoil.org&quot;
}

let _ = print_endline (Cow.Xml.to_string (xml_of_author anil))
</programlisting>
    <para>
      This will generate the following XML output on the terminal when
      you execute it:
    </para>
    <programlisting language="xml">
&lt;?xml version=&quot;1.0&quot; encoding=&quot;UTF-8&quot;?&gt;
&lt;email&gt;anil@recoil.org&lt;/email&gt;
&lt;uri&gt;http://anil.recoil.org&lt;/uri&gt;
&lt;name&gt;Anil Madhavapeddy&lt;/name&gt;
</programlisting>
    <para>
      This is convenient, but just one small portion of Atom. How do we
      express the full Atom scheme from earlier? The answer is with just
      a few more records that match the Atom XML schema.
    </para>
    <programlisting language="ocaml">
type author = {
  name: string;
  uri: string option;
  email: string option;
} with xml

type date =
  int * int * int * int * int (* year, month, date, hour, minute *)
with xml

let xml_of_date (year,month,day,hour,min) =
  let d = Printf.sprintf &quot;%.4d-%.2d-%.2dT%.2d:%.2d:00Z&quot; year month day hour min in
  &lt;:xml&lt; $str:d$ &gt;&gt;

type meta = {
  id: string;
  title: string;
  subtitle: string option;
  author: author option;
  rights: string option;
  updated: date;
} with xml
</programlisting>
    <para>
      We've now filled in more of the Atom schema with these records.
      The first problem we run into is that occasionally there is a
      mismatch between the syntax extension's idea of what the
      auto-generated XML should look like, and the reality of the
      protocol you are mapping to.
    </para>
    <para>
      The Atom date field is a good example. We define it as a tuple of
      integers, but the format mandated by the specification is actually
      a free-form text format and not XML. However, because the syntax
      extension generates normal OCaml functions, we can just override
      the <literal>xml_of_date</literal> function with a custom one
      which returns the correct XML fragment. Any references further
      down the module will just use our overridden version and ignore
      the auto-generated one.
    </para>
    <para>
      There's another interesting bit of new syntax in the
      <literal>xml_of_date</literal> function known as a
      <emphasis>quotation</emphasis>. OCaml not only allows code to be
      generated during pre-processing, but also to override the core
      language grammar with new constructs. The most common way of doing
      this is by embedding the custom grammars inside
      <literal>&lt;:foo&lt; ... &gt;&gt;</literal> tags, where
      <literal>foo</literal> represents the particular grammar being
      used. In the case of COW, this lets you generate XMLM-compatible
      OCaml values just by typing in XML tags.
    </para>
    <para>
      TODO antiquotations.
    </para>
    <para>
      TODO finish the atom example.
    </para>
    <sect2 id="working-with-xhtmlx">
      <title>Working with XHTMLx</title>
      <para>
        TODO use Cow.Html to generate a more complete Atom feed.
      </para>
    </sect2>
  </sect1>
</chapter><chapter id="concurrent-programming-with-async">
  <title>Concurrent Programming with Async</title>
  <para>
    When you start building OCaml code that interfaces with external
    systems, you'll soon need to handle concurrent operations. Consider
    the case of a web server sending a large file to many clients, or a
    GUI waiting for a mouse clicks. These applications often need to
    block while waiting for input for a particular task, and process
    something else during that time. Meanwhile, when new data does
    appear, the blocked task needs to be resumed as quickly as possible.
  </para>
  <para>
    Busy servers can often handle tens of thousands of simultaneous
    connections, so runtime efficiency really matters. An equally
    important concern is readable source code, so that the control flow
    of the program is obvious at a glance.
  </para>
  <para>
    A common approach to concurrency is to use
    <emphasis>preemptive</emphasis> system threads, most commonly in
    Java or C#. In this threading model, each task is given an operating
    system thread of its own, and the kernel schedules them with
    arbitrary interleavings. Other language runtimes such as Javascript
    are single-threaded, and applications register function callbacks to
    be triggered upon external events such as a timeout or browser
    click.
  </para>
  <para>
    Both of these mechanisms have tradeoffs. Preemptive threads require
    more resources per thread and can be memory hungry. The operating
    system can also arbitrarily interleave the execution of preemptive
    threads, putting a load on the programmer to lock shared data
    structures. Single-threaded event-driven systems execute a single
    task at a time and require less locking. However, the program
    structure can often descend into a maze of event callbacks for even
    a simple operation that blocks a few times. Code readability
    matters, and so we'd like to avoid such spaghetti control flow.
  </para>
  <para>
    The <literal>Async</literal> OCaml library offers a hybrid model
    that lets you write event-driven code that can block
    <emphasis>without</emphasis> the complexity of preemptive threading.
    Lets begin by constructing a simple thread. Async follows the Core
    convention and provides an <literal>Async.Std</literal> that
    provides threaded variants of many standard library functions.
  </para>
  <programlisting language="ocaml">
# require &quot;async.unix&quot; ;;
# open Async.Std ;;
# return 5 ;;
- : int Deferred.t = &lt;abstr&gt;
</programlisting>
  <note>
  <title>
  When to open <literal>Async.Std</literal>
  </title>
  <para>
    The <literal>Core.Std</literal> module is normally opened up in
    every file you write. You need to be a little more careful when
    opening <literal>Async.Std</literal> as it replaces standard
    blocking functions with asynchronous equivalents. This comes across
    most obviously with the standard input and output descriptors.
  </para>
  <programlisting language="ocaml">
# open Core.Std;;
# print_endline &quot;hello world&quot;;;
hello world
- : unit = ()
# open Async.Std;;
# print_endline &quot;hello world&quot;;;
- : unit = ()
TODO what is the manual flush call here?
</programlisting>
  <para>
    With just <literal>Core.Std</literal> open, the
    <literal>print_endline</literal> function immediately displayed its
    output to the console. When <literal>Async.Std</literal> was opened,
    the call to <literal>print_endline</literal> is buffered and needs
    to be manually flushed before the output is displayed.
  </para>
  </note>
  <sect1 id="creating-your-first-async-threads">
    <title>Creating your first async threads</title>
    <para>
      Async threads are co-operative and never preempt each other, and
      the library internally converts blocking code into a single event
      loop. The threads are normal OCaml heap-allocated values (without
      any runtime magic!) and can be allocated very fast. Concurrency is
      mostly limited only by your available main memory, or operating
      system limits on other resources such as file descriptors.
    </para>
    <para>
      The basic type of an Async thread is a
      <literal>'a Deferred.t</literal>, which can be constructed by the
      <literal>return</literal> function. The type parameter (in this
      case <literal>int</literal>) represents the ultimate type of the
      thread once it has completed in the future. This return value
      cannot be used directly while it is wrapped in a
      <literal>Deferred.t</literal> as it may not be available yet.
      Instead, we can <literal>bind</literal> a functional closure to
      the thread that is called once the value is eventually ready.
    </para>
    <programlisting language="ocaml">
# open Async.Std ;;
# let x = return 5 ;;
val x : int Deferred.t = &lt;abstr&gt;
</programlisting>
    <para>
      The <literal>return</literal> function constructs a constant
      <literal>int Deferred.t</literal> whose value will be available
      immediately.
    </para>
    <programlisting language="ocaml">
# let y = Deferred.bind x (fun v -&gt; return (string_of_int v)) ;;
val y : string Deferred.t = &lt;abstr&gt;
</programlisting>
    <para>
      We've now bound a function to <literal>x</literal> that will be
      called over its resulting value. The closure simply converts the
      <literal>int</literal> to a <literal>string</literal> and returns
      it as a <literal>string Deferred.t</literal>. Notice that while
      both <literal>x</literal> and <literal>y</literal> share a common
      <literal>Deferred.t</literal> type, their type variables differ
      and so they cannot be interchangably used except in polymorphic
      functions. This is useful when refactoring large codebases as you
      can tell if any function will block simply by the presence of an
      <literal>Deferred.t</literal> in the signature.
    </para>
    <para>
      Another important note is that the result of the bound function
      must also be a <literal>Deferred</literal> value. If we try to
      return a <literal>string</literal> immediately, then we get the
      following type error.
    </para>
    <para>
      `<literal>ocaml # let y = Deferred.bind x (fun v -&gt; string_of_int v);; Error: This expression has type string but an expression was expected of type          'a Deferred.t = 'a Ivar.Deferred.t</literal>
    </para>
    <para>
      This requirement makes <literal>bind</literal> operations
      composable. You can take the <literal>y</literal> value and
      <literal>bind</literal> it again to another thread, and expect
      them all to run in the correct sequence. Let's examine the
      function signatures of <literal>bind</literal> and
      <literal>return</literal> more closely to understand this better.
    </para>
    <programlisting language="ocaml">
# return ;;
- : 'a -&gt; 'a Deferred.t = &lt;fun&gt;
# Deferred.bind ;;
- : 'a Deferred.t -&gt; ('a -&gt; 'b Deferred.t) -&gt; 'b Deferred.t = &lt;fun&gt;
</programlisting>
    <para>
      <literal>return</literal>, <literal>bind</literal> and the
      <literal>Deferred.t</literal> type all contain polymorphic type
      variables (the <literal>'a</literal>) which represent the type of
      the thread, and are inferred based on how they are used in your
      code. The <literal>'a</literal> type of the argument passed to the
      <literal>bind</literal> callback <emphasis>must</emphasis> be the
      same as the <literal>'a Deferred.t</literal> of the input thread,
      preventing runtime mismatches between thread callbacks.
    </para>
    <para>
      Both <literal>bind</literal> and <literal>return</literal> form a
      design pattern in functional programming known as
      <emphasis>monads</emphasis>, and you will run across this
      signature in many applications beyond just threads. <emphasis>TODO
      avsm: figure out where to talk about all the monads in Core in
      more detail</emphasis>. The <literal>&gt;&gt;=</literal> inline
      operator is provided as a more succinct alias to
      <literal>bind</literal>, as shown below.
    </para>
    <programlisting language="ocaml">
# let x = return 5 ;;
val x : int Deferred.t = &lt;abstr&gt;
# x &gt;&gt;= fun y -&gt; return (string_of_int y) ;;
val - : string Deferred.t = &lt;abstr&gt;
</programlisting>
    <para>
      The <literal>&gt;&gt;=</literal> operator is exactly the same as
      <literal>bind</literal> and unpacks the integer future into the
      <literal>y</literal> variable. The subsequent closure is called
      with the resulting integer and builds a new string future.
    </para>
    <para>
      It can be a little verbose to keep calling <literal>bind</literal>
      and <literal>return</literal> to wrap simple functions such as
      <literal>string_of_int</literal>. The <literal>&gt;&gt;|</literal>
      operator maps a non-Async function directly across a
      <literal>Deferred.t</literal> value. In the example below, the
      deferred <literal>x</literal> is mapped to
      <literal>string_of_int</literal> directly, and the result is a
      <literal>string Deferred.t</literal>.
    </para>
    <programlisting language="ocaml">
# x &gt;&gt;| string_of_int ;;
val - : string Deferred.t = &lt;abstr&gt;
</programlisting>
    <para>
      Multiple threads can be chained together with successive calls to
      <literal>bind</literal> to sequentially compose blocking
      operations.
    </para>
    <programlisting language="ocaml">
# return 5
  &gt;&gt;= fun v -&gt; return (string_of_int v)
  &gt;&gt;= fun v -&gt; return (v = &quot;5&quot;)
- : bool Deferred.t = &lt;abstr&gt;
</programlisting>
    <para>
      The example above constructs an <literal>int</literal> thread,
      converts it to a <literal>string</literal> thread, and then to a
      <literal>bool</literal> thread via a string comparison. Of course,
      there's no interesting threading going on in this example beyond
      building a constant value, but let's look at how to run it next.
    </para>
  </sect1>
  <sect1 id="executing-async-applications">
    <title>Executing async applications</title>
    <para>
      All async threads run within a <emphasis>scheduler</emphasis> that
      is responsible for associating blocked threads with system
      resources (such as file descriptors) and waking them up when
      external I/O or timer events fire.
    </para>
    <sect2 id="running-threads-within-the-toplevel">
      <title>Running threads within the toplevel</title>
      <para>
        If you're experimenting with async programming, the
        <literal>utop</literal> toplevel is a convenient place to write
        code interactively. Async threads can be evaluated into a
        concrete value by wrapping them in
        <literal>Thread_safe.block_on_async_exn</literal>, which spawns
        a system thread that waits until a result is available.
      </para>
      <programlisting language="ocaml">
# Thread_safe.block_on_async_exn ;;
- : (unit -&gt; 'a Deferred.t) -&gt; 'a = &lt;fun&gt;
</programlisting>
      <para>
        A neat feature in <literal>utop</literal> is that it detects
        functions with a <literal>Deferred.t</literal> in the return
        type, and automatically translates it into a call to
        <literal>block_on_async_exn</literal> for you.
      </para>
      <programlisting language="ocaml">
# let fn () = return 5 &gt;&gt;| string_of_int ;;
val fn : unit -&gt; string Deferred.t = &lt;abstr&gt;

# Thread_safe.block_on_async_exn fn ;;
- : string = &quot;5&quot;

# fn () ;;
- : string = &quot;5&quot;
</programlisting>
      <para>
        We've defined an <literal>fn</literal> thread in the first
        phrase, and then run it manually using
        <literal>block_on_async_exn</literal>. The final phrase executes
        <literal>fn</literal> directly, and you can see the
        <literal>utop</literal> translation kicking in and returning the
        concrete value.
      </para>
    </sect2>
    <sect2 id="running-threads-within-an-application">
      <title>Running threads within an application</title>
      <para>
        An application can also use the same
        <literal>block_on_async_exn</literal> as the toplevel, but there
        are two alternatives. Once a few threads have been started, the
        <literal>Scheduler.go</literal> function runs them for you.
      </para>
      <programlisting language="ocaml">
# Scheduler.go ;;
- : ?raise_unhandled_exn:bool -&gt; unit -&gt; never_returns = &lt;fun&gt; 
</programlisting>
      <para>
        Notice that this function never returns, even if all of the
        spawned threads are completed. The Async scheduler doesn't
        terminate by default, and so most applications will listen for a
        signal to exit or simply use <literal>CTRL-C</literal> to
        interrupt it from a console.
      </para>
      <para>
        Another common way to execute async threads is via the
        <literal>Command</literal> module we introduced in
        <xref linkend="command-line-parsing"/>. When you open
        <literal>Async.Std</literal>, the <literal>Command</literal>
        module now has an <literal>async_basic</literal> available.
      </para>
      <programlisting language="ocaml">
# Command.async_basic ;;
- : summary:string -&gt; 
    ?readme:(unit -&gt; string) -&gt; 
    ('a, unit -&gt; unit Deferred.t) Command.Spec.t -&gt; 
    'a -&gt; Command.t = &lt;fun&gt;
</programlisting>
      <para>
        This is used in exactly the same way as the usual
        <literal>Command</literal> module, except that the callbacks
        must return a <literal>Deferred.t</literal>. This lets you run
        blocking threads directly from a command-line interface.
      </para>
    </sect2>
  </sect1>
  <sect1 id="timing-and-thread-composition">
    <title>Timing and Thread Composition</title>
    <para>
      Our examples so far have been with static threads, which isn't
      very much use for real programs. We'll now add timing to the mix
      and show you how to coordinate threads and timeouts. Let's write a
      program that spawns two threads, each of which sleep for some
      random time and return either &quot;Heads&quot; or
      &quot;Tails&quot;. The first thread that wakes up returns its
      value.
    </para>
    <programlisting language="ocaml">
# let flip () =
  let span = Time.Span.of_sec 3.0 in
  let span_heads = Time.Span.randomize span ~percent:0.75 in
  let span_tails = Time.Span.randomize span ~percent:0.75 in
  let coin_heads =
    Clock.after span_heads
    &gt;&gt;| fun () -&gt;
    &quot;Heads!&quot;, span_heads, span_tails
  in
  let coin_tails =
    Clock.after span_tails
    &gt;&gt;| fun () -&gt;
    &quot;Tails!&quot;, span_heads, span_tails
  in
  Deferred.any [coin_heads; coin_tails] ;;
val flip : unit -&gt; (string * Time.Span.t * Time.Span.t) Deferred.t = &lt;fun&gt;
</programlisting>
    <para>
      This example introduces a couple of new time-related Async
      functions. The <literal>Time</literal> module contains functions
      to express both absolute and relative temporal relationships. In
      our coin flipping example, we use: *
      <literal>Time.Span.of_sec</literal> to create a relative time span
      of 3 seconds * <literal>Time.Span.randomize</literal> to permute
      this span randomly by 75% * <literal>Clock.after</literal> to
      build a <literal>unit Deferred.t</literal> that will return after
      the specified timespan * <literal>Deferred.any</literal> to select
      between a list of threads and return the value of the first one to
      return a value.
    </para>
    <para>
      It's important to note that there is no need for an explicit
      &quot;thread create&quot; function in Async. Instead, we build up
      functions that manipulate <literal>Deferred.t</literal> values,
      and bind them to names when convenient. In the example above,
      we've created <literal>coin_heads</literal> and
      <literal>coin_tails</literal> which have the following type:
    </para>
    <programlisting>
val coin_heads : (string * Time.Span.t * Time.Span.t) Deferred.t
val coin_tails : (string * Time.Span.t * Time.Span.t) Deferred.t
</programlisting>
    <para>
      Both of the threads encode the time intervals in their return
      value so that you can can easily verify the calculations (you
      could also simply print the time spans to the console as they are
      calculated and simplify the return types). Let's verify this by
      running the <literal>flip</literal> function at the toplevel a few
      times. Remember to run this in <literal>utop</literal>, since it
      will spin up the Async scheduler automatically for you and block
      until a result is available.
    </para>
    <programlisting language="ocaml">
# flip () ;;
# - : string * Time.Span.t * Time.Span.t = (&quot;Heads!&quot;, 2.86113s, 3.64635s)

# flip () ;;
# - : string * Time.Span.t * Time.Span.t = (&quot;Tails!&quot;, 4.44979s, 2.14977s)
</programlisting>
    <para>
      We used <literal>any</literal> in our example to choose the first
      ready thread. The <literal>Deferred</literal> module has a number
      of other ways to select between multiple threads:
    </para>
    <informaltable>
      <tgroup cols="3">
        <colspec align="left"/>
        <colspec align="left"/>
        <colspec align="left"/>
        <thead>
          <row>
            <entry>
              Function
            </entry>
            <entry>
              # Threads
            </entry>
            <entry>
              Behaviour
            </entry>
          </row>
        </thead>
        <tbody>
          <row>
            <entry>
              both
            </entry>
            <entry>
              2
            </entry>
            <entry>
              Combines both threads into a tuple and returns both
              values.
            </entry>
          </row>
          <row>
            <entry>
              any
            </entry>
            <entry>
              list
            </entry>
            <entry>
              Returns the first thread that becomes determined.
            </entry>
          </row>
          <row>
            <entry>
              all
            </entry>
            <entry>
              list
            </entry>
            <entry>
              Waits for all threads to complete and returns their
              values.
            </entry>
          </row>
          <row>
            <entry>
              all_unit
            </entry>
            <entry>
              list
            </entry>
            <entry>
              Waits for all <literal>unit</literal> threads to complete
              and returns <literal>unit</literal>.
            </entry>
          </row>
          <row>
            <entry>
              peek
            </entry>
            <entry>
              1
            </entry>
            <entry>
              Inspects a single thread to see if it is determined yet.
            </entry>
          </row>
        </tbody>
      </tgroup>
    </informaltable>
    <para>
      Try modifying the <literal>Deferred.any</literal> in the above
      example to use some of the other thread joining functions above,
      such as <literal>Deferred.both</literal>.
    </para>
    <sect2 id="cancellation">
      <title>Cancellation</title>
      <para>
      </para>
    </sect2>
  </sect1>
  <sect1 id="a-simple-tcp-echo-server">
    <title>A simple TCP Echo Server</title>
    <para>
    </para>
  </sect1>
  <sect1 id="onto-an-http-server">
    <title>Onto an HTTP Server</title>
    <para>
    </para>
  </sect1>
  <sect1 id="binding-to-the-github-api">
    <title>Binding to the Github API</title>
    <para>
      Show how we can use a monadic style to bind to the Github API and
      make simple JSON requests/responses.
    </para>
    <note><title>
    A Note on Portability
    </title>
    <para>
      Explain libev and why its needed here.
    </para>
    </note>
  </sect1>
  <sect1 id="example-searching-definitions-with-duckduckgo">
    <title>Example: searching definitions with DuckDuckGo</title>
    <para>
      DuckDuckGo is a search engine with a freely available search
      interface. A DuckDuckGo search is executed by making an HTTP
      request to <literal>api.duckduckgo.com</literal>. The result comes
      back in either JSON or XML format, depending on what was requested
      in the original query string. Let's write some functions that
      construct the right URI and can parse the resulting JSON.
    </para>
    <para>
      Before we can make the HTTP calls, we need a couple of helper
      functions with the following signature.
    </para>
    <programlisting language="ocaml">
(* Generate a DuckDuckGo API search URI for [query] *)
val make_ddg_uri : query:string -&gt; Uri.t

(* Extract the Definition field from the DuckDuckGo search
   response, or return [None] if it doesn't exist *)
val get_definition_from_json: string -&gt; string option
</programlisting>
    <para>
      This code uses a couple of new libraries we haven't seen before.
      You will need to OPAM install <literal>uri</literal> and
      <literal>yojson</literal> (refer to
      <xref linkend="installation"/> if you need help). Let's
      see how to implement them first.
    </para>
    <sect2 id="uri-handling">
      <title>URI handling</title>
      <para>
        You're hopefully familiar with HTTP URLs, which identify
        endpoints across the World Wide Web. These are actually part of
        a more general family known as Uniform Resource Identifiers
        (URIs). The full URI specification is defined in
        <ulink url="http://tools.ietf.org/html/rfc3986">RFC3986</ulink>
        (and is rather complicated!). Luckily, the
        <literal>ocaml-uri</literal> library provides a strongly-typed
        interface which takes care of much of the hassle.
      </para>
      <programlisting language="ocaml">
(* Generate a DuckDuckGo search URI from a query string *)
let make_ddg_uri =
  let base_uri = &quot;http://api.duckduckgo.com/?format=json&quot; in
  let uri = Uri.of_string base_uri in
  fun ~query -&gt;
    Uri.add_query_param uri (&quot;q&quot;, [query])
</programlisting>
      <para>
        A <literal>Uri.t</literal> is constructed from the
        <literal>Uri.of_string</literal> function, and a query parameter
        <literal>q</literal> is added with the desired search query. The
        library takes care of encoding the URI correctly when outputting
        it in the network protocol.
      </para>
      <para>
        Note that the URI manipulation functions are all
        <emphasis>pure</emphasis> functions which return a new URI
        value, and never modify the input. This makes it easier to pass
        around URI values through your application stack without fear of
        modification.
      </para>
    </sect2>
    <sect2 id="parsing-json-strings">
      <title>Parsing JSON strings</title>
      <para>
        The HTTP response from DuckDuckGo is in JSON, a common (and
        thankfully simple) format that is specified in
        <ulink url="http://www.ietf.org/rfc/rfc4627.txt">RFC4627</ulink>.
        There are quite a few JSON parsers available for OCaml, and
        we've picked
        <ulink url="http://mjambon.com/yojson.html"><literal>Yojson</literal></ulink>
        for this example.
      </para>
      <para>
        There are a few non-standard extensions to JSON, so Yojson
        exposes them as the <literal>Basic</literal> and
        <literal>Safe</literal> sub-modules. It doesn't really matter
        which one we pick for this simple example, so we'll go with
        <literal>Safe</literal>.
      </para>
      <para>
        The input <literal>string</literal> is parsed using
        <literal>Yojson.Safe.from_string</literal> into an OCaml data
        type. The JSON values are represented using polymorphic
        variants, and can thus be pattern matched more easily once they
        have been parsed by Yojson.
      </para>
      <programlisting language="ocaml">
type json = [
  | `Assoc of (string * json) list
  | `Bool of bool
  | `Float of float
  | `Int of int
  | `List of json list
  | `Null
  | `String of string
  | `Tuple of json list
]
</programlisting>
      <para>
        We're expecting the DuckDuckGo response to be a record, with an
        optional <literal>Description</literal> field being one of the
        keys in the record. The
        <literal>get_definition_from_json</literal> does a pattern match
        on this, and returns an optional string if a definition is found
        within the result.
      </para>
      <programlisting language="ocaml">
(* Extract the Definition field from the DuckDuckGo search
   response, or return [None] if it doesn't exist *)
let get_definition_from_json (json:string) =
  match Yojson.Safe.from_string json with
  |`Assoc kv_list -&gt;
      let open Option in
      List.Assoc.find kv_list &quot;Definition&quot; &gt;&gt;|
      Yojson.Safe.to_string
  |_ -&gt; None
</programlisting>
      <para>
        Notice that we use options here instead of throwing exceptions
        on an error. When the <literal>Option</literal> module is
        opened, it provides a <literal>map</literal> operator
        (<literal>&gt;&gt;|</literal>) which calls the bound closure if
        the value exists. If no result is found, then the
        <literal>Yojson.Safe.to_string</literal> conversion function is
        simply ignored, and a <literal>None</literal> returned.
      </para>
    </sect2>
    <sect2 id="executing-an-http-client-query">
      <title>Executing an HTTP client query</title>
      <para>
        Now that we've written those utility functions, let's look at
        the Async code that performs the actual search:
      </para>
      <programlisting language="ocaml">
(* Execute the DuckDuckGo search *)
(* TODO: This client API is being simplified in Cohttp *)
let do_ddg_query query =
  Cohttp_async.Client.call `GET (make_ddg_uri ~query)
  &gt;&gt;= function
  | Some (res, Some body) -&gt;
      let buf = Buffer.create 128 in
      Pipe.iter_without_pushback body ~f:(Buffer.add_string buf)
      &gt;&gt;| fun () -&gt;
      get_definition_from_json (Buffer.contents buf)
      |&gt; Option.value ~default:&quot;???&quot;
  | Some (_, None) | None -&gt;
      failwith &quot;no body in response&quot;
</programlisting>
      <para>
        For this code, you'll need to OPAM install the
        <literal>cohttp</literal> library. The
        <literal>Cohttp_async.Client</literal> module executes the HTTP
        call, and returns a status and response body wrapped. This whole
        result is wrapped in a type you haven't seen before:
        <literal>Async.Deferred.t</literal>.
      </para>
      <para>
        The <literal>Deferred.t</literal> represents a
        <emphasis>future</emphasis> value whose result is not available
        yet. You can &quot;wait&quot; for the result by binding a
        callback using the <literal>&gt;&gt;=</literal> operator (which
        is imported when you open <literal>Async.Std</literal>). This is
        the same monad pattern available in other Core libraries such as
        <literal>Option</literal>, but instead of operating on optional
        values, we are now mapping over future values. We'll come back
        to monads later in this chapter. (<emphasis>avsm</emphasis>:
        TODO xref)
      </para>
      <para>
        The <literal>ddg_query</literal> function invokes the HTTP
        client call, and returns a tuple containing the response codes
        and headers, and a <literal>string Pipe.Reader</literal>. Pipes
        in Async are often used to transmit large amounts of data
        between two processes or concurrent threads. The
        <literal>Cohttp</literal> library creates a
        <literal>Pipe.Writer</literal> which it outputs the HTTP body
        into, and provides your application with the
        <literal>Reader</literal> end.
      </para>
      <para>
        In this case, the HTTP body probably isn't very large, so we
        just iterate over the Pipe's contents until we have the full
        HTTP body in a <literal>Buffer.t</literal>. Once the full body
        has been retrieved into our buffer, the next callback passes it
        through the JSON parser and returns a human-readable string of
        the search description that DuckDuckGo gave us.
      </para>
      <programlisting language="ocaml">
(* Run a single search *)
let run_one_search =
  do_ddg_query &quot;Camel&quot; &gt;&gt;| prerr_endline

(* Start the Async scheduler *)
let _ = Scheduler.go ()
</programlisting>
      <para>
        Let's actually use the search function to run a real query now.
        The fragment above spawns a single search, and then fires up the
        Async scheduler. The scheduler is where all the work happens,
        and must be started in every application that uses Async.
        Without it, logging won't be output, nor will blocked functions
        ever wake up. When the scheduler is active, it is waiting for
        incoming I/O events and waking up function callbacks that were
        sleeping on that particular file descriptor or timeout.
      </para>
      <para>
        A single connection isn't that interesting from a concurrency
        perspective. Luckily, Async makes it very easy to run multiple
        parallel searches:
      </para>
      <programlisting language="ocaml">
(* Run many searches in parallel *)
let run_many_searches =
  let searches = [&quot;Duck&quot;; &quot;Sheep&quot;; &quot;Cow&quot;; &quot;Llama&quot;; &quot;Camel&quot;] in
  Deferred.List.map ~how:`Parallel searches ~f:do_ddg_query &gt;&gt;|
  List.iter ~f:print_endline
</programlisting>
      <para>
        The <literal>Deferred.List</literal> module lets you specify
        exactly how to map over a collection of futures. The searches
        will be executed simultaneously, and the map thread will
        complete once all of the sub-threads are complete. If you
        replace the <literal>Parallel</literal> parameter with
        <literal>Serial</literal>, the map will wait for each search to
        fully complete before issuing the next one.
      </para>
    </sect2>
  </sect1>
</chapter><chapter id="fast-binary-serialization-with-bin_prot">
  <title>Fast Binary Serialization with bin_prot</title>
  <para>
    S-expressions are a good serialization format when you need
    something machine-parseable as well as human readable and editable.
    But Sexplib's s-expressions are not particularly performant for a
    couple of reasons:
  </para>
  <itemizedlist>
    <listitem>
      <para>
        s-expression serialization goes through an intermediate type,
        <literal>Sexp.t</literal>, which must be allocated and is then
        typically thrown away, putting non-trivial pressure on the
        garbage collector.
      </para>
    </listitem>
    <listitem>
      <para>
        parsing and printing to strings in an ASCII format can be
        expensive for types like <literal>int</literal>s,
        <literal>float</literal>s and <literal>Time.t</literal>s where
        some real computation needs to be done to produce or parse the
        ASCII representation.
      </para>
    </listitem>
  </itemizedlist>
  <para>
    <literal>Bin_prot</literal> is a library that addresses these issues
    by providing fast serialization in a compact binary format. We'll
    also introduce the Core <literal>Bigstring</literal> library for
    handling large binary strings efficiently during this chapter.
  </para>
  <note>
  <title>
  Using <literal>bin_prot</literal> in the toplevel
  </title>
  <para>
    The <literal>bin_prot</literal> syntax extension isn't activated by
    default in the toplevel, but is easily available if you add this to
    your <literal>~/.ocamlinit</literal> file. You can also just type
    this in directly into <literal>utop</literal> (with
    <literal>;;</literal> to finish the line) instead.
  </para>
  <programlisting>
#require &quot;bin_prot.syntax&quot;
</programlisting>
  <para>
    The extension is activated by putting <literal>with bin_io</literal>
    after the type declaration. This looks a bit unsightly in the
    toplevel because of all the definitions that are generated. We'll
    elide those definitions in the book, but you can see them for
    yourself in the toplevel.
  </para>
  </note>
  <sect1 id="defining-a-message-broker">
    <title>Defining a message broker</title>
    <para>
      Here's a small complete example of a program that can read and
      write values using <literal>bin_io</literal>. Here, the
      serialization is of types that might be used as part of a
      message-queue, where each message has a topic, some content, and a
      source, which is in turn a hostname and a port.
    </para>
    <programlisting language="ocaml">
open Core.Std

module Message = struct
  module Source = struct
    type t = { hostname: string;
               port: int;
             }
    with bin_io
  end

  type t = { topic: string;
             content: string;
             source: Source.t;
           }
  with bin_io
end
</programlisting>
    <para>
      You can can combine multiple syntax generators in the same type
      declaration by comma-separating them, so you could generate both
      formats via <literal>with bin_io,sexp</literal> above.
    </para>
    <para>
      Next we need to define how to marshal and unmarshal these
      messages. The interface is a little more complex than for
      s-expressions since we don't just want to serialise from the
      normal OCaml <literal>string</literal>, but also to the
      <literal>bigstring</literal> type. We'll explain what this is in
      more detail shortly, but for now think of it as a more efficient
      alternative for large binary data.
    </para>
    <programlisting language="ocaml">
let binable =
  (module Message : Binable.S with type t = Message.t)

let save_message outc msg =
  let s = Binable.to_bigstring binable msg in
  let len = Bigstring.length s in
  Out_channel.output_binary_int outc len;
  Bigstring.really_output outc s
</programlisting>
    <para>
      The <literal>binable</literal> value above captures all the
      auto-generated <literal>bin_io</literal> functions into a
      first-class module of type <literal>Binable.S</literal>. This
      module has the low-level reader and writer functions which we
      don't want to have to manually construct.
    </para>
    <para>
      The <literal>save_message</literal> is then responsible for
      writing the binary content out to a <literal>bigstring</literal>.
      It first invokes the <literal>Binable.to_bigstring</literal> on a
      <literal>Message.t</literal> value to retrieve a marshalled
      string. It then determines the length of this string, and writes
      out the length and the string to the output channel.
    </para>
    <para>
      The <literal>Binable</literal> interface in Core is pretty simple:
      <literal>type 'a m = (module Binable.S with type t = 'a)  val of_bigstring : 'a m -&gt; bigstring -&gt; 'a val to_bigstring : ?prefix_with_length:bool -&gt; 'a m -&gt; 'a -&gt; bigstring val of_string : 'a m -&gt; string -&gt; 'a val to_string : 'a m -&gt; 'a -&gt; string</literal>
    </para>
    <para>
      Since the <literal>Binable.S</literal> module values are generated
      for you automatically, the only functions you'll need to regularly
      use are the conversion functions above.
    </para>
    <para>
      Reading back the binary value we've just defined is quite similar.
      We read in the length field, read that much data into a
      <literal>bigstring</literal>, and convert it to our type using
      <literal>Binable.of_bigstring</literal>.
    </para>
    <programlisting language="ocaml">
let load_message inc =
  match In_channel.input_binary_int inc with
  | None -&gt; failwith &quot;length missing from header&quot;
  | Some len -&gt;
    let buf = Bigstring.create len in
    Bigstring.really_input ~pos:0 ~len inc buf;
    Binable.of_bigstring binable buf
</programlisting>
    <para>
      The code to generate and read and write these messages now just
      uses the static <literal>Message.t</literal> type, with no need to
      worry about the marshalling mechanism.
    </para>
    <programlisting language="ocaml">
(* Generate some example messages *)
let example content =
  let source =
    { Message.Source.
      hostname = &quot;ocaml.org&quot;; port = 2322 }
  in
  { Message.
    topic = &quot;rwo-example&quot;; content; source; }

(* write out three messages... *)
let write_messages () =
  let outc = Out_channel.create &quot;tmp.bin&quot; in
  List.iter ~f:(save_message outc) [
    example &quot;a wonderful&quot;;
    example &quot;trio&quot;;
    example &quot;of messages&quot;;
  ];
  Out_channel.close outc

(* ... and read them back in *)
let read_messages () =
  let inc = In_channel.create &quot;tmp.bin&quot; in
  for i = 1 to 3 do
    let msg = load_message inc in
    printf &quot;msg %d: %s\n&quot; i msg.Message.content
  done

let () =
  write_messages (); read_messages ()
</programlisting>
  </sect1>
  <sect1 id="bigstring">
    <title>Bigstring</title>
    <para>
      We earlier mentioned that <literal>bigstring</literal> is a more
      efficient version of <literal>string</literal>. Understanding the
      difference requires some understanding of how OCaml allocates
      values. TODO.
    </para>
  </sect1>
  <sect1 id="fieldslib">
    <title>Fieldslib</title>
    <para>
      TODO: out of place
    </para>
    <para>
      One common idiom when using records is to provide field accessor
      functions for a particular record.
    </para>
    <programlisting language="ocaml">
type t = { topic: string;
           content: string;
           source: Source.t;
         }

let topic   t = t.topic
let content t = t.content
let source  t = t.source
</programlisting>
    <para>
      Similarly, sometimes you simultaneously want an accessor to a
      field of a record and a textual representation of the name of that
      field. This might come up if you were validating a field and
      needed the string representation to generate an error message, or
      if you wanted to scaffold a form in a GUI automatically based on
      the fields of a record. Fieldslib provides a module
      <literal>Field</literal> for this purpose. Here's some code for
      creating <literal>Field.t</literal>'s for all the fields of our
      type <literal>t</literal>.
    </para>
    <programlisting language="ocaml">
# module Fields = struct
    let topic =
      { Field.
        name   = &quot;topic&quot;;
        setter = None;
        getter = (fun t -&gt; t.topic);
        fset   = (fun t topic -&gt; { t with topic });
      }
    let content =
      { Field.
        name   = &quot;content&quot;;
        setter = None;
        getter = (fun t -&gt; t.content);
        fset   = (fun t content -&gt; { t with content });
      }
    let source =
      { Field.
        name   = &quot;source&quot;;
        setter = None;
        getter = (fun t -&gt; t.source);
        fset   = (fun t source -&gt; { t with source });
      }
  end ;;
module Fields :
  sig
    val topic : (t, string list) Core.Std.Field.t
    val content : (t, string) Core.Std.Field.t
    val source : (t, Source.t) Core.Std.Field.t
  end
</programlisting>
    <para>
      There are several syntax extensions distributed with Core,
      including:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          <emphasis role="strong">Sexplib</emphasis>: provides
          serialization for s-expressions.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis role="strong">Bin_prot</emphasis>: provides
          serialization to an efficient binary format.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis role="strong">Fieldslib</emphasis>: generates
          first-class values that represent fields of a record, as well
          as accessor functions and setters for mutable record fields.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis role="strong">Variantslib</emphasis>: like Fieldslib
          for variants, producing first-class variants and other helper
          functions for interacting with variant types.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis role="strong">Pa_compare</emphasis>: generates
          efficient, type-specialized comparison functions.
        </para>
      </listitem>
      <listitem>
        <para>
          <emphasis role="strong">Pa_typehash</emphasis>: generates a
          hash value for a type definition, <emphasis>i.e.</emphasis>,
          an integer that is highly unlikely to be the same for two
          distinct types.
        </para>
      </listitem>
    </itemizedlist>
    <para>
      We'll discuss each of these syntax extensions in detail, starting
      with Sexplib.
    </para>
  </sect1>
</chapter><chapter id="first-class-modules">
  <title>First class modules</title>
  <para>
    _(jyh: I'm going to start some new text on FCM. We might want
    another chapter, but let's see how it goes. I've kept Ron's original
    text below.)
  </para>
  <para>
    OCaml provides several mechanisms for organizing your programs,
    including modules and functors, files and compilation units, and
    classes and objects. Files and compilation units
    (<literal>.ml</literal> and <literal>.mli</literal> files) are
    really just a simplified module system. Classes and objects are a
    different form of organization altogether (as we'll see in
    <xref linkend="object-oriented-programming"/>. Yet, in
    each of these cases, there is a clear separation between types and
    values -- values cannot contain types, and types cannot contain
    values. And since modules can contain types, modules can't be
    values.
  </para>
  <para>
    <emphasis>(yminsky: Instead of saying that ml and mli files are a
    simplified module system, maybe say that they &quot;provide a simple
    way of creating modules and interfaces&quot;, or some such? It's not
    like there's a simplified module system floating around)</emphasis>
  </para>
  <para>
    <emphasis>(yminsky: consider dropping &quot;Yet&quot; in the
    above.)</emphasis>
  </para>
  <para>
    Next, we'll relax this restriction with <emphasis>first-class
    modules</emphasis>. &quot;First-class&quot; means that modules can
    be passed around as ordinary values that can be created from and
    converted back to regular modules. This is a relatively recent
    addition to the OCaml language, and while it might seem trivial to
    say, it has profound consequences on the language. First-class
    modules are strictly more expressive than any other organization
    mechanism, including classes and objects. Once you use first-class
    modules, you'll never want to go back.
  </para>
  <para>
    <emphasis>(yminsky: I wouldn't say they're strictly more expressive.
    For example, they don't give you a way of expressing sub typing
    relationships effectively, which objects do.)</emphasis>
  </para>
  <para>
    This is not say that first-class modules should be used
    indiscriminately. When you pass modules as values, the reason is to
    support dynamic behavior, and this can have a negative impact on
    understandability. As we proceed, we'll compare first-class modules
    to other techniques, and suggest alternatives when it seems
    appropriate.
  </para>
  <para>
    _(jyh: Original text You can think of OCaml as being broken up into
    two sub-language: a core language that is concerned with values and
    types, and a module language that is concerned with modules and
    module signatures. These sub-languages are stratified, in that
    modules can contain types and values, but ordinary values can't
    contain modules or module types. That means you can't do things like
    define a variable whose definition is a module, or a function that
    takes a module as an argument.
  </para>
  <para>
    OCaml provides a way around this stratification in the form of
    <emphasis>first-class modules</emphasis>. First-class modules are
    ordinary values that can be created from and converted back to
    regular modules. As we'll see, letting modules into the core
    language makes it possible to use more flexible and dynamic
    module-oriented designs.)_
  </para>
  <sect1 id="another-trivial-example">
    <title>Another trivial example</title>
    <para>
      Much as we did with functors, we'll start out with an utterly
      trivial example, to allow us to show the basic mechanics of first
      class modules with a minimum of fuss.
    </para>
    <para>
      A first-class module is created by packaging up a module with a
      signature that it satisfies. The following defines a simple
      signature and a module that matches it.
    </para>
    <programlisting language="ocaml">
# module type X_int = sig val x : int end;;
module type X_int = sig val x : int end
# module Three : X_int = struct let x = 3 end;;
module Three : X_int
# Three.x;;
- : int = 3
</programlisting>
    <para>
      We can then create a first-class module using the
      <literal>module</literal> keyword.
    </para>
    <programlisting language="ocaml">
# let three = (module Three : X_int);;
val three : (module X_int) = &lt;module&gt;
</programlisting>
    <para>
      Note that the type of the first-class module,
      <literal>(module X_int)</literal>, is based on the name of the
      signature that we used in constructing it.
    </para>
    <para>
      To get at the contents of <literal>three</literal>, we need to
      unpack it into a module again, which we can do using the
      <literal>val</literal> keyword.
    </para>
    <programlisting language="ocaml">
# module New_three = (val three : X_int) ;;
module New_three : X_int
# New_three.x;;
- : int = 3
</programlisting>
    <para>
      Using these conversions as building blocks, we can create tools
      for working with first-class modules in a natural way. The
      following shows the definition of two function,
      <literal>to_int</literal>, which converts a
      <literal>(module X_int)</literal> into an <literal>int</literal>.
      And <literal>plus</literal>, which adds two
      <literal>(module X_int)</literal>s.
    </para>
    <programlisting language="ocaml">
# let to_int m =
    let module M = (val m : X_int) in
    M.x
  ;;
val to_int : (module X_int) -&gt; int = &lt;fun&gt;
# let plus m1 m2 =
    (module struct
       let x = to_int m1 + to_int m2
     end : X_int)
  ;;
val plus : (module X_int) -&gt; (module X_int) -&gt; (module X_int) = &lt;fun&gt;
</programlisting>
    <para>
      With these functions in hand, we can start operating on our
      <literal>(module X_int)</literal>'s in a more natural style,
      taking full advantage of the concision and simplicity of the core
      language.
    </para>
    <programlisting language="ocaml">
# let six = plus three three;;
val six : (module X_int) = &lt;module&gt;
# to_int (List.fold ~init:six ~f:plus [three;three]);;
- : int = 12
</programlisting>
    <para>
      Of course, all we've really done with this example is come up with
      a more cumbersome way of working with integers. Let's see what
      happens when with work with more complex abstract types.
    </para>
  </sect1>
  <sect1 id="standard-vs.-first-class-modules">
    <title>Standard vs. first-class modules</title>
    <para>
      <emphasis>(yminsky: I'm not in solve with the example. It feels in
      some sense too artificial, and that aside, when you get to the end
      of the example, you haven't really gotten any juice of first-class
      modules)</emphasis>
    </para>
    <para>
      <emphasis>(yminsky: using &quot;standard&quot; in quotes seems a
      little awkward. Maybe just drop the quotes, and talk about
      standard or ordinary modules directly?)</emphasis>
    </para>
    <para>
      Let's compare the style of &quot;standard&quot; modules to
      first-class modules, using a simple library of abstract geometric
      shapes. In a &quot;standard&quot; module definition, we would
      define the shapes using abstract data types, where there is a type
      <literal>t</literal> that defines the actual representation, and
      the module would include functions that operate on the values of
      type <literal>t</literal>. In the following code, the module type
      <literal>Shape</literal> defines the type of generic shape, and
      the modules <literal>Rectangle</literal> and
      <literal>Line</literal> implement some concrete shapes.
    </para>
    <programlisting language="ocaml">
module type Shape = sig
  type t
  val area : t -&gt; int
  val position : t -&gt; int * int
end

module Rectangle = struct
   type t = { width : int; height : int; x : int; y : int }
   let make ~x ~y ~width ~height =
      { width = width; height = height; x = x; y = y }
   let area { width = width; height = height } = width * height
   let position { x = x; y = y } = (x, y)
end

module Line = struct
   type t = { dx : int; dy : int; x : int; y : int }
   let make ~x ~y ~dx ~dy = { dx = dx; dy = dy; x = x; y = y }
   let area _ = 0
   let position { x = x; y = y } = (x, y)
end
</programlisting>
    <para>
      Next, if we want to define a generic shape that is either a
      rectangle or a line, we would probably use a variant type. The
      following module <literal>Shapes</literal> is entirely
      boilerplate. We define the variant type, then functions to perform
      a dynamic dispatch based on the type of object.
    </para>
    <programlisting language="ocaml">
module Shapes = struct
   type t = [ `Rect of Rectangle.t | `Line of Line.t ]
   let make_rectangle = Rectangle.make
   let make_line = Line.make
   let area = function
      `Rect r -&gt; Rectangle.area r
    | `Line l -&gt; Line.area l
   let position = function
      `Rect r -&gt; Rectangle.position r
    | `Line l -&gt; Line.position l
end;;
</programlisting>
    <para>
      In fact, confronted with this boilerplate, we would probably
      choose not use modules at all, but simply define a single module
      with a variant type and the code for all of the shapes. This isn't
      to say that separate code for separate shapes is bad, it just
      means that the language doesn't support it well (at least with
      standard modules).
    </para>
    <para>
      With first-class modules, the situation changes, but we have to
      dispense with the representation type altogether. For immutable
      shapes, the implementation is now trivial.
    </para>
    <programlisting language="ocaml">
# module type Shape = sig
    val area : int
    val position : int * int
  end;;
module type Shape = sig val area : int val position : int * int end
# let make_rectangle ~x ~y ~width ~height =
   let module Rectangle = struct
      let area = width * height
      let position = (x, y)
   end in
   (module Rectangle : Shape);;
val make_rectangle :
  x:int -&gt; y:int -&gt; width:int -&gt; height:int -&gt; (module Shape) = &lt;fun&gt;
# let make_line ~x ~y ~dx ~dy =
   let module Line = struct
      let area = 0
      let position = (x, y)
   end in
   (module Line : Shape);;
val make_line : x:int -&gt; y:int -&gt; dx:'a -&gt; dy:'b -&gt; (module Shape) = &lt;fun&gt;
</programlisting>
    <para>
      For mutable shapes, it isn't much different, but we have to
      include the state as values in the module implementations. For
      this, we'll define a representation type <literal>t</literal> in
      the module implementation, and for rectangles, a value
      <literal>rect</literal> of that type. The code for lines is
      similar.
    </para>
    <programlisting language="ocaml">
# module type Shape = sig
     val area : unit -&gt; int
     val position : unit -&gt; int * int
     val moveby : dx:int -&gt; dy:int -&gt; unit
     val enlargeby : size:int -&gt; unit
  end;;
module type Shape = ...
# let make_rectangle ~x ~y ~width ~height =
    let module Rectangle = struct
      type t = { mutable x : int; mutable y : int;
                 mutable width : int; mutable height : int }
      let rect = { x = x; y = y; width = width; height = height }
      let area () = rect.width * rect.height
      let position () = (rect.x, rect.y)
      let moveby ~dx ~dy =
         rect.x &lt;- rect.x + dx;
         rect.y &lt;- rect.y + dy
      let enlargeby ~size =
         rect.width &lt;- rect.width * size;
         rect.height &lt;- rect.height * size
    end in
    (module Rectangle : Shape);;
val make_rectangle :
  x:int -&gt; y:int -&gt; width:int -&gt; height:int -&gt; (module Shape) = &lt;fun&gt;
</programlisting>
  </sect1>
  <sect1 id="a-more-complete-example----containers">
    <title>A more complete example -- containers</title>
    <para>
      So far, we haven't done anything that really needs modules. The
      type <literal>Shape</literal> could just as well be specified as a
      record type
      <literal>type shape = { area : int; position : int * int; ... }</literal>.
    </para>
    <para>
      To explore the topic more fully, let's implement a system of
      dynamic containers. OCaml already provides a set of standard
      containers like <literal>List</literal>, <literal>Set</literal>,
      <literal>Hashtbl</literal>, etc., but these types have to be
      selected statically. If a function expects a value of type
      <literal>Set.Make(ElementType).t</literal>, then you have to pass
      it a set of exactly that type. What we would like is a kind of
      container where the container implementation is chosen by the
      caller. We define an abstract <emphasis>interface</emphasis>, as a
      module type, then define one or more concrete module
      implementations.
    </para>
    <para>
      Let's start by defining an abstract container interface. It
      contains some elements of type <literal>elt</literal>, and
      functions to examine and iterate through the contents. For
      convenience, we also define a normal type
      <literal>'a container</literal> to represent containers with
      elements of type <literal>'a</literal>.
    </para>
    <programlisting language="ocaml">
module type Container = sig
   type elt
   val empty : unit -&gt; bool
   val iter : (elt -&gt; unit) -&gt; unit
   val fold : ('a -&gt; elt -&gt; 'a) -&gt; 'a -&gt; 'a
end;;

type 'a container = (module Container with type elt = 'a)
</programlisting>
    <sect2 id="imperative-containers">
      <title>Imperative containers</title>
      <para>
        For imperative containers, will also want functions to mutate
        the contents by adding or removing elements. For example, a
        stack can be implemented as a module <literal>Stack</literal>
        that includes all the functions in the generic
        <literal>Container</literal> module, as well as functions to
        push and pop elements.
      </para>
      <programlisting language="ocaml">
module type Stack = sig
   include Container
   val push : elt -&gt; unit
   val pop : unit -&gt; elt
end;;

type 'a stack = (module Stack with type elt = 'a)
</programlisting>
      <para>
        Now that the types are defined, the next step is to define a
        concrete container implementation. For this simple example,
        we'll use a list to represent a stack. The function
        <literal>make_list_stack</literal> constructs module
        implementation using a <literal>let module</literal>
        construction, then returns the result.
      </para>
      <programlisting language="ocaml">
# let make_list_stack (type element) () : element stack =
    let module ListStack = struct
      type elt = element
      let contents = ref []
      let empty () = !contents = []
      let iter f = List.iter f !contents
      let fold f x = List.fold_left f x !contents
      let push x = contents := x :: !contents
      let pop () =
         match !contents with
            x :: rest -&gt; contents := rest; x
          | [] -&gt; raise (Invalid_argument &quot;stack is empty&quot;)
    end in
    (module ListStack : Stack with type elt = element);;
val make_list_stack : unit -&gt; 'a stack = &lt;fun&gt;
</programlisting>
      <para>
        Note the use of the explicit type parameter
        <literal>element</literal>. This is required because the use of
        a type variable in the module definition (like
        <literal>type elt = 'a</literal>) would be rejected by the
        compiler. The construction and use of the stack is
        straightforward.
      </para>
      <programlisting language="ocaml">
# let demo (s : int stack) =
    let module S = (val s) in
    S.push 5;
    S.push 17;
    S.iter (fun i -&gt; Printf.printf &quot;Element: %d\n&quot; i);;
val demo : int stack -&gt; unit = &lt;fun&gt;
# demo (make_list_stack ());;
Element: 17
Element: 5
- : unit = ()
</programlisting>
      <para>
        The <literal>demo</literal> function is entirely oblivious to
        the implementation of the stack. Instead of passing a module
        implementation based on lists, we could pass a different
        implementation based on arrays.
      </para>
      <para>
        We could go on to define other containers, sets, dictionaries,
        queues, etc. but the implementations would be similar to what we
        have seen. Instead, let's look at functional data structures,
        which require a little more work to express.
      </para>
    </sect2>
    <sect2 id="pure-functional-containers">
      <title>Pure functional containers</title>
      <para>
        Imperative data structures have simpler types that functional
        ones because the return type of imperative functions is just
        <literal>unit</literal>. When we look at pure functional data
        structures, we immediately run into a problem with type
        recursion.
      </para>
      <programlisting language="ocaml">
# module type Container = sig
    type elt
    val empty : bool
    val iter : (elt -&gt; unit) -&gt; unit
    val fold : ('a -&gt; elt -&gt; 'a) -&gt; 'a -&gt; 'a
    val add : elt -&gt; (module Container)
  end;;
Characters 160-178:
     val add : elt -&gt; (module Container)
                      ^^^^^^^^^^^^^^^^^^
Error: Unbound module type Container
</programlisting>
      <para>
        The problem here is that module type definitions are not
        recursive -- we can't use the type being defined in its own
        definition.
      </para>
      <para>
        Recursive modules provide a solution, but it requires a
        &quot;trick&quot;, where we define a module that is equal to
        itself. This module contains only type definitions, and the only
        purpose of the outer recursive module is to allow the recursion
        in the definition. While we're at it, let's include a
        <literal>map</literal> function with the usual semantics.
      </para>
      <programlisting language="ocaml">
module rec Container : sig
   module type T = sig
      type elt
      val empty : bool
      val iter : (elt -&gt; unit) -&gt; unit
      val fold : ('a -&gt; elt -&gt; 'a) -&gt; 'a -&gt; 'a
      val map : (elt -&gt; 'a) -&gt; 'a Container.t
      val add : elt -&gt; elt Container.t
   end
   type 'a t = (module Container.T with type elt = 'a)
end = Container;;
</programlisting>
      <para>
        There are several ways to write this model, but this definition
        is convenient because it defines both a module type
        <literal>Container.T</literal> and a value type
        <literal>'a Container.t</literal>. The outer recursive module
        <literal>Container</literal> allows the module type
        <literal>T</literal> to refer to the value type
        <literal>t</literal> and <emphasis>vice versa</emphasis>. Note
        that the module <literal>Container</literal> is defined as
        itself (as <literal>Container</literal>).
      </para>
      <para>
        With this first technicality out of the way, the next one is how
        to construct values of type <literal>Container.t</literal>. In
        the imperative version of the stack, we used a function
        <literal>make_list_stack</literal>. We want to do the same here,
        but the function definition must be both recursive and
        polymorphic.
      </para>
      <programlisting language="ocaml">
# let make_stack () =
    let rec make : 'a. 'a list -&gt; 'a Container.t = fun
      (type element) (contents : element list) -&gt;
      let module NewList = struct
         type elt = element
         let empty = contents = []
         let iter f = List.iter f contents
         let fold f x = List.fold_left f x contents
         let map f = make (List.map f contents)
         let add x = make (x :: contents)
      end in
      (module NewList : Container.T with type elt = element)
   in
   make [];;
val make_stack : unit -&gt; 'a Container.t = &lt;fun&gt;
</programlisting>
      <para>
        The recursion here is particularly important. The functions
        <literal>map</literal> and <literal>add</literal> return new
        collections, so they call the function <literal>make</literal>
        recursively. The explicit polymorphic type
        <literal>make : 'a. 'a list -&gt; 'a Container.t</literal> means
        that the function <literal>make</literal> is properly
        polymorphic, so that the <literal>map</literal> function is
        polymorphic.
      </para>
      <para>
        Now that the construction is done, the usage is similar to the
        imperative case, except that now the data structure is
        functional.
      </para>
      <programlisting language="ocaml">
# let demo (s : int Container.t) =
    let module S = (val s) in
    let module S = (val (S.add 5)) in
    let module S = (val (S.add 17)) in
    S.iter (fun i -&gt; Printf.printf &quot;Int Element: %d\n&quot; i);
    let s = S.map (fun i -&gt; float_of_int i +. 0.1) in
    let module S = (val s) in
    S.iter (fun x -&gt; Printf.printf &quot;Float Element: %f\n&quot; x);
    s;;
val demo : int Container.t -&gt; float Container.t = &lt;fun&gt;
# demo (make_stack ());;
Int Element: 17
Int Element: 5
Float Element: 17.100000
Float Element: 5.100000
- : unit = ()
</programlisting>
      <para>
        The syntactic load here is pretty high, requiring a
        <literal>let module</literal> expression to name every
        intermediate value. First-class modules are fairly new to the
        language, and this is likely to change, but in the meantime the
        syntactic load can be pretty daunting.
      </para>
      <para>
        Let's look a some other more typical examples, where dynamic
        module selection is more localized.
      </para>
      <para>
        _(jyh: This is a rough draft, I'm not sure about the ordering
        and the topics, yet. Switching back to Ron's text now.)
      </para>
    </sect2>
  </sect1>
  <sect1 id="dynamically-choosing-a-module">
    <title>Dynamically choosing a module</title>
    <para>
      Perhaps the simplest thing you can do with first-class modules
      that you can't do without them is to pick the implementation of a
      module at runtime.
    </para>
    <para>
      Consider an application that does I/O multiplexing using a system
      call like <literal>select</literal> to determine which file
      descriptors are ready to use. There are in fact multiple APIs you
      might want to use, including <literal>select</literal> itself,
      <literal>epoll</literal>, and <literal>libev</literal>, where
      different multiplexers make somewhat different performance and
      portability trade-offs. You could support all of these in one
      application by defining a single module, let's call it
      <literal>Mutliplexer</literal>, whose implementation is chosen at
      run-time based on an environment variable.
    </para>
    <para>
      To do this, you'd first need an interface <literal>S</literal>
      that all of the different multiplexer implementations would need
      to match, and then an implementation of each multiplexer.
    </para>
    <programlisting language="ocaml">
(* file: multiplexer.ml *)

(* An interface the OS-specific functionality *)
module type S = sig ... end

(* The implementations of each individual multiplexer *)
module Select : S = struct ... end
module Epoll  : S = struct ... end
module Libev  : S = struct ... end
</programlisting>
    <para>
      We can choose the first-class module that we want based on looking
      up an environment variable.
    </para>
    <programlisting language="ocaml">
let multiplexer =
  match Sys.getenv &quot;MULTIPLEXER&quot; with
  | None
  | Some &quot;select&quot; -&gt; (module Select : S)
  | Some &quot;epoll&quot;  -&gt; (module Epoll : S)
  | Some &quot;libev&quot;  -&gt; (module Libev : S)
  | Some other -&gt; failwithf &quot;Unknown multiplexer: %s&quot; other ()
</programlisting>
    <para>
      Finally, we can convert the resulting first-class module back to
      an ordinary module, and then include that so it becomes part of
      the body of our module.
    </para>
    <programlisting language="ocaml">
(* The final, dynamically chosen, implementation *)
include (val multiplexer : S)
</programlisting>
  </sect1>
  <sect1 id="example-a-service-bundle">
    <title>Example: A service bundle</title>
    <para>
      This section describes the design of a library for bundling
      together multiple services, where a service is a piece of code
      that exports a query interface. A service bundle combines together
      multiple individual services under a single query interface that
      works by dispatching incoming queries to the appropriate
      underlying service.
    </para>
    <para>
      The following is a first attempt at an interface for our
      <literal>Service</literal> module, which contains both a module
      type <literal>S</literal>, which is the interface that a service
      should meet, as well as a <literal>Bundle</literal> module which
      is for combining multiple services.
    </para>
    <programlisting language="ocaml">
(* file: service.mli *)

open Core.Std

(** The module type for a service. *)
module type S = sig
  type t
  val name           : string
  val create         : unit -&gt; t
  val handle_request : t -&gt; Sexp.t -&gt; Sexp.t Or_error.t
end

(** Bundles multiple services together *)
module Bundle : sig
  type t
  val create : (module S) list -&gt; t
  val handle_request : t -&gt; Sexp.t -&gt; Sexp.t Or_error.t
  val service_names  : t -&gt; string list
end
</programlisting>
    <para>
      Here, a service has a state, represented by the type
      <literal>t</literal>, a name by which the service can be
      referenced, a function <literal>create</literal> for instantiating
      a service, and a function by which a service can actually handle a
      request. Here, requests and responses are delivered as
      s-expressions. At the <literal>Bundle</literal> level, the
      s-expression of a request is expected to be formatted as follows:
    </para>
    <programlisting>
(&lt;service-name&gt; &lt;body&gt;)
</programlisting>
    <para>
      where <literal>&lt;service_name&gt;</literal> is the service that
      should handle the request, and <literal>&lt;body&gt;</literal> is
      the body of the request.
    </para>
    <para>
      Now let's look at how to implement <literal>Service</literal>. The
      core datastructure of <literal>Bundle</literal> is a hashtable of
      request handlers, one per service. Each request handler is a
      function of type
      <literal>(Sexp.t -&gt; Sexp.t Or_error.t)</literal>. These request
      handlers really stand in for the underlying service, with the
      particular state of the service in question being hidden inside of
      the request handler.
    </para>
    <para>
      The first part of <literal>service.ml</literal> is just the
      preliminaries: the definition of the module type
      <literal>S</literal>, and the definition of the type
      <literal>Bundle.t</literal>.
    </para>
    <programlisting language="ocaml">
(* file: service.ml *)

open Core.Std

module type S = sig
  type t
  val name           : string
  val create         : unit -&gt; t
  val handle_request : t -&gt; Sexp.t -&gt; Sexp.t Or_error.t
end

module Bundle = struct
  type t = { handlers: (Sexp.t -&gt; Sexp.t Or_error.t) String.Table.t; }
</programlisting>
    <para>
      The next thing we need is a function for creating a
      <literal>Bundle.t</literal>. This <literal>create</literal>
      function builds a table to hold the request handlers, and then
      iterates through the services, unpacking each module, constructing
      the request handler, and then putting that request handler in the
      table.
    </para>
    <programlisting language="ocaml">
  (** Creates a handler given a list of services *)
  let create services =
    let handlers = String.Table.create () in
    List.iter services ~f:(fun service_m -&gt;
      let module Service = (val service_m : S) in
      let service = Service.create () in
      if Hashtbl.mem handlers Service.name then
        failwith (&quot;Attempt to register duplicate handler for &quot;^Service.name);
      Hashtbl.replace handlers ~key:Service.name
        ~data:(fun sexp -&gt; Service.handle_request service sexp)
    );
    {handlers}
</programlisting>
    <para>
      Note that the <literal>Service.t</literal> that is created is
      referenced by the corresponding request handler, so that it is
      effectively hidden behind the function in the
      <literal>handlers</literal> table.
    </para>
    <para>
      Now we can write the function for the bundle to handle requests.
      The handler will examine the s-expression to determine the body of
      the query and the name of the service to dispatch to. It then
      looks up the handler calls it to generate the response.
    </para>
    <programlisting language="ocaml">
  let handle_request t sexp =
    match sexp with
    | Sexp.List [Sexp.Atom name;query] -&gt;
      begin match Hashtbl.find t.handlers name with
      | None -&gt; Or_error.error_string (&quot;Unknown service: &quot;^name)
      | Some handler -&gt;
        try handler query
        with exn -&gt; Error (Error.of_exn exn)
      end
    | _ -&gt; Or_error.error_string &quot;Malformed query&quot;
</programlisting>
    <para>
      Last of all, we define a function for looking up the names of the
      available services.
    </para>
    <programlisting language="ocaml">
  let service_names t = Hashtbl.keys t.handlers

end
</programlisting>
    <para>
      To see this system in action, we need to define some services,
      create the corresponding bundle, and then hook that bundle up to
      some kind of client. For simplicity, we'll build a simple
      command-line interface. There are two functions below:
      <literal>handle_one</literal>, which handles a single interaction;
      and <literal>handle_loop</literal>, which creates the bundle and
      then runs <literal>handle_one</literal> in a loop.
    </para>
    <programlisting language="ocaml">
(* file: service_client.ml *)

open Core.Std

(** Handles a single request coming from stdin *)
let handle_one bundle =
  printf &quot;&gt;&gt;&gt; %!&quot;; (* prompt *)
  match In_channel.input_line stdin with
  | None -&gt; `Stop (* terminate on end-of-stream, so Ctrl-D will exit *)
  | Some line -&gt;
    let line = String.strip line in (* drop leading and trailing whitespace *)
    if line = &quot;&quot; then `Continue
    else match Or_error.try_with (fun () -&gt; Sexp.of_string line) with
    | Error err -&gt;
      eprintf &quot;Couldn't parse query: %s\n%!&quot; (Error.to_string_hum err);
      `Continue
    | Ok query_sexp -&gt;
      let resp = Service.Bundle.handle_request bundle query_sexp in
      Sexp.output_hum stdout (&lt;:sexp_of&lt;Sexp.t Or_error.t&gt;&gt; resp);
      Out_channel.newline stdout;
      `Continue

let handle_loop services =
  let bundle = Service.Bundle.create services in
  let rec loop () =
    match handle_one bundle with
    | `Stop -&gt; ()
    | `Continue -&gt; loop ()
  in
  loop ()
</programlisting>
    <para>
      Now we'll create a couple of toy services. One service is a
      counter that can be updated by query; and the other service lists
      a directory. The last line then kicks off the shell with the
      services we've defined.
    </para>
    <programlisting language="ocaml">
module Counter : Service.S = struct
  type t = int ref

  let name = &quot;update-counter&quot;
  let create () = ref 0

  let handle_request t sexp =
    match Or_error.try_with (fun () -&gt; int_of_sexp sexp) with
    | Error _ as err -&gt; err
    | Ok x -&gt;
      t := !t + x;
      Ok (sexp_of_int !t)
end

module List_dir : Service.S = struct
  type t = unit

  let name = &quot;ls&quot;
  let create () = ()

  let handle_request () sexp =
    match Or_error.try_with (fun () -&gt; string_of_sexp sexp) with
    | Error _ as err -&gt; err
    | Ok dir -&gt; Ok (Array.sexp_of_t String.sexp_of_t (Sys.readdir dir))
end

let () =
  handle_loop [(module List_dir : Service.S); (module Counter : Service.S)]
</programlisting>
    <para>
      And now we can go ahead and start up the client.
    </para>
    <programlisting>
$ ./service_client.byte
&gt;&gt;&gt; (update-counter 1)
(Ok 1)
&gt;&gt;&gt; (update-counter 10)
(Ok 11)
&gt;&gt;&gt; (ls .)
(Ok
 (_build _tags service.ml service.mli service.mli~ service.ml~
  service_client.byte service_client.ml service_client.ml~))
&gt;&gt;&gt;
</programlisting>
    <para>
      Now, let's consider what happens to the design when we want to
      make the interface of a service a bit more realistic. In
      particular, right now services are created without any
      configuration. Let's add a config type to each service, and change
      the interface of <literal>Bundle</literal> so that services can be
      registered along with their configs. At the same time, we'll
      change the <literal>Bundle</literal> API to allow services to be
      changed dynamically, rather than just added at creation time.
    </para>
  </sect1>
</chapter></part><part label="III"><title>Advanced Topics</title><chapter id="foreign-function-interface">
  <title>Foreign Function Interface</title>
  <para>
    Much of the static type information contained within an OCaml
    program is checked and discarded at compilation time, leaving a much
    simpler <emphasis>runtime</emphasis> representation for values.
    Understanding this difference is important for writing efficient
    programs, and also for interfacing with C libraries that work
    directly with the runtime system.
  </para>
  <note>
  <title>
  Why do OCaml types disappear at runtime?
  </title>
  <para>
    The OCaml compiler runs through several phases of during the
    compilation process. After syntax checking, the next stage is
    <emphasis>type checking</emphasis>. In a validly typed program, a
    function cannot be applied with an unexpected type. For example, the
    <literal>print_endline</literal> function must receive a single
    <literal>string</literal> argument, and an <literal>int</literal>
    will result in a type error.
  </para>
  <para>
    Since OCaml verifies these properties at compile time, it doesn't
    need to keep track of as much information at runtime. Thus, later
    stages of the compiler can discard and simplify the type
    declarations to a much more minimal subset that's actually required
    to distinguish polymorphic values at runtime. This is a major
    performance win versus something like a Java or .NET method call,
    where the runtime must look up the concrete instance of the object
    and dispatch the method call. Those languages amortize some of the
    cost via &quot;Just-in-Time&quot; dynamic patching, but OCaml
    prefers runtime simplicity instead.
  </para>
  </note>
  <para>
    Let's start by explaining the memory layout, and then move onto the
    details of how C bindings work.
  </para>
  <sect1 id="the-garbage-collector">
    <title>The garbage collector</title>
    <para>
      A running OCaml program uses blocks of memory (i.e. contiguous
      sequences of words in RAM) to represent many of the values that it
      deals with such as tuples, records, closures or arrays. An OCaml
      program implicitly allocates a block of memory when such a value
      is created.
    </para>
    <programlisting>
# let x = { foo = 13; bar = 14 } ;;
</programlisting>
    <para>
      An expression such as the record above requires a new block of
      memory with two words of available space. One word holds the
      <literal>foo</literal> field and the second word holds the
      <literal>bar</literal> field. The OCaml compiler translates such
      an expression into an explicit allocation for the block from
      OCaml's runtime system: a C library that provides a collection of
      routines that can be called by running OCaml programs. The runtime
      system manages a <emphasis>heap</emphasis>, which a collection of
      memory regions it obtains from the operating system using
      <emphasis>malloc(3)</emphasis>. The OCaml runtime uses these
      memory regions to hold <emphasis>heap blocks</emphasis>, which it
      then fills up in response to allocation requests by the OCaml
      program.
    </para>
    <para>
      When there is'nt enough memory available to satisfy an allocation
      request from the allocated heap blocks, the runtime system invokes
      the <emphasis>garbage collector</emphasis> (or GC). An OCaml
      program does not explicitly free a heap block when it is done with
      it, and the GC must determine which heap blocks are
      &quot;alive&quot; and which heap blocks are
      <emphasis>dead</emphasis>, i.e. no longer in use. Dead blocks are
      collected and their memory made available for re-use by the
      application.
    </para>
    <para>
      The garbage collector does not keep constant track of blocks as
      they are allocated and used. Instead, it regularly scans blocks by
      starting from a set of <emphasis>roots</emphasis>, which are
      values that the application always has access to (such as the
      stack). The GC maintains a directed graph in which heap blocks are
      nodes, and there is an edge from heap block <literal>b1</literal>
      to heap block <literal>b2</literal> if some field of
      <literal>b1</literal> points to <literal>b2</literal>. All blocks
      reachable from the roots by following edges in the graph must be
      retained, and unreachable blocks can be reused.
    </para>
    <para>
      With the typical OCaml programming style, many small blocks are
      frequently allocated, used for a short period of time, and then
      never used again. OCaml takes advantage of this fact to improve
      the performance of allocation and collection by using a
      <emphasis>generational</emphasis> garbage collector. This means
      that it has different memory regions to hold blocks based on how
      long the blocks have been alive. OCaml's heap is split in two;
      there is a small, fixed-size <emphasis>minor heap</emphasis> used
      for initially allocating most blocks, and a large, variable-sized
      <emphasis>major heap</emphasis> for holding blocks that have been
      alive longer or are larger than 4KB. A typical functional
      programming style means that young blocks tend to die young, and
      old blocks tend to stay around for longer than young ones (this is
      referred to as the <emphasis>generational hypothesis</emphasis>).
      To reflect this, OCaml uses different memory layouts and garbage
      collection algorithms for the major and minor heaps.
    </para>
    <sect2 id="the-fast-minor-heap">
      <title>The fast minor heap</title>
      <para>
        The minor heap is one contiguous chunk of memory containing a
        sequence of heap blocks that have been allocated. If there is
        space, allocating a new block is a fast constant-time operation
        in which the pointer to the end of the heap is incremented by
        the desired size. To garbage collect the minor heap, OCaml uses
        <emphasis>copying collection</emphasis> to copy all live blocks
        in the minor heap to the major heap. This only takes work
        proportional to the number of live blocks in the minor heap,
        which is typically small according to the generational
        hypothesis.
      </para>
      <para>
        One complexity of generational collection is that in order to
        know which blocks in the minor heap are live, the collector must
        know which minor-heap blocks are directly pointed to by
        major-heap blocks. To do this, OCaml maintains a set of such
        inter-generational pointers, and, through cooperation with the
        compiler, uses a write barrier to update this set whenever a
        major-heap block is modified to point at a minor-heap block.
      </para>
    </sect2>
    <sect2 id="the-long-lived-major-heap">
      <title>The long-lived major heap</title>
      <para>
        The major heap consists of a number of chunks of memory, each
        containing live blocks interspersed with regions of free memory.
        The runtime system maintains a free list data structure that
        indexes all the free memory, and this list is used to satisfy
        allocation requests. OCaml uses mark and sweep garbage
        collection for the major heap. The <emphasis>mark</emphasis>
        phase to traverses the block graph and marks all live blocks by
        setting a bit in the color tag of the block header.
        (<emphasis>avsm</emphasis>: we only explain the color tag in the
        next section, so rephrase or xref).
      </para>
      <para>
        The <emphasis>sweep</emphasis> phase sequentially scans all heap
        memory and identifies dead blocks that weren't marked earlier.
        The <emphasis>compact</emphasis> phase relocates live blocks to
        eliminate the gaps of free memory between them and ensure memory
        does not fragment.
      </para>
      <para>
        A garbage collection must <emphasis>stop the world</emphasis>
        (that is, halt the application) in order to ensure that blocks
        can be safely moved. The mark and sweep phases run incrementally
        over slices of memory, and are broken up into a number of steps
        that are interspersed with the running OCaml program. Only a
        compaction touches all the memory in one go, and is a relatively
        rare operation.
      </para>
      <para>
        The <literal>Gc</literal> module lets you control all these
        parameters from your application, and we will discuss garbage
        collection tuning in (<emphasis>avsm</emphasis>: crossref).
      </para>
    </sect2>
  </sect1>
  <sect1 id="the-representation-of-values">
    <title>The representation of values</title>
    <para>
      Every OCaml <emphasis>value</emphasis> is a single word that is
      either an integer or a pointer. If the lowest bit of the word is
      non-zero, the value is an unboxed integer. Several OCaml types map
      onto this integer representation, including
      <literal>bool</literal>, <literal>int</literal>, the empty list,
      <literal>unit</literal>, and variants without constructors.
      Integers are the only unboxed runtime values in OCaml, and are the
      cheapest values to allocate.
    </para>
    <para>
      If the lowest bit of the <literal>value</literal> is zero, then
      the value is a pointer. A pointer value is stored unmodified,
      since pointers are guaranteed to be word-aligned and the bottom
      bits are always zero. If the pointer is inside an area managed by
      the OCaml runtime, it is assumed to point to an OCaml
      <emphasis>block</emphasis>. If it points outside the OCaml runtime
      area, it is is treated as an opaque C pointer to some other system
      resource.
    </para>
    <sect2 id="blocks-and-values">
      <title>Blocks and values</title>
      <para>
        An OCaml <emphasis>block</emphasis> is the basic unit of
        allocation on the heap. A block consists of a one-word header
        (either 32- or 64-bits) followed by variable-length data, which
        is either opaque bytes or <emphasis>fields</emphasis>. The
        collector never inspects opaque bytes, but fields are valid
        OCaml values. The runtime always inspects fields, and follows
        them as part of the garbage collection process described
        earlier. Every block header has a multipurpose tag byte that
        defines whether to interprete the subsequent data as opaque or
        OCaml fields.
      </para>
      <para>
        (<emphasis>avsm</emphasis>: pointers to blocks actually point
        4/8 bytes into it, for some efficiency reason that I cannot
        recall right now).
      </para>
      <programlisting>
+------------------------+-------+----------+----------+----------+----
| size of block in words |  col  | tag byte | value[0] | value[1] | ...
+------------------------+-------+----------+----------+----------+----
 &lt;-either 22 or 54 bits-&gt; &lt;2 bit&gt; &lt;--8 bit--&gt;
</programlisting>
      <para>
        The size field records the length of the block in memory words.
        Note that it is limited to 22-bits on 32-bit platforms, which is
        the reason why OCaml strings are limited to 16MB on that
        architecture. If you need bigger strings, either switch to a
        64-bit host, or use the <literal>Bigarray</literal> module
        (<emphasis>avsm</emphasis>: xref). The 2-bit color field is used
        by the garbage collector to keep track of its status, and is not
        exposed directly to OCaml programs.
      </para>
      <informaltable>
        <tgroup cols="2">
          <colspec align="left"/>
          <colspec align="left"/>
          <thead>
            <row>
              <entry>
                Tag Color
              </entry>
              <entry>
                Block Status
              </entry>
            </row>
          </thead>
          <tbody>
            <row>
              <entry>
                blue
              </entry>
              <entry>
                on the free list and not currently in use
              </entry>
            </row>
            <row>
              <entry>
                white
              </entry>
              <entry>
                not reached yet, but possibly reachable
              </entry>
            </row>
            <row>
              <entry>
                gray
              </entry>
              <entry>
                reachable, but its fields have not been scanned
              </entry>
            </row>
            <row>
              <entry>
                black
              </entry>
              <entry>
                reachable, and its fields have been scanned
              </entry>
            </row>
          </tbody>
        </tgroup>
      </informaltable>
      <para>
        A block's tag byte is multi-purpose, and indicates whether the
        data array represents opaque bytes or fields. If a block's tag
        is greater than or equal to <literal>No_scan_tag</literal>
        (251), then the block's data are all opaque bytes, and are not
        scanned by the collector. The most common such block is the
        <literal>string</literal> type, which we describe more below.
      </para>
      <para>
        (<emphasis>avsm</emphasis>: too much info here) If the header is
        zero, then the object has been forwarded as part of minor
        collection, and the first field points to the new location.
        Also, if the block is on the
        <literal>oldify_todo_list</literal>, part of the minor gc, then
        the second field points to the next entry on the
        oldify_todo_list.
      </para>
      <para>
        The exact representation of values inside a block depends on
        their OCaml type. They are summarised in the table below, and
        then we'll examine some of them in greater detail.
      </para>
      <informaltable>
        <tgroup cols="2">
          <colspec align="left"/>
          <colspec align="left"/>
          <thead>
            <row>
              <entry>
                OCaml Value
              </entry>
              <entry>
                Representation
              </entry>
            </row>
          </thead>
          <tbody>
            <row>
              <entry>
                any <literal>int</literal> or <literal>char</literal>
              </entry>
              <entry>
                directly as a value, shifted left by 1 bit, with the
                least significant bit set to 1
              </entry>
            </row>
            <row>
              <entry>
                <literal>unit</literal>, <literal>[]</literal>,
                <literal>false</literal>
              </entry>
              <entry>
                as OCaml <literal>int</literal> 0.
              </entry>
            </row>
            <row>
              <entry>
                <literal>true</literal>
              </entry>
              <entry>
                as OCaml <literal>int</literal> 1.
              </entry>
            </row>
            <row>
              <entry>
                <literal>Foo | Bar</literal>
              </entry>
              <entry>
                as ascending OCaml <literal>int</literal>s, starting
                from 0.
              </entry>
            </row>
            <row>
              <entry>
                <literal>Foo | Bar of int</literal>
              </entry>
              <entry>
                variants with parameters are boxed, while entries with
                no parameters are unboxed (see below).
              </entry>
            </row>
            <row>
              <entry>
                polymorphic variants
              </entry>
              <entry>
                variable space usage depending on the number of
                parameters (see below).
              </entry>
            </row>
            <row>
              <entry>
                floating point number
              </entry>
              <entry>
                as a block with a single field containing the
                double-precision float.
              </entry>
            </row>
            <row>
              <entry>
                string
              </entry>
              <entry>
                word-aligned byte arrays that are also directly
                compatible with C strings.
              </entry>
            </row>
            <row>
              <entry>
                <literal>[1; 2; 3]</literal>
              </entry>
              <entry>
                as <literal>1::2::3::[]</literal> where
                <literal>[]</literal> is an int, and
                <literal>h::t</literal> a block with tag 0 and two
                parameters.
              </entry>
            </row>
            <row>
              <entry>
                tuples, records and arrays
              </entry>
              <entry>
                an array of values. Arrays can be variable size, but
                structs and tuples are fixed size.
              </entry>
            </row>
            <row>
              <entry>
                records or arrays, all float
              </entry>
              <entry>
                special tag for unboxed arrays of floats. Doesn't apply
                to tuples.
              </entry>
            </row>
          </tbody>
        </tgroup>
      </informaltable>
    </sect2>
    <sect2 id="integers-characters-and-other-basic-types">
      <title>Integers, characters and other basic types</title>
      <para>
        Many basic types are stored directly as unboxed values at
        runtime. The native <literal>int</literal> type is the most
        obvious, although it drops a single bit of precision due to the
        tag bit described earlier. Other atomic types such as the
        <literal>unit</literal> and empty list <literal>[]</literal>
        value are stored as constant integers. Boolean values have a
        value of <literal>0</literal> and <literal>1</literal> for
        <literal>true</literal> and <literal>false</literal>
        respectively.
      </para>
      <note>
      <title>
      Why are OCaml integers missing a bit?
      </title>
      <para>
        Since the lowest bit of an OCaml value is reserved, native OCaml
        integers have a maximum allowable length of 31- or 63-bits,
        depending on the host architecture. The rationale for reserving
        the lowest bit is for efficiency. Pointers always point to
        word-aligned addresses, and so their lower bits are normally
        zero. By setting the lower bit to a non-zero value for integers,
        the garbage collector can simply iterate over every header tag
        to distinguish integers from pointers. This reduces the garbage
        collection overhead on the overall program.
      </para>
      </note>
      <para>
        (<emphasis>avsm</emphasis>: explain that integer manipulation is
        almost as fast due to isa quirks)
      </para>
    </sect2>
    <sect2 id="tuples-records-and-arrays">
      <title>Tuples, records and arrays</title>
      <programlisting>
+---------+----------+----------- - - - - 
| header  | value[0] | value[1] | ....
+---------+----------+----------+- - - - -
</programlisting>
      <para>
        Tuples, records and arrays are all represented identically at
        runtime, with a block with tag <literal>0</literal>. Tuples and
        records have constant sizes determined at compile-time, whereas
        arrays can be of variable length. While arrays are restricted to
        containing a single type of element in the OCaml type system,
        this is not required by the memory representation.
      </para>
      <para>
        You can check the difference between a block and a direct
        integer yourself using the <literal>Obj</literal> module, which
        exposes the internal representation of values to OCaml code.
      </para>
      <programlisting language="ocaml">
# Obj.is_block (Obj.repr (1,2,3)) ;;
- : bool = true
# Obj.is_block (Obj.repr 1) ;;
- : bool = false
</programlisting>
      <para>
        The <literal>Obj.repr</literal> function retrieves the runtime
        representation of any OCaml value.
        <literal>Obj.is_block</literal> checks the bottom bit to
        determine if the value is a block header or an unboxed integer.
      </para>
    </sect2>
    <sect2 id="floating-point-numbers-and-arrays">
      <title>Floating point numbers and arrays</title>
      <para>
        Floating point numbers in OCaml are always stored as full
        double-precision values. Individual floating point values are
        stored as a block with a single field that contains the number.
        This block has the <literal>Double_tag</literal> set which
        signals to the collector that the floating point value is not to
        be scanned.
      </para>
      <programlisting language="ocaml">
# Obj.tag (Obj.repr 1.0) = Obj.double_tag ;;
- : int = 253
# Obj.double_tag ;;
- : int = 253
</programlisting>
      <para>
        Since each floating-point value is boxed in a separate memory
        block, it can be inefficient to handle large arrays of floats in
        comparison to unboxed integers. OCaml therefore special-cases
        records or arrays that contain <emphasis>only</emphasis>
        <literal>float</literal> types. These are stored in a block that
        contains the floats packed directly in the data section, with
        the <literal>Double_array_tag</literal> set to signal to the
        collector that the contents are not OCaml values.
      </para>
      <programlisting>
+---------+----------+----------- - - - - 
| header  | float[0] | float[1] | ....
+---------+----------+----------+- - - - -
</programlisting>
      <para>
        You can test this for yourself using the
        <literal>Obj.tag</literal> function to check that the allocated
        block has the expected runtime tag, and
        <literal>Obj.double_field</literal> to retrieve a float from
        within the block.
      </para>
      <programlisting language="ocaml">
# open Obj ;;
# tag (repr [| 1.0; 2.0; 3.0 |]) ;;
- : int = 254
# tag (repr (1.0, 2.0, 3.0) ) ;;
- : int = 0 
# double_field (repr [| 1.1; 2.2; 3.3 |] ) 1 ;;
- : float = 2.2
# Obj.double_field (Obj.repr 1.234) 0;;
- : float = 1.234
</programlisting>
      <para>
        Notice that float tuples are <emphasis>not</emphasis> optimized
        in the same way as float records or arrays, and so they have the
        usual tuple tag value of <literal>0</literal>. Only records and
        arrays can have the array optimization, and only if every single
        field is a float.
      </para>
    </sect2>
    <sect2 id="variants-and-lists">
      <title>Variants and lists</title>
      <para>
        Basic variant types with no extra parameters for any of their
        branches are simply stored as an OCaml integer, starting with
        <literal>0</literal> for the first option and in ascending
        order.
      </para>
      <programlisting language="ocaml">
# open Obj ;;
# type t = Apple | Orange | Pear ;;
type t = Apple | Orange | Pear
# ((magic (repr Apple)) : int) ;;
- : int = 0
# ((magic (repr Pear)) : int) ;;
- : int = 2
# is_block (repr Apple) ;;
- : bool = false
</programlisting>
      <para>
        <literal>Obj.magic</literal> unsafely forces a type cast between
        any two OCaml types; in this example the <literal>int</literal>
        type hint retrieves the runtime integer value. The
        <literal>Obj.is_block</literal> confirms that the value isn't a
        more complex block, but just an OCaml <literal>int</literal>.
      </para>
      <para>
        Variants that have parameters arguments are a little more
        complex. They are stored as blocks, with the value
        <emphasis>tags</emphasis> ascending from 0 (counting from
        leftmost variants with parameters). The parameters are stored as
        words in the block.
      </para>
      <programlisting language="ocaml">
# type t = Apple | Orange of int | Pear of string | Kiwi ;;
type t = Apple | Orange of int | Pear of string | Kiwi
# is_block (repr (Orange 1234)) ;;
- : bool = true
# tag (repr (Orange 1234)) ;; 
- : int = 0
# tag (repr (Pear &quot;xyz&quot;)) ;;
- : int = 1
# (magic (field (repr (Orange 1234)) 0) : int) ;;
- : int = 1234
(magic (field (repr (Pear &quot;xyz&quot;)) 0) : string) ;;
- : string = &quot;xyz&quot;
</programlisting>
      <para>
        In the above example, the <literal>Apple</literal> and
        <literal>Kiwi</literal> values are still stored as normal OCaml
        integers with values <literal>0</literal> and
        <literal>1</literal> respectively. The <literal>Orange</literal>
        and <literal>Pear</literal> values both have parameters, and are
        stored as blocks whose tags ascend from <literal>0</literal>
        (and so <literal>Pear</literal> has a tag of
        <literal>1</literal>, as the use of <literal>Obj.tag</literal>
        verifies). Finally, the parameters are fields which contain
        OCaml values within the block, and <literal>Obj.field</literal>
        can be used to retrieve them.
      </para>
      <para>
        Lists are stored with a representation that is exactly the same
        as if the list was written as a variant type with
        <literal>Head</literal> and <literal>Cons</literal>. The empty
        list <literal>[]</literal> is an integer <literal>0</literal>,
        and subsequent blocks have tag <literal>0</literal> and two
        parameters: a block with the current value, and a pointer to the
        rest of the list.
      </para>
      <warning>
      <title>
      <literal>Obj</literal> module considered harmful
      </title>
      <para>
        The <literal>Obj</literal> module is an undocumented module that
        exposes the internals of the OCaml compiler and runtime. It is
        very useful for examining and understanding how your code will
        behave at runtime, but should <emphasis>never</emphasis> be used
        for production code unless you understand the implications. The
        module bypasses the OCaml type system, making memory corruption
        and segmentation faults possible.
      </para>
      <para>
        Some theorem provers such as Coq do output code which uses
        <literal>Obj</literal> internally, but the external module
        signatures never expose it. Unless you too have a machine proof
        of correctness to accompany your use of <literal>Obj</literal>,
        stay away from it except for debugging!
      </para>
      </warning>
      <para>
        Due to this encoding, there is a limit around 240 variants with
        parameters that applies to each type definition, but the only
        limit on the number of variants without parameters is the size
        of the native integer (either 31- or 63-bits). This limit arises
        because of the size of the tag byte, and that some of the high
        numbered tags are reserved.
      </para>
    </sect2>
    <sect2 id="polymorphic-variants-1">
      <title>Polymorphic variants</title>
      <para>
        Polymorphic variants are more flexible than normal variants when
        writing code, but can be less efficient at runtime. This is
        because there isn't as much static compile-time information
        available to optimise their memory layout. This isn't always the
        case, however. A polymorphic variant without any parameters is
        stored as an unboxed integer and so only takes up one word of
        memory. Unlike normal variants, the integer value is determined
        by apply a hash function to the <emphasis>name</emphasis> of the
        variant. The hash function isn't exposed directly by the
        compiler, but the <literal>type_conv</literal> library from Core
        provides an alternative implementation.
      </para>
      <programlisting language="ocaml">
# #require &quot;type_conv&quot; ;;
# Pa_type_conv.hash_variant &quot;Foo&quot; ;;
- : int = 3505894
# (Obj.magic (Obj.repr `Foo) : int) ;;
- : int = 3505894
</programlisting>
      <para>
        The hash function is designed to give the same results on 32-bit
        and 64-bit architectures, so the memory representation is stable
        across different CPUs and host types.
      </para>
      <para>
        Polymorphic variants use more memory space when parameters are
        included in the datatype constructors. Normal variants use the
        tag byte to encode the variant value, but this byte is
        insufficient to encode the hashed value for polymoprhic
        variants. Therefore, they must allocate a new block (with tag
        <literal>0</literal>) and store the value in there instead. This
        means that polymorphic variants with constructors use one word
        of memory more than normal variant constructors.
      </para>
      <para>
        Another inefficiency is when a polymorphic variant constructor
        has more than one parameter. Normal variants hold parameters as
        a single flat block with multiple fields for each entry, but
        polymorphic variants must adopt a more flexible uniform memory
        representation since they may be re-used in a different context.
        They allocate a tuple block for the parameters that is pointed
        to from the argument field of the variant. Thus, there are three
        additional words for such variants, along with an extra memory
        indirection due to the tuple.
      </para>
    </sect2>
    <sect2 id="string-values">
      <title>String values</title>
      <para>
        Strings are standard OCaml blocks with the header size defining
        the size of the string in machine words. The
        <literal>String_tag</literal> (252) is higher than the
        <literal>No_scan_tag</literal>, indicating that the contents of
        the block are opaque to the collector. The block contents are
        the contents of the string, with padding bytes to align the
        block on a word boundary.
      </para>
      <programlisting language="ocaml">
+---------------+----------------+--------+-----------+
| header        | 'a' 'b' 'c' 'd' 'e' 'f' | '\O' '\1' |
+---------------+----------------+--------+-----------+
                L data                    L padding
</programlisting>
      <para>
        On a 32-bit machine, the padding is calculated based on the
        modulo of the string length and word size to ensure the result
        is word-aligned. A 64-bit machine extends the potential padding
        up to 7 bytes instead of 3.
      </para>
      <informaltable>
        <tgroup cols="2">
          <colspec align="left"/>
          <colspec align="left"/>
          <thead>
            <row>
              <entry>
                String length mod 4
              </entry>
              <entry>
                Padding
              </entry>
            </row>
          </thead>
          <tbody>
            <row>
              <entry>
                0
              </entry>
              <entry>
                <literal>00 00 00 03</literal>
              </entry>
            </row>
            <row>
              <entry>
                1
              </entry>
              <entry>
                <literal>00 00 02</literal>
              </entry>
            </row>
            <row>
              <entry>
                2
              </entry>
              <entry>
                <literal>00 01</literal>
              </entry>
            </row>
            <row>
              <entry>
                3
              </entry>
              <entry>
                <literal>00</literal>
              </entry>
            </row>
          </tbody>
        </tgroup>
      </informaltable>
      <para>
        This string representation is a clever way to ensure that the
        string contents are always zero-terminated by the padding word,
        and still compute its length efficiently without scanning the
        whole string. The following formula is used:
      </para>
      <programlisting>
number_of_words_in_block * sizeof(word) - last_byte_of_block - 1
</programlisting>
      <para>
        The guaranteed NULL-termination comes in handy when passing a
        string to C, but is not relied upon to compute the length from
        OCaml code. Thus, OCaml strings can contain null bytes at any
        point within the string, but care should be taken that any C
        library functions can also cope with this.
      </para>
    </sect2>
    <sect2 id="custom-heap-blocks">
      <title>Custom heap blocks</title>
      <para>
        OCaml supports <emphasis>custom</emphasis> heap blocks via a
        <literal>Custom_tag</literal> that let the runtime perform
        user-defined operations over OCaml values. A custom block lives
        in the OCaml heap like an ordinary block and can be of whatever
        size the user desires. The <literal>Custom_tag</literal> (255)
        is higher than <literal>No_scan_tag</literal> and so cannot
        contain any OCaml values.
      </para>
      <para>
        The first word of the data within the custom block is a C
        pointer to a <literal>struct</literal> of custom operations. The
        custom block cannot have pointers to OCaml blocks and is opaque
        to the garbage collector.
      </para>
      <programlisting>
struct custom_operations {
  char *identifier;
  void (*finalize)(value v);
  int (*compare)(value v1, value v2);
  intnat (*hash)(value v);
  void (*serialize)(value v,
                    /*out*/ uintnat * wsize_32 /*size in bytes*/,
                    /*out*/ uintnat * wsize_64 /*size in bytes*/);
  uintnat (*deserialize)(void * dst);
  int (*compare_ext)(value v1, value v2);
};
</programlisting>
      <para>
        The custom operations specify how the runtime should perform
        polymorphic comparison, hashing and binary marshalling. They
        also optionally contain a finalizer, which the runtime will call
        just before the block is garbage collected. This finalizer has
        nothing to do with ordinary OCaml finalizers, as created by
        <literal>Gc.finalise</literal>. (<emphasis>avsm</emphasis>: xref
        to GC module explanation)
      </para>
      <para>
        When a custom block is allocated, you can also specify the
        proportion of &quot;extra-heap resources&quot; consumed by the
        block, which will affect the garbage collector's decision as to
        how much work to do in the next major slice.
        (<emphasis>avsm</emphasis>: elaborate on this or move to the C
        interface section)
      </para>
    </sect2>
  </sect1>
  <sect1 id="interfacing-with-c">
    <title>Interfacing with C</title>
    <para>
      Now that you understand the runtime structure of the garbage
      collector, interfacing with C libraries is actually pretty simple.
      OCaml defines an <literal>external</literal> keyword that maps
      OCaml functions to a C symbol. That C function will be passed the
      arguments with the C <literal>value</literal> type which
      corresponds to the memory layout for OCaml values described
      earlier.
    </para>
    <sect2 id="getting-started-with-a-hello-world-c-binding">
      <title>Getting started with a &quot;Hello World&quot; C
      binding</title>
      <para>
        Let's define a simple &quot;Hello World&quot; C binding to see
        how this works. First create a <literal>hello.ml</literal> that
        contains the external declaration:
      </para>
      <programlisting language="ocaml">
external hello_world: unit -&gt; unit = &quot;caml_hello_world&quot;
let _ = hello_world ()
</programlisting>
      <para>
        If you try to compile this module now, you should receive a
        linker error:
      </para>
      <programlisting>
$ ocamlopt -o hello hello.ml
Undefined symbols for architecture x86_64:
  &quot;_caml_hello_world&quot;, referenced from:
      .L100 in hello.o
      _camlHello in hello.o
ld: symbol(s) not found for architecture x86_64
clang: error: linker command failed with exit code 1 (use -v to see invocation)
File &quot;caml_startup&quot;, line 1:
Error: Error during linking
</programlisting>
      <para>
        This is the system linker telling you that there is a missing
        <literal>caml_hello_world</literal> symbol that must be provided
        before a binary can be linked. Now create a file called
        <literal>hello_stubs.c</literal> which contains the C function.
      </para>
      <programlisting language="c">
#include &lt;stdio.h&gt;
#include &lt;caml/mlvalues.h&gt;

CAMLprim value
caml_hello_world(value v_unit)
{
  printf(&quot;Hello OCaml World!\n&quot;);
  return Val_unit;
}
</programlisting>
      <para>
        Now attempt to recompile the <literal>hello</literal> binary
        with the C file also included in the compiler invocation, and it
        should succeed:
      </para>
      <programlisting>
$ ocamlopt -o hello hello.ml hello_stubs.c
$ ./hello
Hello OCaml World!
</programlisting>
      <para>
        The compiler uses the file extensions to determine how to
        compile each file. In the case of the <literal>.c</literal>
        extension, it passes it to the system C compiler and appends an
        include directory containing the OCaml runtime header files that
        define conversion functions to-and-from OCaml values.
      </para>
      <para>
        The <literal>mlvalues.h</literal> header is the basic header
        that all C bindings need. Locate it in your system by using
        <literal>ocamlc -where</literal> to find your system OCaml
        installation. It defines a few important typedefs early on that
        should be familiar after the earlier explanations:
      </para>
      <programlisting language="c">
typedef intnat value;

#define Is_long(x)   (((x) &amp; 1) != 0)
#define Is_block(x)  (((x) &amp; 1) == 0)

#define Val_unit Val_int(0)
</programlisting>
      <para>
        The <literal>value</literal> typedef is a word that can either
        be an integer if <literal>Is_long</literal> is true, or a heap
        block if <literal>Is_block</literal> is true. Our C function
        definition of <literal>caml_hello_world</literal> accepts a
        single parameter, and returns a <literal>value</literal>. In our
        simple example, all the types of parameters and returns are
        <literal>unit</literal>, and so we use the
        <literal>Val_unit</literal> macro to construct the return value.
      </para>
      <para>
        You must be <emphasis>very</emphasis> careful that the value you
        return from the C function corresponds exactly to the memory
        representation of the types you declared earlier in the
        <literal>external</literal> declaration of the ML file, or else
        heap carnage and corruption will ensure.
      </para>
      <tip>
      <title>
      Activating the debug runtime
      </title>
      <para>
        Despite your best efforts, it is easy to introduce a bug into C
        bindings that cause heap invariants to be violated. OCaml
        includes a variant of the runtime library that is compiled with
        debugging symbols, and includes regular memory integrity checks
        upon every garbage collection. Running these often will abort
        the program near the point of corruption and helps track it down
        quickly.
      </para>
      <para>
        To use this, just recompile with
        <literal>-runtime-variant d</literal> set:
      </para>
      <programlisting>
$ ocamlopt -runtime-variant d -verbose -o hello hello.ml hello_stubs.c
$ ./hello 
### OCaml runtime: debug mode ###
Initial minor heap size: 2048k bytes
Initial major heap size: 992k bytes
Initial space overhead: 80%
Initial max overhead: 500%
Initial heap increment: 992k bytes
Initial allocation policy: 0
Hello OCaml World!
</programlisting>
      </tip>

    </sect2>
  </sect1>
</chapter><chapter id="managing-external-memory-with-bigarrays">
  <title>Managing external memory with Bigarrays</title>
  <sect1 id="bigarrays-for-external-memory-blocks">
    <title>Bigarrays for external memory blocks</title>
    <para>
      An OCaml bigarray is a useful custom block provided as standard to
      manipulate memory blocks outside the OCaml heap. It has
      <literal>Custom_tag</literal> in the header, and the first word
      points to the <literal>custom_operations</literal> struct for
      bigarrays. Following this is a <literal>caml_ba_array</literal>
      struct.
    </para>
    <programlisting>
struct caml_ba_array {
  void * data;                  /* Pointer to raw data */
  intnat num_dims;              /* Number of dimensions */
  intnat flags;                 /* Kind of element array + memory layout + allocation status */
  struct caml_ba_proxy * proxy; /* The proxy for sub-arrays, or NULL */
  intnat dim[]  /*[num_dims]*/; /* Size in each dimension */
};
</programlisting>
    <para>
      The <literal>data</literal> is usually a pointer to a
      <literal>malloc</literal>'ed chunk of memory, which the custom
      finalizer operation <literal>free</literal>'s when the block is
      free. The <literal>flags</literal> field encodes three values,
      located in the bits as specified by three masks:
    </para>
    <programlisting>
CAML_BA_KIND_MASK = 0xFF     /* Mask for kind in flags field */
CAML_BA_LAYOUT_MASK = 0x100  /* Mask for layout in flags field */
CAML_BA_MANAGED_MASK = 0x600 /* Mask for &quot;managed&quot; bits in flags field */
</programlisting>
    <para>
      The <literal>CAML_BA_KIND_MASK</literal> bits hold a value of the
      <literal>caml_ba_kind</literal> enum that identifies the kind of
      value in the bigarray <literal>data</literal>.
    </para>
    <programlisting>
enum caml_ba_kind {
  CAML_BA_FLOAT32,             /* Single-precision floats */
  CAML_BA_FLOAT64,             /* Double-precision floats */
  CAML_BA_SINT8,               /* Signed 8-bit integers */
  CAML_BA_UINT8,               /* Unsigned 8-bit integers */
  CAML_BA_SINT16,              /* Signed 16-bit integers */
  CAML_BA_UINT16,              /* Unsigned 16-bit integers */
  CAML_BA_INT32,               /* Signed 32-bit integers */
  CAML_BA_INT64,               /* Signed 64-bit integers */
  CAML_BA_CAML_INT,            /* OCaml-style integers (signed 31 or 63 bits) */
  CAML_BA_NATIVE_INT,          /* Platform-native long integers (32 or 64 bits) */
  CAML_BA_COMPLEX32,           /* Single-precision complex */
  CAML_BA_COMPLEX64,           /* Double-precision complex */
}
</programlisting>
    <para>
      The <literal>CAML_BA_LAYOUT_MASK</literal> bit says whether
      multi-dimensional arrays are layed out C or Fortran style.
    </para>
    <programlisting>
enum caml_ba_layout {
  CAML_BA_C_LAYOUT = 0,           /* Row major, indices start at 0 */
  CAML_BA_FORTRAN_LAYOUT = 0x100, /* Column major, indices start at 1 */
};
</programlisting>
    <para>
      The <literal>CAML_BA_MANAGED_MASK</literal> bits hold a value of
      the <literal>caml_ba_managed</literal> enum that identifies
      whether OCaml is responsible for freeing the
      <literal>data</literal> or some other code is.
    </para>
    <programlisting>
enum caml_ba_managed {
  CAML_BA_EXTERNAL = 0,        /* Data is not allocated by OCaml */
  CAML_BA_MANAGED = 0x200,     /* Data is allocated by OCaml */
  CAML_BA_MAPPED_FILE = 0x400, /* Data is a memory mapped file */
};
</programlisting>
  </sect1>
</chapter><chapter id="inside-the-runtime">
  <title>Inside the Runtime</title>
  <para>
    (<emphasis>avsm</emphasis>: this chapter is still being chopped and
    changed)
  </para>
  <sect1 id="runtime-memory-management">
    <title>Runtime Memory Management</title>
    <para>
      The OCaml runtime divides the address space into memory pages of
      4KB each (this is configurable by recompiling the runtime). At any
      given time, every page that is in use is used for a single
      purpose: major heap, minor heap, static data or code. The runtime
      guarantees this by always allocating slightly more memory than
      requested so that that it can choose align the memory it will
      actually use at the beginning of a 4KB page.
    </para>
    <para>
      The runtime maintains a <emphasis>page table</emphasis> that
      allows it to determine the status of any virtual memory address in
      the operating system process. The status defines whether that
      address is a page in use by the OCaml runtime, and if so, which of
      the four purposes it is being used for.
    </para>
    <para>
      Since the virtual memory space can be very large and sparsely used
      (especially on a 64-bit CPU), the page table is implemented as a
      hash table in which keys are page-aligned addresses and values are
      a single byte. The hash table is represented as an array of words,
      with each word being a key-value pair. The key-value pair is the
      bitwise <literal>or</literal> of the virtual address of the start
      of the page (which has zeros for its lower 12-bits due to being
      aligned to 4KB), and the lower 8 bits are used for the value. To
      look up an address, one masks out the lower 12-bits of the memory
      address, compute a multiplicative hash to get a table index, and
      then compares against the address (i.e. the key) at that index.
      Linear probing is used to resolve collisions.
    </para>
    <para>
      The byte value stored is a bitwise <literal>or</literal> of the
      following status bits:
    </para>
    <informaltable>
      <tgroup cols="3">
        <colspec align="left"/>
        <colspec align="left"/>
        <colspec align="left"/>
        <thead>
          <row>
            <entry>
              Page table status
            </entry>
            <entry>
              Value
            </entry>
            <entry>
              Meaning
            </entry>
          </row>
        </thead>
        <tbody>
          <row>
            <entry>
              <literal>In_heap</literal>
            </entry>
            <entry>
              1
            </entry>
            <entry>
              in the major heap
            </entry>
          </row>
          <row>
            <entry>
              <literal>In_young</literal>
            </entry>
            <entry>
              2
            </entry>
            <entry>
              in the minor heap
            </entry>
          </row>
          <row>
            <entry>
              <literal>In_static_data</literal>
            </entry>
            <entry>
              4
            </entry>
            <entry>
              in the statically allocated data segment
            </entry>
          </row>
          <row>
            <entry>
              <literal>In_code_area</literal>
            </entry>
            <entry>
              8
            </entry>
            <entry>
              in the statically allocated code segment
            </entry>
          </row>
        </tbody>
      </tgroup>
    </informaltable>
    <para>
      The page table starts with a size aiming to be between 25% and 50%
      full of entries, and is automatically doubled in size if it
      becomes half full. It is never shrunk.
    </para>
    <sect2 id="allocating-on-the-minor-heap">
      <title>Allocating on the minor heap</title>
      <para>
        The minor heap is a contiguous chunk of virtual memory. Its size
        is set on program startup and decided by the
        <literal>OCAMLRUNPARAM</literal> environment variable
        (<emphasis>avsm</emphasis>: xref), and then only changed later
        by calls to <literal>Gc.set</literal>. The default size is 256k.
      </para>
      <para>
        The range of memory usable for allocation goes from the
        <literal>caml_young_start</literal> to
        <literal>caml_young_end</literal> C variables managed by the
        runtime.
      </para>
      <programlisting>
                &lt;---- size ----&gt;
 base --- start ---------------- end
          limit      ptr &lt;------
                          blocks
</programlisting>
      <para>
        In a fresh minor heap, the <literal>limit</literal> will equal
        the <literal>start</literal>, and the current
        <literal>ptr</literal> will equal the <literal>end</literal>. As
        blocks are allocated, <literal>caml_young_ptr</literal> will
        decrease until it reaches <literal>caml_young_limit</literal>,
        at which point a minor garbage collection is triggered. To
        allocate a block in the minor heap, we decrement
        <literal>caml_young_ptr</literal> by the size of the block
        (including the header), and then set the the header to a valid
        value. If there isn't enough space left for the block without
        decrementing past the <literal>limit</literal>, a minor
        collection is triggered.
      </para>
      <para>
        To force a minor gc to occur, one can set the
        <literal>caml_young_limit</literal> to equal
        <literal>caml_young_end</literal>, which causes signal handlers
        to be run and to &quot;urge&quot; the runtime
        (<emphasis>avsm</emphasis>: elaborate on this urging business,
        and how to set young from within OCaml via
        <literal>Gc.??</literal>).
      </para>
    </sect2>
    <sect2 id="allocating-on-the-major-heap">
      <title>Allocating on the major heap</title>
      <para>
        The major heap is a singly linked list of contiguous memory
        chunks, sorted in increasing order of virtual address. Each
        chunk is a single memory chunk allocated via
        <emphasis>malloc(3)</emphasis> and consists of a header and a
        data area which contains OCaml blocks. A pointer to a heap chunk
        points to the start of the data area, and access to the header
        is done by a negative offset from this pointer. A chunk header
        has:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            the address of the memory that the chunk is in, as allocated
            by <emphasis>malloc(3)</emphasis>. It is needed when the
            chunk is freed.
          </para>
        </listitem>
        <listitem>
          <para>
            the size in bytes of the data area
          </para>
        </listitem>
        <listitem>
          <para>
            an allocation size in bytes, used during heap compaction to
            merge small blocks to defragment the heap.
          </para>
        </listitem>
        <listitem>
          <para>
            a link to the next heap chunk in the list.
          </para>
        </listitem>
      </itemizedlist>
      <para>
        The chunk's data area always starts on a page boundary, and its
        size is a multiple of the page size (4KB). It contains a
        contiguous sequence of heap blocks. These can be as small as one
        or two 4KB pages, but are usually allocated in 1MB chunks (or
        512KB on 32-bit architectures). You can modify these defaults by
        editing <literal>Heap_chunk_def</literal> in
        <literal>byterun/config.h</literal> and recompiling the runtime.
        (<emphasis>avsm</emphasis>: talk about modifying the defaults in
        a separate callout, as there are quite a few variables which can
        be tweaked)
      </para>
      <para>
        Allocating a block on the major heap first checks the free list
        of blocks (see below). If there isn't enough room on the free
        list, the runtime expands the major heap with a fresh block that
        will be large enough. That block is then added to the free list,
        and the free list is checked again (and this time will
        definitely succeed).
      </para>
    </sect2>
    <sect2 id="the-major-heap-free-list">
      <title>The major heap free list</title>
      <para>
        The free space in the major heap's chunks is organized as a
        singly linked list of OCaml blocks, ordered by increasing
        virtual address. The runtime has a pointer to the first block in
        the free list. A free list block is at least two words: a header
        followed by a pointer to the next free-list block. The header
        specifies the length of the block just as with a normal block.
        (<emphasis>avsm</emphasis>: I'm not sure that this is quite
        true. It seems from <literal>freelist.c</literal> that the
        freelist blocks are normal OCaml blocks, with the first data
        entry being the next pointer. when detached, they become normal
        ocaml blocks)
      </para>
      <para>
        As soon as the runtime finds a free block that is larger than
        the request, there are three possibilities:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            If the free block is exactly the right size, it is unlinked
            from the free list and returned as the requested block.
          </para>
        </listitem>
        <listitem>
          <para>
            If the free block is one word too large, it is unlinked from
            the free list, and the first word is given a special header
            recognizable to the collector as an unused word, while the
            rest of the block is returned as the requested block.
          </para>
        </listitem>
        <listitem>
          <para>
            If the free block is two or more words larger than the
            requested block, it remains in the free list, with its
            length shortened, and the end of the free block is returned
            for the requested block. Since the allocated block is
            right-justified within the free block, the linking of the
            free list doesn't need to be changed at all as the block
            that remains in the free list is the original one.
          </para>
        </listitem>
      </itemizedlist>
    </sect2>
    <sect2 id="memory-allocation-strategies">
      <title>Memory allocation strategies</title>
      <para>
        Allocating a new block in the major heap always looks in the
        free list. There are two allocation policies: first fit and next
        fit (the default).
      </para>
      <sect3 id="next-fit-allocation">
        <title>Next-fit allocation</title>
        <para>
          Next-fit allocation keeps a pointer to the block in the free
          list that was most recently used to satisfy a request. When a
          new request comes in, the allocator searches from the next
          block until the end of the free list, and then from the
          beginning of the free list up to that block.
        </para>
      </sect3>
      <sect3 id="first-fit-allocation">
        <title>First-fit allocation</title>
        <para>
          First-fit allocation focusses on reducing memory
          fragmentation, at the expense of slower block allocation. For
          some workloads, the reduction in the frequency in heap
          compaction will outweigh the extra allocation cost.
          (<emphasis>avsm</emphasis>: example?)
        </para>
        <para>
          The runtime maintains an ordered array of freelist chunks,
          called the <literal>flp</literal> array. Imagine a function
          mapping a block's index in the free list to its size. The flp
          array pointers are to the high points of this graph. That is,
          if you walk the free list between <literal>flp[i]</literal>
          and <literal>flp[i+1]</literal>, you will come across blocks
          that have sizes at most the size of <literal>flp[i]</literal>.
          Furthermore this sequence of smaller-than-flp[i] blocks cannot
          be extended, which is equivalent to saying
          <literal>size(flp[i+1]) &gt; size(flp[i])</literal>.
        </para>
        <para>
          When allocating, we first check the flp-array. If
          <literal>flp[i]</literal> is not big enough for our new block,
          then we may as well skip to <literal>flp[i+1]</literal>,
          because everything in the free list before then will also be
          too small.
        </para>
        <para>
          If there's nothing big enough in the <literal>flp</literal>
          array, we extend it by walking the free list starting at the
          <emphasis>last</emphasis> pointer in the
          <literal>flp</literal>-array, say <literal>flp[N]</literal>.
          We extend the <literal>flp</literal> array along the way, so
          that at each block, if this block is bigger than the current
          last thing in <literal>flp</literal> (which is equivalent to
          saying this is the biggest block we've ever seen, since the
          blocks pointed to by the <subscript>flp</subscript> array are
          increasing in size), we add it to the end of
          <subscript>flp</subscript>. We stop this walk when we come
          across a block big enough to house our desired new block.
        </para>
        <para>
          There's also the case when the <literal>flp</literal> array
          has its ceiling size of <literal>FLP_MAX</literal> (default
          100). Then we just start at the end of the
          <literal>flp</literal> array and walk until we find something
          big enough. This is known in the as a slow first-fit search,
          since this linear walk may take a long time.
        </para>
        <para>
          If we did manage to find something suitable in the
          <literal>flp</literal> array, say at index
          <literal>i</literal>, we need to update
          <literal>flp</literal>. This update is rather complex, and the
          reason why first-fit allocation is slower than next-fit. We
          walk through the free list between <literal>flp[i-1]</literal>
          and <literal>flp[i]</literal> and record every high point we
          come across. Say we find <literal>j</literal> such points. We
          move the upper portion of <literal>flp</literal> (from
          <literal>flp[i+1]</literal> to the end) to the right by
          <literal>j</literal> places and insert each new high point
          into the array. There is a further corner case when adding in
          <literal>j</literal> new high points would make
          <literal>flp</literal> bigger than <literal>FLP_MAX</literal>.
        </para>
        <para>
          (<emphasis>avsm</emphasis>: this really needs a diagram)
        </para>
        <note>
        <title>
        Which allocation policy should I use?
        </title>
        <para>
          (<emphasis>avsm</emphasis>: 0 is the next-fit policy, which is
          quite fast but can result in fragmentation. 1 is the first-fit
          policy, which can be slower in some cases but can be better
          for programs with fragmentation problems. )
        </para>
        </note>
      </sect3>
    </sect2>
    <sect2 id="inter-generational-pointers">
      <title>Inter-generational pointers</title>
      <para>
        Most incremental generational garbage collectors have to keep
        careful track of values pointing from old generations to younger
        ones. The OCaml runtime is no exception, and maintains a set of
        addresses in the major heap that may point into the minor heap.
        These addresses are <emphasis>not</emphasis> OCaml pointers, and
        just literal memory addresses. The runtime ensures that it never
        relocates values in the major heap unless this
        &quot;remembered&quot; set is empty. The set is maintained as a
        dynamically resized array of pointers, which is itself
        maintained via a collection of pointers known as the
        <literal>caml_ref_table</literal>.
      </para>
      <programlisting language="c">
struct caml_ref_table {
  value **base;
  value **end;
  value **threshold;
  value **ptr;
  value **limit;
  asize_t size;
  asize_t reserve;
};
</programlisting>
      <para>
        The relationships of the pointers are as follows:
      </para>
      <programlisting>
                   limit
   base &lt;= ptr     threshold          end
     |----------------|----------------|
           size            reserve
</programlisting>
      <para>
        An address is added to <literal>caml_ref_table</literal> when
        all of these conditions are satisfied:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            a field in a block in the major heap is mutated
          </para>
        </listitem>
        <listitem>
          <para>
            the field previously did not point to the minor heap
          </para>
        </listitem>
        <listitem>
          <para>
            the field is being changed to point into the minor heap
          </para>
        </listitem>
      </itemizedlist>
      <para>
        In that case the entry is added at
        <literal>caml_ref_table.ptr</literal>, which is then
        incremented. If <literal>ptr</literal> is already at
        <literal>limit</literal>, the table is doubled in size before
        adding the address.
      </para>
      <para>
        The same address can occur in <literal>caml_ref_table</literal>
        multiple times if a block field is mutated repeatedly and
        alternated between pointing at the minor heap and the major
        heap. The field in <literal>caml_ref_table</literal> also may
        not always point into the minor heap (if it was changed after
        being added), since fields are never removed. The entire table
        is cleared as part of the minor collection process.
      </para>
      <sect3 id="the-write-barrier">
        <title>The write barrier</title>
        <para>
          The write barrier is one of the reasons why using immutable
          data structures can sometimes be faster than mutable records.
          The OCaml compiler keeps track of any mutable types and adds a
          call to <literal>caml_modify</literal> before making the
          change. The <literal>caml_modify</literal> checks that the
          remembered set is consistent, which, although reasonably
          efficient, can be slower than simply allocating a fresh value
          on the fast minor heap.
        </para>
        <para>
          Let's see this for ourselves with a simple test program:
        </para>
        <programlisting language="ocaml">
type t1 = { mutable iters1: int; mutable count1: float }
type t2 = { iters2: int; count2: float }

let rec test_mutable t1 =
  match t1.iters1 with
  |0 -&gt; ()
  |n -&gt;
    t1.iters1 &lt;- t1.iters1 - 1;
    t1.count1 &lt;- t1.count1 +. 1.0;
    test_mutable t1

let rec test_immutable t2 =
  match t2.iters2 with
  |0 -&gt; ()
  |n -&gt;
    let iters2 = n - 1 in
    let count2 = t2.count2 +. 1.0 in
    test_immutable { iters2; count2 }

open Printf
let time name fn arg =
  Gc.compact ();
  let w1 = Gc.((stat ()).minor_collections) in
  let t1 = Unix.gettimeofday () in
  fn arg;
  let w2 = Gc.((stat ()).minor_collections) in
  let t2 = Unix.gettimeofday () in
  printf &quot;%s: %.4fs (%d minor collections)\n&quot; name (t2 -. t1) (w2 - w1)

let _ =
  let iters = 1000000000 in
  time &quot;mutable&quot; test_mutable { iters1=iters; count1=0.0 };
  time &quot;immutable&quot; test_immutable { iters2=iters; count2=0.0 }
</programlisting>
        <para>
          This program defines a type <literal>t1</literal> that is
          mutable, and <literal>t2</literal> that is immutable. The main
          loop iterates over both fields and runs a simple counter. It
          measures two things: the wallclock time that all the
          iterations take, and the number of minor garbage collections
          that occurred during the test. The results should look
          something like this:
        </para>
        <programlisting>
mutable: 8.6923s (7629 minor collections)
immutable: 2.6186s (19073 minor collections)
</programlisting>
        <para>
          Notice the space/time tradeoff here. The mutable version runs
          almost 4 times slower than the immutable one, but has
          significantly fewer garbage collection cycles. Minor
          collections in OCaml are very fast, and so it is often
          acceptable to use immutable data structures in preference to
          the more conventional mutable versions. On the other hand, if
          you only rarely mutable a value, it can be faster to take the
          write barrier hit and not allocate at all.
        </para>
        <para>
          (<emphasis>avsm</emphasis>: it would be really nice to use a
          benchmark suite here and shorten the example. Investigate the
          options and edit this section)
        </para>
        <para>
          (<emphasis>avsm</emphasis>: need to mention when a value is
          allocated directly into the major heap somewhere)
        </para>
      </sect3>
    </sect2>
  </sect1>
  <sect1 id="how-garbage-collection-works">
    <title>How garbage collection works</title>
    <sect2 id="collecting-the-minor-heap">
      <title>Collecting the minor heap</title>
      <para>
        For those familiar with garbage collection terminology, here is
        OCaml's minor colection in one sentence. OCaml's minor
        collection uses copying collection with forwarding pointers, and
        does a depth-first traversal of the block graph using a stack
        represented as a linked list threaded through blocks that need
        to be scanned.
      </para>
      <para>
        The goal of minor collection is to empty the minor heap by
        moving to the major heap every block in the minor heap that
        might be used in the future, and updating each pointer to a
        moved block to the new version of the block. A block is
        <emphasis>live</emphasis> if is reachable by starting at some
        <emphasis>root</emphasis> pointer into a block in the minor
        heap,a nd then following pointers in blocks. There are many
        different kinds of roots:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            OCaml stack(s)
          </para>
        </listitem>
        <listitem>
          <para>
            C stack(s), identified by <literal>BeginRoots</literal> or
            <literal>CAMLparam</literal> in C code
            (<emphasis>avsm</emphasis>: xref C bindings chapter)
          </para>
        </listitem>
        <listitem>
          <para>
            Global roots
          </para>
        </listitem>
        <listitem>
          <para>
            Finalized values (<emphasis>avsm</emphasis>: ?)
          </para>
        </listitem>
        <listitem>
          <para>
            Intergenerational pointers in the
            <literal>caml_ref_table</literal>
            (<emphasis>avsm</emphasis>: xref above?)
          </para>
        </listitem>
      </itemizedlist>
      <para>
        Moving a block between heaps is traditionally called
        <emphasis>forwarding</emphasis>. The OCaml runtime code uses
        that term as well as the term <emphasis>oldify</emphasis>, which
        is useful to understand when profiling hotspots in your code.
        The minor collector first visits all roots and forwards them if
        they point to a block in the minor heap. When a block is
        forwarded, the collector sets the tag of the original block to a
        special <literal>Forward_tag</literal> (250), and the first
        field of the original block to point to the new block. Then, if
        the collector ever encounters a pointer to the original block
        again, it can simply update the pointer directly into the
        forwarded block.
      </para>
      <para>
        Because a forwarded block might itself contain pointers, it must
        at some point be scanned to see if those pointers point to
        blocks in the minor heap, so that those blocks can also be
        forwarded. The collector maintains a linked list (called the
        <literal>oldify_todo_list</literal>) of forwarded objects that
        it still needs to scan. That linked list looks like:
      </para>
      <programlisting>
                oldify_todo_list
                   |
                   |
                   v
   minor heap  | 0 | v | f1 ... |      | 0 | v | f1 ... |
                     |                     ^ |
                     |      +--------------+ |      ---------- ...
                     v      |                v      |
   major heap    | h | f0 | ^ | ... |    | h | f0 | ^
</programlisting>
      <para>
        Each value on the <literal>oldify_todo_list</literal> is marked
        as forwarded, and the first word points to the new block in the
        major heap. That new version contains the actual value header,
        the real first field of the value, and a link (pointer) to the
        next value on the oldify_todo_list, or
        <subscript>NULL</subscript> at the end of the list. Clearly this
        approach won't work if an value has only one field, since there
        will be no second field to store the link in. Values with
        exactly one field are never put on the
        <literal>oldify_todo_list</literal>; instead, the collector
        immediately traverses them, essentially making a tail call in
        the depth-first search.
      </para>
      <para>
        Values that are known from the tag in their header to not
        contain pointers are simply forwarded and completely copied, and
        never placed on the <literal>oldify_todo_list</literal>. These
        tags are all greater than <literal>No_scan_tag</literal> and
        include strings and float arrays.
      </para>
      <para>
        (<emphasis>avsm</emphasis>: note from sweeks to investigate:
        There is a hack for objects whose tag is
        <literal>Forward_tag</literal> that does some kind of path
        compression, or at least removal of one link, but I'm not sure
        what's going on.)
      </para>
      <para>
        (<emphasis>avsm</emphasis>: I dont think we've introduced weak
        references yet, so this needs rearranging) At the end of the
        depth-first search in minor collection, the collector scans the
        <literal>weak-ref</literal> table, and clears any weak
        references that are still pointing into the minor heap. The
        collector then empties the <literal>weak-ref</literal> table and
        the minor heap.
      </para>
    </sect2>
    <sect2 id="collecting-the-major-heap">
      <title>Collecting the major heap</title>
      <para>
        The major heap collections operates incrementally, as the amount
        of memory being tracked is a lot larger than the minor heap. The
        major collector can be in any of a number of phases:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            <literal>Phase_idle</literal>
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>Phase_mark</literal>
          </para>
          <itemizedlist>
            <listitem>
              <para>
                <literal>Subphase_main</literal>: main marking phase
              </para>
            </listitem>
            <listitem>
              <para>
                <literal>Subphase_weak1</literal>: clear weak pointers
              </para>
            </listitem>
            <listitem>
              <para>
                <literal>Subphase_weak2</literal>: remove dead weak
                arrays, observe finalized values
              </para>
            </listitem>
            <listitem>
              <para>
                <literal>Subphase_final</literal>: initialise for the
                sweep phase
              </para>
            </listitem>
          </itemizedlist>
        </listitem>
        <listitem>
          <para>
            <literal>Phase_sweep</literal>
          </para>
        </listitem>
      </itemizedlist>
    </sect2>
    <sect2 id="marking-the-major-heap">
      <title>Marking the major heap</title>
      <para>
        Marking maintains an array of gray blocks,
        <literal>gray_vals</literal>. It uses as them as a stack,
        pushing on a white block that is then colored gray, and popping
        off a gray block when it is scanned and colored black. The
        <literal>gray_vals</literal> array is allocated via
        <emphasis>malloc(3)</emphasis>, and there is a pointer,
        <literal>gray_vals_cur</literal>, to the next open spot in the
        array.
      </para>
      <para>
        The <literal>gray_vals</literal> array initially has 2048
        elements. <literal>gray_vals_cur</literal> starts at
        <literal>gray_vals</literal>, and increases until it reachs
        <literal>gray_vals_end</literal>, at which point the
        <literal>gray_vals</literal> array is doubled, as long as its
        size (in bytes) is less than 1/2^10th of the heap size
        (<literal>caml_stat_heap_size</literal>). When the gray vals is
        of its maximum allowed size, it isn't grown any further, and the
        heap is marked as impure (<literal>heap_is_pure=0</literal>),
        and last half of <literal>gray_vals</literal> is ignored (by
        setting <literal>gray_vals_cur</literal> back to the middle of
        the <literal>gray_vals</literal> array.
      </para>
      <para>
        If the marking is able to complete using just the gray list, it
        will. Otherwise, once the gray list is emptied, the mark phase
        will observe that the heap is impure and initiate a backup
        approach to marking. In this approach it marks the heap as pure
        and then walks through the entire heap block by block, in
        increasing order of memory address. If it finds a gray block, it
        adds it to the gray list and does a DFS marking using the gray
        list as a stack in the usual way. Once the scan of the complete
        heap is finished, the mark phase checks again whether the heap
        has again become impure, and if so initiates another scan. These
        full-heap scans will continue until a successful scan completes
        without overflowing the gray list.
      </para>
      <para>
        (<emphasis>avsm</emphasis>: I need to clarify this more,
        possibly a diagram too. It's not really clear what the
        implications of an impure heap are atm)
      </para>
    </sect2>
    <sect2 id="sweeping-unused-blocks-from-the-major-heap">
      <title>Sweeping unused blocks from the major heap</title>
      <para>
      </para>
    </sect2>
    <sect2 id="compaction-and-defragmenting-the-major-heap">
      <title>Compaction and defragmenting the major heap</title>
      <para>
      </para>
    </sect2>
  </sect1>
</chapter><chapter id="performance-tuning-and-profiling">
  <title>Performance Tuning and Profiling</title>
  <para>
    Read http://janestreet.github.com/ocaml-perf-notes.html
  </para>
  <sect1 id="byte-code-profiling">
    <title>Byte code Profiling</title>
    <para>
      ocamlcp and call trace information
    </para>
  </sect1>
  <sect1 id="native-code-profiling">
    <title>Native Code Profiling</title>
    <sect2 id="gdb">
      <title>gdb</title>
      <para>
        requires shinwell's patch in ocaml trunk via opam
      </para>
    </sect2>
    <sect2 id="perf">
      <title>perf</title>
      <para>
        requires fabrice's frame pointer patch
      </para>
    </sect2>
    <sect2 id="dtrace">
      <title>dtrace</title>
      <para>
        requires my dtrace/instruments patch for libasmrun
      </para>
    </sect2>
  </sect1>
</chapter><chapter id="parsing-with-ocamllex-and-ocamlyacc">
  <title>Parsing with OCamllex and OCamlyacc</title>
  <para>
    TODO: talk about ulex too
  </para>
</chapter><chapter id="object-subtyping-and-inheritance">
  <title>Object Subtyping and Inheritance</title>
  <sect1 id="subtyping">
    <title>Subtyping</title>
    <para>
      Subtyping is a central concept in object-oriented programming. It
      governs when an object with one type <emphasis>A</emphasis> can be
      used in an expression that expects an object of another type
      <emphasis>B</emphasis>. When this is true, we say that
      <emphasis>A</emphasis> is a <emphasis>subtype</emphasis> of
      <emphasis>B</emphasis>. Actually, more concretely, subtyping
      determines when the coercion operator <literal>e :&gt; t</literal>
      can be applied. This coercion works only if the expression
      <literal>e</literal> has some type <literal>s</literal> and
      <literal>s</literal> is a subtype of <literal>t</literal>.
    </para>
    <para>
      To explore this, let's define some simple classes for geometric
      shapes. The generic type <literal>shape</literal> has a method to
      compute the area, and a <literal>square</literal> is a specific
      kind of shape.
    </para>
    <programlisting language="ocaml">
type shape = &lt; area : float &gt;;;

class square w =
object (self : 'self)
  method area = self#width *. self#width
  method width = w
end;;
</programlisting>
    <para>
      A <literal>square</literal> has a method <literal>area</literal>
      just like a <literal>shape</literal>, and an additional method
      <literal>width</literal>. Still, we expect a
      <literal>square</literal> to be a <literal>shape</literal>, and it
      is. The coercion <literal>:&gt;</literal> must be explicit.
    </para>
    <programlisting language="ocaml">
# let new_square x : shape = new square x;;
Characters 27-39:
  let new_square x : shape = new square x;;
                             ^^^^^^^^^^^^
Error: This expression has type square but an expression was expected of type shape
The second object type has no method width
# let new_square x : shape = (new square x :&gt; shape);;
val new_square : float -&gt; shape = &lt;fun&gt;
</programlisting>
    <para>
      What are the rules for subtyping? In general, object subtyping has
      two general forms, called <emphasis>width</emphasis> and
      <emphasis>depth</emphasis> subtyping. Width subtyping means that
      an object type <emphasis>A</emphasis> is a a subtype of
      <emphasis>B</emphasis>, if <emphasis>A</emphasis> has all of the
      methods of <emphasis>B</emphasis>, and possibly more. A
      <literal>square</literal> is a subtype of <literal>shape</literal>
      because it implements all of the methods of
      <literal>shape</literal> (the <literal>area</literal> method).
    </para>
    <para>
      The subtyping rules are purely technical, they have no relation to
      object semantics. We can define a class
      <literal>rectangle</literal> that has all of the methods of a
      <literal>square</literal>, so it is a subtype of square and can be
      used wherever a <literal>square</literal> is expected.
    </para>
    <programlisting language="ocaml">
# class rectangle h w =
  object (self : 'self)
     inherit square w
     method area = self#width *. self#height
     method height = h
  end;;
# let square_rectangle h w : square = (new rectangle h w :&gt; square);;
val square_rectangle : float -&gt; float -&gt; square = &lt;fun&gt;
</programlisting>
    <para>
      This may seem absurd, but this concept is expressible in all
      object-oriented languages. The contradiction is semantic -- we
      know that in the real world, not all rectangles are squares; but
      in the programming world, rectangles have all of the features of
      squares (according to our definition), so they can be used just
      like squares. Suffice it to say that it is usually better to avoid
      such apparent contradictions.
    </para>
    <para>
      Next, let's take a seemingly tiny step forward, and start building
      collections of shapes. It is easy enough to define a
      <literal>slist</literal> of squares.
    </para>
    <programlisting language="ocaml">
# let squares =
     let l = SList.make () in
     l#insert (new square 1.0);
     l#insert (new square 2.0);
     l;;
val squares : square slist = &lt;obj&gt;
</programlisting>
    <para>
      We can also define a function to calculate the total area of a
      list of shapes. There is no reason to restrict this to squares, it
      should work for any list of shapes with type
      <literal>shape slist</literal>. The problem is that doing so
      raises some serious typing questions -- can a
      <literal>square slist</literal> be passed to a function that
      expects a <literal>shape slist</literal>? If we try it, the
      compiler produces a verbose error message.
    </para>
    <programlisting language="ocaml">
# let total_area (l : shape slist) : float =
     let total = ref 0.0 in
     let it = l#iterator in
     while it#has_value do
        total := !total +. it#get#area;
        it#next
     done;
     !total;;
val total_area : shape slist -&gt; float = &lt;fun&gt;
# total_area squares;;
Characters 11-18:
  total_area squares;;
             ^^^^^^^
Error: This expression has type
         square slist =
           &lt; insert : square -&gt; unit; is_empty : bool;
             iterator : square iterator &gt;
       but an expression was expected of type
         shape slist =
           &lt; insert : shape -&gt; unit; is_empty : bool;
             iterator : shape iterator &gt;
       Type square = &lt; area : float; width : float &gt;
       is not compatible with type shape = &lt; area : float &gt; 
       The second object type has no method width
</programlisting>
    <para>
      It might seem tempting to give up at this point, especially
      because the subtyping is not even true -- the type
      <literal>square slist</literal> is not a subtype of
      <literal>shape slist</literal>. The problem is with the
      <literal>insert</literal> method. For
      <literal>shape slist</literal>, the <literal>insert</literal>
      method takes an arbitrary <literal>shape</literal> and inserts it
      into the list. So if we could coerce a
      <literal>square slist</literal> to a
      <literal>shape slist</literal>, then it would be possible to
      insert an arbitrary shape into the list, which would be an error.
    </para>
    <sect2 id="using-more-precise-types-to-address-subtyping-problems">
      <title>Using more precise types to address subtyping
      problems</title>
      <para>
        Still, the <literal>total_area</literal> function should be
        fine, in principle. It doesn't call <literal>insert</literal>,
        so it isn't making that error. To make it work, we need to use a
        more precise type that indicates we are not going to be mutating
        the list. We define a type
        <literal>readonly_shape_slist</literal> and confirm that we can
        coerce the list of squares.
      </para>
      <programlisting language="ocaml">
# type readonly_shape_slist = &lt; iterator : shape iterator &gt;;;
type readonly_shape_slist = &lt; iterator : shape iterator &gt;
# (squares :&gt; readonly_shape_slist);;
- : readonly_shape_slist = &lt;obj&gt;
# let total_area (l : readonly_shape_slist) : float = ...;;
val total_area : readonly_shape_slist -&gt; float = &lt;fun&gt;
#   total_area (squares :&gt; readonly_shape_slist);;
- : float = 5.
</programlisting>
      <para>
        Why does this work, why is a <literal>square slist</literal> a
        subtype of <literal>readonly_shape_slist</literal>. The
        reasoning is in two steps. First, the easy part is width
        subtyping: we can drop the other methods to see that
        <literal>square slist</literal> is a subtype of
        <literal>&lt; iterator : square iterator &gt;</literal>. The
        next step is to use <emphasis>depth</emphasis> subtyping, which,
        in its general form, says that an object type
        <literal>&lt; m : t1 &gt;</literal> is a subtype of a type
        <literal>&lt; m : t2&gt;</literal> iff <literal>t1</literal> is
        a subtype of <literal>t2</literal>. In other words, instead of
        reasoning about the number of methods in a type (the width), the
        number of methods is fixed, and we look within the method types
        themselves (the &quot;depth&quot;).
      </para>
      <para>
        In this particular case, depth subtyping on the
        <literal>iterator</literal> method requires that
        <literal>square iterator</literal> be a subtype of
        <literal>shape iterator</literal>. Expanding the type definition
        for the type <literal>iterator</literal>, we again invoke depth
        subtyping, and we need to show that the type
        <literal>&lt; get : square &gt;</literal> is a subtype of
        <literal>&lt;get : shape &gt;</literal>, which follows because
        <literal>square</literal> is a subtype of
        <literal>shape</literal>.
      </para>
      <para>
        This reasoning may seem fairly long and complicated, but it
        should be pointed out that this typing
        <emphasis>works</emphasis>, and in the end the type annotations
        are fairly minor. In most typed object-oriented languages, the
        coercion would simply not be possible. For example, in C++, a
        STL type <literal>slist&lt;T&gt;</literal> is invariant in
        <literal>T</literal>, it is simply not possible to use
        <literal>slist&lt;square&gt;</literal> where
        <literal>slist&lt;shape&gt;</literal> is expected (at least
        safely). The situation is similar in Java, although Java
        supports has an escape hatch that allows the program to fall
        back to dynamic typing. The situation in OCaml is much better;
        it works, it is statically checked, and the annotations are
        pretty simple.
      </para>
    </sect2>
    <sect2 id="using-elided-types-to-address-subtyping-problems">
      <title>Using elided types to address subtyping problems</title>
      <para>
        Before we move to the next topic, there is one more thing to
        address. The typing we gave above, using
        <literal>readonly_shape_slist</literal>, requires that the
        caller perform an explicit coercion before calling the
        <literal>total_area</literal> function. We would like to give a
        better type that avoids the coercion.
      </para>
      <para>
        A solution is to use an elided type. Instead of
        <literal>shape</literal>, we can use the elided type
        <literal>&lt; area : float; .. &gt;</literal>. In fact, once we
        do this, it also becomes possible to use the
        <literal>slist</literal> type.
      </para>
      <programlisting language="ocaml">
# let total_area (l : &lt; area : float; .. &gt; slist) : float = ...;;
val total_area : &lt; area : float; .. &gt; slist -&gt; float = &lt;fun&gt;
# total_area squares;;
- : float = 5.
</programlisting>
      <para>
        This works, and it removes the need for explicit coercions. This
        type is still fairly simple, but it does have the drawback that
        the programmer needs to remember that the types
        <literal>&lt; area : float; ..&gt;</literal> and
        <literal>shape</literal> are related.
      </para>
      <para>
        OCaml supports an abbreviation in this case, but it works only
        for classes, not object types. The type expression
        <literal># classname</literal> is an abbreviation for an elided
        type containing all of the methods in the named class, and more.
        Since <literal>shape</literal> is an object type, we can't write
        <literal>#shape</literal>. However, if a class definition is
        available, this abbreviation can be useful. The following
        definition is exactly equivalent to the preceeding.
      </para>
      <programlisting language="ocaml">
# class cshape = object method area = 0.0 end;;
class cshape : object method area : float end
# let total_area (l : #cshape list) : float = ...;;
val total_area : #cshape slist -&gt; float = &lt;fun&gt;
# total_area squares;;
- : float = 5.
</programlisting>
    </sect2>
    <sect2 id="narrowing">
      <title>Narrowing</title>
      <para>
        Narrowing, also called <emphasis>down casting</emphasis>, is the
        ability to coerce an object to one of its subtypes. For example,
        if we have a list of shapes <literal>shape slist</literal>, we
        might know (for some reason) what the actual type of each shape
        is. Perhaps we know that all objects in the list have type
        <literal>square</literal>. In this case,
        <emphasis>narrowing</emphasis> would allow the re-casting of the
        object from type <literal>shape</literal> to type
        <literal>square</literal>. Many languages support narrowing
        through dynamic type checking. For example, in Java, a coercion
        <literal>(Square) x</literal> is allowed if the value
        <literal>x</literal> has type <literal>Square</literal> or one
        of its subtypes; otherwise the coercion throws an exception.
      </para>
      <para>
        Narrowing is <emphasis>not permitted</emphasis> in OCaml.
        Period.
      </para>
      <para>
        Why? There are two reasonable explanations, one based on a
        design principle, and another technical (the technical reason is
        simple: it is hard to implement).
      </para>
      <para>
        The design argument is this: narrowing violates abstraction. In
        fact, with a structural typing system like in OCaml, narrowing
        would essentially provide the ability to enumerate the methods
        in an object. To check whether an object <literal>obj</literal>
        has some method <literal>foo : int</literal>, one would attempt
        a coercion <literal>(obj :&gt; &lt; foo : int &gt;)</literal>.
      </para>
      <para>
        More commonly, narrowing leads to poor object-oriented style.
        Consider the following Java code, which returns the name of a
        shape object.
      </para>
      <programlisting>
String GetShapeName(Shape s) {
  if (s instanceof Square) {
    return &quot;Square&quot;;
  } else if (s instanceof Circle) {
    return &quot;Circle&quot;;
  } else {
    return &quot;Other&quot;;
  }
}
</programlisting>
      <para>
        Most programmers would consider this code to be
        &quot;wrong.&quot; Instead of performing a case analysis on the
        type of object, it would be better to define a method to return
        the name of the shape. Instead of calling
        <literal>GetShapeName(s)</literal>, we should call
        <literal>s.Name()</literal> instead.
      </para>
      <para>
        However, the situation is not always so obvious. The following
        code checks whether an array of shapes looks like a
        &quot;barbell,&quot; composed to two <literal>Circle</literal>
        objects separated by a <literal>Line</literal>, where the
        circles have the same radius.
      </para>
      <programlisting>
boolean IsBarBell(Shape[] s) {
  return s.length == 3 &amp;&amp; (s[0] instanceof Circle) &amp;&amp;
    (s[1] instanceof Line) &amp;&amp; (s[2] instanceof Circle) &amp;&amp;
    ((Circle) s[0]).radius() == ((Circle) s[2]).radius();
}
</programlisting>
      <para>
        In this case, it is much less clear how to augment the
        <literal>Shape</literal> class to support this kind of pattern
        analysis. It is also not obvious that object-oriented
        programming is well-suited for this situation. Pattern matching
        seems like a better fit.
      </para>
      <programlisting language="ocaml">
let is_bar_bell = function
 | [Circle r1; Line _; Circle r2] when r1 == r2 -&gt; true
 | _ -&gt; false;;
</programlisting>
      <para>
        Regardless, there is a solution if you find yourself in this
        situation, which is to augment the classes with variants. You
        can define a method <literal>variant</literal> that injects the
        actual object into a variant type.
      </para>
      <programlisting language="ocaml">
type shape = &lt; variant : repr; area : float&gt;
and circle = &lt; variant : repr; area : float; radius : float &gt;
and line = &lt; variant : repr; area : float; length : float &gt;
and repr =
 | Circle of circle
 | Line of line;;
 
let is_bar_bell = function
 | [s1; s2; s3] -&gt;
   (match s1#variant, s2#variant, s3#variant with
     | Circle c1, Line _, Circle c2 when c1#radius == c2#radius -&gt; true
     | _ -&gt; false)
 | _ -&gt; false;;
</programlisting>
      <para>
        This pattern works, but it has drawbacks. In particular, the
        recursive type definition should make it clear that this pattern
        is essentially equivalent to using variants, and that objects do
        not provide much value here.
      </para>
    </sect2>
    <sect2 id="binary-methods">
      <title>Binary methods</title>
      <para>
        A <emphasis>binary method</emphasis> is a method that takes an
        object of <literal>self</literal> type. One common example is
        defining a method for equality.
      </para>
      <programlisting language="ocaml">
# class square w =
  object (self : 'self) 
    method width = w
    method area = self#width * self#width
    method equals (other : 'self) = other#width = self#width
  end;;
class square : int -&gt;
  object ('a)
    method area : int
    method equals : 'a -&gt; bool
    method width : int
  end
# class rectangle w h =
  object (self : 'self)
    method width = w
    method height = h
    method area = self#width * self#height
    method equals (other : 'self) = other#width = self#width &amp;&amp; other#height = self#height
  end;;
...
# (new square 5)#equals (new square 5);;
- : bool = true
# (new rectangle 5 6)#equals (new rectangle 5 7);;
- : bool = false
</programlisting>
      <para>
        This works, but there is a problem lurking here. The method
        <literal>equals</literal> takes an object of the exact type
        <literal>square</literal> or <literal>rectangle</literal>.
        Because of this, we can't define a common base class
        <literal>shape</literal> that also includes an equality method.
      </para>
      <programlisting language="ocaml">
# type shape = &lt; equals : shape -&gt; bool; area : int &gt;;;
# let sq = new square 5;;
# (sq :&gt; shape);;
Characters 0-13:
  (sq :&gt; shape);;
  ^^^^^^^^^^^^^
Error: Type square = &lt; area : int; equals : square -&gt; bool; width : int &gt;
       is not a subtype of shape = &lt; area : int; equals : shape -&gt; bool &gt; 
Type shape = &lt; area : int; equals : shape -&gt; bool &gt; is not a subtype of
  square = &lt; area : int; equals : square -&gt; bool; width : int &gt; 
</programlisting>
      <para>
        The problem is that a <literal>square</literal> expects to be
        compared with a <literal>square</literal>, not an arbitrary
        shape; similarly for <literal>rectangle</literal>.
      </para>
      <para>
        This problem is fundamental. Many languages solve it either with
        narrowing (with dynamic type checking), or by method
        overloading. Since OCaml has neither of these, what can we do?
      </para>
      <para>
        One proposal we could consider is, since the problematic method
        is equality, why not just drop it from the base type
        <literal>shape</literal> and use polymorphic equality instead?
        Unfortunately, the builtin equality has very poor behavior when
        applied to objects.
      </para>
      <programlisting language="ocaml">
# (object method area = 5 end) = (object method area = 5 end);;
- : bool = false
</programlisting>
      <para>
        The problem here is that the builtin polymorphic equality
        compares the method implementations, not their return values.
        The method implementations (the function values that implement
        the methods) are different, so the equality comparison is false.
        There are other reasons not to use the builtin polymorphic
        equality, but these false negatives are a showstopper.
      </para>
      <para>
        If we want to define equality for shapes in general, the
        remaining solution is to use the same approach as we described
        for narrowing. That is, introduce a
        <emphasis>representation</emphasis> type implemented using
        variants, and implement the comparison based on the
        representation type.
      </para>
      <programlisting language="ocaml">
type shape_repr =
 | Square of int
 | Circle of int
 | Rectangle of int * int;;
 
type shape = &lt; repr : shape_repr; equals : shape -&gt; bool; area : int &gt;;;

class square w =
object (self : 'self)
  method width = w
  method area = self#width * self#width
  method repr = Square self#width
  method equals (other : shape) = self#repr = other#repr
end;;
</programlisting>
      <para>
        The binary method <literal>equals</literal> is now implemented
        in terms of the concrete type <literal>shape_repr</literal>. In
        fact, the objects are now isomorphic to the
        <literal>shape_repr</literal> type. When using this pattern, you
        will not be able to hide the <literal>repr</literal> method, but
        you can hide the type definition using the module system.
      </para>
      <programlisting language="ocaml">
module Shapes : sig
  type shape_repr
  type shape = &lt; repr : shape_repr; equals : shape -&gt; bool; area -&gt; int &gt;
  
  class square : int -&gt;
    object
      method width : int
      method area : int
      method repr : shape_repr
      method equals : shape -&gt; bool
    end
end = struct
  type shape_repr = Square of int | Circle of int | Rectangle of int * int
  ...
end;;
</programlisting>
    </sect2>
  </sect1>
  <sect1 id="private-methods">
    <title>Private methods</title>
    <para>
      Methods can be declared <emphasis>private</emphasis>, which means
      that they may be called by subclasses, but they are not visible
      otherwise (similar to a <emphasis>protected</emphasis> method in
      C++).
    </para>
    <para>
      To illustrate, let's build a class <literal>vector</literal> that
      contains an array of integers, resizing the storage array on
      demand. The field <literal>values</literal> contains the actual
      values, and the <literal>get</literal>, <literal>set</literal>,
      and <literal>length</literal> methods implement the array access.
      For clarity, the resizing operation is implemented as a private
      method <literal>ensure_capacity</literal> that resizes the array
      if necessary.
    </para>
    <programlisting language="ocaml">
# class vector =
  object (self : 'self)
     val mutable values : int array = [||]
  
     method get i = values.(i)
     method set i x =
        self#ensure_capacity i;
        values.(i) &lt;- x
     method length = Array.length values
  
     method private ensure_capacity i =
        if self#length &lt;= i then
           let new_values = Array.create (i + 1) 0 in
           Array.blit values 0 new_values 0 (Array.length values);
           values &lt;- new_values
  end;;
# let v = new vector;;
# v#set 5 2;;
# v#get 5;;
- 2 : int
# v#ensure_capacity 10;;
Characters 0-1:
  v#ensure_capacity 10;;
  ^
Error: This expression has type vector
       It has no method ensure_capacity
</programlisting>
    <para>
      To be precise, the method <literal>ensure_capacity</literal> is
      part of the class type, but it is not part of the object type.
      This means the object <literal>v</literal> has no method
      <literal>ensure_capacity</literal>. However, it is available to
      subclasses. We can extend the class, for example, to include a
      method <literal>swap</literal> that swaps two elements.
    </para>
    <programlisting language="ocaml">
# class swappable_vector =
  object (self : 'self)
     inherit vector

     method swap i j =
        self#ensure_capacity (max i j);
        let tmp = values.(i) in
        values.(i) &lt;- values.(j);
        values.(j) &lt;- tmp
  end;;
</programlisting>
    <para>
      Yet another reason for private methods is to factor the
      implementation and support recursion. Moving along with this
      example, let's build a binary heap, which is a binary tree in heap
      order: where the label of parent elements is smaller than the
      labels of its children. One efficient implementation is to use an
      array to represent the values, where the root is at index 0, and
      the children of a parent node at index <literal>i</literal> are at
      indexes <literal>2 * i</literal> and <literal>2 * i + 1</literal>.
      To insert a node into the tree, we add it as a leaf, and then
      recursively move it up the tree until we restore heap order.
    </para>
    <programlisting language="ocaml">
class binary_heap =
object (self : 'self)
   val values = new swappable_vector

   method min =
      if values#length = 0 then
         raise (Invalid_argument &quot;heap is empty&quot;);
      values#get 0

   method add x =
      let pos = values#length in
      values#set pos x;
      self#move_up pos

   method private move_up i =
      if i &gt; 0 then
         let parent = (i - 1) / 2 in
            if values#get i &lt; values#get parent then begin
               values#swap i parent;
               self#move_up parent
            end
end;;
</programlisting>
    <para>
      The method <literal>move_up</literal> implements the process of
      restoring heap order as a recursive method (though it would be
      straightforward avoid the recursion and use iteration here).
    </para>
    <para>
      The key property of private methods is that they are visible to
      subclasses, but not anywhere else. If you want the stronger
      guarantee that a method is <emphasis>really</emphasis> private,
      not even accessible in subclasses, you can use an explicit typing
      that omits the method. In the following code, the
      <literal>move_up</literal> method is explicitly omitted from the
      object type, and it can't be invoked in subclasses.
    </para>
    <programlisting language="ocaml">
# class binary_heap :
  object
    method min : int
    method add : int -&gt; unit
  end =
  object (self : 'self) {
    ...
    method private move_up i = ...
  end;;
</programlisting>
  </sect1>
  <sect1 id="virtual-classes-and-methods">
    <title>Virtual classes and methods</title>
    <para>
      A <emphasis>virtual</emphasis> class is a class where some methods
      or fields are declared, but not implemented. This should not be
      confused with the word &quot;virtual&quot; as it is used in C++.
      In C++, a &quot;virtual&quot; method uses dynamic dispatch,
      regular non-virtual methods use static dispatched. In OCaml,
      <emphasis>all</emphasis> methods use dynamic dispatch, but the
      keyword <emphasis>virtual</emphasis> means the method or field is
      not implemented.
    </para>
    <para>
      In the previous section, we defined a class
      <literal>swappable_vector</literal> that inherits from
      <literal>array_vector</literal> and adds a <literal>swap</literal>
      method. In fact, the <literal>swap</literal> method could be
      defined for any object with <literal>get</literal> and
      <literal>set</literal> methods; it doesn't have to be the specific
      class <literal>array_vector</literal>.
    </para>
    <para>
      One way to do this is to declare the
      <literal>swappable_vector</literal> abstractly, declaring the
      methods <literal>get</literal> and <literal>set</literal>, but
      leaving the implementation for later. However, the
      <literal>swap</literal> method can be defined immediately.
    </para>
    <programlisting language="ocaml">
class virtual abstract_swappable_vector =
object (self : 'self)
   method virtual get : int -&gt; int
   method virtual set : int -&gt; int -&gt; unit
   method swap i j =
      let tmp = self#get i in
      self#set i (self#get j);
      self#set j tmp
end;;
</programlisting>
    <para>
      At some future time, we may settle on a concrete implementation
      for the vector. We can inherit from the
      <literal>abstract_swappable_bvector</literal> to get the
      <literal>swap</literal> method &quot;for free.&quot; Here's one
      implementation using arrays.
    </para>
    <programlisting language="ocaml">
class array_vector =
object (self : 'self)
   inherit abstract_swappable_vector

   val mutable values = [||]
   method get i = values.(i)
   method set i x =
      self#ensure_capacity i;
      values.(i) &lt;- x
   method length = Array.length values

   method private ensure_capacity i =
      if self#length &lt;= i then
         let new_values = Array.create (i + 1) 0 in
            Array.blit values 0 new_values 0 (Array.length values);
            values &lt;- new_values
end
</programlisting>
    <para>
      Here's a different implementation using
      <literal>HashTbl</literal>.
    </para>
    <programlisting language="ocaml">
class hash_vector =
object (self : 'self)
   inherit abstract_swappable_vector

   val table = Hashtbl.create 19

   method get i =
      try Hashtbl.find table i with
         Not_found -&gt; 0

   method set = Hashtbl.add table
end;;
</programlisting>
    <para>
      One way to view a <literal>virtual</literal> class is that it is
      like a functor, where the &quot;inputs&quot; are the declared, but
      not defined, virtual methods and fields. The functor application
      is implemented through inheritance, when virtual methods are given
      concrete implementations.
    </para>
    <para>
      We've been mentioning that fields can be virtual too. Here is
      another implementation of the swapper, this time with direct
      access to the array of values.
    </para>
    <programlisting language="ocaml">
class virtual abstract_swappable_array_vector =
object (self : 'self)
   val mutable virtual values : int array
   method private virtual ensure_capacity : int -&gt; unit

   method swap i j =
      self#ensure_capacity (max i j);
      let tmp = values.(i) in
      values.(i) &lt;- values.(j);
      values.(j) &lt;- tmp
end;;
</programlisting>
    <para>
      This level of dependency on the implementation details is
      possible, but it is hard to justify the use of a virtual class --
      why not just define the <literal>swap</literal> method as part of
      the concrete class? Virtual classes are better suited for
      situations where there are multiple (useful) implementations of
      the virtual parts. In most cases, this will be public virtual
      methods.
    </para>
  </sect1>
  <sect1 id="multiple-inheritance">
    <title>Multiple inheritance</title>
    <para>
      When a class inherits from more than one superclass, it is using
      <emphasis>multiple inheritance</emphasis>. Multiple inheritance
      extends the variety of ways in which classes can be combined, and
      it can be quite useful, particularly with virtual classes.
      However, it can be tricky to use, particularly when the
      inheritance hierarchy is a graph rather than a tree, so it should
      be used with care.
    </para>
    <sect2 id="how-names-are-resolved">
      <title>How names are resolved</title>
      <para>
        The main &quot;trickiness&quot; of multiple inheritance is due
        to naming -- what happens when a method or field with some name
        is defined in more than one class?
      </para>
      <para>
        If there is one thing to remember about inheritance in OCaml, it
        is this: inheritance is like textual inclusion. If there is more
        than one definition for a name, the last definition wins. Let's
        look at some artificial, but illustrative, examples.
      </para>
      <para>
        First, let's consider what happens when we define a method more
        than once. In the following example, the method
        <literal>get</literal> is defined twice; the second definition
        &quot;wins,&quot; meaning that it overrides the first one.
      </para>
      <programlisting language="ocaml">
# class m1 =
object (self : 'self)
   method get = 1
   method f = self#get
   method get = 2
end;;
class m1 : object method f : int method get : int end
# (new m1)#f;;
- : int = 2
</programlisting>
      <para>
        Fields have similar behavior, though the compiler produces a
        warning message about the override.
      </para>
      <programlisting language="ocaml">
# class m2 =
# class m2 =
  object (self : 'self)
     val x = 1
     method f = x
     val x = 2
  end;;
Characters 69-74:
     val x = 2
         ^^^^^
Warning 13: the instance variable x is overridden.
The behaviour changed in ocaml 3.10 (previous behaviour was hiding.)
class m2 : object val x : int method f : int end
# (new m2)#f;;
- : int = 2
</programlisting>
      <para>
        Of course, it is unlikely that you will define two methods or
        two fields of the same name in the same class. However, the
        rules for inheritance follow the same pattern: the last
        definition wins. In the following definition, the
        <literal>inherit</literal> declaration comes last, so the method
        definition <literal>method get = 2</literal> overrides the
        previous definition, always returning 2.
      </para>
      <programlisting language="ocaml">
# class m4 = object method get = 2 end;;
# class m5 =
  object
    val mutable x = 1
    method get = x
    method set x' = x &lt;- x'
    inherit m4
  end;;
class m5 : object val mutable x : int method get : int method set : int -&gt; unit end
# let x = new m5;;
val x : m5 = &lt;obj&gt;
# x#set 5;;
- : unit = ()
# x#get;;
- : int = 2
</programlisting>
      <para>
        To reiterate, to understand what inheritance means, replace each
        <literal>inherit</literal> directive with its definition, and
        take the last definition of each method or field. This holds
        even for private methods. However, it does
        <emphasis>not</emphasis> hold for private methods that are
        &quot;really&quot; private, meaning that they have been hidden
        by a type constraint. In the following definitions, there are
        three definitions of the private method <literal>g</literal>.
        However, the definition of <literal>g</literal> in
        <literal>m8</literal> is not overridden, because it is not part
        of the class type for <literal>m8</literal>.
      </para>
      <programlisting language="ocaml">
# class m6 =
  object (self : 'self)
     method f1 = self#g
     method private g = 1
  end;;
class m6 : object method f1 : int method private g : int end
# class m7 =
  object (self : 'self)
     method f2 = self#g
     method private g = 2
  end;;
class m7 : object method f2 : int method private g : int end
# class m8 : object method f3 : int end =
  object (self : 'self)
     method f3 = self#g
     method private g = 3
  end;;
class m8 : object method f3 : int end
# class m9 =
  object (self : 'self)
     inherit m6
     inherit m7
     inherit m8
  end;;
# class m9 :
  object
    method f1 : int
    method f2 : int
    method f3 : int
    method private g : int
  end
# let x = new m9;;
val x : m9 = &lt;obj&gt;
# x#f1;;
- : int = 2
# x#f3;;
- : int = 3
</programlisting>
    </sect2>
    <sect2 id="mixins">
      <title>Mixins</title>
      <para>
        When should you use multiple inheritance? If you ask multiple
        people, you're likely to get multiple (perhaps heated) answers.
        Some will argue that multiple inheritance is overly complicated;
        others will argue that inheritance is problematic in general,
        and one should use object composition instead. But regardless of
        who you talk to, you will rarely hear that multiple inheritance
        is great and you should use it widely.
      </para>
      <para>
        In any case, if you're programming with objects, there's one
        general pattern for multiple inheritance that is both useful and
        reasonably simple, the <emphasis>mixin</emphasis> pattern.
        Generically, a <emphasis>mixin</emphasis> is just a virtual
        class that implements a feature based on another one. If you
        have a class that implements methods <emphasis>A</emphasis>, and
        you have a mixin <emphasis>M</emphasis> that provides methods
        <emphasis>B</emphasis> from <emphasis>A</emphasis>, then you can
        inherit from <emphasis>M</emphasis> -- &quot;mixing&quot; it in
        -- to get features <emphasis>B</emphasis>.
      </para>
      <para>
        That's too abstract, so let's give an example based on
        collections. In Section XXX:Objecttypes, we introduced the
        <emphasis>iterator</emphasis> pattern, where an
        <emphasis>iterator</emphasis> object is used to enumerate the
        elements of a collection. Lots of containers can have iterators,
        singly-linked lists, dictionaries, vectors, etc.
      </para>
      <programlisting language="ocaml">
type 'a iterator = &lt; get : 'a; has_value : bool; next : unit &gt;;;
class ['a] slist : object ... method iterator : 'a iterator end;;
class ['a] vector : object ... method iterator : 'a iterator end;;
class ['a] deque : object ... method iterator : 'a iterator end;;
class ['a, 'b] map : object ... method iterator : 'b iterator end;;
...
</programlisting>
      <para>
        The collections are different is some ways, but they share a
        common pattern for iteration that we can re-use. For a simple
        example, let's define a mixin that implements an arithmetic sum
        for a collection of integers.
      </para>
      <programlisting language="ocaml">
# class virtual int_sum_mixin =
  object (self : 'self)
     method virtual iterator : int iterator
     method sum =
        let it = self#iterator in
        let total = ref 0 in
        while it#has_value do
           total := !total + it#get;
           it#next
        done;
        !total
  end;;
# class int_slist =
  object
     inherit [int] slist
     inherit int_sum_mixin
  end;;
# let l = new int_slist;;
val l : int_slist = &lt;obj&gt;
# l#insert 5;;
# l#insert 12;;
# l#sum;;
- : int = 17
# class int_deque =
  object
     inherit [int] deque
     inherit int_sum_mixin
  end;;
</programlisting>
      <para>
        In this particular case, the mixin works only for a collection
        of integers, so we can't add the mixin to the polymorphic class
        definition <literal>['a] slist</literal> itself. However, the
        result of using the mixin is that the integer collection has a
        method <literal>sum</literal>, and it is done with very little
        of the fuss we would need if we used object composition instead.
      </para>
      <para>
        The mixin pattern isn't limited to non-polymorphic classes, of
        course. We can use it to implement generic features as well. The
        following mixin defines functional-style iteration in terms of
        the imperative iterator pattern.
      </para>
      <programlisting language="ocaml">
class virtual ['a] fold_mixin =
object (self : 'self)
   method virtual iterator : 'a iterator
   method fold : 'b. ('b -&gt; 'a -&gt; 'b) -&gt; 'b -&gt; 'b =
      (fun f x -&gt;
            let y = ref x in
            let it = self#iterator in
            while it#has_value do
               y := f !y it#get;
               it#next
            done;
            !y)
end;;

class ['a] slist_with_fold =
object
   inherit ['a] slist
   inherit ['a] fold_mixin
end;;
</programlisting>
    </sect2>
  </sect1>
</chapter></part><appendix id="installation">
  <title>Installation</title>
  <para>
    The easiest way to use OCaml is via the binary packages available
    for many operating systems. For day-to-day code development however,
    it's much easier to use a source-code manager that lets you modify
    individual libraries and automatically recompile all the
    dependencies.
  </para>
  <para>
    An important difference between OCaml and scripting languages such
    as Python or Ruby is the static type safety that means that you
    can't just mix-and-match compiled libraries. Interfaces are checked
    when libraries are compiled, so when an interface is changed, all
    the dependent libraries must also be recompiled. Source-based
    package managers automate this process for you and make development
    life much easier.
  </para>
  <para>
    To work through Real World OCaml, you'll need three major components
    installed:
  </para>
  <itemizedlist>
    <listitem>
      <para>
        The OCaml compiler itself.
      </para>
    </listitem>
    <listitem>
      <para>
        The OPAM source package manager, through which we'll install
        several extra libraries.
      </para>
    </listitem>
    <listitem>
      <para>
        The <literal>utop</literal> interactive toplevel, a modern
        interactive toplevel with command history and tab completion.
      </para>
    </listitem>
  </itemizedlist>
  <para>
    Let's get started with how to install OCaml on various operating
    systems, and we'll get OPAM and <literal>utop</literal> running
    after that.
  </para>
  <sect1 id="getting-ocaml">
    <title>Getting OCaml</title>
    <para>
      The OCaml compiler is available as a binary distribution on many
      operating systems. This is the simplest and preferred installation
      route, but we'll also describe how to do a manual installation as
      a last resort.
    </para>
    <sect2 id="mac-os-x">
      <title>Mac OS X</title>
      <para>
        The
        <ulink url="http://github.com/mxcl/homebrew">Homebrew</ulink>
        package manager has an OCaml installer, which is usually updated
        pretty quickly to the latest stable release. Before installing
        OCaml, make sure that you have the latest XCode installed from
        the App Store so that a C compiler is available.
      </para>
      <programlisting>
$ brew install ocaml
$ brew install pcre
</programlisting>
      <para>
        The Perl-compatible Regular Expression library (PCRE) is used by
        the Core suite. It's not strictly needed to use OCaml, but is a
        commonly used library that we're installing now to save time
        later.
      </para>
      <para>
        Another popular package manager on Mac OS X is
        <ulink url="http://macports.org">MacPorts</ulink>, which also
        has an OCaml port. As with Homebrew, make sure you have XCode
        installed and have followed the rest of the MacPorts
        installation instructions, and then type in:
      </para>
      <programlisting>
$ sudo port install ocaml
$ sudo port install ocaml-pcre
</programlisting>
    </sect2>
    <sect2 id="debian-linux">
      <title>Debian Linux</title>
      <para>
        On Debian Linux, you should install OCaml via binary packages.
        You'll need at least OCaml version 3.12.1 to bootstrap OPAM,
        which means using Debian Wheezy or greater. Don't worry about
        getting the absolute latest version of the compiler, as you just
        need one new enough to compile the OPAM package manager, after
        which you'll use OPAM to manage your compiler installation.
      </para>
      <programlisting>
$ sudo apt-get install ocaml ocaml-native-compilers camlp4-extra
$ sudo apt-get install git libpcre3-dev curl build-essential m4
</programlisting>
      <para>
        Notice that we've installed a few more packages than just the
        OCaml compiler here. The second command line installs enough
        system packages to let you build your own OCaml packages. You
        may find that some OCaml libraries require more system libraries
        (for example, <literal>libssl-dev</literal>), but we'll
        highlight these in the book when we introduce the library.
      </para>
      <para>
        Fedora and Red Hat
      </para>
      <para>
        OCaml has been included in the basic distribution since Fedora
        8. To install the latest compiler, just run:
      </para>
      <programlisting>
# yum install ocaml
# yum install pcre-devel
</programlisting>
      <para>
        The PCRE package is used by Core and is just included here for
        convenience later.
      </para>
    </sect2>
    <sect2 id="arch-linux">
      <title>Arch Linux</title>
      <para>
        Arch Linux provides OCaml 4.00.1 (or later) in the standard
        repositories, so the easiest method of installation is using
        <literal>pacman</literal>:
      </para>
      <programlisting>
$ pacman -Sy ocaml
</programlisting>
    </sect2>
    <sect2 id="windows">
      <title>Windows</title>
      <para>
        Windows is not currently supported by the examples in Real World
        OCaml, although it is being worked on. Until that's ready, we
        recommend using a virtual machine running Debian Linux on your
        local machine.
      </para>
    </sect2>
    <sect2 id="building-from-source">
      <title>Building from source</title>
      <para>
        To install OCaml from source code, first make sure that you have
        a C compilation environment (usually either
        <literal>gcc</literal> or <literal>llvm</literal> installed).
      </para>
      <programlisting>
$ curl -OL https://github.com/ocaml/ocaml/archive/trunk.tar.gz
$ tar -zxvf trunk.tar.gz
$ cd ocaml-trunk
$ ./configure
$ make world world.opt
$ sudo make install
</programlisting>
      <para>
        The final step requires administrator privilege to install in
        your system directory. You can also install it in your home
        directory by passing the <literal>prefix</literal> option to the
        configuration script:
      </para>
      <programlisting>
$ ./configure -prefix $HOME/my-ocaml
</programlisting>
      <para>
        Once the installation is completed into this custom location,
        you will need to add <literal>$HOME/my-ocaml/bin</literal> to
        your <literal>PATH</literal>, normally by editing the
        <literal>~/.bash_profile</literal> file. You shouldn't really to
        do this unless you have special reasons, so try to install
        binary packages before trying a source installation.
      </para>
      <note>
      <title>
      Note to reviewers
      </title>
      <para>
        We instruct you install the unreleased trunk version of OCaml in
        these instructions, as we take advantage of some recent
        additions to the language that simplify explanations in the
        book. The 4.01 release will happen before the book is released,
        but you may run into &quot;bleeding edge&quot; bugs with the
        trunk release. Leave a comment here if you do and we'll address
        them.
      </para>
      </note>
    </sect2>
  </sect1>
  <sect1 id="getting-opam">
    <title>Getting OPAM</title>
    <para>
      OPAM manages multiple simultaneous OCaml compiler and library
      installations, tracks library versions across upgrades, and
      recompiles dependencies automatically if they get out of date.
      It's used throughout Real World OCaml as the mechanism to retrieve
      and use third-party libraries.
    </para>
    <para>
      Before installing OPAM, make sure that you have the OCaml compiler
      installed as described above. Once installed, the entire OPAM
      database is held in your home directory (normally
      <literal>$HOME/.opam</literal>). If something goes wrong, just
      delete this <literal>.opam</literal> directory and start over from
      a clean slate. If youre using a beta version of OPAM, please
      upgrade it to at least version 1.0.0 or greater before proceeding.
    </para>
    <sect2 id="mac-os-x-1">
      <title>Mac OS X</title>
      <para>
        Source installation of OPAM will take a minute or so on a modern
        machine. There is a Homebrew package for the latest OPAM:
      </para>
      <programlisting>
$ brew update
$ brew install opam
</programlisting>
      <para>
        And on MacPorts, install it like this:
      </para>
      <programlisting>
$ sudo port install opam
</programlisting>
    </sect2>
    <sect2 id="debian-linux-1">
      <title>Debian Linux</title>
      <para>
        There are experimental binary packages available for Debian
        Wheezy/amd64. You should be able to use these on 64-bit Ubuntu
        and other derivative distributions such as Linux Mint also. Just
        add the following line to your
        <literal>/etc/apt/sources.list</literal>:
      </para>
      <programlisting>
deb http://www.recoil.org/~avsm/ wheezy main
</programlisting>
      <para>
        When this is done, update your packages and install OPAM. You
        can ignore the warning about unsigned packages, which will
        disappear when OPAM is upstreamed into Debian mainline.
      </para>
      <programlisting>
# apt-get update
# apt-get install opam
</programlisting>
      <note>
      <title>
      Note to reviewers
      </title>
      <para>
        The OPAM instructions will be simplified when integrated
        upstream into Debian and Fedora, which is ongoing. Until then,
        we're leaving source-code installation instructions here. Please
        leave a comment with any amended instructions you encounter
      </para>
      </note>
      <para>
        If the binary packages aren't suitable, you need to install the
        latest OPAM release from source. The distribution only requires
        the OCaml compiler to be installed, so this should be pretty
        straightforward. Download the latest version, which is always
        marked with a <literal>stable</literal> tag on the project
        <ulink url="https://github.com/OCamlPro/opam/tags">homepage</ulink>.
      </para>
      <programlisting>
$ curl -OL https://github.com/OCamlPro/opam/archive/latest.tar.gz
$ tar -zxvf latest.tar.gz
$ cd opam-latest
$ ./configure &amp;&amp; make
$ sudo make install
</programlisting>
    </sect2>
    <sect2 id="fedora-and-red-hat">
      <title>Fedora and Red Hat</title>
      <para>
        There is currently no RPM available for Fedora or Red Hat, so
        please install OPAM via the source code instructions for the
        moment.
      </para>
    </sect2>
    <sect2 id="arch-linux-1">
      <title>Arch Linux</title>
      <para>
        OPAM is available in the Arch User Repository (AUR) in two
        packages. You'll need both <literal>ocaml</literal> and the
        <literal>base-devel</literal> packages installed first:
      </para>
      <itemizedlist>
        <listitem>
          <para>
            <literal>opam</literal> contains the most recent stable
            release, and is the recommended package.
          </para>
        </listitem>
        <listitem>
          <para>
            <literal>opam-git</literal> builds the package from the
            latest upstream source, and should only be used if you are
            looking for a specific bleeding-edge feature.
          </para>
        </listitem>
      </itemizedlist>
      <para>
        Run these commands to install the stable OPAM package:
      </para>
      <programlisting>
$ sudo pacman -Sy base-devel
$ wget https://aur.archlinux.org/packages/op/opam/opam.tar.gz
$ tar -xvf opam.tar.gz &amp;&amp; cd opam
$ makepkg
$ sudo pacman -U opam-_version_.pkg.tar.gz
</programlisting>
    </sect2>
  </sect1>
  <sect1 id="configuring-the-opam-package-manager">
    <title>Configuring the OPAM package manager</title>
    <para>
      The entire OPAM package database is held in the
      <literal>.opam</literal> directory in your home directory,
      including compiler installations. On Linux and Mac OS X, this will
      be the <literal>~/.opam</literal> directory. You shouldn't switch
      to an admin user to install packages as nothing will be installed
      outside of this directory. If you run into problems, just delete
      the whole <literal>~/.opam</literal> directory and follow the
      installations instructions from the <literal>opam init</literal>
      stage again.
    </para>
    <para>
      Let's begin by initialising the OPAM package database. This will
      require an active Internet connection, and ask you a few
      interactive questions at the end. It's safe to answer yes to these
      unless you want to manually control the configuration steps
      yourself as an advanced user.
    </para>
    <programlisting>
$ opam init
&lt;...&gt;
=-=-=-= Configuring OPAM =-=-=-=
Do you want to update your configuration to use OPAM ? [Y/n] y
[1/4] Do you want to update your shell configuration file ? [default: ~/.profile] y
[2/4] Do you want to update your ~/.ocamlinit ? [Y/n] y
[3/4] Do you want to install the auto-complete scripts ? [Y/n] y
[4/4] Do you want to install the `opam-switch-eval` script ? [Y/n] y
User configuration:
  ~/.ocamlinit is already up-to-date.
  ~/.profile is already up-to-date.
Gloabal configuration:
  Updating &lt;root&gt;/opam-init/init.sh
    auto-completion : [true]
    opam-switch-eval: [true]
  Updating &lt;root&gt;/opam-init/init.zsh
    auto-completion : [true]
    opam-switch-eval: [true]
  Updating &lt;root&gt;/opam-init/init.csh
    auto-completion : [true]
    opam-switch-eval: [true]
</programlisting>
    <para>
      You only need to run this command once, and it will create the
      <literal>~/.opam</literal> directory and sync with the latest
      package list from the online OPAM database.
    </para>
    <para>
      When the <literal>init</literal> command finishes, you'll see some
      instructions about environment variables. OPAM never installs
      files into your system directories (which would require
      administrator privileges). Instead, it puts them into your home
      directory by default, and can output a set of shell commands which
      configures your shell with the right <literal>PATH</literal>
      variables so that packages will just work. This requires just one
      command:
    </para>
    <programlisting>
$ eval `opam config -env`
</programlisting>
    <para>
      This evaluates the results of running
      <literal>opam config env</literal> in your current shell, and sets
      the variables so that subsequent commands will use them. This only
      works with your current shell, and it can be automated for all
      future shells by adding the line to your login scripts. On Mac OS
      X or Debian, this is usually the
      <literal>~/.bash_profile</literal> file if you're using the
      default shell. If you've switched to another shell, it might be
      <literal>~/.zshrc</literal> instead. OPAM isn't unusual in this
      approach; the SSH <literal>ssh-agent</literal> also works
      similarly, so if you're having any problems just hunt around in
      your configuration scripts to see how that's being invoked.
    </para>
    <para>
      If you answered <literal>yes</literal> to the auto-complete
      scripts question during <literal>opam init</literal>, this should
      have all been set up for you. You can verify this worked by
      listing the available packages:
    </para>
    <programlisting>
$ opam list
</programlisting>
    <note>
    <title>
    Note to reviewers
    </title>
    <para>
      OPAM 1.0's always places the login commands into your
      <literal>~/.profile</literal> directory, which isn't executed if
      your shell is <literal>bash</literal>. This has been fixed in
      subsequent versions, but for now you'll need to manually copy the
      contents of <literal>~/.profile</literal> over to
      <literal>~/.bash_profile</literal> via:
    </para>
    <programlisting>
$ cat ~/.profile &gt;&gt; ~/.bash_profile
</programlisting>
    </note>
    <para>
      The most important package we need to install is Core, which is
      the replacement standard library that all of the examples in this
      book use. Before doing this, let's make sure you have exactly the
      right compiler version you need. We've made some minor
      modifications to the way the OCaml compiler displays type
      signatures, and the next command will install a patched
      <literal>4.01.0</literal> compiler with this functionality
      enabled.
    </para>
    <programlisting>
$ opam switch 4.01.0dev+trunk
</programlisting>
    <para>
      This step will take about 5-10 minutes on a modern machine, and
      will download and install (within the <literal>~/.opam</literal>
      directory) a custom OCaml compiler. OPAM supports multiple such
      installations, and you'll find this very useful if you ever decide
      to hack on the internals of the compiler itself, or you want to
      experiment with the latest release without sacrificing your
      current installation. You only need to install this compiler once,
      and future updates will be much faster as they only recompile
      libraries within the compiler installation.
    </para>
    <para>
      The new compiler will be installed into
      <literal>~/.opam/4.01.0dev+trunk</literal> and any libraries you
      install for it will be tracked separately from your system
      installation. You can have any number of compilers installed
      simultaneously, but only one can be active at any time. Browse
      through the available compilers by running
      <literal>opam switch list</literal>.
    </para>
    <para>
      Finally, we're ready to install the Core libraries. Run this:
    </para>
    <programlisting>
$ opam install core core_extended async
</programlisting>
    <para>
      This will take about five minutes to install, and install a series
      of packages. OPAM figures out the dependencies you need
      automatically, but the three packages that really matter are:
    </para>
    <itemizedlist>
      <listitem>
        <para>
          <literal>core</literal> is the main, well-supported Core
          distribution from Jane Street.
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>core_extended</literal> contains a number of
          experimental, but useful, extension libraries that are under
          review for inclusion in Core. We use some of these in places,
          but much less than Core itself.
        </para>
      </listitem>
      <listitem>
        <para>
          <literal>async</literal> is the network programming library
          that we use in Part II to communicate with other hosts. You
          can skip this for the initial installation until you get to
          Part II, if you prefer.
        </para>
      </listitem>
    </itemizedlist>
    <sect2 id="editing-environment">
      <title>Editing Environment</title>
      <para>
        There's one last tool you need before getting started on the
        examples. The default <literal>ocaml</literal> command gives us
        an interactive command-line to experiment with code without
        compiling it. However, it's quite a spartan experience and so we
        use a more modern alternative.
      </para>
      <programlisting>
$ opam install utop
</programlisting>
      <para>
        The <literal>utop</literal> package is an interactive
        command-line interface to OCaml that has tab-completion,
        persistent history and integration with Emacs so that you can
        run it within your editing environment.
      </para>
      <para>
        Remember from earlier that OPAM never installs files directly
        into your system directories, and this applies to
        <literal>utop</literal> too. You'll find the binary in
        <literal>~/.opam/4.01.0dev+trunk/bin</literal>. However, just
        typing in <literal>utop</literal> from your shell should just
        work, due to the <literal>opam config env</literal> step which
        configures your shell. Don't forget to automate this as
        described earlier, as it makes life much easier when developing
        OCaml code!
      </para>
    </sect2>
    <sect2 id="command-line">
      <title>Command Line</title>
      <para>
        The <literal>utop</literal> tool provides a convenient
        interactive toplevel, with full command history, command macros
        and module name completion. When you first run
        <literal>utop</literal>, you'll find yourself at an interactive
        prompt with a bar at the bottom of the screen. The bottom bar
        dynamically updates as you write text, and contains the possible
        names of modules or variables that are valid at that point in
        the phrase you are entering. You can press the
        <literal>&lt;tab&gt;</literal> key to complete the phrase with
        the first choice.
      </para>
      <para>
        The <literal>~/.ocamlinit</literal> file in your home directory
        initialises <literal>utop</literal> with common libraries and
        syntax extensions so you don't need to type them in every time.
        Now that you have Core installed, you should update it to load
        it every time you start <literal>utop</literal>, by adding this
        to it:
      </para>
      <programlisting language="ocaml">
#use &quot;topfind&quot;
#camlp4o
#require &quot;core.top&quot;
#require &quot;core_extended&quot;
#require &quot;async&quot;
open Core.Std
</programlisting>
      <para>
        When you run <literal>utop</literal> with this initialization
        file, it should start up with Core opened and ready to use.
      </para>
    </sect2>
    <sect2 id="editors">
      <title>Editors</title>
      <sect3 id="emacs">
        <title>Emacs</title>
        <para>
          TODO: Emacs users have tuareg and
          <ulink url="http://www.typerex.org/">Typerex</ulink>.
        </para>
        <para>
          To use <literal>utop</literal> directly in Emacs, add the
          following line to your <literal>~/.emacs</literal> file:
        </para>
        <programlisting language="scheme">
(autoload 'utop &quot;utop&quot; &quot;Toplevel for OCaml&quot; t)
</programlisting>
        <para>
          You also need to make the <literal>utop.el</literal> file
          available to your Emacs installation. The OPAM version of
          <literal>utop</literal> installs it into the
          <literal>~/.opam</literal> hierarchy, for example in
          <literal>~/.opam/system/share/emacs/site-lisp/utop.el</literal>.
          You may need to replace <literal>system</literal> with your
          current compiler switch, such as
          <literal>4.01.0dev+trunk</literal>.
        </para>
        <para>
          Once this successfully loads in Emacs, you can run utop by
          executing the command <literal>utop</literal> in Emacs. There
          are more details instructions at the
          <ulink url="https://github.com/diml/utop#integration-with-emacs">utop
          homepage</ulink>.
        </para>
      </sect3>
      <sect3 id="vim">
        <title>Vim</title>
        <para>
          TODO: Vim users can use the built-in style, and
          <ulink url="http://github.com/avsm/ocaml-annot">ocaml-annot</ulink>
          may also be useful.
        </para>
      </sect3>
      <sect3 id="eclipse">
        <title>Eclipse</title>
        <para>
          Eclipse is a popular IDE usually used for Java development.
          The OCaml Development Tools (ODT) project provides equivalent
          IDE features for editing and compiling OCaml code, such as
          automatic compilation and name completion.
        </para>
        <para>
          ODT is distributed as a set of plugins for the Eclipse IDE
          environment from the
          <ulink url="http://ocamldt.free.fr">homepage</ulink>. You just
          have to copy these plugins into your Eclipse distribution in
          order to access the new OCaml facilities.
        </para>
      </sect3>
    </sect2>
  </sect1>
</appendix><appendix id="packaging">
  <title>Packaging</title>
  <para>
    The OCaml toolchain is structured much like a C compiler, with
    several tools that generate intermediate files and finally link
    against a runtime. The final outputs don't have to be just
    executables. Many people embed OCaml code as object files that are
    called from other applications, or even compile it to Javascript and
    other esoteric targets. Let's start by covering some of the standard
    OCaml tools, and then move on to some of the higher level methods
    for packaging and publishing your code online.
  </para>
  <sect1 id="the-ocaml-toolchain">
    <title>The OCaml toolchain</title>
    <para>
      There are two distinct compilers for OCaml code included in the
      standard distribution. The first outputs bytecode that is
      interpreted at runtime, and the second generates fast, efficient
      native code directly. Both of these share the front-end
      type-checking logic, and only diverge when it comes to code
      generation.
    </para>
    <sect2 id="the-ocamlc-bytecode-compiler">
      <title>The <literal>ocamlc</literal> bytecode compiler</title>
      <para>
        The simplest code generator is the <literal>ocamlc</literal>
        compiler, which outputs bytecode that is interpreted via the
        <literal>ocamlrun</literal> runtime. The OCaml bytecode virtual
        machine is a stack machine (much like the Java Virtual Machine),
        with the exception of a single register that stores the most
        recent result. This provides a simple runtime model that is easy
        to implement or embed within other systems, but executes rather
        slowly due to being interpreted.
      </para>
      <para>
        Here are some of the intermediate files generated by
        <literal>ocamlc</literal>:
      </para>
      <informaltable>
        <tgroup cols="2">
          <colspec align="left"/>
          <colspec align="left"/>
          <thead>
            <row>
              <entry>
                Extension
              </entry>
              <entry>
                Purpose
              </entry>
            </row>
          </thead>
          <tbody>
            <row>
              <entry>
                .ml
              </entry>
              <entry>
                Source files for compilation unit module
                implementations.
              </entry>
            </row>
            <row>
              <entry>
                .mli
              </entry>
              <entry>
                Source files for compilation unit module interfaces. If
                missing, generated from the <literal>.ml</literal> file.
              </entry>
            </row>
            <row>
              <entry>
                .cmi
              </entry>
              <entry>
                Compiled module interface from a corresponding
                <literal>.mli</literal> source file.
              </entry>
            </row>
            <row>
              <entry>
                .cmo
              </entry>
              <entry>
                Compiled bytecode object file of the module
                implementation.
              </entry>
            </row>
            <row>
              <entry>
                .cma
              </entry>
              <entry>
                Library of bytecode object files packed into a single
                file.
              </entry>
            </row>
            <row>
              <entry>
                .o
              </entry>
              <entry>
                C source files are compiled into native object files by
                the system <literal>cc</literal>.
              </entry>
            </row>
          </tbody>
        </tgroup>
      </informaltable>
      <para>
        To obtain a bytecode executable, you need to compile a set of
        <literal>cmo</literal> object files, and then link them into an
        executable
      </para>
    </sect2>
    <sect2 id="the-ocamlopt-native-code-compiler">
      <title>The <literal>ocamlopt</literal> native code
      compiler</title>
      <informaltable>
        <tgroup cols="2">
          <colspec align="left"/>
          <colspec align="left"/>
          <thead>
            <row>
              <entry>
                Extension
              </entry>
              <entry>
                Purpose
              </entry>
            </row>
          </thead>
          <tbody>
            <row>
              <entry>
                .cmi
              </entry>
              <entry>
                Compiled module interface from a corresponding
                <literal>.mli</literal> source file.
                (<emphasis>avsm</emphasis>: this is not compatible with
                the ocamlc version iirc)
              </entry>
            </row>
            <row>
              <entry>
                .o
              </entry>
              <entry>
                Compiled native object file of the module
                implementation.
              </entry>
            </row>
            <row>
              <entry>
                .cmx
              </entry>
              <entry>
                Contains extra information for linking and cross-module
                optimization of the object file.
              </entry>
            </row>
            <row>
              <entry>
                .cmxa/.a
              </entry>
              <entry>
                Library of <literal>cmx</literal> and
                <literal>o</literal> units, stored in the
                <literal>cmxa</literal> and <literal>a</literal> files
                respectively.
              </entry>
            </row>
          </tbody>
        </tgroup>
      </informaltable>
    </sect2>
    <sect2 id="the-ocaml-toplevel-loop">
      <title>The <literal>ocaml</literal> toplevel loop</title>
      <para>
      </para>
    </sect2>
  </sect1>
  <sect1 id="the-findlib-compiler-frontend">
    <title>The Findlib compiler frontend</title>
    <para>
    </para>
  </sect1>
  <sect1 id="packaging-applications-with-oasis">
    <title>Packaging applications with OASIS</title>
    <sect2 id="ocamlbuild">
      <title>ocamlbuild</title>
      <para>
      </para>
    </sect2>
  </sect1>
  <sect1 id="distributing-applications-with-opam">
    <title>Distributing applications with OPAM</title>
    <para>
    </para>
  </sect1>
</appendix></book>